equations

[ppSDFGenerator-start]
  ppSDFGenerator($Module*) = generateModule($Module*, )

[generateModule-0]
  module $ModuleName $ImpSection* $Sections := $Module0,
  $ImpSection := imports $ModuleName languages/box/syntax/box,
  $Grammar := context-free syntax getProductions($Module0, ),
  $Section := exports $Grammar,
  $Module1 := module makePPModuleName($ModuleName) $ImpSection $Section
  ===>
  generateModule($Module0, $Module*) = $Module* $Module1

[getProductions-0]
  context-free syntax $Production*1 := $Grammar
  ===>
  getProductions($Grammar, $Production*0) 
    = generateProductions($Production*1, $Production*0)

[getProductions-1]
  lexical syntax $Production*1 := $Grammar
  ===>
  getProductions($Grammar, $Production*0) 
    = generateProductions($Production*1, $Production*0)

[generateProductions-0]
  $Symbols -> $Symbol1 $Attributes := $Production0,
  $Symbol2 := makePPSymbol($Symbol1),
  $Production1 := $Symbol2 addBrackets($Symbol1) -> Box
  ===>
  generateProductions($Production0, $Production*) = $Production* $Production1

[generateProductions-1]
  $Literal ( $Symbol* ) -> $Symbol1 $Attributes := $Production0,
  $Symbol2 := makePPSymbol($Symbol1),
  $Production1 := $Symbol2 addBrackets($Symbol1) -> Box
  ===>
  generateProductions($Production0, $Production*) = $Production* $Production1

[makePPSymbol-0]
  bytes(bytes($Char+)) := unparse-to-bytes($Symbol),
  bytes($Char*) := removeWhitespace(bytes($Char+))
  ===>
  makePPSymbol($Symbol) = uqliteral("p" "p" $Char*)

[makePPModuleName-0]
  bytes(bytes($Char+)) := unparse-to-bytes($ModuleName)
  ===>
  makePPModuleName($ModuleName) = moduleid("P" "P" $Char+)

[addBrackets-0]
  bytes(bytes($Char+)) := unparse-to-bytes($Symbol)
  ===>
  addBrackets($Symbol) = uqliteral("(" $Char+ ")")

[default-removeWhitespace]
  removeWhitespace($Bytes) = $Bytes

[removeWhitespace-1]
  isWhitespace(bytes($Char)) == true
  ===>
  removeWhitespace(bytes($Char*1 $Char $Char*2)) 
    = removeWhitespace(bytes($Char*1 $Char*2))

[default-isWhitespace]
  isWhitespace($Bytes) = false 

[isWhitespace-0]
  isWhitespace(bytes(" ")) = true 
