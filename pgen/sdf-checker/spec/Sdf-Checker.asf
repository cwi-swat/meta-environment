equations

  [cs-1'1] InfoMsgTuple := check-sdf(Definition, $ModuleId, false),
           Msg* := get-messages(InfoMsgTuple),
           $StrCon := moduleid2str($ModuleId)
           ====>
           check-sdf(definition Definition, $ModuleId) =
             summary("SdfChecker", $StrCon, [Msg*])

  [cs-1'1] InfoMsgTuple := check-sdf(Definition, Unknown, false),
           Msg*1 := get-messages(InfoMsgTuple),
           Bool := contains-main(Definition, false),
           Msg*2 := main-message(Bool)
           ====>
           check-sdf(definition Definition) =
             summary("SdfChecker", "Unknown", [Msg*2, Msg*1])

  [cs-1'1] Msg*1 := check-labels(Definition1),
           Definition2 := remove-labels(Definition1),
           InfoMsgTuple := check-sorts-and-prods(Definition2),
           Msg*2 := get-messages(InfoMsgTuple),
           Msg*3 := check-sdf-attributes(Definition2),
           Msg*4 := contains-visible-startsymbols(Definition2, $ModuleId)
           ====>
           check-sdf(Definition1, $ModuleId, false) = 
             put-messages(Msg*1, Msg*2, Msg*3, Msg*4, InfoMsgTuple)

  [cs-1'1] Msg*1 := check-labels(Definition1),
           Definition2 := remove-labels(Definition1),
           InfoMsgTuple := check-sorts-and-prods(Definition2),
           Msg*2 := get-messages(InfoMsgTuple),
           Msg*3 := check-sdf-attributes(Definition2)
           ====>
           check-sdf(Definition1, $ModuleId, true) =
             put-messages(Msg*1, Msg*2, Msg*3, InfoMsgTuple)
 
  [cms-1'1] InfoMsgTuple2 := check-module(Module, Definition, InfoMsgTuple1)
            ===============================================================
            check-modules(Module Module*, Definition,  InfoMsgTuple1) =
              check-modules(Module*, Definition, InfoMsgTuple2)

  [cms-1'2] check-modules(, Definition, InfoMsgTuple) = InfoMsgTuple
 
  [pcms1'1] InfoMsgTuple2 := pre-check-module(Module, Definition, InfoMsgTuple1)
            ===================================================================
            pre-check-modules(Module Module*, Definition,  InfoMsgTuple1) =
              pre-check-modules(Module*, Definition, InfoMsgTuple2)

  [pcms1'2] pre-check-modules(, Definition, InfoMsgTuple) = InfoMsgTuple
 
  [ci-1'1] $ModuleName := get-modulename(Import),
           $ModuleId := get-module-id($ModuleName),
           Module := get-module($ModuleId, Definition),
           Module != module-not-available,
           InfoMsgTuple2 := check-module(Module, Definition, InfoMsgTuple1)
           ===============================================================
           check-imports(imports(Import Import*), 
                         Definition, InfoMsgTuple1) =
             check-imports(imports(Import*), Definition, InfoMsgTuple2)
 
  [ci-1'2] $ModuleName := get-modulename(Import),
           $ModuleId := get-module-id($ModuleName),
           get-module($ModuleId, Definition) == module-not-available
           ========================================================
           check-imports(imports(Import Import*), 
                         Definition, InfoMsgTuple) = InfoMsgTuple

  [ci-1'3] check-imports(imports(), Definition, InfoMsgTuple) = InfoMsgTuple
 
  [pci-1'1] $ModuleName := get-modulename(Import),
            $ModuleId := get-module-id($ModuleName),
            Module := get-module($ModuleId, Definition),
            Module != module-not-available,
            InfoMsgTuple2 := pre-check-module(Module, Definition, InfoMsgTuple1)
            ===================================================================
            pre-check-imports(imports(Import Import*), 
                              Definition, InfoMsgTuple1) =
              pre-check-imports(imports(Import*), Definition, InfoMsgTuple2)
 
  [pci-12] $ModuleName := get-modulename(Import),
           $Location := get-location(Import),
           $ModuleId := get-module-id($ModuleName),
           get-module($ModuleId, Definition) == module-not-available,
           ModuleInfoTable1 := get-module-info(InfoMsgTuple1),
           ModuleInfoTable2 := create-module-entry($ModuleId, ModuleInfoTable1), 
           InfoMsgTuple2 :=  put-module-info(ModuleInfoTable2, InfoMsgTuple1),
           Msgs1 := get-messages(InfoMsgTuple2),
           $StrCon := moduleid2str($ModuleId),
           Msgs2 := Msgs1,
make-error("module not available", $StrCon, $Location),
           InfoMsgTuple3 := put-messages(Msgs2, InfoMsgTuple2)
           ===================================================
           pre-check-imports(imports(Import Import*), 
                             Definition, InfoMsgTuple1) = InfoMsgTuple3

  [pci-1'3] pre-check-imports(imports(), Definition, InfoMsgTuple) = InfoMsgTuple


  [csp-1'1] InfoMsgTuple1 := pre-check-modules(Definition, Definition, 
                              create-info-msg-tuple()),
            ModuleInfoTable1 := get-module-info(InfoMsgTuple1),
            ModuleInfoTable2 := mark-modules-as-unchecked(ModuleInfoTable1),
            InfoMsgTuple2 :=  put-module-info(ModuleInfoTable2, InfoMsgTuple1),
            InfoMsgTuple3 := check-modules(Definition, Definition, InfoMsgTuple2),
            InfoMsgTuple4 := post-check-modules(Definition, imports(), InfoMsgTuple3)
            ====>
            check-sorts-and-prods(Definition) = InfoMsgTuple4
	
  [pcm-1'1] $ModuleId := get-module-name(Module),
            ModuleInfoTable := get-module-info(InfoMsgTuple),
            module-is-checked($ModuleId, ModuleInfoTable) == true
            ====================================================
            pre-check-module(Module, Definition, InfoMsgTuple) = InfoMsgTuple
 	
  [pcm-1'2] $ModuleId := get-module-name(Module),
            ModuleInfoTable1 := get-module-info(InfoMsgTuple1),

            module-is-checked($ModuleId, ModuleInfoTable1) == false,
            ModuleInfoTable2 := create-module-entry($ModuleId, ModuleInfoTable1), 

            ImportsSet1 := get-all-imports-transitive(Module, Definition),
            ModuleInfoTable3 := put-imports-info($ModuleId, ImportsSet1, ModuleInfoTable2),
             
            InfoMsgTuple2 :=  put-module-info(ModuleInfoTable3, InfoMsgTuple1),

            ImportsSet2 := get-all-imports(Module),
            InfoMsgTuple3 := 
              pre-check-imports(ImportsSet2, Definition, InfoMsgTuple2),
            InfoMsgTuple4 := pre-check-module-sorts(Module, InfoMsgTuple3)
            ==============================================================
            pre-check-module(Module, Definition, InfoMsgTuple1) = InfoMsgTuple4
	
  [cm-1'1] $ModuleId := get-module-name(Module),
           ModuleInfoTable := get-module-info(InfoMsgTuple),
           module-is-checked($ModuleId, ModuleInfoTable) == true
           ====================================================
           check-module(Module, Definition, InfoMsgTuple) = InfoMsgTuple
	
  [cm-1'2] $ModuleId := get-module-name(Module),
           ModuleInfoTable1 := get-module-info(InfoMsgTuple1),

           module-is-checked($ModuleId, ModuleInfoTable1) == false,
           ModuleInfoTable2 := set-module-as-checked($ModuleId, ModuleInfoTable1), 
           InfoMsgTuple2 :=  put-module-info(ModuleInfoTable2, InfoMsgTuple1),

           ImportsSet := get-all-imports(Module),
           InfoMsgTuple4 := check-imports(ImportsSet, Definition, InfoMsgTuple2),

           InfoMsgTuple5 := check-module-sorts(Module, InfoMsgTuple4),
           InfoMsgTuple6 := check-module-prods(Module, InfoMsgTuple5)
           ====>
           check-module(Module, Definition, InfoMsgTuple1) = InfoMsgTuple6

 [pcm-1'1] post-check-modules(,ImportsSet, InfoMsgTuple) = 
             post-check-modules(ImportsSet, InfoMsgTuple)

 [pcm-1'1] $ModuleId := get-module-name(Module)
           ====>
           post-check-modules(Module Module*, imports(Import*), InfoMsgTuple) = 
              post-check-modules(Module*, imports($ModuleId Import*), InfoMsgTuple)

 [pcm-2'1] DeclaredSymbolPairSet := 
             get-declared-symbols(Import*, get-module-info(InfoMsgTuple1)),
           AliasedSymbolPairSet := 
             get-aliased-symbols(Import*, get-module-info(InfoMsgTuple1)),
           DefinedSymbolPairSet :=
             get-defined-symbols(Import*, get-module-info(InfoMsgTuple1)),

           SymbolsSet1 := 
             get-used-symbols(Import*, get-module-info(InfoMsgTuple1)),
           $Msgs1 := get-messages(InfoMsgTuple1),
           SymbolsSet2 := diff(merge(DeclaredSymbolPairSet, 
                                     AliasedSymbolPairSet),
                               SymbolsSet1),
           $Msgs2 := make-symbol-warnings(SymbolsSet2, "unused sort definitions"),
           SymbolsSet3 := diff(SymbolsSet1,
                               DefinedSymbolPairSet),
           $Msgs3 := make-symbol-errors(SymbolsSet3, "used symbols but not defined in any right-hand side"),

           InfoMsgTuple2 := put-messages($Msgs1, $Msgs2, $Msgs3, InfoMsgTuple1)
           ====>
           post-check-modules(imports(Import*), InfoMsgTuple1) = InfoMsgTuple2

  [cm-1'1] Main == get-module-name(Module)
           ====>
           contains-main(Module, Bool) = true

  [mm-1'1] main-message(true) =

  [mm-1'1] main-message(false) = 
             make-error("Main module not defined", "Main")