equations

  [cd1] check-labels(Definition1) = Msgs1,
        remove-labels(Definition1) = Definition2,
        check-sorts-and-prods(Definition2) = Msgs2,
        check-sdf-attributes(Definition2) = Msgs3
        =========================================
        check-sdf(definition Definition1) =
          Msgs1 ++ Msgs2 ++ Msgs3
 
  [cms1] check-module(Module, Definition, InfoMsgTuple1) = InfoMsgTuple2
         ===============================================================
         check-modules(Module Module*, Definition,  InfoMsgTuple1) =
           check-modules(Module*, Definition, InfoMsgTuple2)

  [cms2] check-modules(, Definition, InfoMsgTuple) = InfoMsgTuple
 
  [cms1] get-modulename(Import) = $ModuleName,
         get-module-id($ModuleName) = $ModuleId,
         get-module($ModuleId, Definition) = Module,
         Module != module-not-available,
         check-module(Module, Definition, InfoMsgTuple1) = InfoMsgTuple2
         ===============================================================
         check-imports(imports(Import Import*), 
                       Definition, InfoMsgTuple1) =
           check-imports(imports(Import*), Definition, InfoMsgTuple2)
 
  [cms1] get-modulename(Import) = $ModuleName,
         get-module-id($ModuleName) = $ModuleId,
         get-module($ModuleId, Definition) = module-not-available,
         get-module-info(InfoMsgTuple1) = ModuleInfoTable1,
         ModuleInfoTable2 = create-module-entry($ModuleId, ModuleInfoTable1), 
         put-module-info(ModuleInfoTable2, InfoMsgTuple1) =  InfoMsgTuple2,
         Msgs1 = get-messages(InfoMsgTuple2),
         Msgs2 = Msgs1 +
module-not-available($ModuleId),
         InfoMsgTuple3 = put-messages(Msgs2, InfoMsgTuple2)
         ===================================================
         check-imports(imports(Import Import*), 
                       Definition, InfoMsgTuple1) = InfoMsgTuple3

  [cms2] check-imports(imports(), Definition, InfoMsgTuple) = InfoMsgTuple


  [cs-1'1] check-modules(Definition, Definition, 
                         create-info-msg-tuple()) = InfoMsgTuple
           =====================================================
           check-sorts-and-prods(Definition) = get-messages(InfoMsgTuple)
	
  [cm1] get-module-name(Module) = $ModuleId,
        get-module-info(InfoMsgTuple) = ModuleInfoTable,
        module-is-checked($ModuleId, ModuleInfoTable) = true
        ====================================================
        check-module(Module, Definition, InfoMsgTuple) = InfoMsgTuple
	
  [cm2] get-module-name(Module) = $ModuleId,
        get-module-info(InfoMsgTuple1) = ModuleInfoTable1,

        module-is-checked($ModuleId, ModuleInfoTable1) = false,
        ModuleInfoTable2 = create-module-entry($ModuleId, ModuleInfoTable1), 
        put-module-info(ModuleInfoTable2, InfoMsgTuple1) =  InfoMsgTuple2,
       
        get-all-imports(Module) = ImportsSet,
        check-imports(ImportsSet, Definition, InfoMsgTuple2) = InfoMsgTuple3,

        check-module-sorts(Module, InfoMsgTuple3) = InfoMsgTuple4,
        check-module-prods(Module, InfoMsgTuple4) = InfoMsgTuple5,
        check-module-constructors(Module, InfoMsgTuple5) = InfoMsgTuple6
        ================================================================
        check-module(Module, Definition, InfoMsgTuple1) = InfoMsgTuple6
