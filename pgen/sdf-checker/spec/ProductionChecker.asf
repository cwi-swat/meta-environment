equations

  [gep-1'1] get-exported-cf-prods(exports Grammar, ProductionsSet) =
              get-cf-prods(Grammar, ProductionsSet)

  [ghp-1'1] get-hidden-cf-prods(exports Grammar, ProductionsSet) =
              get-cf-prods(Grammar, ProductionsSet)

  [gp-1'1] get-cf-prods(context-free syntax Prod*, ProductionsSet) =
              get-prods(Prod*, ProductionsSet)

  [gp-2'1] get-prods(Symbol* -> Symbol Attrs, productions(Prod*2)) =
             productions(Symbol* -> Symbol Prod*2)

  [gp-2'1] get-prods($Literal ( Symbol,* ) -> Symbol Attrs, 
                     productions(Prod*2)) =
             productions($Literal ( Symbol,* ) -> Symbol Prod*2)

  [gpp-1'1] get-prods-in-prios(lexical syntax Prod*, 
                               ProductionsSet) = ProductionsSet

  [gpp-1'1] get-prods-in-prios(context-free syntax Prod*, 
                               ProductionsSet) = ProductionsSet

  [gpp-1'1] get-prods-in-prios(variables Prod*, 
                               ProductionsSet) = ProductionsSet

  [gpp-1'1] get-prods-in-prios(lexical variables Prod*, 
                               ProductionsSet) = ProductionsSet

  [gpp-1'1] get-prods-in-prios(context-free priorities Priorities, 
                               ProductionsSet) =
              get-prods-in-prios(Priorities, ProductionsSet)

  [gpp-1'1] get-prods-in-prios(Symbol* -> Symbol Attrs, productions(Prod*)) =
              productions(Symbol* -> Symbol Prod*)

  [gpp-1'1] get-prods-in-prios($Literal ( Symbol,* ) -> Symbol Attrs,
                               productions(Prod*)) =
              productions($Literal ( Symbol,* ) -> Symbol Prod*)
	
  [cm1] $ModuleId := get-module-name(Module),
        productions(Prod*1) := get-exported-cf-prods(Module, productions()),
        productions(Prod*2) :=  get-hidden-cf-prods(Module, productions()),
        productions(Prod*3) := get-prods-in-prios(Module, productions()),
       
        imports(Import*) := get-all-imports(Module),

        productions(Prod*4) := 
          get-defined-productions(Import*, get-module-info(InfoMsgTuple1)),

        productions(Prod*5) := productions(Prod*1) ++ productions(Prod*4),
        productions(Prod*6) := productions(Prod*2) ++ productions(Prod*5),

        Msgs1 := check-priorities(Module, , $ModuleId, productions(Prod*6)),
        Msgs2 := check-right-hand-sides(Module, , $ModuleId),
        Msgs3 := check-right-hand-side-lexical-prod(Module, , $ModuleId),

        productions(Prod*7) := diff(productions(Prod*3), 
                                    productions(Prod*6)),

        $String1 := moduleid2str($ModuleId),
        $String2 := productions2str(Prod*7),
        $String3 := "used in priorities but undefined in " || $String1,
 
        Msgs4 := get-messages(InfoMsgTuple1),
        Msgs5 := Msgs4, make-warning($String3, $String2),

        InfoMsgTuple3 := put-messages(Msgs5, Msgs1, Msgs2, Msgs3, InfoMsgTuple1),

        ModuleInfoTable1 := get-module-info(InfoMsgTuple3),
        ModuleInfoTable2 := put-production-info($ModuleId, 
                            productions(Prod*5), 
                            ModuleInfoTable1),
        InfoMsgTuple4 :=  put-module-info(ModuleInfoTable2, InfoMsgTuple3)
        =================================================================
        check-module-prods(Module, InfoMsgTuple1) = InfoMsgTuple4

  [cp-1'1] symbols(Symbol) := get-rhs-production(Group, symbols()),
           Msgs2 := check-priorities-given-symbol(Group, Msgs1, $ModuleId, Symbol, ProductionsSet),
           Msgs3 := check-priorities-given-symbol(Group+, Msgs2, $ModuleId, Symbol, ProductionsSet)
           =======================================================================================
           check-priorities(Group > Group+, Msgs1, $ModuleId, ProductionsSet) = Msgs3

  [cp-1'2] symbols(Symbol) := get-rhs-production(Group, symbols()),
           Msgs2 := check-priorities-given-symbol(Group, Msgs1, $ModuleId, Symbol, ProductionsSet)
           ======================================================================================
           check-priorities(Group, Msgs1, $ModuleId, ProductionsSet) = Msgs2

  [cp-1'3] symbols(Symbol) := get-rhs-production(Group1, symbols()),
           Msgs2 := check-priorities-given-symbol(Group1, Msgs1, $ModuleId, Symbol, ProductionsSet),
           Msgs3 := check-priorities-given-symbol(Group2, Msgs2, $ModuleId, Symbol, ProductionsSet)
           =======================================================================================
           check-priorities(Group1 Assoc Group2, Msgs1, $ModuleId, ProductionsSet) = Msgs3 

  [cp-2'1] Symbol1 != Symbol2,
           is-injection-production(Symbol1, Symbol2, ProductionsSet) |
           is-injection-production(Symbol2, Symbol1, ProductionsSet) == false,
           $String1 := moduleid2str($ModuleId),
           $String2 := production2str(Symbol* -> Symbol1 Attrs),
           $String3 := "inconsistent rhs in priorities in " || $String1
           ====>
           check-priorities-given-symbol(Symbol* -> Symbol1 Attrs, 
                                         Msgs, $ModuleId, Symbol2, ProductionsSet) =
             Msgs, make-warning($String3, $String2) 

  [cp-2'2] Symbol1 != Symbol2,
           is-injection-production(Symbol1, Symbol2, ProductionsSet) |
           is-injection-production(Symbol2, Symbol1, ProductionsSet) == false,
           $String1 := moduleid2str($ModuleId),
           $String2 := production2str($Literal (Symbol,*) -> Symbol1 Attrs),
           $String3 := "inconsistent rhs in priorities in " || $String1
           ====>
           check-priorities-given-symbol($Literal (Symbol,*) -> Symbol1 Attrs, 
                                         Msgs, $ModuleId, Symbol2, ProductionsSet) =
             Msgs, make-warning($String3, $String2)

  [grp-2'1] get-rhs-production(Symbol* -> Symbol Attrs, symbols()) = 
              symbols(Symbol)

  [grp-2'2] get-rhs-production($Literal (Symbol,*) -> Symbol Attrs, 
                               symbols()) = symbols(Symbol)

  [iip-1'1] is-injection-production(Symbol1, Symbol2, productions(Prod*1 Symbol1 -> Symbol2 Prod*2)) = true

  [iip-1'1] is-injection-production(Symbol3, Symbol2, productions(Prod*1 Symbol1 -> Symbol3 Prod*2)) == true
            ===============================================================================================
            is-injection-production(Symbol1, Symbol2, productions(Prod*1 Symbol1 -> Symbol3 Prod*2)) = true

  [default-iip-1'2] is-injection-production(Symbol1, Symbol2, ProductionsSet) = false

  [crhs-1'1] $String1 := moduleid2str($ModuleId),
             $String2 := production2str(Symbol* -> $Literal Attrs),
             $String3 := "literal in right-hand-side not allowed in " || $String1
             ====>
             check-right-hand-sides(Symbol* -> $Literal Attrs, Msgs, $ModuleId) =
               Msgs, make-error($String3, $String2)

  [crhs-1'2] $String1 := moduleid2str($ModuleId),
             $String2 := production2str($Literal1 (Symbol,*) -> $Literal2 Attrs),
             $String3 := "literal in right-hand-side not allowed in " || $String1
             ====>
             check-right-hand-sides($Literal1 (Symbol,*) -> $Literal2 Attrs, Msgs, $ModuleId) =
               Msgs, make-error($String3, $String2)

  [crl-1'1] check-right-hand-side-lexical-prod(lexical syntax Prod*, Msgs, $ModuleId) =
              check-right-hand-side-lexical-prod(Prod*, Msgs, $ModuleId)

  [default-crl-1'1] check-right-hand-side-lexical-prod(Grammar, Msgs, $ModuleId) = Msgs

  [crl-1'2] check-right-hand-side-lexical-prod(Symbol* -> $Sort Attrs, Msgs, $ModuleId) = Msgs

  [crl-1'3] check-right-hand-side-lexical-prod(Symbol* -> LAYOUT Attrs, Msgs, $ModuleId) = Msgs

  [default-crl-1'1] $String1 := moduleid2str($ModuleId),
                    $String2 := production2str(Symbol* -> Symbol Attrs),
                    $String3 := "only sort allowed in right-hand-side of lexical-function in " || $String1
                    ====>
                    check-right-hand-side-lexical-prod(Symbol* -> Symbol Attrs, Msgs, $ModuleId) =
                      Msgs, make-error($String3, $String2)

  [crl-1'2] check-right-hand-side-lexical-prod($Literal (Symbol,*) -> $Sort Attrs, Msgs, $ModuleId) = Msgs

  [default-crl-1'2] $String1 := moduleid2str($ModuleId),
                    $String2 := production2str($Literal (Symbol,*) -> Symbol Attrs),
                    $String3 := "only sort allowed in right-hand-side of lexical-function in " || $String1
                    ====>
                    check-right-hand-side-lexical-prod($Literal (Symbol,*) -> Symbol Attrs, Msgs, $ModuleId) =
                      Msgs, make-error($String3, $String2)
 
