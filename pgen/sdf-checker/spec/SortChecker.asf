equations

%% Type checking rules for an (expanded) SDF module

  [tc1] get-exported-declared-symbols(exports $Grammar, SymbolsMsgs, $ModuleId, SymbolsSet) =
       	  get-declared-symbols($Grammar, SymbolsMsgs, $ModuleId, SymbolsSet)

  [tc2] get-hidden-declared-symbols(hiddens $Grammar, SymbolsMsgs, $ModuleId, SymbolsSet) =
       	  get-declared-symbols($Grammar, SymbolsMsgs, $ModuleId, SymbolsSet)

  [tc3] get-declared-symbols(sorts $Symbol*1, SymbolsMsgs, $ModuleId, SymbolsSet) =
       	  get-declared-symbol($Symbol*1, SymbolsMsgs, $ModuleId, SymbolsSet)

  [tc4] get-declared-symbol($Symbol, SymbolsMsgs, $ModuleId, SymbolsSet) =
          add-symbol($Symbol, SymbolsMsgs, $ModuleId, SymbolsSet)

  [as-1'1] contains($Symbol, SymbolsSet2) = true
           =====================================
           add-symbol($Symbol,
                      [SymbolsSet1, $Msgs], $ModuleId, SymbolsSet2) =
             [SymbolsSet1,
              $Msgs + double-declared-symbol($ModuleId, $Symbol)]

  [as-1'2] contains($Symbol, SymbolsSet2) = false,
           contains($Symbol, SymbolsSet1) = true
           =====================================
           add-symbol($Symbol,
                      [SymbolsSet1, $Msgs], $ModuleId, SymbolsSet2) =
             [SymbolsSet1,
              $Msgs + double-declared-symbol($ModuleId, $Symbol)]

  [as-1'2] contains($Symbol, SymbolsSet2) = false,
           contains($Symbol, symbols($Symbol*)) = false
           ============================================
           add-symbol($Symbol, [symbols($Symbol*), $Msgs], $ModuleId, SymbolsSet2) =
                     [symbols($Symbol $Symbol*), $Msgs]

  [geas-1'1] get-exported-aliased-symbols(exports $Grammar, SymbolsMsgs, 
                                          $ModuleId, SymbolsSet) =
       	       get-aliased-symbols($Grammar, SymbolsMsgs, $ModuleId, SymbolsSet)

  [ghas-1'1] get-hidden-aliased-symbols(hiddens $Grammar, SymbolsMsgs, 
                                        $ModuleId, SymbolsSet) =
       	       get-aliased-symbols($Grammar, SymbolsMsgs, $ModuleId, SymbolsSet)

  [gas-1'1] contains($Symbol2, SymbolsSet2) = true
            ======================================
            get-aliased-symbols($Symbol1 -> $Symbol2,
                                [SymbolsSet1, $Msgs], $ModuleId, SymbolsSet2) =
             [SymbolsSet1,
              $Msgs + 
aliased-symbol-already-declared($ModuleId, $Symbol2)]

  [gas-1'2] contains($Symbol2, SymbolsSet2) = false,
            contains($Symbol2, SymbolsSet1) = true
            ======================================
            get-aliased-symbols($Symbol1 -> $Symbol2,
                                [SymbolsSet1, $Msgs], $ModuleId, SymbolsSet2) =
             [SymbolsSet1,
              $Msgs + 
aliased-symbol-already-declared($ModuleId, $Symbol2)]

  [gas-1'2] contains($Symbol2, SymbolsSet2) = false,
            contains($Symbol2, symbols($Symbol*)) = false
            =============================================
            get-aliased-symbols($Symbol1 -> $Symbol2, 
                                [symbols($Symbol*), $Msgs], 
                                $ModuleId, SymbolsSet2) =
                     [symbols($Symbol2 $Symbol*), $Msgs]
  
  [tc2] get-used-symbols(CHAR, SymbolsSet) = SymbolsSet
  
  [tc2] $Sort != CHAR
        =============
        get-used-symbols($Sort, symbols($Symbol*)) = symbols($Sort $Symbol*)

  [tc3] get-defined-symbols(variables Prod*, SymbolsSet) = SymbolsSet

  [tc4] get-defined-symbols($Symbols -> $Symbol $Attributes, 
                            symbols($Symbol*)) =
	  symbols($Symbol $Symbol*)

  [tc5] get-defined-symbols($Literal ( $Symbol,* ) -> $Symbol $Attributes, 
                            symbols($Symbol*)) =
	  symbols($Symbol $Symbol*)

%% Check one module
	
  [cm1] get-module-name($Module) = $ModuleId,
       
        get-all-imports($Module) = ImportsSet,
        get-declared-symbols(ImportsSet, 
                             get-module-info(InfoMsgTuple1)) = 
          symbols(DeclaredSymbol*1),
        get-aliased-symbols(ImportsSet, 
                            get-module-info(InfoMsgTuple1)) = 
          symbols(AliasedSymbol*1),

        get-exported-declared-symbols($Module, [symbols(),{}], $ModuleId, 
                                      symbols(DeclaredSymbol*1)) = 
          [symbols(DeclaredSymbol*2), $Msgs1],
        get-hidden-declared-symbols($Module, [symbols(), {}], $ModuleId, 
                                    symbols(DeclaredSymbol*2
                                            DeclaredSymbol*1)) = 
          [symbols(DeclaredSymbol*3), $Msgs2],

        get-exported-aliased-symbols($Module, [symbols(), {}], $ModuleId, 
                                     symbols(AliasedSymbol*1)) = 
          [symbols(AliasedSymbol*2), $Msgs3],
        get-hidden-aliased-symbols($Module, [symbols(), {}], $ModuleId, 
                                   symbols(AliasedSymbol*2
                                           AliasedSymbol*1)) = 
          [symbols(AliasedSymbol*3), $Msgs4],

        get-used-symbols($Module, symbols()) = symbols($Symbol*3),
        get-defined-symbols($Module, symbols()) = symbols($Symbol*4),

        $Msgs5 = get-messages(InfoMsgTuple1),
        symbols($Symbol*5) = diff(symbols(DeclaredSymbol*3 
                                          DeclaredSymbol*2), 
                                  symbols(AliasedSymbol*3
                                          AliasedSymbol*2
                                          AliasedSymbol*1)),
        $Msgs6 = $Msgs5 + 
undeclared-symbols($ModuleId, diff(symbols($Symbol*3), 
                                   symbols(AliasedSymbol*3
                                           DeclaredSymbol*3 
                                           AliasedSymbol*2
                                           DeclaredSymbol*2
                                           AliasedSymbol*1
                                           DeclaredSymbol*1))) +
sorts-without-production-definitions($ModuleId,  diff(symbols($Symbol*5), 
                                                      symbols($Symbol*4))),
        InfoMsgTuple3 = put-messages($Msgs1 ++ $Msgs2 ++ $Msgs3 ++ 
                                     $Msgs4 ++ $Msgs6, 
                                     InfoMsgTuple1),

        get-module-info(InfoMsgTuple3) = ModuleInfoTable1,
        put-declared-symbols-info($ModuleId, 
                                  symbols(DeclaredSymbol*2 DeclaredSymbol*1), 
                                  ModuleInfoTable1) = ModuleInfoTable2,
        put-aliased-symbols-info($ModuleId, 
                                 symbols(AliasedSymbol*2 AliasedSymbol*1), 
                                 ModuleInfoTable2) = ModuleInfoTable3,
        put-module-info(ModuleInfoTable3, InfoMsgTuple3) =  InfoMsgTuple4
        =================================================================
        check-module-sorts($Module, InfoMsgTuple1) = InfoMsgTuple4