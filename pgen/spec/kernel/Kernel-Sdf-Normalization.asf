%%%% $Id$

equations

%% \noindent
%% An empty list of productions is equivalent to an empty grammar and
%% multiple syntax sections are merged into one.

  [mrg1] syntax                    = (/)
  [mrg2] syntax p*1 <> syntax p*2  = <syntax p*1 p*2, (/)>

%% The normalization function \fun{k} merges productions
%% with the same arguments and result, using the auxiliary
%% function \fun{merge}.

  [k4]
        k[[ _G ]] = syntax lexical-merge(merge(P(_G))) ~P(_G)
%%        k[[ _G ]] = syntax merge(P(_G)) ~P(_G)

  [rmd1]
        merge(p*) = new-merge(,p*)

  [default-rmd2]
        merge() =

  [nrmd0]
        new-merge(p*,) = p*

  [nrmd1]
        prod-contains(p1, p*1) = true
        =============================
        new-merge(p*1, p1 p*2) = new-merge(p*1, p*2)

  [nrmd2]
        prod-contains(p1, p*1) = false,
        attr-collect(p1, p*2) = p2
        ==========================
        new-merge(p*1, p1 p*2) = new-merge(p2 p*1, p*2)

  [pc1]
        p1 = 'a -> _A!1, p2 = 'a -> _A!2
        ================================
        prod-contains(p1, p*1 p2 p*2) = true

  [default-pc2]
        prod-contains(p, p*) = false

  [ac-2'1] attr-collect(p,) = p

  [ac-2'2] p1 = 'a -> _A!1, p2 = 'a -> _A!2, !1 ++ !2 = !3, p3 = 'a -> _A!3
           ================================================================
           attr-collect(p1,p2 p*) = attr-collect(p3,p*)

  [default-ac-2'2] attr-collect(p1,p2 p*) = attr-collect(p1,p*)

  [lm-1'1] lexical-merge(p*) = lex-merge(,p*)

  [default-lm-1'2] lexical-merge() =  

  [lm-2'1] lex-merge(p*,) = p*

  [lm-2'2] p1 = cc1 'a -> <_A-LEX>!,
           lex-lhs-merge(p1, p*2) = (p2,p*3)
           =================================
           lex-merge(p*1, p1 p*2) = lex-merge(p2 p*1, p*3)

  [default-lm-2'3] lex-merge(p*1, p1 p*2) = lex-merge(p1 p*1, p*2)

  [llm-1'1] lex-lhs-merge(p,) = (p,)

  [ac-2'2] p1 = cc1 'a -> <_A-LEX>!1,
           p2 = cc2 'a -> <_A-LEX>!2, 
           !1 ++ !2 = !3, 
           p3 = cc1 \/ cc2 'a -> <_A-LEX>!3
           =============================
           lex-lhs-merge(p1,p2 p*) = lex-lhs-merge(p3,p*)

  [default-ac-2'2] lex-lhs-merge(p1,p*1) = (p3,p*2)
                   ================================
                   lex-lhs-merge(p1,p2 p*1) = (p3,p2 p*2)
