%%%% $Id$

equations

%% We assume that the goto function is composed from the functions closure and shift.

  [GOTO] goto[[_G]](v, l) = closure[[_G]](shift[[_G]](v, l))

%% We assume the goto function to have certain properties: from a given
%% vertex only a finite number of other vertices can be reached and
%% only a finite number of labels give a non-empty vertex as result.
%%
%% The

  [table-0] table[[_G]] = goto-graph[[_G]](init-table[[_G]])

%% A graph is constructed by adding all transitions for all labels from a
%% given vertex.

  [gg0]
        init[[_G]] = v, pt[v] = <pt', z>
        ==========================================================
        goto-graph[[_G]](pt) = goto-graph[[_G]](pt'[init := z], z)

%% If vertex $v$ is empty or already defined in the table nothing has to be done for it.
%% If it is not empty and not defined, first the closure $v'$ of the vertex is computed and then
%% all transitions starting at $v'$ are computed.

  [gg0]
        pt[states][last] <= z = true
        ============================
        goto-graph[[_G]](pt, z) = pt
  [default-gg2]
        pt[states][last] > z = true, vertex[[_G]](pt, z) = pt'
        ======================================================
        goto-graph[[_G]](pt, z) = goto-graph[[_G]](pt', z + 1)

  [vertex1]
        state     = pt[states][z],
        v         = state[kernel],
        v'        = closure[[_G]](v),
        ls        = outgoing[[_G]](v'),
        <pt', gt> = gotos[[_G]](pt, v', ls),
        at        = actions[[_G]](pt', v', gt),
        state'    = state[closure := v'][gotos := gt][actions := at]
        =============================================================
        vertex[[_G]](pt, z) = pt'[states := pt'[states][z := state']]

  [gotos1]
        gotos[[_G]](pt, v, {}) = <pt, new-goto-table>
  [gotos2]
        <pt', gt> = gotos[[_G]](pt, v, {l*}),
        v'        = shift[[_G]](v, l),
        <pt'', z> = pt'[v']
        ===========================================================
        gotos[[_G]](pt, v, {l l*}) = <pt'', gt[pt[labels][l] := z]>

%%%
