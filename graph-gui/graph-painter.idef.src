#ifndef __GRAPH_PAINTER__
#define __GRAPH_PAINTER__

tool graph-painter is { }

process GraphPainter is
let
  T: graph-painter,
  Closable: bool,
  Graph: term,
  GraphId: str,
  Menu: term,
  PopupMenu: list,
  NodeId: term
in
  rec-connect(T?)
  .
  (
    rec-msg(gp-display-graph(GraphId?, Graph?))
    . snd-do(T, display-graph(GraphId, Graph))
  +
    rec-msg(gp-display-graph(GraphId?, Graph?, Closable?))
    . snd-do(T, display-graph(GraphId, Graph, Closable))
  +
    rec-msg(gp-render-graph(GraphId?, Graph?))
    . snd-eval(T, size-graph(GraphId, Graph))
    . rec-value(T, sized-graph(Graph?))
    . snd-msg(gp-rendered-graph(GraphId, Graph))
  +
    rec-msg(gp-select-node(GraphId?, NodeId?))
    . snd-do(T, select-node(GraphId, NodeId))
  +
    rec-event(T, node-selected(GraphId?, NodeId?))
    . snd-note(gp-node-selected(GraphId, NodeId))
    . snd-ack-event(T, node-selected(GraphId, NodeId))
  +
    rec-event(T, request-popup-event(GraphId?, NodeId?))
    . snd-msg(gp-request-popup-event(GraphId, NodeId))
    . snd-ack-event(T, request-popup-event(GraphId, NodeId))
  +
    rec-msg(gp-show-popup(GraphId?, NodeId?, PopupMenu?))
    . snd-do(T, show-popup(GraphId, NodeId, PopupMenu))
  +
    rec-event(T, popup-menu-event(GraphId?, NodeId?, Menu?))
    . snd-msg(gp-menu-selected(GraphId, NodeId, Menu))
    . snd-ack-event(T, popup-menu-event(GraphId, NodeId, Menu))
  )
  *
  rec-disconnect(T)
endlet

process GP-AddListener is
  subscribe(gp-node-selected(<str>,<term>))

process GP-RequestPopup(GraphId: str?, NodeId: term?) is
  rec-msg(gp-request-popup-event(GraphId?, NodeId?))

process GP-ShowPopup(GraphId: str, NodeId: term, PopupMenu: list) is
  snd-msg(gp-show-popup(GraphId, NodeId, PopupMenu))

process GP-PopupEvent(GraphId: str?, NodeId: term?, MenuItem: term?) is
  rec-msg(gp-menu-selected(GraphId?, NodeId?, MenuItem?))

process StartGraphPanel is
  snd-msg(load-jar("file://__PKGDATA__/graph-painter.jar", "__PKGDATA__/prefuse.jar:__PKGDATA__/gui-util.jar:__EXTERNAL_JARS__"))

toolbus(GraphPainter)

#endif /* __GRAPH_PAINTER__ */
