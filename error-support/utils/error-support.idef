/* $Id$ */

tool error-support is {
  command = "error-support"
}

process ErrorSupport is
let
  Description : str,
  ES : error-support,
  Error : term,
  Errors : list,
  FileName : str,
  Id : str,
  Location : term,
  Offset : int,
  Producer : str,
  Summary : term
in
  execute(error-support, ES?) .
  (
    rec-msg(convert-feedback(Summary?)) .
    snd-eval(ES, lower-summary(Summary)) .
    rec-value(ES, lowered-summary(Summary?)) .
    snd-msg(converted-feedback(Summary))

  +
    rec-msg(get-feedback-producer(Summary?)) .
    snd-eval(ES, get-summary-producer(Summary)) .
    rec-value(ES, producer(Producer?)) .
    snd-msg(producer(Producer))
  +
    rec-msg(get-feedback-identification(Summary?)) .
    snd-eval(ES, get-summary-id(Summary)) .
    rec-value(ES, id(Id?)) .
    snd-msg(identification(Id))
  +
    rec-msg(get-feedback-subjects(Summary?)) .
    snd-eval(ES, get-summary-errors(Summary)) .
    rec-value(ES, errors(Errors?)) .
    snd-msg(subjects(Errors))
  +
    rec-msg(get-summary-first-feedback(Summary?)) .
    snd-eval(ES, get-summary-first-error(Summary)) .
    rec-value(ES, error(Error?)) .
    snd-msg(summary-first-feedback(Error))

  +
    rec-msg(get-feedback-first-location(Error?)) .
    snd-eval(ES, get-error-first-subject-location(Error)) .
    rec-value(ES, location(Location?)) .
    snd-msg(feedback-first-location(Location))
  +
    rec-msg(get-location-filename(Location?)) .
    snd-eval(ES, get-location-filename(Location)) .
    rec-value(ES, filename(FileName?)) .
    snd-msg(location-filename(FileName))
  +
    rec-msg(get-location-offset(Location?)) .
    snd-eval(ES, get-location-area-offset(Location)) .
    (
      rec-value(ES, offset(Offset?)) .
      snd-msg(location-offset(Offset))
    +
      rec-value(ES, no-offset) .
      snd-msg(no-location-offset)
    )
  +
    rec-msg(display-feedback(Summary?)) .
    snd-do(ES, display-summary(Summary))
  ) * delta
endlet

toolbus(ErrorSupport)
