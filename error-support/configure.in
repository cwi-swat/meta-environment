dnl $Id$

dnl Process this file with autoconf to produce a configure script.
AC_INIT
AC_CONFIG_SRCDIR([configure.in])
AM_INIT_AUTOMAKE(error-support, 0.1)

ACLOCAL="${ACLOCAL} -I ."

dnl NOTA BENE: This package is not used in all meta-environment packages
dnl            YET. But the plan is to use it as widely as possible.
dnl            Do not add any dependencies on other packages here! 
dnl            That would exclude such a package from using the ErrorAPI,
dnl            because it would introduce a circular dependency.


dnl {{{  Which compiler to use?
AC_PROG_CC

dnl }}}
dnl {{{  Handle --with-cflags option
AC_ARG_WITH(cflags,
   [  --with-cflags=CFLAGS    standard options of c compiler],
   CFLAGS=${withval},
   if test "a${GCC}" = "ayes"; then
     CFLAGS="-Wall -g -O2"
   fi;
)
AC_SUBST(CFLAGS)

dnl }}}
dnl {{{  Require ATerm library
AC_PACKAGE_REQUIRE(aterm, baffle,
   [  --with-aterm=DIR        ATerm-library is in DIR])
AC_SUBST(ATERM)

dnl }}}
dnl {{{  Require ToolBus library

dnl Require ToolBus library
AC_PACKAGE_REQUIRE(toolbuslib, toolbuslib-config,
   [  --with-toolbuslib=DIR   ToolBus library is in DIR])
AC_SUBST(TOOLBUSLIB)

dnl }}}

dnl {{{  Our own version of libdir whose value is set dynamically
AC_SUBST(INSTLIBDIR)
INSTLIBDIR="$libdir"

dnl }}}
dnl {{{  Require socket/gethostname

NOSOCKLIBS=$LIBS
dnl {{{  check for socket in -lsun and -lsocket
SOCKET_FOUND=no
SOCKLIBS=
AC_CHECK_LIB(sun,socket,[SOCKET_FOUND=yes;SOCKLIBS="-lsun"])
if test ${SOCKET_FOUND} = no; then
  AC_CHECK_LIB(socket, socket,[SOCKET_FOUND=yes;SOCKLIBS="-lsocket"])
fi

dnl }}}
dnl {{{  check for gethostname in -lsun and -lsocket

GHN_FOUND=no
AC_CHECK_LIB(sun,gethostname,[GHN_FOUND=yes])
if test ${GHN_FOUND} = no; then
  AC_CHECK_LIB(nsl,gethostname,[GHN_FOUND=yes;SOCKLIBS="$SOCKLIBS -lnsl"])
fi

dnl }}}
AC_SUBST(SOCKLIBS)
LIBS=$NOSOCKLIBS

dnl }}}
dnl {{{  Is ranlib needed on this system?

AC_PROG_RANLIB
AC_PATH_PROG(AR,ar,/usr/bin/ar)

dnl }}}
dnl {{{  What commands are needed to set the variable MAKE in Makefiles?
AC_PROG_MAKE_SET([])

dnl }}}

dnl {{{  Which files need updating?
AC_CONFIG_FILES([Makefile
  lib/Makefile
  doc/Makefile
  utils/Makefile
  utils/lift-error/Makefile
  utils/lower-error/Makefile
])
AC_OUTPUT

dnl }}}
