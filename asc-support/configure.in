dnl $Id$

dnl Process this file with autoconf to produce a configure script.
AC_INIT

dnl Product name and version number
PRODUCT=asc-support
VERSION=0.02

AC_SUBST(PRODUCT)
AC_SUBST(VERSION)

dnl Default locations of other components
TOOLBUS='${prefix}'
ATERM='${prefix}'
META='${top_srcdir}/..'

AC_DEFUN(AC_PROG_MAKEDEPEND, [
AC_CHECK_PROGS(MAKEDEP, gccmakedep makedepend, no)
AC_REQUIRE([AC_PROG_CC])
AC_MSG_CHECKING([how to generate dependencies])
AC_CACHE_VAL(ac_cv_prog_MAKEDEPEND,
if test "${GCC}" = "yes" ; then
	ac_cv_prog_MAKEDEPEND="${CC-cc} -MM"
else
	ac_cv_prog_MAKEDEPEND="$MAKEDEP -f-"
fi
)
MAKEDEPEND=$ac_cv_prog_MAKEDEPEND
AC_MSG_RESULT([${ac_cv_prog_MAKEDEPEND}])
AC_SUBST([MAKEDEPEND])
])dnl

dnl Installation scripts
INSTALL='${top_srcdir}/install-sh'
INSTALL_PROG='${top_srcdir}/install-prog-sh'
INSTALL_DATA='${top_srcdir}/install-data-sh'
UNINSTALL='${top_srcdir}/uninstall-sh'
AC_SUBST(INSTALL)
AC_SUBST(INSTALL_PROG)
AC_SUBST(INSTALL_DATA)
AC_SUBST(UNINSTALL)

dnl Is ranlib needed on this system?
AC_PROG_RANLIB
AC_PATH_PROG(AR,ar,/usr/bin/ar)

dnl Which compiler to use?
AC_PROG_CC
AC_PROG_MAKEDEPEND

dnl Where to locate a bourne-compatible shell?
SHELL='/bin/sh'
AC_SUBST(SHELL)

dnl What commands are needed to set the variable MAKE in Makefiles?
AC_SET_MAKE

dnl Which CFLAGS to use?
if test "${GCC}" = "yes" ; then
	CFLAGS="-g -Wall -O4"
fi
AC_SUBST(CFLAGS)
AC_SUBST(DEFS)
AC_SUBST(INCLUDES)

dnl Handle --with-aterm=... argument to configure
AC_ARG_WITH(aterm,
[  --with-aterm=DIR        ATerm-library is in DIR],
ATERM=${withval},)
AC_SUBST(ATERM)
AC_ARG_WITH(aterm-bin,
[  --with-aterm-bin=DIR    ATerm executables are in DIR],
ATERMBIN=${withval},ATERMBIN=${ATERM}/bin)
AC_SUBST(ATERMBIN)
AC_ARG_WITH(aterm-lib,
[  --with-aterm-lib=DIR    ATerm libraries are in DIR],
ATERMLIB=${withval},ATERMLIB=${ATERM}/lib)
AC_SUBST(ATERMLIB)
AC_ARG_WITH(aterm-inc,
[  --with-aterm-inc=DIR    ATerm header files are in DIR],
ATERMINC=${withval},ATERMINC=${ATERM}/include)
AC_SUBST(ATERMINC)

dnl Handle --with-toolbus[-...]=... arguments to configure
AC_ARG_WITH(toolbus,
[  --with-toolbus=DIR      ToolBus distribution is in DIR],
TOOLBUS=${withval},)
AC_SUBST(TOOLBUS)
AC_ARG_WITH(toolbus-bin,
[  --with-toolbus-bin=DIR  ToolBus executables are in DIR],
TOOLBUSBIN=${withval},TOOLBUSBIN=${TOOLBUS}/bin)
AC_SUBST(TOOLBUSBIN)
AC_ARG_WITH(toolbus-lib,
[  --with-toolbus-lib=DIR  ToolBus libraries are in DIR],
TOOLBUSLIB=${withval},TOOLBUSLIB=${TOOLBUS}/lib)
AC_SUBST(TOOLBUSLIB)
AC_ARG_WITH(toolbus-inc,
[  --with-toolbus-inc=DIR  ToolBus header files are in DIR],
TOOLBUSINC=${withval},TOOLBUSINC=${TOOLBUS}/include)
AC_SUBST(TOOLBUSINC)

dnl Handle --with-meta=... argument to configure
AC_ARG_WITH(meta,
[  --with-meta=DIR         Meta distribution is in DIR],
META=${withval})
AC_SUBST(META)
AC_ARG_WITH(meta-bin,
[  --with-meta-bin=DIR     Meta-Environment executables are in DIR],
METABIN=${withval},METABIN=${META}/bin)
AC_SUBST(METABIN)
AC_ARG_WITH(meta-lib,
[  --with-meta-lib=DIR     Meta-Environment libraries are in DIR],
METALIB=${withval},METALIB=${META}/lib)
AC_SUBST(METALIB)
AC_ARG_WITH(meta-inc,
[  --with-meta-inc=DIR     Meta-Environment header files are in DIR],
METAINC=${withval},METAINC=${META}/include)
AC_SUBST(METAINC)

dnl Make a good guess about idef2tif..
AC_PATH_PROG(IDEF2TIF, idef2tif, no)
if test ${IDEF2TIF} = no; then
  IDEF2TIF="${META}/utils/idef2tif"
fi

dnl Determine which libraries to use for programs using sockets
NOSOCKLIBS=$LIBS
dnl first, the socket functions
SOCKET_FOUND=no
SOCKLIBS=
AC_CHECK_LIB(sun,socket,[SOCKET_FOUND=yes;SOCKLIBS="-lsun"])
if test ${SOCKET_FOUND} = no; then
  AC_CHECK_LIB(socket,socket,[SOCKET_FOUND=yes;SOCKLIBS="-lsocket"])
  if test ${SOCKET_FOUND} = no; then
    AC_CHECK_LIB(bsd,socket,[SOCKET_FOUND=yes;SOCKLIBS="-lbsd";DEFS="$DEFS -D_BSD=43"])
  fi
fi

dnl then gethostname
GHN_FOUND=no
AC_CHECK_LIB(sun,gethostname,[GHN_FOUND=yes])
if test ${GHN_FOUND} = no; then
  AC_CHECK_LIB(nsl,gethostname,[GHN_FOUND=yes;SOCKLIBS="$SOCKLIBS -lnsl"])
fi

LIBS=$NOSOCKLIBS
AC_CHECK_LIB(dl, dlopen)

dnl Checks for header files.
AC_CHECK_HEADER(getopt.h)

AC_SUBST(SOCKLIBS)

dnl Which files need updating?
AC_OUTPUT(
    Makefile
  , chmod +x install-prog-sh install-data-sh
)
