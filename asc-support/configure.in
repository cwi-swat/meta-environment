dnl $Id$

dnl Process this file with autoconf to produce a configure script.
AC_INIT(configure.in)
AM_INIT_AUTOMAKE(asc-support, 1.7.1)

ACLOCAL="${ACLOCAL} -I ."

dnl Which compiler to use?
AC_PROG_CC

dnl Handle --with-cflags option
AC_ARG_WITH(cflags,
   [  --with-cflags=CFLAGS    standard options of c compiler],
   CFLAGS=${withval},
   if test "a${GCC}" = "ayes"; then
     CFLAGS="-Wall -g -O2"
   fi;
)
AC_SUBST(CFLAGS)

dnl Check for required packages and perform version check when needed.

dnl Require ATerm library
AC_PACKAGE_REQUIRE(aterm, baffle,
   [  --with-aterm=DIR        ATerm-library is in DIR])
AC_SUBST(ATERM)

dnl Require pt-support package
AC_PACKAGE_REQUIRE(pt-support, unparsePT,
   [  --with-pt-support=DIR   pt-support is in DIR])
AC_SUBST(PT_SUPPORT)

dnl Require asf-support package
AC_PACKAGE_REQUIRE(asf-support, asf-support-config,
   [  --with-asf-support=DIR  asf-support is in DIR])
AC_SUBST(ASF_SUPPORT)


dnl Require ToolBus library
AC_PACKAGE_REQUIRE(toolbuslib, toolbuslib-config,
   [  --with-toolbuslib=DIR   ToolBus library is in DIR])
AC_SUBST(TOOLBUSLIB)

dnl {{{  Require socket/gethostname

NOSOCKLIBS=$LIBS
dnl {{{  check for socket in -lsun and -lsocket
SOCKET_FOUND=no
SOCKLIBS=
AC_CHECK_LIB(sun,socket,[SOCKET_FOUND=yes;SOCKLIBS="-lsun"])
if test ${SOCKET_FOUND} = no; then
  AC_CHECK_LIB(socket, socket,[SOCKET_FOUND=yes;SOCKLIBS="-lsocket"])
fi

dnl }}}
dnl {{{  check for gethostname in -lsun and -lsocket

GHN_FOUND=no
AC_CHECK_LIB(sun,gethostname,[GHN_FOUND=yes])
if test ${GHN_FOUND} = no; then
  AC_CHECK_LIB(nsl,gethostname,[GHN_FOUND=yes;SOCKLIBS="$SOCKLIBS -lnsl"])
fi

dnl }}}
AC_SUBST(SOCKLIBS)
LIBS=$NOSOCKLIBS

dnl }}}

dnl Our own version of libdir whose value is set dynamically
AC_SUBST(INSTLIBDIR)
INSTLIBDIR="$libdir"

dnl Is ranlib needed on this system?
AC_PROG_RANLIB
AC_PATH_PROG(AR,ar,/usr/bin/ar)

dnl What commands are needed to set the variable MAKE in Makefiles?
AC_SET_MAKE

dnl Which files need updating?
AC_OUTPUT(
  Makefile
  lib/Makefile
  lib/tb/Makefile
  utils/Makefile
  builtins/Makefile
  builtins/utils/Makefile
  builtins/lib/Makefile
)
