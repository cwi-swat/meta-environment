dnl $Id$

dnl Process this file with autoconf to produce a configure script.
AC_INIT(Makefile.in)
AM_INIT_AUTOMAKE(asc-support, 0.2)

dnl Default locations of used components
AC_PATH_PROG(TOOLBUS, toolbus, no)
if test "${TOOLBUS}" = "no"; then
	TOOLBUS='${prefix}'
else
	TOOLBUS=`dirname \`dirname ${TOOLBUS}\``
fi

AC_PATH_PROG(ATERM, tifstoc, no)
if test "${ATERM}" = "no"; then
	ATERM='${prefix}'
else
	ATERM=`dirname \`dirname ${ATERM}\``
fi

AC_REQUIRE([AC_PROG_CC])

dnl Is ranlib needed on this system?
AC_PROG_RANLIB
AC_PATH_PROG(AR,ar,/usr/bin/ar)

dnl Which compiler to use?
AC_PROG_CC

dnl What commands are needed to set the variable MAKE in Makefiles?
AC_SET_MAKE

dnl Handle --with-aterm=... argument to configure
AC_ARG_WITH(aterm,
   [  --with-aterm=DIR        ATerm-library is in DIR], ATERM=${withval},)
AC_SUBST(ATERM)

AC_ARG_WITH(aterm-bin,
   [  --with-aterm-bin=DIR    ATerm executables are in DIR],
   ATERMBIN=${withval},ATERMBIN=${ATERM}/bin)
AC_SUBST(ATERMBIN)

AC_ARG_WITH(aterm-lib,
   [  --with-aterm-lib=DIR    ATerm libraries are in DIR],
   ATERMLIB=${withval},ATERMLIB=${ATERM}/lib)
AC_SUBST(ATERMLIB)

AC_ARG_WITH(aterm-inc,
   [  --with-aterm-inc=DIR    ATerm header files are in DIR],
   ATERMINC=${withval},ATERMINC=${ATERM}/include)
AC_SUBST(ATERMINC)

dnl Handle --with-toolbus[-...]=... arguments to configure
AC_ARG_WITH(toolbus,
   [  --with-toolbus=DIR      ToolBus distribution is in DIR],
   TOOLBUS=${withval},)
AC_SUBST(TOOLBUS)

AC_ARG_WITH(toolbus-bin,
   [  --with-toolbus-bin=DIR  ToolBus executables are in DIR],
   TOOLBUSBIN=${withval},TOOLBUSBIN=${TOOLBUS}/bin)
AC_SUBST(TOOLBUSBIN)

dnl Determine which libraries to use for programs using sockets
NOSOCKLIBS=$LIBS
dnl first, the socket functions
SOCKET_FOUND=no
SOCKLIBS=
AC_CHECK_LIB(sun,socket,[SOCKET_FOUND=yes;SOCKLIBS="-lsun"])
if test ${SOCKET_FOUND} = no; then
  AC_CHECK_LIB(socket,socket,[SOCKET_FOUND=yes;SOCKLIBS="-lsocket"])
  if test ${SOCKET_FOUND} = no; then
    AC_CHECK_LIB(bsd,socket,[SOCKET_FOUND=yes;SOCKLIBS="-lbsd";DEFS="$DEFS -D_BSD=43"])
  fi
fi

dnl then gethostname
GHN_FOUND=no
AC_CHECK_LIB(sun,gethostname,[GHN_FOUND=yes])
if test ${GHN_FOUND} = no; then
  AC_CHECK_LIB(nsl,gethostname,[GHN_FOUND=yes;SOCKLIBS="$SOCKLIBS -lnsl"])
fi

LIBS=$NOSOCKLIBS
AC_CHECK_LIB(dl, dlopen)

AC_SUBST(SOCKLIBS)

dnl Default values
AC_PATH_PROG(CC_GCC,gcc,true)
CFLAGS_GCC="-Wall -O4"
LFLAGS_GCC=""

CC_CC=${CC}
CFLAGS_CC="-O"
LFLAGS_CC=""

CC_DBG=${CC_GCC}
CFLAGS_DBG="-Wall -g"
LFLAGS_DBG="-g"

CC_PROF=${CC_GCC}
CFLAGS_PROF="-Wall -pg"
LFLAGS_PROF="-pg"

AC_SUBST(CC_GCC)
AC_SUBST(CC_CC)
AC_SUBST(CC_DBG)
AC_SUBST(CC_PROF)

AC_SUBST(CFLAGS_CC)
AC_SUBST(CFLAGS_GCC)
AC_SUBST(CFLAGS_DBG)
AC_SUBST(CFLAGS_PROF)
AC_SUBST(CFLAGS_SHARED)
AC_SUBST(CFLAGS_CC64)

AC_SUBST(DEFS_CC)
AC_SUBST(DEFS_GCC)
AC_SUBST(DEFS_DBG)
AC_SUBST(DEFS_PROF)

AC_SUBST(INCL_CC)
AC_SUBST(INCL_GCC)
AC_SUBST(INCL_DBG)
AC_SUBST(INCL_PROF)

AC_SUBST(LFLAGS_CC)
AC_SUBST(LFLAGS_GCC)
AC_SUBST(LFLAGS_DBG)
AC_SUBST(LFLAGS_PROF)

dnl Which files need updating?
AC_OUTPUT(
    Makefile
)
