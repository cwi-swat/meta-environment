equations

%%%%%%%%%%%%%%%%%%%%%%%%%
%% Exported functionality
%%%%%%%%%%%%%%%%%%%%%%%%%

%% This is a fixed point calculation, the set of reachable sorts is 
%% recalculated while the set is growing. 

[c-0] close_sorts(Productions, $SymbolSet1, $SymbolSet1) = $SymbolSet2,
      $SymbolSet2 = $SymbolSet1  
      ==============================================
      close_sorts_set(Productions, $SymbolSet1) = $SymbolSet1

[c-1] close_sorts(Productions, $SymbolSet1, $SymbolSet1) = $SymbolSet2,
      $SymbolSet2  !=  $SymbolSet1,
      close_sorts_set(Productions, $SymbolSet2) = $SymbolSet3
      =================================================
      close_sorts_set(Productions, $SymbolSet1) = $SymbolSet3

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% hiddens part of reachability closure calculation
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

[css-0]  close_sorts(Productions, [], $SymbolSet) = $SymbolSet

[css-1]  close_sort(Productions, $Symbol, $SymbolSet) = $SymbolSet2,
        close_sorts(Productions, [ $Symbol*0 ], $SymbolSet2) = $SymbolSet3
        ========================================================
        close_sorts(Productions, [ $Symbol $Symbol*0 ], $SymbolSet) = $SymbolSet3


[cs-0] close_sort(Productions, $Symbol, $SymbolSet) =
        close_sort_prods(Productions, $Symbol, $SymbolSet)

[csps-0]  close_sort_prods(,$Symbol,$SymbolSet) = $SymbolSet

[csps-1]  close_sort_prod(Production, $Symbol, $SymbolSet) = $SymbolSet2,
          close_sort_prods(Production*, $Symbol, $SymbolSet2) = $SymbolSet3
          ========================================================
          close_sort_prods(Production Production*, $Symbol, $SymbolSet) = $SymbolSet3

[csp-0]  close_sort_prod($Symbol*0 -> $Symbol Attributes, $Symbol, $SymbolSet) =
          add sorts in $Symbol*0 to $SymbolSet

[default-csp]  close_sort_prod(Production, $Symbol, $SymbolSet) = $SymbolSet 

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% Utility functions on SymbolSets
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

[at-0] add $Symbol to [ $Symbol*1 $Symbol $Symbol*2 ] = [ $Symbol*1 $Symbol $Symbol*2 ]

[default-at] add $Symbol to [ $Symbol*0 ] = [ $Symbol $Symbol*0 ]

[ats-0] add [] to $SymbolSet = $SymbolSet

[ats-1] add $Symbol to $SymbolSet = $SymbolSet2,
        add [ $Symbol*0 ] to $SymbolSet2 = $SymbolSet3
        ====================================
        add [ $Symbol $Symbol*0 ] to $SymbolSet = $SymbolSet3

[si-1] sorts in $Symbol*0 $Sort $Symbol*2 = add $Sort to sorts in $Symbol*2

[si-2] sorts in $Symbol*0 $Sort * $Symbol*2 = add [ $Sort $Sort * ] to sorts in $Symbol*0 $Symbol*2

[si-3] sorts in $Symbol*0 $Sort + $Symbol*2 = add [ $Sort $Sort + ] to sorts in $Symbol*0 $Symbol*2

[si-4] sorts in $Symbol*0 {$Sort $Symbol}* $Symbol*2 = 
        add [$Sort {$Sort $Symbol}*] to sorts in $Symbol*0 $Symbol*2

[si-5] sorts in $Symbol*0 {$Sort $Symbol}+ $Symbol*2 = 
        add [$Sort {$Sort $Symbol}+] to sorts in $Symbol*0 $Symbol*2

[default-si] sorts in $Symbol*0 = [ ]

