/* $Id$ */

process Asfe is
let
  Eval       : asfe,
  ModuleName : str,
  Term       : term,
  Equations  : term,
  NormalForm : term,
  ErrorList  : list,
  Id         : term,
  Tide       : term,
  ParseTable : term,
  Tests : term
in
  Id := process-id .
  Tide := off .
  (
    rec-msg(debugging(Tide?))  /* 'on' or 'off' */
  +
    (
      rec-msg(rewrite(ModuleName?, Equations?, Term?)) .
      ParseTable := none
    +
      rec-msg(rewrite(ModuleName?, Equations?, ParseTable?, Term?))
    ) .
    execute(asfe, Eval?) .
    snd-eval(Eval, interpret(ModuleName, Equations, ParseTable, Term, Tide)) .
    (
      rec-value(Eval, rewrite-result(NormalForm?)) .
      snd-msg(rewrite-result(NormalForm))
    +
      rec-value(Eval, rewrite-errors(ErrorList?)) .
      snd-msg(rewrite-errors(ErrorList))
    ).
    snd-terminate(Eval,"done").
    %% Equations and terms can be big, so we clear the variables here
    Equations := none.
    Term      := none 
  +
    rec-msg(run-tests(ModuleName?, Equations?, Tests?)) .
    execute(asfe, Eval?) .
    snd-eval(Eval, run-tests(ModuleName, Equations, Tests, Tide)) .
    (
      rec-value(Eval, test-results(ErrorList?)) .
      snd-msg(test-results(ErrorList))
    +
      rec-value(Eval, rewrite-errors(ErrorList?)) .
      snd-msg(rewrite-errors(ErrorList))
    ) .
    Equations := none .
    Tests := none
  ) *
  delta
endlet

tool asfe is { command = "asfe" }
