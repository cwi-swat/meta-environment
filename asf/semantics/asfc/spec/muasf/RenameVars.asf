equations

  [rir-1'1] rename-in-rules() =

  [rir-1'2] Rule2 := rename-in-rule(Rule1,[1,]),
            Rule*2 := rename-in-rules(Rule*1)
            ================================
            rename-in-rules(Rule1;Rule*1) = Rule2;Rule*2

  [rir-2'1] [Term3,Ivvarenv2] := rename-in-term(Term1,Ivvarenv1),
            [Term4,Ivvarenv3] := rename-in-term(Term2,Ivvarenv2)
            ===================================================
            rename-in-rule(Term1 = Term2,Ivvarenv1) = Term3 = Term4

  [rir-2'2] [Term3,Ivvarenv2] := rename-in-term(Term1,Ivvarenv1),
            [Term4,Ivvarenv3] := rename-in-term(Term2,Ivvarenv2)
            ===================================================
            rename-in-rule(default:Term1 = Term2,Ivvarenv1) = default:Term3 = Term4

  [rir-2'3] [Term3,Ivvarenv2] := rename-in-term(Term1,Ivvarenv1),
            [Cond+2,Ivvarenv3] := rename-in-conds(Cond+1,Ivvarenv2),
            [Term4,Ivvarenv4] := rename-in-term(Term2,Ivvarenv3)
            ===================================================
            rename-in-rule(Cond+1 ==> Term1 = Term2,Ivvarenv1) = Cond+2 ==> Term3 = Term4

  [rir-2'4] [Term3,Ivvarenv2] := rename-in-term(Term1,Ivvarenv1),
            [Cond+2,Ivvarenv3] := rename-in-conds(Cond+1,Ivvarenv2),
            [Term4,Ivvarenv4] := rename-in-term(Term2,Ivvarenv3)
            ===================================================
            rename-in-rule(default:Cond+1 ==> Term1 = Term2,Ivvarenv1) =
              default:Cond+2 ==> Term3 = Term4

  [rit-1'1] [Varid2,Ivvarenv2] := vv-enter(Varid1,Ivvarenv1)
            ===============================================
            rename-in-term(Varid1,Ivvarenv1) = [Varid2,Ivvarenv2]

  [default-rit-1'2] rename-in-term(Term,Ivvarenv) = [Term,Ivvarenv]

  [rit-1'3] [Term+2,Ivvarenv2] := rename-in-terms(Term+1,Ivvarenv1)
            ======================================================
            rename-in-term(Funid(Term+1),Ivvarenv1) = [Funid(Term+2),Ivvarenv2]

  [rit-2'1] [Term2,Ivvarenv2] := rename-in-term(Term1,Ivvarenv1)
            ===================================================
            rename-in-terms(Term1,Ivvarenv1) = [Term2,Ivvarenv2]

  [rit-2'2] [Term2,Ivvarenv2] := rename-in-term(Term1,Ivvarenv1),
            [Term+2,Ivvarenv3] := rename-in-terms(Term+1,Ivvarenv2)
            ======================================================
            rename-in-terms(Term1,Term+1,Ivvarenv1) = [Term2,Term+2,Ivvarenv3]

  [cit-1'1] [Cond2,Ivvarenv2] := rename-in-cond(Cond1,Ivvarenv1)
            ===================================================
            rename-in-conds(Cond1,Ivvarenv1) = [Cond2,Ivvarenv2]

  [cit-1'2] [Cond2,Ivvarenv2] := rename-in-cond(Cond1,Ivvarenv1),
            [Cond+2,Ivvarenv3] := rename-in-conds(Cond+1,Ivvarenv2)
            ======================================================
            rename-in-conds(Cond1 & Cond+1,Ivvarenv1) = [Cond2 & Cond+2,Ivvarenv3]

  [cit-2'1] [Term3,Ivvarenv2] := rename-in-term(Term1,Ivvarenv1),
            [Term4,Ivvarenv3] := rename-in-term(Term2,Ivvarenv2)
            ===================================================
            rename-in-cond(Term1 == Term2,Ivvarenv1) = [Term3 == Term4,Ivvarenv3]

  [cit-2'2] [Term3,Ivvarenv2] := rename-in-term(Term1,Ivvarenv1),
            [Term4,Ivvarenv3] := rename-in-term(Term2,Ivvarenv2)
            ===================================================
            rename-in-cond(Term1 != Term2,Ivvarenv1) = [Term3 != Term4,Ivvarenv3]

  [cit-2'3] [Term3,Ivvarenv2] := rename-in-term(Term1,Ivvarenv1),
            [Term4,Ivvarenv3] := rename-in-term(Term2,Ivvarenv2)
            ===================================================
            rename-in-cond(Term1 =: Term2,Ivvarenv1) = [Term3 =: Term4,Ivvarenv3]
