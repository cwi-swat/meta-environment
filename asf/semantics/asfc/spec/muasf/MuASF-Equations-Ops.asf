equations

%% An auxilary function to construct the rule section of a
%% \muasf\ specification.
 [cnro-1'1] create-new-rulesopt() =

 [cnro-1'2] create-new-rulesopt(Rule+) = rules Rule+

%% An auxilary function to add new created rules
%% to the (existing) rule option.
 [ats-1'1] addto-rulesopt(,) =

 [ats-2'3] addto-rulesopt(rules,) =

 [ats-1'2] addto-rulesopt(,Rule+) = rules Rule+

 [ats-1'2] addto-rulesopt(rules,Rule+) = rules Rule+

 [ats-1'3] addto-rulesopt(rules Rule+,) = rules Rule+

 [ats-1'4] addto-rulesopt(rules Rule+1,Rule+2) =
             rules Rule+1;Rule+2

%% An auxilary function to get the function name of
%% a rewrite rule.
 [gfi-1'1] get-funid(Funid) = Funid

 [gfi-1'1] get-funid(Funid(Term+)) = Funid

 [gfi-2'1] get-funid(Term1 = Term2) = get-funid(Term1)

 [gfi-2'2] get-funid(Cond+ ==> Term1 = Term2) = get-funid(Term1)

 [gfi-2'3] get-funid(default:Term1 = Term2) = get-funid(Term1)

 [gfi-2'4] get-funid(default:Cond+ ==> Term1 = Term2) = get-funid(Term1)

  [gfi-1'1] get-funid(Term) = Funid1,
            is-trans-list-funid(Funid1) = true,
            remove-trans-from-list-id(Funid1) = Funid2,
            Funid2 = funid(""""{" Char* "}" """)
            ====================================
            get-funid(Term,Namepair*) = funid(Char*)

  [gfi-1'2] get-funid(Term) = Funid,
            is-trans-list-funid(Funid) = false
            ==================================
            get-funid(Term,Namepair*) = Funid

%% An auxilary function to get the function args of
%% a rewrite rule.
 [gfa-1'1] get-funargs(Funid) =

 [gfa-1'2] get-funargs(Funid(Term+)) = Term+

 [gfa-2'1] get-funargs(Term1 = Term2) = get-funargs(Term1)

 [gfa-2'2] get-funargs(Cond+ ==> Term1 = Term2) = get-funargs(Term1)

 [gfa-2'3] get-funargs(default:Term1 = Term2) = get-funargs(Term1)

 [gfa-2'4] get-funargs(default:Cond+ ==> Term1 = Term2) = get-funargs(Term1)

%% The next rule adds conditions to a non-conditional rule
%% if this is needed.
 [mcr-1'1] make-conditional-rule(,Rule) = Rule

 [mcr-1'2] make-conditional-rule(Cond+,Term1 = Term2) =
             Cond+ ==> Term1 = Term2

 [mcr-1'3] make-conditional-rule(Cond+,default:Term1 = Term2) =
             default:Cond+ ==> Term1 = Term2

  [mr-1'1] make-rule(true,Term1,Term2) = default: Term1 = Term2

  [mr-1'2] make-rule(false,Term1,Term2) = Term1 = Term2

  [mr-2'1] make-rule(true,Cond+,Term1,Term2) = default: Cond+ ==> Term1 = Term2

  [mr-2'2] make-rule(false,Cond+,Term1,Term2) = Cond+ ==> Term1 = Term2

  [mr-2'3] make-rule(true,,Term1,Term2) = default: Term1 = Term2

  [mr-2'4] make-rule(false,,Term1,Term2) = Term1 = Term2

%% 
[isf] is-list-funid(Funid) = false,
      is-returns-list-funid(Funid) = false,
      Funid = funid("""Char+""")
      ===>
      is-string-funid(Funid) = true

[default-isf] is-string-funid(Funid) = false

%% A function to check whether we are dealing with a transformer
[itfi-1] is-traversal-funid(funid("l""f""_""t""r""a""f""o""_" Char+ )) = true
[itfi-2] is-traversal-funid(funid("l""f""_""a""c""c""u""_" Char+ )) = true
[itfi-3] is-traversal-funid(
           funid("l""f""_""a""c""c""u""t""r""a""f""o""_" Char+ )) = true
[itfi-4] is-traversal-funid(funid(""""[" Char+ "]" """)) = 
           is-traversal-funid(funid(Char+))
[default-itfi] is-traversal-funid(Funid) = false

[gtt-1] get-traversal-type(funid(Char+1 "_""t""r""a""f""o""_" Char+2)) = trafo
[gtt-2] get-traversal-type(funid(Char+1 "_""a""c""c""u""_" Char+2)) = accu
[gtt-3] get-traversal-type(funid(Char+1 "_""a""c""c""u""t""r""a""f""o""_" Char+2)) = 
          accutrafo
[gtt-4] get-traversal-type(funid(""""[" Char+ "]" """)) = 
          get-traversal-type(funid(Char+))

[gts-2] get-traversal-strategy(funid(Char+1 "_""t""d""_" Char+2)) = top-down
[default-gts] is-traversal-funid(Funid) = true
              ===>
              get-traversal-strategy(Funid) = bottom-up

%% A function to check whether we are dealing with a list.
  [ilfi-1'1] is-list-funid(funid(""" "{" Char+ "}" """)) = true

  [default-ilfi-1'1] is-list-funid(Funid) = false

%% A function to check whether we are dealing with a list.
  [ilfi-1'1] is-trans-list-funid(funid(""" "{" Char+ "}" """)) = true

  [ilfi-1'1] is-trans-list-funid(funid(""" "{" Char+ "}"  "-" "t""r""a""n""s" """)) = true

  [default-ilfi-1'1] is-trans-list-funid(Funid) = false

  [mtli-1'1] make-trans-list-id(funid(""" "{" Char+ "}" """)) =
               funid(""" "{" Char+ "}" "-" "t""r""a""n""s" """)

  [rtli-1'1] remove-trans-from-list-id(funid(""" "{" Char+ "}" """)) =
               funid(""" "{" Char+ "}" """)

  [trli-1'2] remove-trans-from-list-id(funid(""" "{" Char+ "}" "-" "t""r""a""n""s" """)) =
               funid(""" "{" Char+ "}" """)

%% functions for funid's that indicate that the function is returning a list
[it]
is-returns-list-funid(funid(""""[" Char+ "]"""" Char*)) = true

[default-it]
is-returns-list-funid(Funid) = false

[rt]
remove-returns-list(funid(""""[" Char+ "]"""" Char* )) = funid(Char+ Char*)

[default-rt]
remove-returns-list(Funid) = Funid

%% functions to check whether we are dealing with an ignored function as part
%% of an optimization
  [mrli-1'1] is-list-funid(funid("""Char+""")) = false
             ====
             make-ignored-funid(funid(""" Char+ """)) =
               funid(""" Char+ "-""i""g""n""o""r""e""d" """)

  [mrli-1'3] make-ignored-funid(funid(""" "{" Char+ "}" """)) =
               funid(""" "{" Char+ "-""i""g""n""o""r""e""d" "}" """)

  [mrli-1'2] is-quoted-funid(funid(Char+)) = false
             ====
             make-ignored-funid(funid(Char+)) =
               funid(Char+ "-""i""g""n""o""r""e""d")


  [riff-1'1] remove-ignored-from-funid(funid(""" Char+ "-""i""g""n""o""r""e""d" """)) = 
               funid(""" Char+ """)

  [riff-1'2] remove-ignored-from-funid(funid(""" "{" Char+ 
               "-""i""g""n""o""r""e""d" "}" """)) = funid(""" "{" Char+ "}" """)

  [riff-1'3] remove-ignored-from-funid(funid(Char+ "-""i""g""n""o""r""e""d" )) = 
                funid(Char+)

  [riff-1'4] remove-ignored-from-funid(funid(""" "{" Char+ "-""i""g""n""o""r""e""d" "}" 
                                                  "-""t""r""a""n""s" """)) =
                funid(""" "{" Char+ "}"  "-""t""r""a""n""s" """)

  [irli-1'1] is-ignored-funid(funid(""" Char+ "-""i""g""n""o""r""e""d" """)) =
               true

  [irli-1'2] is-ignored-funid(funid(""" "{" Char+ "-""i""g""n""o""r""e""d" "}" """)) =
               true

  [irli-1'3] is-ignored-funid(funid(Char+ "-""i""g""n""o""r""e""d")) =
               true

  [irli-1'4] is-ignored-funid(funid(""" "{" Char+ "-""i""g""n""o""r""e""d" "}" 
                                                  "-""t""r""a""n""s" """)) = true

  [default-irli] is-ignored-funid(Funid) = false

  [ic-1'1] is-cons(funid("c""o""n""s")) = true

  [default-ic-1'2] is-cons(Funid) = false

  [ilvt-1'1] is-list-var-term(*Varid) = true

  [ilvt-1'2] is-list-var-term(+Varid) = true

  [default-ilvt-1'3] is-list-var-term(Term) = false

  [asv-1'1] is-var-term(Term) = true,
            is-star-var(Term) = true
            ===>
            are-star-vars(Term) = true

  [asv-1'2] is-var-term(Term) = true,
            is-star-var(Term) = true
            ===>
            are-star-vars(Term, Term+) = 
              are-star-vars(Term+)

  [default-asv-1'3] are-star-vars(Term+) = false

  [default-asv-1'1] are-star-vars(Term) = false

  [ic-1'1] is-quoted-funid(funid(""" Char* """)) = true

  [default-ic-1'2] is-quoted-funid(Funid) = false

  [idr-1'1] is-default-rule(default:Term1 = Term2) = true

  [idr-1'2] is-default-rule(default:Cond* ==> Term1 = Term2) = true

  [default-idr-1'3] is-default-rule(Rule) = false

  [ibt-1'1] is-boolean-term(t) = true

  [ibt-1'2] is-boolean-term(f) = true

  [default-ibt-1'3] is-boolean-term(Term) = false

  [ivt-1'1] is-var-term(Varid) = true

  [ivt-1'2] is-var-term(*Varid) = true

  [ivt-1'3] is-var-term(+Varid) = true

  [default-ivt-1'4] is-var-term(Term) = false

   [cv-1'1] is-var-term(Term) = true
            ========================
            term-contains-var(Term) = true

   [cv-1'1] is-var-term(Term) = false,
            get-funargs(Term) = Term*
            =========================
            term-contains-var(Term) = args-contain-var(Term*)

   [cv-2'1] args-contain-var() = false

   [cv-2'2] args-contain-var(Term,Term*) =
              term-contains-var(Term) \/ args-contain-var(Term*)

   [mf-1'1] make-func(Funid,) = Funid

   [mf-1'2] make-func(Funid,Term+)  = Funid(Term+)

%% Constructor function for list functions. If the
%% list is a list of characters a different constructor
%% function for the arguments should be applied.
  [mlf-1'1] make-list-func(Funid,) = Funid(null)

  [mlf-1'2] make-list-func(Funid,Term+) = Funid(Term+)

  [mla-1'1] make-list-args(Term) = Term

  [mla-1'2] make-list-args(Term+) = Term2
            =============================
            make-list-args(Term1,Term+) = cons(Term1,Term2)

  [pi-1'1] funid(Char*1 Char*3) = funid(Char*4)
           ====================================
           prefix-id(funid(""" Char*1 """), funid(""" Char*2 """),natcon(Char*3)) =
              funid(""" Char*4 "_" "_" Char*2 """)

  [default-pi-1'2] funid(Char*1 Char*3) = funid(Char*4)
                    ====================================
                    prefix-id(funid(""" Char*1 """), funid(Char*2),natcon(Char*3)) =
                     funid(Char*4 "_" "_" Char*2)

  [iif-1'1] is-internal-funid(funid(""" "n""e""w""_""_" Char* """)) =
              is-internal-funid-new(funid(Char*))

  [iif-1'4] is-internal-funid(funid("n""e""w""_""_" Char*)) =
              is-internal-funid-new(funid(Char*))

  [default-iif-1'7] is-internal-funid(funid(Char*)) = false

  [iifn-1'1] natcon(Char*1) = Aint
             =====================
             is-internal-funid-new(funid(Char*1 "_""_" Char*2)) = true

  [default-iifn-1'2] is-internal-funid-new(funid(Char*)) = false

  [mdr-1'1] make-default-rule(Term1 = Term2) = default:Term1 = Term2

  [mdr-1'2] make-default-rule(Cond+ ==> Term1 = Term2) = default:Cond+ ==> Term1 = Term2

  [ipv-1'1] is-plus-var(+ Varid) = true

  [default-ipv-1'2] is-plus-var(Term2) = false

  [isv-1'1] is-star-var(* Varid) = true

  [default-isv-1'2] is-star-var(Term2) = false

%% If there are no rewrite rules we are finished.
  [soo-1'1] select-on-ofs(Funid,) = (,)

%% If a rule has the same ofs it is added to the first set.
  [soo-1'2] get-funid(Rule) = Funid,
            select-on-ofs(Funid,Rule*) = (Rule*1,Rule*2)
            ============================================
            select-on-ofs(Funid,Rule; Rule*) =
              (Rule;Rule*1,Rule*2)

%% A rewrite rule with conditions is added to the set of conditional
%% rewrite rules.
  [soc-1'3] get-funid(Rule) = Funid2,
            Funid1 != Funid2,
            select-on-ofs(Funid1,Rule*) = (Rule*1,Rule*2)
            ============================================
            select-on-ofs(Funid1,Rule; Rule*) =
              (Rule*1,Rule;Rule*2)

%% If there are no rewrite rules we are finished.
  [sod-1'1] select-on-defaults() = (,)

%% If a default rule with no conditions is added to the set of
%% default rules.
  [sod-1'2] select-on-defaults(Rule*) = (Rule*1,Rule*2)
            ===========================================
            select-on-defaults(Term1 = Term2; Rule*) =
              (Rule*1,Term1 = Term2;Rule*2)

  [sod-1'3] select-on-defaults(Rule*) = (Rule*1,Rule*2)
            ===========================================
            select-on-defaults(default:Term1 = Term2; Rule*) =
              (default:Term1 = Term2;Rule*1,Rule*2)

%% A default rewrite rule with conditions is added to the set of default
%% rewrite rules.
  [sod-1'4] select-on-defaults(Rule*) = (Rule*1,Rule*2)
            ===========================================
            select-on-defaults(Cond+ ==> Term1 = Term2; Rule*) =
              (Rule*1,Cond+ ==> Term1 = Term2;Rule*2)

  [sod-1'5] select-on-defaults(Rule*) = (Rule*1,Rule*2)
            ===========================================
            select-on-defaults(default:Cond+ ==> Term1 = Term2; Rule*) =
              (default:Cond+ ==> Term1 = Term2;Rule*1,Rule*2)