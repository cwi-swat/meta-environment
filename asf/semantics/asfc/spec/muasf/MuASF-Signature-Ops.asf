equations

  [fdlc-2'1] ++ Funcdef* = Funcdef*

  [fdlc-2'2] is-external-funcdef(Funcdef1) == true,
             Funcdef2 := remove-external-funcdef(Funcdef1),
             Funcdef2 in Funcdef*2 == false,
             Funcdef1 in Funcdef*2 == true
             ============================
             Funcdef1;Funcdef*1 ++ Funcdef*2 = Funcdef*1 ++ Funcdef*2

  [fdlc-2'3] is-external-funcdef(Funcdef1) == true,
             Funcdef2 := remove-external-funcdef(Funcdef1),
             Funcdef2 in Funcdef*2 == true
             ============================
             Funcdef1;Funcdef*1 ++ Funcdef*2 = Funcdef*1 ++ Funcdef*2

  [fdlc-2'4] is-external-funcdef(Funcdef) == false,
             Funcdef in Funcdef*2 == true
             ===========================
             Funcdef;Funcdef*1 ++ Funcdef*2 = Funcdef*1 ++ Funcdef*2

  [fdlc-2'5] is-external-funcdef(Funcdef1) == false,
             Funcdef1 in Funcdef*2 == false,
             Funcdef2 := add-external-funcdef(Funcdef1),
             Funcdef2 in Funcdef*2 == true,
             Funcdef*3 := remove-funcdef(Funcdef2,Funcdef*2),
             Funcdef*4 := Funcdef*1 ++ Funcdef*3
             ==================================
             Funcdef1;Funcdef*1 ++ Funcdef*2 = Funcdef1;Funcdef*4

  [fdlc-2'6] is-external-funcdef(Funcdef1) == true,
             Funcdef1 in Funcdef*2 == false,
             Funcdef2 := remove-external-funcdef(Funcdef1),
             Funcdef2 in Funcdef*2 == false,
             Funcdef*3 := Funcdef*1 ++ Funcdef*2
             ==================================
             Funcdef1;Funcdef*1 ++ Funcdef*2 = Funcdef1;Funcdef*3

  [fdlc-3'7] is-external-funcdef(Funcdef1) == false,
             Funcdef1 in Funcdef*2 == false,
             Funcdef2 := add-external-funcdef(Funcdef1),
             Funcdef2 in Funcdef*2 == false,
             Funcdef*3 := Funcdef*1 ++ Funcdef*2
             ==================================
             Funcdef1;Funcdef*1 ++ Funcdef*2 = Funcdef1;Funcdef*3

  [rdl-2'1] remove-funcdef(Funcdef,) =

  [rdl-2'2] remove-funcdef(Funcdef,Funcdef;Funcdef*) = Funcdef*

  [rdl-2'3] Funcdef1 != Funcdef2,
            Funcdef*2 := remove-funcdef(Funcdef1,Funcdef*1)
            ==============================================
            remove-funcdef(Funcdef1,Funcdef2;Funcdef*1) =
              Funcdef2;Funcdef*2

[rd] 
remove-funcdef(funid(""""{"Char*"}""""),Funcdef*) = 
remove-funcdef1(funid(Char*),Funcdef*)

[rd]
remove-funcdef(funid(""""["Char*1"]""""Char*2),Funcdef*) =
remove-funcdef1(funid(Char*1 Char*2),Funcdef*)

[default-rd]
remove-funcdef(Funid,Funcdef*) = remove-funcdef1(Funid,Funcdef*)

  [rfd-1'1] remove-funcdef1(Funid,) =

  [rfd-2'2] remove-funcdef1(Funid,Funid;Funcdef*) = Funcdef*

  [rfd-2'3] remove-funcdef1(Funid,Funid Attrs; Funcdef*) = Funcdef*

  [rfd-2'4] remove-funcdef1(Funid,Funid(Sigarg+);Funcdef*) = Funcdef*

  [rfd-2'5] remove-funcdef1(Funid,Funid(Sigarg+) Attrs;Funcdef*) = Funcdef*

  [rfd-2'6] Funid1 != Funid2,
            Funcdef*2 := remove-funcdef1(Funid1,Funcdef*1)
            ============================================
            remove-funcdef1(Funid1,Funid2;Funcdef*1) =
              Funid2;Funcdef*2

  [rfd-2'7] Funid1 != Funid2,
            Funcdef*2 := remove-funcdef1(Funid1,Funcdef*1)
            ============================================
            remove-funcdef1(Funid1,Funid2 Attrs;Funcdef*1) =
              Funid2 Attrs;Funcdef*2

  [rfd-2'8] Funid1 != Funid2,
            Funcdef*2 := remove-funcdef1(Funid1,Funcdef*1)
            ============================================
            remove-funcdef1(Funid1,Funid2(Sigarg+);Funcdef*1) =
              Funid2(Sigarg+);Funcdef*2

  [rfd-2'9] Funid1 != Funid2,
            Funcdef*2 := remove-funcdef1(Funid1,Funcdef*1)
            ============================================
            remove-funcdef1(Funid1,Funid2(Sigarg+) Attrs;Funcdef*1) =
              Funid2(Sigarg+) Attrs;Funcdef*2

  [iefd-2'1] is-external-funcdef(Funid {external}) = true

  [iefd-2'2] is-external-funcdef(Funid2(Sigarg+) {external}) = true

  [default-iefd-2'3] is-external-funcdef(Funcdef) = false

  [ae-2'1] add-external-funcdef(Funid) =
             Funid {external}

  [ae-2'2] add-external-funcdef(Funid(Sigarg+)) =
             Funid(Sigarg+) {external}

  [default-ae-2'3] add-external-funcdef(Funcdef) = Funcdef

  [re-2'1] remove-external-funcdef(Funid {external}) =
             Funid

  [re-2'2] remove-external-funcdef(Funid(Sigarg+) {external}) =
             Funid(Sigarg+)

  [default-re-2'3] remove-external-funcdef(Funcdef) = Funcdef

  [fdli-2'1] Funcdef in = false

  [fdli-2'2] Funcdef in Funcdef;Funcdef* = true

  [fdli-2'3] Funcdef1 != Funcdef2
             ====================
             Funcdef1 in Funcdef2;Funcdef* = Funcdef1 in Funcdef*

%% Auxilary functions to create new function definition to be
%% included in the signature.
 [mfd-1'1] make-funcdef(Funid,) = Funid

 [mfd-1'2] make-funcdef(Funid,Term) = Funid(_)

 [mfd-1'3] Funid2(Sigarg+) := make-funcdef(Funid1,Term+)
           ============================================
           make-funcdef(Funid1,Term,Term+) = Funid2(_,Sigarg+)

%% Equations to create external function definitions.
 [mefd-1'1] make-external-funcdef(Funid,) = Funid {external}

 [mefd-1'2] make-external-funcdef(Funid,Term) =
              Funid(_) {external}

 [mefd-1'3] Funid2(Sigarg+) {external} := make-external-funcdef(Funid1,Term+)
            ================================================================
            make-external-funcdef(Funid1,Term,Term+) =
              Funid2(_,Sigarg+) {external}

%%% [mnfd-1'1] make-new-funcdef(Funid1(Term),Funid2) = Funid2(_)

%%% [mnfd-1'2] make-new-funcdef(Funid1(Term+),Funid2) = Funid3(Sigarg+)
%%%            ========================================================
%%%            make-new-funcdef(Funid1(Term,Term+),Funid2) = Funid3(_,Sigarg+)

%% An auxilary function to add new created function definitions
%% to the (existing) signature.
 [ats-1'1] addto-sign(,) =

 [ats-2'3] addto-sign(signature,) =

 [ats-1'2] addto-sign(,Funcdef+) = signature Funcdef+

 [ats-1'2] addto-sign(signature,Funcdef+) = signature Funcdef+

 [ats-1'3] addto-sign(signature Funcdef+,) = signature Funcdef+

 [ats-1'4] Funcdef+3 := Funcdef+1 ++ Funcdef+2
           ==================================
           addto-sign(signature Funcdef+1,Funcdef+2) =
             signature Funcdef+3

  [ie-1'1] is-external({Term*1, external, Term*2}) = true

  [default-ie-1'1] is-external(Annos) = false

[gf-0]
get-funcdef-funid(Funid) = Funid

[gf-1]
get-funcdef-funid(Funid(Sigarg+)) = Funid

[gf-2]
get-funcdef-funid(Funid {Term+}) = Funid

[gf-3]
get-funcdef-funid(Funid(Sigarg+) {Term+}) = Funid 