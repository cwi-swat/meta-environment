equations

  [ca-1'1] check-asf-attributes(Definition) =
             check-attributes(Definition, {})

  [ca-1'2] get-module-name(Module) = $ModuleId
           ====================================
           check-attributes(Module, Msgs) = 
             check-attributes(Module, Msgs, $ModuleId)

  [ca-2'1] check-attributes($Symbol* -> $Symbol 
                            {Attr*1, traversal(Aterm+), Attr*2},
                            Msgs, $ModuleId) = Msgs +
traversal-attributes-in-non-prefix-function($ModuleId, $Symbol* -> $Symbol {Attr*1, traversal(Aterm+), Attr*2})

  [ca-2'2] contains-accu(Aterm+) |
             contains-trafo(Aterm+) = true,
           contains-break(Aterm+) |
             contains-continue(Aterm+) = true,
           contains-top-down(Aterm+) |
             contains-bottom-up(Aterm+) = true,
           contains-only-traversal-attributes(Aterm+) = false
           ==================================================
           check-attributes($Literal ( $Symbol,* ) -> $Symbol 
                            {Attr*1, traversal(Aterm+), Attr*2},
                            Msgs, $ModuleId) = Msgs +
illegal-traversal-attribute($ModuleId, $Literal ( $Symbol,* ) -> $Symbol {Attr*1, traversal(Aterm+), Attr*2})

  [ca-2'3] contains-accu(Aterm+) |
             contains-trafo(Aterm+) = false,
           contains-break(Aterm+) |
             contains-continue(Aterm+) = true,
           contains-top-down(Aterm+) |
             contains-bottom-up(Aterm+) = true
           ===================================
           check-attributes($Literal ( $Symbol,* ) -> $Symbol 
                            {Attr*1, traversal(Aterm+), Attr*2},
                            Msgs, $ModuleId) = Msgs +
missing-trafo-and-or-accu-attribute($ModuleId, $Literal ( $Symbol,* ) -> $Symbol {Attr*1, traversal(Aterm+), Attr*2})

  [ca-2'4] contains-accu(Aterm+) |
             contains-trafo(Aterm+) = true,
           contains-break(Aterm+) |
             contains-continue(Aterm+) = false,
           contains-top-down(Aterm+) |
             contains-bottom-up(Aterm+) = true
           ===================================
           check-attributes($Literal ( $Symbol,* ) -> $Symbol 
                            {Attr*1, traversal(Aterm+), Attr*2},
                            Msgs, $ModuleId) = Msgs +
missing-break-or-continue-attribute($ModuleId, $Literal ( $Symbol,* ) -> $Symbol {Attr*1, traversal(Aterm+), Attr*2})

  [ca-2'5] contains-accu(Aterm+) |
             contains-trafo(Aterm+) = true,
           contains-break(Aterm+) |
             contains-continue(Aterm+) = true,
           contains-top-down(Aterm+) |
             contains-bottom-up(Aterm+) = false
           ====================================
           check-attributes($Literal ( $Symbol,* ) -> $Symbol 
                            {Attr*1, traversal(Aterm+), Attr*2},
                            Msgs, $ModuleId) = Msgs +
missing-bottom-up-or-top-down-attribute($ModuleId, $Literal ( $Symbol,* ) -> $Symbol {Attr*1, traversal(Aterm+), Attr*2})

  [ca-2'6] contains-accu(Aterm+) = true,
           contains-trafo(Aterm+) = false,
           is-accu-definition($Literal ( $Symbol,* ) -> $Symbol) = false
           =============================================================
           check-attributes($Literal ( $Symbol,* ) -> $Symbol 
                            {Attr*1, traversal(Aterm+), Attr*2},
                            Msgs, $ModuleId) = Msgs +
accu-should-return-accumulated-type($ModuleId, $Literal ( $Symbol,* ) -> $Symbol {Attr*1, traversal(Aterm+), Attr*2})

  [ca-2'6] contains-trafo(Aterm+) = true,
           contains-accu(Aterm+) = false,
           is-trafo-definition($Literal ( $Symbol,* ) -> $Symbol) = false
           ==============================================================
           check-attributes($Literal ( $Symbol,* ) -> $Symbol 
                            {Attr*1, traversal(Aterm+), Attr*2},
                            Msgs, $ModuleId) = Msgs +
trafo-should-return-traversed-type($ModuleId, $Literal ( $Symbol,* ) -> $Symbol {Attr*1, traversal(Aterm+), Attr*2})

  [ca-2'7] contains-trafo(Aterm+) = true,
           contains-accu(Aterm+) = true,
           is-accu-trafo-definition($Literal ( $Symbol,* ) -> $Symbol) = false
           ===================================================================
           check-attributes($Literal ( $Symbol,* ) -> $Symbol 
                            {Attr*1, traversal(Aterm+), Attr*2},
                            Msgs, $ModuleId) = Msgs +
accutrafo-should-return-tuple-of-correct-types($ModuleId, $Literal ( $Symbol,* ) -> $Symbol {Attr*1, traversal(Aterm+), Attr*2})

  [ca-2'5] contains-accu(Aterm+) = true,
           contains-trafo(Aterm+) = false,
           is-accu-definition($Literal ( $Symbol,* ) -> $Symbol) = true,
           contains-break(Aterm+) |
             contains-continue(Aterm+) = true,
           contains-top-down(Aterm+) |
             contains-bottom-up(Aterm+) = true,
           contains-only-traversal-attributes(Aterm+) = true
           =================================================
           check-attributes($Literal ( $Symbol,* ) -> $Symbol 
                            {Attr*1, traversal(Aterm+), Attr*2},
                            Msgs, $ModuleId) = Msgs

  [ca-2'6] contains-trafo(Aterm+) = true,
           contains-accu(Aterm+) = false,
           is-trafo-definition($Literal ( $Symbol,* ) -> $Symbol) = true,
           contains-break(Aterm+) |
             contains-continue(Aterm+) = true,
           contains-top-down(Aterm+) |
             contains-bottom-up(Aterm+) = true,
           contains-only-traversal-attributes(Aterm+) = true
           =================================================
           check-attributes($Literal ( $Symbol,* ) -> $Symbol 
                            {Attr*1, traversal(Aterm+), Attr*2},
                            Msgs, $ModuleId) = Msgs

  [ca-2'7] contains-accu(Aterm+) = true,
           contains-trafo(Aterm+) = true,
           is-accu-trafo-definition($Literal ( $Symbol,* ) -> $Symbol) = true,
           contains-break(Aterm+) |
             contains-continue(Aterm+) = true,
           contains-top-down(Aterm+) |
             contains-bottom-up(Aterm+) = true,
           contains-only-traversal-attributes(Aterm+) = true
           =================================================
           check-attributes($Literal ( $Symbol,* ) -> $Symbol 
                            {Attr*1, traversal(Aterm+), Attr*2},
                            Msgs, $ModuleId) = Msgs

  [default-ca-2'8] check-attributes($Literal ( $Symbol,* ) -> $Symbol 
                                     {Attr*1, traversal(Aterm+), Attr*2},
                             Msgs, $ModuleId) = 
              Msgs + 
illegal-traversal-attribute($ModuleId, $Literal ( $Symbol,* ) -> $Symbol {Attr*1, traversal(Aterm+), Attr*2})

  [ca-2'9]  $Literal2 != traversal,
            contains-accu(Aterm+) | 
              contains-trafo(Aterm+) |
              contains-break(Aterm+) |
              contains-continue(Aterm+) |
              contains-top-down(Aterm+) |
              contains-bottom-up(Aterm+) = true
            ===================================
            check-attributes($Literal1 ( $Symbol,* ) -> $Symbol 
                             {Attr*1, $Literal2(Aterm+), Attr*2},
                             Msgs, $ModuleId) = 
              Msgs + 
illegal-attribute($ModuleId, $Literal1 ( $Symbol,* ) -> $Symbol {Attr*1, $Literal2(Aterm+), Attr*2})

  [ca-2'10] $Literal2 != traversal,
            contains-accu(Aterm+) | 
              contains-trafo(Aterm+) |
              contains-break(Aterm+) |
              contains-continue(Aterm+) |
              contains-top-down(Aterm+) |
              contains-bottom-up(Aterm+) = true
            ===================================
            check-attributes($Symbol* -> $Symbol 
                             {Attr*1, $Literal2(Aterm+), Attr*2},
                             Msgs, $ModuleId) = 
              Msgs + 
illegal-attribute($ModuleId, $Symbol* -> $Symbol {Attr*1, $Literal2(Aterm+), Attr*2})

  [iad-1'1] is-accu-definition($Literal1 ( $Symbol1, $Symbol2, $Symbol,*) -> $Symbol2) = true

  [default-iad-1'2] is-accu-definition(Production) = false

  [itd-1'1] is-trafo-definition($Literal1 ( $Symbol, $Symbol,*) -> $Symbol) = true

  [default-itd-1'2] is-trafo-definition(Production) = false

  [iatd-1'1] is-accu-trafo-definition($Literal1 ( $Symbol1, $Symbol2, $Symbol,*) -> $Symbol1 # $Symbol2) = true

  [default-iatd-1'2] is-accu-trafo-definition(Production) = false

  [ca-3'1] contains-accu(Aterm*1, accu, Aterm*2) = true

  [default-ca-3'2] contains-accu(Aterm*) = false

  [ct-3'1] contains-trafo(Aterm*1, trafo, Aterm*2) = true

  [default-ct-3'2] contains-trafo(Aterm*) = false

  [cb-1'1] contains-break(Aterm*1, break, Aterm*2) = true

  [default-cb-1'2] contains-break(Aterm*) = false

  [cc-1'1] contains-continue(Aterm*1, continue, Aterm*2) = true

  [default-cc-1'2] contains-continue(Aterm*) = false

  [ctd-1'1] contains-top-down(Aterm*1, top-down, Aterm*2) = true

  [default-ctd-1'2] contains-top-down(Aterm*) = false

  [cbu-1'1] contains-bottom-up(Aterm*1, bottom-up, Aterm*2) = true

  [default-cbu-1'2] contains-bottom-up(Aterm*) = false

  [cota-1'1] contains-only-traversal-attributes() = true

  [cota-1'2] contains-only-traversal-attributes(Aterm*1, accu, Aterm*2) =
               contains-only-traversal-attributes(Aterm*1, Aterm*2)

  [cota-1'3] contains-only-traversal-attributes(Aterm*1, trafo, Aterm*2) =
               contains-only-traversal-attributes(Aterm*1, Aterm*2)

  [cota-1'4] contains-only-traversal-attributes(Aterm*1, break, Aterm*2) =
               contains-only-traversal-attributes(Aterm*1, Aterm*2)

  [cota-1'5] contains-only-traversal-attributes(Aterm*1, continue, Aterm*2) =
               contains-only-traversal-attributes(Aterm*1, Aterm*2)

  [cota-1'6] contains-only-traversal-attributes(Aterm*1, top-down, Aterm*2) =
               contains-only-traversal-attributes(Aterm*1, Aterm*2)

  [cota-1'7] contains-only-traversal-attributes(Aterm*1, bottom-up, Aterm*2) =
               contains-only-traversal-attributes(Aterm*1, Aterm*2)

  [default-cota-1'8] contains-only-traversal-attributes(Aterm+) = false
