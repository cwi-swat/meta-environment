equations

  [cc-1'1] check-constructors(Definition, InfoMsgTuple) = 
             check-module-constructors(Definition, InfoMsgTuple)
	
  [cmc-1'1] $ModuleId := get-module-name(Module),
       
            imports(Import*) := get-all-imports(Module),
            constructors(SymbolCons*1) := 
              get-constructors(Import*, 
                             get-module-info(InfoMsgTuple1)),

            [constructors(SymbolCons*2), Msgs1] := 
              get-constructor-symbol-pairs(Module, 
                                         [constructors(SymbolCons*1), ], 
                                         $ModuleId),

            Msgs2 := get-messages(InfoMsgTuple1), 

            InfoMsgTuple2 := put-messages(Msgs1, Msgs2, InfoMsgTuple1),

            ModuleInfoTable1 := get-module-info(InfoMsgTuple2),
            ModuleInfoTable2 := put-constructor-info($ModuleId, 
                                 constructors(SymbolCons*2), 
                                 ModuleInfoTable1),
            InfoMsgTuple3 :=  put-module-info(ModuleInfoTable2, InfoMsgTuple2)
            =================================================================
            check-module-constructors(Module, InfoMsgTuple1) = InfoMsgTuple3

  [gcsp-1'1] get-constructor-symbol-pairs(context-free priorities Group+, 
                                          ConsMsgTuple,
                                          $ModuleId) = 
               check-constructor-in-priorities(Group+, 
                                               ConsMsgTuple,
                                               $ModuleId)

  [gcsp-1'2] get-constructor-symbol-pairs(priorities Group+, 
                                          ConsMsgTuple,
                                          $ModuleId) = 
               check-constructor-in-priorities(Group+, 
                                               ConsMsgTuple,
                                               $ModuleId)

  [gcsp-1'3] get-constructor-symbol-pairs(Symbol* -> Symbol Attrs,
                                          ConsMsgTuple,
                                          $ModuleId) =
               get-constructor-symbol-pairs2(Attrs, ConsMsgTuple, 
                                             $ModuleId, Symbol,
                                             Symbol* -> Symbol Attrs)

  [gcsp-1'4] get-constructor-symbol-pairs($Literal(Symbol,*) -> Symbol Attrs,
                                          ConsMsgTuple,
                                          $ModuleId) =
               get-constructor-symbol-pairs2(Attrs, ConsMsgTuple, 
                                             $ModuleId, Symbol,
                                             $Literal(Symbol,*) -> Symbol Attrs)

  [gcsp-2'1] cons( $Literal ) := $Attribute,
             $Location := get-location($Attribute),
             contains(Symbol, $Literal, ConstructorSet) == true,
             $String1 := moduleid2str($ModuleId),
             $String2 := production2str(Prod),
             $String3 := "constructor has already been used in " || $String1
             ====>
             get-constructor-symbol-pairs2($Attribute, 
                                           [ConstructorSet, Msgs], 
                                           $ModuleId, Symbol, Prod) =
               [ConstructorSet, 
                Msgs, make-error($String3, $String2, $Location)]

  [gcsp-2'2] contains(Symbol, $Literal, ConstructorSet) == false
             ==================================================
             get-constructor-symbol-pairs2(cons( $Literal ), 
                                           [ConstructorSet, Msgs], 
                                           $ModuleId, Symbol, Prod) =
               [add(Symbol, $Literal, ConstructorSet), Msgs]

  [ccip-1'1] check-constructor-in-priorities(Symbol* -> Symbol Attrs,
                                             ConsMsgTuple,
                                             $ModuleId) =
               check-constructor-in-priorities2(Attrs, ConsMsgTuple, 
                                               $ModuleId, Symbol,
                                               Symbol* -> Symbol Attrs)

  [ccip-1'2] check-constructor-in-priorities($Literal(Symbol,*) -> Symbol Attrs,
                                             ConsMsgTuple,
                                             $ModuleId) =
               check-constructor-in-priorities2(Attrs, ConsMsgTuple, 
                                               $ModuleId, Symbol,
                                               $Literal(Symbol,*) -> Symbol Attrs)

  [ccip-2'1] contains(Symbol, $Literal, ConstructorSet) == true
             ====>
             check-constructor-in-priorities2(cons( $Literal ), 
                                              [ConstructorSet, Msgs], 
                                               $ModuleId, Symbol, Prod) = 
               [ConstructorSet, Msgs]

  [ccip-2'2] cons( $Literal ) := $Attribute,
             $Location := get-location($Attribute),
             contains(Symbol, $Literal, ConstructorSet) == false,
             $String1 := moduleid2str($ModuleId),
             $String2 := production2str(Prod),
             $String3 := "unknown constructor used in priorities in " || $String1
             ====>
             check-constructor-in-priorities2($Attribute, 
                                              [ConstructorSet, Msgs], 
                                              $ModuleId, Symbol, Prod) =
               [ConstructorSet, 
                Msgs, make-warning($String3, $String2, $Location)]
