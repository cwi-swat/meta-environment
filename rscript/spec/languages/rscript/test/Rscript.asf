equations

[main] start(RSCRIPT, $Rscript) = start(Summary, eval-rscript-testsuite($Rscript))

[test-environment-top]
	test-environment = assign-variable(NPASSED,int, 0, 
				assign-variable(NFAILED,int,0, 
					assign-variable(FAILED, set[str], [], new-renvironment())))

equations

%% ---- Evaluate test suite

%%[ETS1]	$Stat* := $Rscript,
%%         $REnv := eval-rstats($Stat*, test-environment),
%%	$NatCon1 := value(NPASSED, $REnv),
%%	$NatCon2 := value(NFAILED, $REnv),
%%	[$Error*] := value(FAILED, $REnv)
%%	===
%%	eval-rscript-testsuite($Rscript) = summary("Rscript test runner", get-filename(get-location($Rscript)), [$Error*])
[] eval-rscript-testsuite($Rscript) = eval-rscript-testsuite-with-rstore($Rscript, test-environment)

%% ---- Evaluate test suite with given Env

[ETS2]	$Stat* := $Rscript,
	$REnv1 := assign-variable(NPASSED, int, 0, $REnv),
	$REnv2 := assign-variable(NFAILED, int, 0, $REnv1),
	$REnv3 := assign-variable(FAILED, set[str], [], $REnv2),
	$REnv4 := eval-rstats($Stat*, $REnv3),
	$NatCon1 := value(NPASSED, $REnv4),
	$NatCon2 := value(NFAILED, $REnv4),
	[$Error*] := value(FAILED, $REnv4)
	===
	eval-rscript-testsuite-with-rstore($Rscript, $REnv) = summary("Rscript test runner", get-filename(get-location($Rscript)), [$Error*])

%% ---- Evaluate test suite with given RStore

[ETS3]   $REnv := rstore-to-renvironment($RStore)
         ===
         eval-rscript-testsuite-with-rstore($Rscript, $RStore) = eval-rscript-testsuite-with-rstore($Rscript, $REnv)

%% ---- Increment a variable

[INC1]	$NatCon := value($Var, $REnv),
	$Integer :=  $NatCon + 1,
	$NatCon' := $Integer
	===
	increment($Var, $REnv) = assign-variable($Var, $NatCon', $REnv)

%% ---- Evaluate assert

[S10]	evale($Exp,$REnv) == true
	===
	eval-rstat(assert $StrCon: $Exp, $REnv)= increment(NPASSED, $REnv)

[S11]	assert $StrCon : $Exp := $Stat,
         evale($Exp,$REnv) == false, 
	$Error := error("Failed test", [localized($StrCon, get-location($Stat))]),
 	[$Error*] := value(FAILED, $REnv),
	$REnv' := assign-variable(FAILED, [$Error*, $Error], $REnv)
	===
	eval-rstat($Stat, $REnv) = increment(NFAILED, $REnv')
