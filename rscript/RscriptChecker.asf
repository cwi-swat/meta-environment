equations

%%-----------------------------------------------------------------------------------

[xxx] fun [] to $Etype = $Etype

%% --- Simplify n-ary tuple types

[st1]	$Etype+ = $Etype'
	=============================================================
	<$Etype1, $Etype2, $Etype+> = <$Etype1, <$Etype2, $Etype'>>

[st2]	$Etype+1 = $Etype', $Etype+2
	=============================================================
	<$Etype1, $Etype2, $Etype+1> = <$Etype1, <$Etype2, $Etype', $Etype+2>>

%% --- Simpleify n-ary relation type

[sr1]	<$Etype1, $Etype2, $Etype+1> = <$Etype1', $Etype2'>
	============================================================= 
	rel[$Etype1, $Etype2, $Etype+1] = rel[$Etype1', $Etype2']

%% -- Simplify n-ary tuple expressions

[st1]	$Exps = $Exp'
	=============================================================
	<$Exp1, $Exp2, $Exps> = <$Exp1, <$Exp2, $Exp'>>

[st2]	$Exps1 = $Exp', $Exps2
	=============================================================
	<$Exp1, $Exp2, $Exps1> = <$Exp1, <$Exp2, $Exp', $Exps2>>


%% --- Type of element

[to1]	type-of($Integer) = int 
[to2]	type-of($Boolean) = bool
[to3]	type-of($String)  = str
[to4]	type-of($Area)    = node

%% --- Equality on types: ==

[eqt1]	$Etype == $Etype = true

[eqt2]	rel[$Etype1,$Etype2] == bag[<$Etype1, $Etype2>] = true

[eqt3]	bag[<$Etype1,$Etype2>] == rel[$Etype1, $Etype2] = true

[default-eqt]
	$Etype1 == $Etype2 = false

%% --- Is simple type

[ist1]	is-simple-type(int)= true

[ist2]	is-simple-type(bool)= true
[ist3]	is-simple-type(str)	= true
[ist4]	is-simple-type(area) 	= true

[default-ist]
	is-simple-type($Etype) 	= false

%% --- Is tuple

[it1]	is-tuple(<$Elem1, $Elem2>) = true
[default-it]
	is-tuple($Elem) = false

%% --- If-then-else on type environments

[ite1]	if true then $Tenv1 else $Tenv2 fi = $Tenv1

[ite2]	if false then $Tenv1 else $Tenv2 fi = $Tenv2

%% --- If-then-else on <Type, TypeEnv>

[ite3]	if true then <$Etype1, $Tenv1> else <$Etype2, $Tenv2> fi = <$Etype1, $Tenv1>

[ite4]	if false then <$Etype1, $Tenv1> else <$Etype2, $Tenv2> fi = <$Etype2, $Tenv2>

%% --- Add error message

[te1] <$Table, errors[$Error*]> + $Error = <$Table, errors[$Error*;
$Error]>

%% --- Errors to ItemList

[i2s] rvar2str(rvar(Char*)) = strcon(""" Char* """)

[cve-1] convert-errors(errors[$Error+]) = convert-errors1(errors[$Error+])
[cve-1] convert-errors(errors[]) =  [listitem("Rscript is type correct!", area("",0,0,0,0))]


[cve-1] convert-errors1(errors[$Error*]) = [$ListItem*]
      	================================================
        convert-errors1(errors[$Error; $Error*])= [e2i($Error), $ListItem*]

[cve-2] convert-errors1(errors[]) = []

[e2i-1] e2i($Exp cannot be typed)	= listitem("Expression cannot be typed", get-area($Exp))
[e2i-2] e2i($Stat cannot be typed)	= listitem("Statement cannot be typed", get-area($Stat))
[e2i-3] e2i($From cannot be typed) 	= listitem("From cannot be typed", get-area($From))
[e2i-4] e2i($Bag cannot be typed) 	= listitem("Bag cannot be typed", get-area($Bag))
[e2i-5] e2i($Rel cannot be typed) 	= listitem("Relation cannot be typed", get-area($Rel))
[e2i-6] e2i(Undeclared $Var) 		= listitem("Undeclared: " || rvar2str($Var), get-area($Var))
[e2i-7] e2i($Var redeclared) 		= listitem("Redeclared: " || rvar2str($Var), get-area($Var))
[e2i-8] e2i(Incompatible argument types $Etype in $Exp) = 
		listitem("Incompatible arguments of type " || t2s($Etype), get-area($Exp))
[e2i-9] e2i($Exp should have type $Etype)= 
	listitem("Expression should have type " || t2s($Etype), get-area($Exp))
[e2i-10] e2i($Exp should be of type bag) = listitem("Expression should be of type bag", get-area($Exp))
[e2i-11] e2i($Exp should be of type rel) = listitem("Expression should be of type relation", get-area($Exp))
[e2i-12] e2i(No definition for $Var with argument $Etype) = 
        listitem("No definition for " || rvar2str($Var) || " with argument " || t2s($Etype), get-area($Var))

[t2s-1]	t2s(int) 			= "int"
[t2s-2] t2s(bool) 			= "bool"
[t2s-3] t2s(str) 			= "str"
[t2s-4] t2s(node) 			= "node"
[t2s-5] t2s(undefined) 			= "undefined"
[t2s-6] t2s(< $Etype1, $Etype2 >)	= "<" || t2s($Etype1) || "," || t2s($Etype2) || ">"
[t2s-7] t2s(bag[$Etype])		= "bag[" || t2s($Etype) || "]"
[t2s-8] t2s(rel[$Etype1, $Etype2]) 	= "rel[" || t2s($Etype1) || "," || t2s($Etype1) || "]"

[t2s-8] t2s([$Etype*]) 			= "[" || tl2sl($Etype*) || "]"
[t2s-9] t2s($Var) 			= rvar2str($Var)

[tl2sl-1]
	tl2sl($Etype, $Etype*)	= t2s($Etype) || " " || tl2sl($Etype*)
[tl2sl-2]
	tl2sl()			= ""


%% --- Lookup/store/delete

[te2] lookup(<$Table, $Errors>, $Var) = lookup($Table, $Var)
[te3] store(<$Table, $Errors>, $Var, $Etype) = <store($Table, $Var, $Etype), $Errors>
[te4] delete(<$Table, $Errors>, $Var)  = <delete($Table, $Var), $Errors>

%%-----------------------------------------------------------------------------------

[check-const-tuple1] 
      	check-const($Elem1, $Tenv) = <$Etype1, $Tenv'>, 
      	check-const($Elem2, $Tenv') = <$Etype2, $Tenv''>
    	============================================
     	check-const-tuple(<$Elem1, $Elem2>, $Tenv) = <<$Etype1, $Etype2>, $Tenv''>

%% --- Check Elementary values

[check-const-bool] 
      check-const($Boolean, $Tenv) = <type-of($Boolean), $Tenv>

[check-const-int] 
      check-const($Integer, $Tenv) = <type-of($Integer), $Tenv>

[check-const-str]
      check-const($String, $Tenv) = <type-of($String), $Tenv>

[check-const-node]
      check-const($Area, $Tenv) = <type-of($Area), $Tenv>

[check-const-tuple1] 
      	check-const($Elem1, $Tenv) = <$Etype1, $Tenv'>, 
      	check-const($Elem2, $Tenv') = <$Etype2, $Tenv''>
    	============================================
     	check-const(<$Elem1, $Elem2>, $Tenv) = <<$Etype1, $Etype2>, $Tenv''>

[check-const-bag1]
      check-const($Bag, $Tenv) = check-const-bag-or-rel($Bag, $Tenv)

[check-const-br1]
      	is-tuple($Elem) = false,
       	check-const($Elem, $Tenv) = <$Etype, $Tenv'>,
       	$Etype' = bag[$Etype],
       	check-const-bag-or-rel({ $Elem+ }, $Tenv') = <$Etype', $Tenv''>
       	==========================================
       	check-const-bag-or-rel({ $Elem, $Elem+ }, $Tenv) = <$Etype', $Tenv''>

[check-const-br2] 
	is-tuple($Elem) = false,
       	check-const($Elem, $Tenv) = <$Etype, $Tenv'>,
       	$Etype' =  bag[$Etype]
       	==========================================
       	check-const-bag-or-rel({ $Elem }, $Tenv) = <$Etype', $Tenv'>

[check-const-br31]
	is-tuple($Elem) = true,   
	check-const-tuple($Elem, $Tenv) = <<$Etype1, $Etype2>, $Tenv'>, 
	$Etype = rel[$Etype1,$Etype2],
      	check-const-bag-or-rel({ $Elem+ }, $Tenv') = <$Etype, $Tenv''>
      	==========================================
      	check-const-bag-or-rel({ $Elem, $Elem+ }, $Tenv) = <$Etype, $Tenv''>

[check-const-br4]
	is-tuple($Elem) = true, 
	check-const-tuple($Elem, $Tenv) = <<$Etype1, $Etype2>, $Tenv'>, 
	$Etype = rel[$Etype1,$Etype2]
       ==========================================
       check-const-bag-or-rel({ $Elem }, $Tenv) = <$Etype, $Tenv'>

%%-----------------------------------------------------------------------------------

%% Check arbitrary expressions

[check-const1]
      check($Elem, $Tenv) = check-const($Elem, $Tenv)

[check-var] 
      lookup($Tenv, $Var) = $Etype
      ============================================
      check($Var, $Tenv) = if $Etype == not-in-table then <undefined, $Tenv + Undeclared $Var> else <$Etype, $Tenv> fi

[check-tuple-const]
 	check($Tuple, $Tenv) = check-const-tuple($Tuple, $Tenv)	

[check-tuple-exp1] 
      	check($Exp1, $Tenv) = <$Etype1, $Tenv'>, 
      	check($Exp2, $Tenv') = <$Etype2, $Tenv''>
      	============================================
      	check(<$Exp1, $Exp2>, $Tenv) = <<$Etype1, $Etype2>, $Tenv''>


[ct1]	container-type(<$Etype1, $Etype2>) = rel[$Etype1,$Etype2]
[default-ct2] 
	container-type($Etype) = bag[$Etype]

%% --- Check bag/relation  expressions

[check-bag-rel1] 
       check($Exp, $Tenv) = <$Etype, $Tenv'>,
       $Etype' =  container-type($Etype),
       check({ $Exp+ }, $Tenv') = <$Etype', $Tenv''>
       ==========================================
       check({ $Exp, $Exp+ }, $Tenv) = <$Etype', $Tenv''>

[check-bag-rel2] 
       check($Exp, $Tenv) = <$Etype, $Tenv'>,
       $Etype' =  container-type($Etype)
       ==========================================
       check({ $Exp }, $Tenv) = <$Etype', $Tenv'>

[check-bag-rel3]
	check( {}, $Tenv) =  <undefined, $Tenv + {} cannot be typed>

[default-bag-rel]
	check({ $Exp* }, $Tenv) = <undefined, $Tenv + {$Exp*} cannot be typed>

%% --- Check Operators

[check-and] 
      $Exp = $Exp1 and $Exp2,
      check($Exp1, $Tenv) = <$Etype1, $Tenv'>, 
      check($Exp2, $Tenv') = <$Etype2, $Tenv''>,
      compat($Exp, $Etype1 and $Etype2, $Tenv'') = <$Etype3, $Tenv'''>
      ===========================================
      check($Exp, $Tenv) = <bool, $Tenv'''>

[compat-and] 
      compat($Exp, bool and bool, $Tenv) = <bool, $Tenv>

[check-or] 
      $Exp = $Exp1 or $Exp2,
      check($Exp1, $Tenv) = <$Etype1, $Tenv'>, 
      check($Exp2, $Tenv') = <$Etype2, $Tenv''>,
      compat($Exp, $Etype1 or $Etype2, $Tenv'') = <$Etype3, $Tenv'''>
      ===========================================
      check($Exp, $Tenv) = <bool, $Tenv'''>

[compat-or] 
      compat($Exp, bool or bool, $Tenv) = <bool, $Tenv>

[check-implies] 
      $Exp = $Exp1 implies $Exp2,
      check($Exp1, $Tenv) = <$Etype1, $Tenv'>, 
      check($Exp2, $Tenv') = <$Etype2, $Tenv''>,
      compat($Exp, $Etype1 or $Etype2, $Tenv'') = <$Etype3, $Tenv'''>
      ===========================================
      check($Exp, $Tenv) = <bool, $Tenv'''>

[compat-implies] 
      compat($Exp, bool implies bool, $Tenv) = <bool, $Tenv>

[check-not] 
      check($Exp, $Tenv) = <bool, $Tenv'>
      ===========================================
      check(not $Exp, $Tenv) = <bool, $Tenv'>

[check-in1] 
      $Exp = $Exp1 in $Exp2,
      check($Exp1, $Tenv) = <$Etype1, $Tenv'>, 
      check($Exp2, $Tenv') = <$Etype2, $Tenv''>,
      compat($Exp, $Etype1 in $Etype2, $Tenv'') = <$Etype3, $Tenv'''>
      ===========================================
      check($Exp, $Tenv) = <bool, $Tenv'''>

[compat-in1] 
      compat($Exp, $Etype in bag[$Etype], $Tenv) = <bool, $Tenv>
[compat-2] 
      compat($Exp, <$Etype1, $Etype2> in rel[$Etype1,$Etype2], $Tenv) = <bool, $Tenv>

[check-notin1] 
      $Exp = $Exp1 notin $Exp2,
      check($Exp1, $Tenv) = <$Etype1, $Tenv'>, 
      check($Exp2, $Tenv') = <$Etype2, $Tenv''>,
      compat($Exp, $Etype1 notin $Etype2, $Tenv'') = <$Etype3, $Tenv'''>
      ===========================================
      check($Exp, $Tenv) = <bool, $Tenv'''>

[compat-notin1] 
      compat($Exp, $Etype notin bag[$Etype], $Tenv) = <bool, $Tenv>
[compat-notin2] 
      compat($Exp, <$Etype1, $Etype2> notin rel[$Etype1,$Etype2], $Tenv) = <bool, $Tenv>

[check-eq] 
      $Exp = $Exp1 == $Exp2,
      check($Exp1, $Tenv) = <$Etype1, $Tenv'>, 
      check($Exp2, $Tenv') = <$Etype2, $Tenv''>,
      compat($Exp, $Etype1 == $Etype2, $Tenv'') = <$Etype3, $Tenv'''>
      ===========================================
      check($Exp, $Tenv) = <bool, $Tenv'''>

[compat-eq]
      compat($Exp, $Etype == $Etype, $Tenv) = <bool, $Tenv>

[check-ne] 
      $Exp = $Exp1 != $Exp2,
      check($Exp1, $Tenv) = <$Etype1, $Tenv'>, 
      check($Exp2, $Tenv') = <$Etype2, $Tenv''>,
      compat($Exp, $Etype1 != $Etype2, $Tenv'') = <$Etype3, $Tenv'''>
      ===========================================
      check($Exp, $Tenv) = <bool, $Tenv'''>

[compat-ne]
      compat($Exp, $Etype != $Etype, $Tenv) = <bool, $Tenv>

[check-le] 
      $Exp = $Exp1 <= $Exp2, 
      check($Exp1, $Tenv) = <$Etype1, $Tenv'>, 
      check($Exp2, $Tenv') = <$Etype2, $Tenv''>,
      compat($Exp, $Etype1 <= $Etype2, $Tenv'') = <$Etype3, $Tenv'''>
      ===========================================
      check($Exp, $Tenv) = <bool, $Tenv'''>

[compat-le]
      compat($Exp, $Etype <= $Etype, $Tenv) = <bool, $Tenv>

[check-lt] 
      $Exp = $Exp1 < $Exp2,
      check($Exp1, $Tenv) = <$Etype1, $Tenv'>, 
      check($Exp2, $Tenv') = <$Etype2, $Tenv''>,
      compat($Exp, $Etype1 < $Etype2, $Tenv'') = <$Etype3, $Tenv'''>
      ===========================================
      check($Exp, $Tenv) = <bool, $Tenv'''>

[compat-lt]
      compat($Exp, $Etype < $Etype, $Tenv) = <bool, $Tenv>

[check-ge]
      $Exp = $Exp1 >= $Exp2,
      check($Exp1, $Tenv) = <$Etype1, $Tenv'>, 
      check($Exp2, $Tenv') = <$Etype2, $Tenv''>,
      compat($Exp, $Etype1 >= $Etype2, $Tenv'') = <$Etype3, $Tenv'''>
      ===========================================
      check($Exp, $Tenv) = <bool, $Tenv'''>

[compat-ge]
      compat($Exp, $Etype >= $Etype, $Tenv) = <bool, $Tenv>

[check-gt]
      $Exp = $Exp1 > $Exp2,
      check($Exp1, $Tenv) = <$Etype1, $Tenv'>, 
      check($Exp2, $Tenv) = <$Etype2, $Tenv''>,
      compat($Exp, $Etype1 > $Etype2, $Tenv'') = <$Etype3, $Tenv'''>
      ===========================================
      check($Exp, $Tenv) = <bool, $Tenv'''>

[compat-gt]
      compat($Exp, $Etype > $Etype, $Tenv) = <bool, $Tenv>

[xxx] compat1($Exp, bag[$Etype], 		bag[$Etype], $Tenv) 		= <bag[$Etype], $Tenv>
[xxx] compat1($Exp, bag[$Etype], 		$Etype, $Tenv) 			= <bag[$Etype], $Tenv>
[xxx] compat1($Exp, $Etype, 			bag[$Etype], $Tenv) 		= <bag[$Etype], $Tenv>
[xxx] compat1($Exp, rel[$Etype1,$Etype2],	rel[$Etype1,$Etype2], $Tenv) 	= <rel[$Etype1,$Etype2], $Tenv>
[xxx] compat1($Exp, <$Etype1,$Etype2>,		rel[$Etype1,$Etype2], $Tenv) 	= <rel[$Etype1,$Etype2], $Tenv>
[xxx] compat1($Exp, rel[$Etype1,$Etype2],	<$Etype1,$Etype2>, $Tenv)    	= <rel[$Etype1,$Etype2], $Tenv>
[xxx] compat1($Exp, <$Etype1,$Etype2>,		<$Etype1,$Etype2>, $Tenv)    	= <rel[$Etype1,$Etype2], $Tenv>
[xxx] compat1($Exp, rel[$Etype1,$Etype2],	bag[<$Etype1,$Etype2>], $Tenv)	= <rel[$Etype1,$Etype2], $Tenv>
[xxx] compat1($Exp, bag[<$Etype1,$Etype2>],	rel[$Etype1,$Etype2], $Tenv)    = <rel[$Etype1,$Etype2], $Tenv>
[xxx]compat1($Exp, $Etype,			$Etype, $Tenv)              	= <bag[$Etype], $Tenv> when is-simple-type($Etype) = true
[default-xxx]
	compat1($Exp, $Etype1, $Etype2, $Tenv) = <undefined, $Tenv + Incompatible argument types  [$Etype1, $Etype2] in $Exp>  

[check-union]
      $Exp = $Exp1 union $Exp2,
      check($Exp1, $Tenv) = <$Etype1, $Tenv'>, 
      check($Exp2, $Tenv') = <$Etype2, $Tenv''>,
      compat1($Exp, $Etype1, $Etype2,$Tenv'') = <$Etype3, $Tenv'''>
      ===========================================
      check($Exp, $Tenv) = <$Etype3, $Tenv'''>

[c03] 
      $Exp = $Exp1 inter $Exp2,
      check($Exp1, $Tenv) = <$Etype1, $Tenv'>, 
      check($Exp2, $Tenv') = <$Etype2, $Tenv''>,
      compat1($Exp, $Etype1, $Etype2, $Tenv'') = <$Etype3, $Tenv'''>
      ===========================================
      check($Exp, $Tenv) = <$Etype3, $Tenv'''>

[c03] $Exp = $Exp1 \ $Exp2,
      check($Exp1, $Tenv) = <$Etype1, $Tenv'>, 
      check($Exp2, $Tenv') = <$Etype2, $Tenv''>,
      compat1($Exp, $Etype1, $Etype2, $Tenv'') = <$Etype3, $Tenv'''>
      ===========================================
      check($Exp, $Tenv) = <$Etype3, $Tenv'''>

[c03] $Exp = $Exp1 o $Exp2,
      check($Exp1, $Tenv) = <$Etype1, $Tenv'>, 
      check($Exp2, $Tenv') = <$Etype2, $Tenv''>,
      compat($Exp, $Etype1 o $Etype2, $Tenv'') = <$Etype3, $Tenv'''>
      ===========================================
      check($Exp, $Tenv) = <$Etype3, $Tenv'''>

[xxx] compat($Exp, rel[$Etype1,$Etype2] o rel[$Etype2,$Etype3], $Tenv) = <rel[$Etype1,$Etype3], $Tenv>
[xxx] compat($Exp, rel[$Etype1,$Etype2] o <$Etype2,$Etype3>, $Tenv) = <rel[$Etype1,$Etype3], $Tenv>
[xxx] compat($Exp, <$Etype1,$Etype2> o rel[$Etype2,$Etype3], $Tenv) = <rel[$Etype1,$Etype3], $Tenv>
[xxx] compat($Exp, <$Etype1,$Etype2> o <$Etype2,$Etype3>, $Tenv) = <rel[$Etype1,$Etype3], $Tenv>
[xxx] compat($Exp, rel[$Etype1,$Etype2] o bag[<$Etype2,$Etype3>], $Tenv) = <rel[$Etype1,$Etype3], $Tenv>
[xxx] compat($Exp, bag[<$Etype1,$Etype2>] o rel[$Etype2,$Etype3], $Tenv) = <rel[$Etype1,$Etype3], $Tenv>

[c03] $Exp = $Exp1 x $Exp2,
      check($Exp1, $Tenv) = <$Etype1, $Tenv'>, 
      check($Exp2, $Tenv') = <$Etype2, $Tenv''>,
      compat($Exp, $Etype1 x $Etype2, $Tenv'') = <$Etype3, $Tenv'''>
      ===========================================
      check($Exp, $Tenv) = <$Etype3, $Tenv'''>

[xxx] compat($Exp, bag[$Etype1] x bag[$Etype2], $Tenv) = <rel[$Etype1,$Etype2], $Tenv>
[xxx] compat($Exp, $Etype1 x bag[$Etype2], $Tenv) = <rel[$Etype1,$Etype2], $Tenv> when is-simple-type($Etype1) = true
[xxx] compat($Exp, bag[$Etype1] x $Etype2, $Tenv) = <rel[$Etype1,$Etype2], $Tenv> when is-simple-type($Etype2) = true
[xxx] compat($Exp, $Etype1 x $Etype2, $Tenv) = <rel[$Etype1,$Etype2], $Tenv> when is-simple-type($Etype1) = true, is-simple-type($Etype2) = true

[c03] $Exp = $Exp1 . $Exp2,
      check($Exp1, $Tenv) = <$Etype1, $Tenv'>, 
      check($Exp2, $Tenv') = <$Etype2, $Tenv''>,
      compat($Exp, $Etype1 . $Etype2, $Tenv'') = <$Etype3, $Tenv'''>
      ===========================================
      check($Exp, $Tenv) = <$Etype3, $Tenv'''>

[xxx] compat($Exp, rel[$Etype1,$Etype2] . $Etype2, $Tenv)              = <bag[$Etype1], $Tenv> %%when is-simple-type($Etype2) = true
[xxx] compat($Exp, bag[<$Etype1,$Etype2>] . $Etype2, $Tenv)            = <bag[$Etype1], $Tenv> %%when is-simple-type($Etype2) = true
[xxx] compat($Exp, rel[$Etype1,$Etype2] . bag[$Etype2], $Tenv)         = <bag[$Etype1], $Tenv>
[xxx] compat($Exp, bag[<$Etype1,$Etype2>] . bag[$Etype2], $Tenv)       = <bag[$Etype1], $Tenv>
[xxx] compat($Exp, $Etype1              . rel[$Etype1,$Etype2], $Tenv) = <bag[$Etype2], $Tenv> %%when is-simple-type($Etype1) = true
[xxx] compat($Exp, $Etype1              . bag[<$Etype1,$Etype2>], $Tenv) = <bag[$Etype2], $Tenv>%% when is-simple-type($Etype1) = true
[xxx] compat($Exp, bag[$Etype1]         . rel[$Etype1,$Etype2], $Tenv) = <bag[$Etype2], $Tenv>
[xxx] compat($Exp, bag[$Etype1]         . bag[<$Etype1,$Etype2>], $Tenv) = <bag[$Etype2], $Tenv>

[xxx] 	$Exp = $Exp1 [ $Exp2 ],
      	check($Exp1, $Tenv) = <$Etype1, $Tenv'>, 
      	check($Exp2, $Tenv') = <$Etype2, $Tenv''>,
      	compat($Exp, $Etype2 . $Etype1, $Tenv'') = <$Etype3, $Tenv'''>
      	===========================================
	check($Exp, $Tenv) = <$Etype3, $Tenv'''>
 
[c03] $Exp = $Exp1 +,
      check($Exp1, $Tenv) = <$Etype, $Tenv'>,
      compat($Exp, $Etype +, $Tenv') = <$Etype1, $Tenv''>
      ===========================================
      check($Exp, $Tenv) = <$Etype1, $Tenv''>

[xxx] compat($Exp, rel[$Etype,$Etype]+, $Tenv) =   <rel[$Etype, $Etype], $Tenv>
[xxx] compat($Exp, bag[<$Etype,$Etype>]+, $Tenv) = <rel[$Etype, $Etype], $Tenv>

[c03] $Exp = $Exp1 *,
      check($Exp1, $Tenv) = <$Etype, $Tenv'>,
      compat($Exp, $Etype *, $Tenv') = <$Etype1, $Tenv''>
      ===========================================
      check($Exp, $Tenv) = <$Etype1, $Tenv''>

[xxx] compat($Exp, rel[$Etype,$Etype] *, $Tenv) = <rel[$Etype, $Etype], $Tenv>
[xxx] compat($Exp, bag[<$Etype,$Etype>] *, $Tenv) = <rel[$Etype, $Etype], $Tenv>

[xxx] $Exp = #  $Exp1,
      check($Exp1, $Tenv) = <$Etype, $Tenv'>,
      compat($Exp, # $Etype, $Tenv') = <$Etype1, $Tenv''>
      ===========================================
      check($Exp, $Tenv) = <$Etype1, $Tenv''>

[xxx] compat($Exp, # bag[$Etype], $Tenv) = <int, $Tenv>
[xxx] compat($Exp, # rel[$Etype1,$Etype2], $Tenv) = <int, $Tenv>


%% Check value formers

[rt1] 	check($Exp, $Tenv) = <$Etype', $Tenv'>
      	=========================================
      	require-type($Exp, $Etype, $Tenv) = 
      	if $Etype == $Etype' then $Tenv' else $Tenv' + $Exp should have type $Etype fi

[rbt1]	store($Tenv, $Var, $Etype) = $Tenv'
      	==========================================
      	require-bag-type($Exp, $Var, bag[$Etype], $Tenv) = $Tenv'

[default-rbt]
      	store($Tenv, $Var, undefined) = $Tenv'
      	==========================================
      	require-bag-type($Exp, $Var, $Etype, $Tenv) = $Tenv' + $Exp should be of type bag

[rbt1]  store($Tenv, $Var1, $Etype1) = $Tenv',
      	store($Tenv', $Var2, $Etype2) = $Tenv''
      	==========================================
     	require-rel-type($Exp, $Var1, $Var2, rel[$Etype1, $Etype2], $Tenv) = $Tenv''

[rbt1]	store($Tenv, $Var1, $Etype1) = $Tenv',
      	store($Tenv', $Var2, $Etype2) = $Tenv''
      	==========================================
      	require-rel-type($Exp, $Var1, $Var2, bag[<$Etype1, $Etype2>], $Tenv) = $Tenv''

[default-rbt]
      	store($Tenv, $Var1, undefined) = $Tenv',
      	store($Tenv', $Var2, undefined) = $Tenv''
      	==========================================
      	require-rel-type($Exp, $Var1, $Var2, $Etype, $Tenv) = $Tenv'' + $Exp should be of type rel

[rnt1]	require-nrel-type($Exp, $Var1, $Var2, $Etype, $Tenv) = require-rel-type($Exp, $Var1, $Var2, $Etype, $Tenv)

[rnt1]	store($Tenv, $Var1, $Etype1) = $Tenv'
	=======================================
	require-nrel-type($Exp, $Var1, $Var2, $Var+, rel[$Etype1, $Etype2], $Tenv) = 
	require-nrel-type($Exp, $Var2, $Var+, bag[$Etype2], $Tenv')

[rnt1]	store($Tenv, $Var1, $Etype1) = $Tenv'
	=======================================
	require-nrel-type($Exp, $Var1, $Var2, $Var+, bag[<$Etype1, $Etype2>], $Tenv) = 
	require-nrel-type($Exp, $Var2, $Var+, bag[$Etype2], $Tenv')

[default-nre]
	require-nrel-type($Exp, $Var+, $Etype, $Tenv) = $Tenv + $Exp should be of type rel
	

%% Check from clauses and add introduced variables to type environment

[af1] 	check($Exp, $Tenv) = <$Etype, $Tenv'>,
      	require-bag-type($Exp, $Var, $Etype, $Tenv') = $Tenv''
      	==========================================
      	add-from($Var : $Exp, $From*, $Tenv) = add-from($From*, $Tenv'')

[af2] 	check($Exp, $Tenv) = <$Etype, $Tenv'>, 
      	require-rel-type($Exp, $Var1, $Var2, $Etype, $Tenv') = $Tenv''
      	==========================================
      	add-from(<$Var1, $Var2> : $Exp, $From*, $Tenv) = add-from($From*, $Tenv'')

[af3] 	check($Exp, $Tenv) = <$Etype, $Tenv'>, 
      	require-nrel-type($Exp, $Var1, $Var2, $Var+, $Etype, $Tenv') = $Tenv''	
      	==========================================
      	add-from(<$Var1, $Var2, $Var+> : $Exp, $From*, $Tenv) = add-from($From*, $Tenv'')

[af2] 	require-type($Exp, bool, $Tenv) = $Tenv'
      	==========================================
      	add-from($Exp, $From*, $Tenv) = add-from($From*, $Tenv')

[af1] 	check($Exp, $Tenv) = <$Etype, $Tenv'>, 
	store($Tenv', $Var, $Etype) = $Tenv''
      	==========================================
      	add-from($Var <- $Exp, $From*, $Tenv) = add-from($From*, $Tenv'')

[af1] 	check($Exp, $Tenv) = <<$Etype1, $Etype2>, $Tenv'>, 
	store-nvar($Var1, $Var2, <$Etype1, $Etype2>, $Tenv') = $Tenv''
      	==========================================
      	add-from(<$Var1, $Var2> <- $Exp, $From*, $Tenv) = add-from($From*, $Tenv'')

[af1] 	check($Exp, $Tenv) = <<$Etype1, $Etype2>, $Tenv'>, 
	store-nvar($Var1, $Var2, $Var+,<$Etype1, $Etype2>, $Tenv') = $Tenv''
      	==========================================
      	add-from(<$Var1, $Var2, $Var+> <- $Exp, $From*, $Tenv) = add-from($From*, $Tenv'')

[af3] 	add-from( , $Tenv) = $Tenv

[default-af]
      	add-from($From, $From*, $Tenv) = $Tenv + $From cannot be typed

%% -- Assign to list of variables

[sv1]	store($Tenv, $Var, $Etype1) = $Tenv'
	=====================================================
	store-nvar($Var, $Var+, <$Etype1, $Etype2>, $Tenv) =
	store-nvar($Var+, $Etype2, $Tenv')

[sv2]	store($Tenv, $Var, $Etype) = $Tenv'
	=====================================================
	store-nvar($Var, $Etype, $Tenv) = $Tenv'

%% ---  Delete a list of variables from the type environment

[dnv1]	delete($Tenv, $Var) = $Tenv'
	=========================================
	delete-nvar($Var, $Var+, $Tenv) = delete-nvar($Var+, $Tenv')

[dnv2]	delete-nvar($Var, $Tenv) = delete($Tenv, $Var)

%% Delete the variables introduced in from clauses from the type environment

[df1] 	delete-nvar($Var, $Tenv) = $Tenv'
      	==========================================
      	del-from($Var : $Exp, $From*, $Tenv) = del-from($From*, $Tenv')

[df2] 	delete-nvar($Var1, $Var2, $Tenv) = $Tenv'
      	==========================================
      	del-from(<$Var1, $Var2> : $Exp, $From*, $Tenv) = del-from($From*, $Tenv')

[df3]	delete-nvar($Var1, $Var2, $Var+, $Tenv) = $Tenv'
      	==========================================
      	del-from(<$Var1, $Var2, $Var+> : $Exp, $From*, $Tenv) = del-from($From*, $Tenv')

[df4]	del-from($Exp, $From*, $Tenv) = del-from($From*, $Tenv)

[df5]	delete($Tenv, $Var) = $Tenv'
	=================================================================
	del-from($Var <- $Exp, $From*, $Tenv) = del-from($From*, $Tenv')

[df6]	delete-nvar($Var1, $Var2, $Tenv) = $Tenv'
	=================================================================
	del-from(<$Var1, $Var2> <- $Exp, $From*, $Tenv) = del-from($From*, $Tenv')

[df7]	delete-nvar($Var1, $Var2, $Var+, $Tenv) = $Tenv'
	=================================================================
	del-from(<$Var1, $Var2, $Var+> <- $Exp, $From*, $Tenv) = del-from($From*, $Tenv')

[df8] 	del-from( , $Tenv) = $Tenv

[cit1] 	container-iter-type(<$Etype1,$Etype2>) = rel[$Etype1, $Etype2]
[cit2] 	container-iter-type(rel[$Etype1,$Etype2]) = rel[$Etype1, $Etype2]
[cit3] 	container-iter-type(bag[$Etype]) = bag[$Etype]

[default-cit]
      	container-iter-type($Etype) = bag[$Etype]

[check-valueformer3] 
      	add-from($From+, $Tenv) = $Tenv',
      	check($Exp, $Tenv') = <$Etype, $Tenv''>,
      	container-iter-type($Etype) = $Etype',
      	del-from($From+, $Tenv'') = $Tenv'''
      	======================================
      	check({ $Exp | $From+ }, $Tenv) = <$Etype', $Tenv'''>

%% --- Check Exists

[check-exists1]
	add-from($From, $Tenv) = $Tenv',
	require-type($Test, bool, $Tenv') = $Tenv'',
	del-from($From, $Tenv'') = $Tenv'''
	=====================================
	check(exists($From | $Test), $Tenv) = <bool, $Tenv'''>

%% --- Check Forall

[check-forall1]
	add-from($From, $Tenv) = $Tenv',
	require-type($Test, bool, $Tenv') = $Tenv'',
	del-from($From, $Tenv'') = $Tenv'''
	=====================================
	check(forall($From | $Test), $Tenv) = <bool, $Tenv'''>

%% --- Check function application

[ca1] check($Exp, $Tenv) = <$Etype, $Tenv'>,
      collect-actuals($Exp*, $Tenv') = < [$Etype*], $Tenv''>
      ===========================================
      collect-actuals($Exp, $Exp*, $Tenv) = <[$Etype, $Etype*], $Tenv''>

[ca2] collect-actuals( , $Tenv) = <[], $Tenv>

[check-appl] 
      collect-actuals($Exp*, $Tenv) = <$Etype, $Tenv'>
      ============================================
      check($Var ( $Exp* ), $Tenv) = find-result-type($Var, $Etype, $Tenv')

%% --- Check that variable is not yet defined

[cnd] check-not-defined($Var, $Tenv) = 
      if lookup($Tenv, $Var) == not-in-table then $Tenv else  $Tenv + $Var redeclared fi

%% add-formals

[af1] store($Tenv, $Var, $Etype) = $Tenv',
      add-formals([$DclVar*], $Tenv') = $Tenv''
      ===========================================
      add-formals([$Etype $Var, $DclVar*], $Tenv) = $Tenv''

[af2] add-formals([], $Tenv) = $Tenv

%% del-formals

[df1] delete($Tenv, $Var) = $Tenv',
      del-formals([$DclVar*], $Tenv') = $Tenv''
      ===========================================
      del-formals([$Etype $Var, $DclVar*], $Tenv) = $Tenv''

[df2] del-formals([], $Tenv) = $Tenv

%% Extract the types from a list of formal parameters

[et1]  extract-types([$DclVar*]) = [$Etype*]
       ===========================================
      extract-types([$Etype $Var, $DclVar*]) = [$Etype, $Etype*]

[et2] extract-types([]) = []

%% --- Check variable declarations

[check-vardef1]
      check-stat($Etype $Var, $Tenv) = store($Tenv, $Var, $Etype)

[check-vardef2] 
      check-stat($Etype $Var = $Exp, $Tenv) =  check-stat($Etype $Var() = $Exp, $Tenv)

[check-vardef3]
      check-stat(bag[$Etype] $Var = {}, $Tenv) = store($Tenv, $Var, bag[$Etype])

[check-vardef4]
      check-stat(rel[$Etype1,$Etype2] $Var = {}, $Tenv) = store($Tenv, $Var, rel[$Etype1,$Etype2])

%% --- Check function declarations

[check-fundef] 
      check-not-defined($Var, $Tenv) = $Tenv1,
      add-formals([$DclVar*], $Tenv1) = $Tenv2,
      require-type($Exp, $Etype, $Tenv2) = $Tenv3,      
      del-formals([$DclVar*], $Tenv3) = $Tenv4, 
      store($Tenv4, $Var, fun extract-types([$DclVar*]) to $Etype) = $Tenv5 
      ===========================================
      check-stat($Etype $Var ($DclVar*) = $Exp, $Tenv) = $Tenv5

%% --- Check assert statement

[check-assert]
	check-stat(assert $String : $Exp, $Tenv) = require-type($Exp, bool, $Tenv)

%% --- Check set of equations

[CSL11]	check-stat(solve ($Integer) { $Var = $Exp }, $Tenv) = 
	check-stat(solve { $Var = $Exp }, $Tenv)

[CSL12]	check($Var, $Tenv) = <$Etype, $Tenv1>,
	require-type($Exp, $Etype, $Tenv1) = $Tenv2
	==========================================
	check-stat(solve { $Var = $Exp }, $Tenv) = $Tenv2

[CSL21]	check-stat(solve ($Integer) { $Var1 = $Exp1, $Var2 = $Exp2 }, $Tenv) =
	check-stat(solve { $Var1 = $Exp1, $Var2 = $Exp2 }, $Tenv)

[CSL22]	check($Var1, $Tenv) = <$Etype1, $Tenv1>,
	require-type($Exp1, $Etype1, $Tenv1) = $Tenv2,
	check($Var2, $Tenv2) = <$Etype2, $Tenv3>,
	require-type($Exp2, $Etype2, $Tenv3) = $Tenv4
	==========================================
	check-stat(solve { $Var1 = $Exp1, $Var2 = $Exp2 }, $Tenv) = $Tenv4

[default-check-stat]
	 check-stat($Stat, $Tenv) = $Tenv +  $Stat cannot be typed

%% -- Default cases for check

[default-check]
      check($Exp, $Tenv) = <undefined, $Tenv +  $Exp cannot be typed>

%% --- Default case for compat

[xxx]  compat($Exp, undefined $Op $Etype2, $Tenv) = <undefined, $Tenv>
[xxx]  compat($Exp, $Etype1 $Op undefined, $Tenv) = <undefined, $Tenv>
[default-xxx]
      compat($Exp, $Etype1 $Op $Etype2, $Tenv) = <undefined, $Tenv + Incompatible argument types [$Etype1, $Etype2] in $Exp>

[xxx]  compat($Exp, undefined $Op, $Tenv) = <undefined, $Tenv>
[default-xxx]
      compat($Exp, $Etype $Op, $Tenv) = <undefined, $Tenv + Incompatible argument types [$Etype] in $Exp>

[xxx]  compat($Exp, $Op undefined, $Tenv) = <undefined, $Tenv>
[default-xxx]
      compat($Exp, $Op $Etype, $Tenv) = <undefined, $Tenv + Incompatible argument types [$Etype] in $Exp>

%% --- Check script

[xxx] $Rscript = $Stat*,
       check-rscript($Stat*,  <built-ins, errors[]>) = <$Table, $Errors>
      ===============================================
      check-rscript($Rscript) = convert-errors($Errors)

%% [xxx] check-rscript($Stat*) = check-rscript($Stat*, <built-ins, []>)
[xxx] check-rscript($Stat $Stat*, $Tenv) = check-rscript($Stat*, check-stat($Stat, $Tenv))
[xxx] check-rscript( , $Tenv) = $Tenv

%% Find result type of a function, given list of argument types

[xxx] lookup($Tenv, $Var) = not-in-table
      ==================================
      find-result-type($Var, [$Etype*], $Tenv) = <undefined, $Tenv + Undeclared $Var>

[xxx] lookup($Tenv, $Var) = fun [$Etype*2] to $Etype,
      match-types([$Etype*2],  [$Etype*1], new-table) = <true, $Table>
     ===========================================
      find-result-type($Var, [$Etype*1], $Tenv) = substitute($Etype, $Table, $Tenv)

[default-xxx]
      find-result-type($Var, [$Etype*1], $Tenv) = <undefined, $Tenv + No definition for $Var with argument [$Etype*1]>

%% is-var 

[iv1]  is-var($Var) = true
[default-iv] 
       is-var($Etype) = false

%% --- match types

[mt1]  is-var($Etype) = false
       ================================================
       match-types($Etype, $Etype, $Table) = <true, $Table>

[mt2]  lookup($Table, $Var) = not-in-table
      ==================================
       match-types($Var, $Etype, $Table) = <true, store($Table, $Var, $Etype)>

[mt3]  lookup($Table, $Var) = $Etype
      ==================================
       match-types($Var, $Etype, $Table) = <true, $Table>

[mt4]  match-types($Etype1, $Etype2, $Table) = <true, $Table'>
       ===================================================
       match-types(bag[$Etype1], bag[$Etype2], $Table) =  <true, $Table'>

[mt5]  match-types($Etype1, $Etype3, $Table) = <true, $Table'>,
       match-types($Etype2, $Etype4, $Table') = <true, $Table''>
       ===================================================
       match-types(rel[$Etype1, $Etype2], rel[$Etype3, $Etype4], $Table) =  <true, $Table''>

[mt6]  match-types($Etype1, $Etype3, $Table) = <true, $Table'>,
       match-types($Etype2, $Etype4, $Table') = <true, $Table''>
       ===================================================
       match-types(rel[$Etype1, $Etype2], bag[<$Etype3, $Etype4>], $Table) =  <true, $Table''>

[mt7]  match-types($Etype1, $Etype3, $Table) = <true, $Table'>,
       match-types($Etype2, $Etype4, $Table') = <true, $Table''>
       ===================================================
       match-types(bag[<$Etype1, $Etype2>], rel[$Etype3, $Etype4], $Table) =  <true, $Table''>

[mt8]  match-types($Etype1, $Etype3, $Table) = <true, $Table'>,
       match-types($Etype2, $Etype4, $Table') = <true, $Table''>
       ===================================================
       match-types(<$Etype1, $Etype2>, <$Etype3, $Etype4>, $Table) =  <true, $Table''>

[mt9]  match-types($Etype1, $Etype2, $Table) = <true, $Table'>,
       match-types([$Etype*1], [$Etype*2], $Table') = <true, $Table''>
       ===================================================
       match-types([$Etype1, $Etype*1], [$Etype2, $Etype*2], $Table) = <true, $Table''>

[mt10]  match-types([], [], $Table) = <true, $Table>

[default-mt]
	match-types($Etype1, $Etype2, $Table) = <false, $Table>

%% --- Substitute type

[sb1]  $Etype = lookup($Table, $Var)
       ======================================================
       substitute($Var, $Table, $Tenv) = if $Etype == not-in-table then <undefined, $Tenv + Undeclared $Var>
                                       else <$Etype, $Tenv> fi

[sb2]  substitute($Etype, $Table, $Tenv) = <$Etype', $Tenv'>
       ======================================================
       substitute(bag[$Etype], $Table, $Tenv) = <bag[$Etype'], $Tenv>

[sb3]  substitute($Etype1, $Table, $Tenv) = <$Etype1', $Tenv'>,
       substitute($Etype2, $Table, $Tenv) = <$Etype2', $Tenv''>
       =======================================================
       substitute(rel[$Etype1,$Etype2], $Table, $Tenv) = <rel[$Etype1', $Etype2'], $Tenv''>

[sb4]  substitute($Etype1, $Table, $Tenv) = <$Etype1', $Tenv'>,
       substitute($Etype2, $Table, $Tenv) = <$Etype2', $Tenv''>
       =======================================================
       substitute(<$Etype1,$Etype2>, $Table, $Tenv) = <<$Etype1', $Etype2'>, $Tenv''>

[default-sb]
       substitute($Etype, $Table, $Tenv) = <$Etype, $Tenv>

%% -- The types of the built-in functions

[bi-1]  built-ins = [
		<id,      	fun [ bag[T] ] 				to rel[T,T]>,
      		<domain,  	fun [ rel[T1,T2] ] 			to bag[T1]>,
     		<range,   	fun [ rel[T1,T2] ] 			to bag[T2]>,
       		<carrier, 	fun [ rel[T,T] ] 			to bag[T]>,
		<top,     	fun [ rel[T,T] ] 			to bag[T]>,
		<bottom,  	fun [ rel[T,T] ] 			to bag[T]>,
       		<inv,     	fun [ rel[T1,T2] ] 			to rel[T2,T1]>,
		<compl,   	fun [ rel[T1, T2] ] 			to rel[T1,T2]>,

       		<domainR, 	fun [ rel[T1,T2], bag[T1] ]		to rel[T1,T2]>,
       		<rangeR,    	fun [ rel[T1,T2], bag[T2] ] 		to rel[T1,T2]>,
       		<carrierR,    	fun [ rel[T,T], bag[T] ] 		to rel[T,T]>,

       		<domainX,    	fun [ rel[T1,T2], bag[T1] ] 		to rel[T1,T2]>,
       		<rangeX,    	fun [ rel[T1,T2], bag[T2] ] 		to rel[T1,T2]>,
       		<carrierX,    	fun [	rel[T,T], bag[T] ] 		to rel[T,T]>,
                <first,     	fun [ <T1,T2>] 				to T1>,
                <second,     	fun [ <T1,T2> ] 			to T2>,

		<power,		fun[ bag[T] ]				to bag[bag[T]]>,

                <reachX,  	fun [ bag[T], bag[T], rel[T,T] ] 	to rel[T,T] >,
                <reachR,  	fun [ bag[T], bag[T], rel[T,T] ] 	to rel[T,T] >

        ]

%% --- Where expressions

[WH1]	$Tenv = <$Table, $Errors>,
	check-rscript($Stat+, $Tenv) = $Tenv',
	check($Exp, $Tenv') = <$Etype, <$Table', $Errors'>>
	===================================================
	check($Exp where $Stat+ endwhere, $Tenv) = <$Etype, <$Table, $Errors'>>


