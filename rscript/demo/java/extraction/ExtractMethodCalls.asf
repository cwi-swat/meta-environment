equations

[extract-method-calls-start]
  &Loc := get-location(&CompilationUnit),
  &Rstore2 := extractMethodCalls(&CompilationUnit, &Rstore1, &Loc)
  ===>
  extractMethodCalls(&CompilationUnit, &Rstore1) = &Rstore2

[extract-method-calls-0]
  &Modifiers class &Identifier &Super &Interfaces &ClassBody := &ClassDeclaration,
  &ClassId := get-location(&Identifier),
  &Rstore2 := extractMethodCalls(&ClassBody, &Rstore1, &ClassId)
  ===>
  extractMethodCalls(&ClassDeclaration, &Rstore1, &Loc) = &Rstore2

[extract-method-calls-1]
  &MethodHeader &MethodBody := &MethodDeclaration,
  &Modifiers &MethodRes &MethodDeclarator &Throws := &MethodHeader,
  &Identifier ( &FormalParameters ) := &MethodDeclarator,
  &MethodId := get-location(&Identifier),
  &Rstore2 := extractMethodCalls(&MethodBody, &Rstore1, &MethodId)
  ===>
  extractMethodCalls(&MethodDeclaration, &Rstore1, &Loc) = &Rstore2

[extract-method-calls-2]
  &Name ( &Expressions ) := &MethodInvocation,
  &Var . &MethodCall := &Name,
  &Rstore2 := addMethodCall(&Var, &MethodCall, &Loc, &Rstore1)
  ===>
  extractMethodCalls(&MethodInvocation, &Rstore1, &Loc) = &Rstore2

[extract-method-calls-3]
  super ( &Expressions ) ; := &ExplicitConstructorInvocation,
  &ExplicitConstructorInvocationId := get-location(&ExplicitConstructorInvocation),
  &Rstore2 := addSuperCall(&ExplicitConstructorInvocationId, &Loc, &Rstore1)
  ===>
  extractMethodCalls(&ExplicitConstructorInvocation, &Rstore1, &Loc) = &Rstore2

[add-method-call-0]
  &VarId := get-location(&Var),
  &Tuple1 := <id2str(&Var), &VarId>,
  &Rstore2 := add-tuple(VARIABLE_USE, &Tuple1, &Rstore1),
  &Tuple2 := <&VarId, &Loc>,
  &Rstore3 := add-tuple(VARIABLE_USE_SCOPE, &Tuple2, &Rstore2),
  &Tuple3 := <&VarId, ids2str(&MethodCall)>,
  &Rstore4 := add-tuple(VARIABLE_USE_CALL, &Tuple3, &Rstore3)
  ===>
  addMethodCall(&Var, &MethodCall, &Loc, &Rstore1) = &Rstore4

[add-super-call-0]
  &Tuple := <&ExplicitConstructorInvocationId, &Loc>,
  &Rstore2 := add-tuple(SUPER_CALL, &Tuple, &Rstore1)
  ===>
  addSuperCall(&ExplicitConstructorInvocationId, &Loc, &Rstore1) = &Rstore2
