equations

[as-1] add-pred($Rel, $Rstore) = add-tuples(PRED, $Rel, $Rstore)
 
[ex1] while Exp do Stat* od := Stat,
      $Area := get-area(Stat),
      $Rstore' := add-pred({<$Area, get-first(Stat*)>}, $Rstore),
      $Rstore'':= add-last(Stat*, $Area, $Rstore'),
      $Rstore''' := extract-used-vars(Exp, $Rstore'', $Area)
      =========================================================
      extract(Stat, $Rstore) = $Rstore'''

[ex2]
      if Exp then Stat*1 else Stat*2 fi := Stat,
      $Area := get-area(Stat),
      $Rstore' := add-pred({ <$Area, get-first(Stat*1)>,
                           <$Area, get-first(Stat*2)> }, $Rstore),
      $Rstore'' :=  extract-used-vars(Exp, $Rstore', $Area)
      ========================================================
      extract(Stat, $Rstore) = $Rstore''

[ex3] $Rstore' := extract(Stat, $Rstore),
      $Rstore'' := extract(Stat+, $Rstore')
      ========================================================
      extract(Stat ; Stat+, $Rstore) = 
      add-pred(product(get-last(Stat), {get-first(Stat+)}), $Rstore'')

[ex4] Id := Exp := Stat,
      $Area := get-area(Stat),
      $Area' := get-area(Id),
      $Rstore' := extract-used-vars(Exp, $Rstore, $Area),
      $Rstore'' := add-tuple(DEFS, <$Area, id2str(Id)>, $Rstore'),
      $Rstore''' := add-tuple(OCCURS, <id2str(Id), $Area'>, $Rstore'')
      ========================================================
      extract(Stat, $Rstore) = $Rstore'''

[default-ex]
     extract(Stat, $Rstore) = extract-used-vars(Stat, $Rstore, get-area(Stat))

[exv-1] $Area' := get-area(Id),
	$Rstore' := add-tuple(USES, <$Area, id2str(Id)> , $Rstore),
        $Rstore'' := add-tuple(OCCURS, <id2str(Id), $Area'>, $Rstore')
	=====================================================
        extract-used-vars(Id, $Rstore, $Area) = $Rstore''

[gf-1] get-first(Stat; Stat*) = get-area(Stat)

[gl-2] get-last(Stat*; if Exp then Stat*1 else Stat*2 fi) = 
       union(get-last(Stat*1), get-last(Stat*2))

[default-gl-3] 
       get-last(Stat*; Stat) = {get-area(Stat)}

[al-1] $Bag := get-last(Stat*)
       ========================================================
       add-last(Stat*, $Area, $Rstore) = add-pred(product($Bag, {$Area}), $Rstore)

[er-1] begin Decls Stat+ end := Program,
       $Area :=  area("",0,0,0,0),
       $Rstore1 := assign(ROOT, area, $Area, rstore()),
       $Rstore2 := assign(USES, rel[area,str], {}, $Rstore1),
       $Rstore3 := assign(OCCURS, rel[str,area], {}, $Rstore2),
       $Rstore4 := assign(PRED, rel[area,area], {}, $Rstore3),
       $Rstore5 := add-pred({<$Area, get-first(Stat+)>}, $Rstore4)
       =======================================================
       extractRelations(Program) = extract(Program, $Rstore5)

[i2s] id2str(pico-id(Char*)) = strcon(""" Char* """)

[cvt-1] convert(<$String, $Area>) = listitem("Unitialized variable " || $String, $Area)

[cvt-2] [$ListItem*] := convertl({$Elem*})
        ================================================
        convertl({<$String, $Area>, $Elem*})= [convert(<$String, $Area>), $ListItem*]

[cvt-3] convertl({}) = []

