equations
    

[as-1] add-succ(Rel, Rstore) = add(SUCC, Rel, Rstore)
 

[ex1] Stat = while Exp do Stat* od,
      NodeId = get-area(Stat),
      Rstore' = add-succ({<NodeId, get-first(Stat*)>}, Rstore),
      Rstore''= add-last(Stat*, NodeId, Rstore'),
      Rstore''' = extract-used-vars(Exp, Rstore'', NodeId)
      =========================================================
      extract(Stat, Rstore) = Rstore'''

[ex2]
      Stat = if Exp then Stat*1 else Stat*2 fi,
      NodeId = get-area(Stat),
      Rstore' = add-succ({ <NodeId, get-first(Stat*1)>,
                           <NodeId, get-first(Stat*2)> }, Rstore),
      Rstore'' =  extract-used-vars(Exp, Rstore', NodeId)
      ========================================================
      extract(Stat, Rstore) = Rstore''

[ex3] extract(Stat, Rstore) = Rstore',
      extract(Stat+, Rstore') = Rstore''
      ========================================================
      extract(Stat ; Stat+, Rstore) = 
      add-succ(product(get-last(Stat), {get-first(Stat+)}), Rstore'')

[ex4] Stat = Id := Exp,
      NodeId = get-area(Stat),
      Rstore' = extract-used-vars(Exp, Rstore, NodeId),
      Rstore'' = add(DEFS, <Id, NodeId>, Rstore')
      ========================================================
      extract(Stat, Rstore) = Rstore''

[default-ex]
     extract(Stat, Rstore) = extract-used-vars(Stat, Rstore, get-area(Stat))


[exv-1] extract-used-vars(Id, Rstore, NodeId) = add(USES, <Id, NodeId> , Rstore)

[gf-1] get-first(Stat; Stat*) = get-area(Stat)

[gl-2] get-last(Stat*; if Exp then Stat*1 else Stat*2 fi) = 
       union(get-last(Stat*1), get-last(Stat*2))

[default-gl-3] 
       get-last(Stat*; Stat) = {get-area(Stat)}

[al-1] get-last(Stat*) = Bag
       ========================================================
       add-last(Stat*, NodeId, Rstore) = add-succ(product(Bag, {NodeId}), Rstore)


[rt-1] root = area("",0,0,0,0)
 
[an-1] Program = begin Decls Stat+ end,
       Rstore = add-succ({<area("",0,0,0,0), get-first(Stat+)>}, []),
       extract(Program, Rstore) = Rstore',
       undefined-query(Rstore') = Rstore''
      
       =========================================================
       analyze(Program) = convertl(rel-value(UN, Rstore''))


[uq-1] undefined-query(Rstore) =
       eval-rscript(
      	def PRED   = inv(SUCC)
        fun reaches_root(R) = elem('area("",0,0,0,0), range(R))
        def UN = { <V, N> in USES | reaches_root(dominators({<V,N>}, DEFS, PRED)) },
        Rstore
       )

[i2s] id2str(pico-id(Char*)) = strcon(""" Char* """)

[cvt-1] convert(<Id, Area>) = listitem("Unitialized variable " || id2str(Id), [ "ShowSource" ], Area)

[cvt-2] convertl({Tuple*}) = list([ListItem*]),
        Tuple = <Id, Area>
        ================================================
        convertl({Tuple, Tuple*})= list([convert(<Id, Area>), ListItem*])

[cvt-3] convertl({}) = list([])



