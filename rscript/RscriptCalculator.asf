 equations

%%---- Store

%% Get value of a variable

[VL1]	value($Var, [<$Var, $Val>, $VPair*]) = $Val
[VL2]	$Var != $Var' ===> value($Var, [<$Var', $Val>, $VPair*]) = value($Var, [$VPair*])

%% Assign value to variable

[ASG1]  assign($Var, $Val, [$VPair*1, <$Var, $Val'>, $VPair*2]) = [$VPair*1, <$Var, $Val>, $VPair*2]
	

[default-ASG1]	
	assign($Var, $Val, [$VPair*]) = [<$Var, $Val>, $VPair*]

%% Assign when undefined

[AWU1]  assign-when-undef($Var, $Val, [$VPair*1, <$Var, $Val'>, $VPair*2]) = [$VPair*1, <$Var, $Val'>, $VPair*2]
	

[default-AWU2]	
	assign-when-undef($Var, $Val, [$VPair*]) = [<$Var, $Val>, $VPair*]


%% Increment a variable

[INC1]	value($Var, $Store) = $Integer
	==================================================
	increment($Var, $Store) = assign($Var, $Integer + 1, $Store)

%% Add elem to tuple

[AE1]	add-elem($Var, $Elem, $Store) = assign($Var, append(value($Var, $Store), $Elem), $Store)


%% Add tuple to a relation

[AT1]	add-tuple($Var, <$Elem1, $Elem2>, [<$Var, $Val>, $VPair*]) = 
	[<$Var, apply({<$Elem1, $Elem2>} union $Val)>, $VPair*]

[AT2]	$Var != $Var', 
	[$VPair*'] = add-tuple($Var, <$Elem1, $Elem2>, [$VPair*])
	===============================================================
	add-tuple($Var, <$Elem1, $Elem2>, [<$Var', $Val>, $VPair*]) = [<$Var', $Val>, $VPair*']

[AT3]	add-tuple($Var, <$Elem1, $Elem2>, []) = [< $Var, {<$Elem1, $Elem2>} >]

%% Extend a relation

[ER1]	extend-rel($Var, $Rel, [<$Var, $Val>, $VPair*]) = [<$Var, apply($Rel union $Val)>, $VPair*]

[ER2]	$Var != $Var', 
	[$VPair*'] = extend-rel($Var, $Rel, [$VPair*])
	===============================================================
	extend-rel($Var, $Rel, [<$Var', $Val>, $VPair*]) = [<$Var', $Val>, $VPair*']

[ER3]	extend-rel($Var, $Rel, []) = [< $Var, $Rel >]

%%---- if on VALUEs

[if1]	if true then $Val1 else $Val2 fi  = $Val1
[if2]	if false then $Val1 else $Val2 fi = $Val2

%%---- Equality on elements

[e1]	equal-elem($Bool, $Bool)	= true
[e1]	equal-elem($Integer, $Integer)	= true
[e2]	equal-elem($String, $String)	= true
[e1]	equal-elem($Bag1, $Bag2) 	= equal($Bag1, $Bag2)
[e2]	equal-elem($Rel1, $Rel2) 	= equal($Rel1, $Rel2)
[default-e4] 
	equal-elem($Elem, $Elem') 		= false

%% --- Equality on RVALUEs

[e1]	equal-rval($Bag1, $Bag2) = equal($Bag1, $Bag2)
[e2]	equal-rval($Rel1, $Rel2) = equal($Rel1, $Rel2)
[e3]	equal-rval($Val, $Val)  = true
[default-e4] 
	equal-rval($Val1, $Val2) = false


[v2b]	rval2bag($Bag) = $Bag
[v2r]	rval2rel($Rel) = $Rel

%%---- Specialized versions of evalx that return value of required type

[EVP]	$Bool = evalx($Exp, $Store)               ===> evalp($Exp, $Store) = $Bool

[EVE2]	*empty* = evalx($Exp, $Store)        	  ===> evale($Exp, $Store) = EmptyElemBag
[EVE2]	$Elem = evalx($Exp, $Store)               ===> evale($Exp, $Store) = $Elem

[EVB1]	$Bag = evalx($Exp, $Store)     ===> evalb($Exp, $Store) = $Bag

[EVB2]	*empty* = evalx($Exp, $Store)  ===> evalb($Exp, $Store) = {}

[EVR1]	$Rel = evalx($Exp, $Store)     ===> evalr($Exp, $Store) = $Rel

[EVR1]	*empty* = evalx($Exp, $Store)  ===> evalr($Exp, $Store) = {}

%% --- Test suite support

[ETS1]	$Store = eval-rscript($Rscript),
	$Integer1 = value(NPASSED, $Store),
	$Integer2 = value(NFAILED, $Store),
	$Val = value(FAILED, $Store)
	==========================================================
	eval-rscript-testsuite($Rscript) = testsuite-report($Integer1, $Integer2, $Val)

[TSR1] testsuite-report($Integer, 0, $Val) = All $Integer tests passed

[TSR1] 	$Integer2 != 0
	===========================================================
	testsuite-report($Integer1, $Integer2, $Val) = 
	$Integer2 of $Integer1 tests failed: $Val

%% ---- Eval complete script

[ES0]	eval-rscript($Stat*, $Var, $Store) = value($Var, eval-rscript($Stat*, $Store))

[ES1]	eval-rscript($Stat*) = eval-rscript($Stat*, [<NPASSED,0>,<NFAILED,0>,<FAILED,*empty*>])
[ES2]	eval-rscript($Stat $Stat*, $Store) = eval-rscript($Stat*, eval-rscript($Stat, $Store))
[ES3]	eval-rscript( , $Store) = $Store


[xxx] EmptyElemBag = EmptyBag

[xxx] EmptyElemRel = EmptyBag

%% ---- Eval statements

[S1]	eval-rscript(bool $Var, $Store) 	= assign-when-undef($Var, true, $Store)
[S2]	eval-rscript(int $Var, $Store)  	= assign-when-undef($Var, 0, $Store)
[S3]	eval-rscript(str $Var, $Store)  	= assign-when-undef($Var, "", $Store)
[S4]	eval-rscript(node $Var, $Store)		= assign-when-undef($Var, area("",0,0,0,0), $Store)
[S5]	eval-rscript(bag[$Etype] $Var, $Store)  = assign-when-undef($Var, *empty*, $Store)	
	
[S6]	eval-rscript(rel[$Etype1, $Etype2] $Var, $Store) 
						 = assign-when-undef($Var, *empty*, $Store)
[S7]	eval-rscript(rel[$Etype1, $Etype2] $Var = {}, $Store) 
						 = assign-when-undef($Var, *empty*, $Store)
[S8]	eval-rscript($Etype $Var, $Store)	= $Store

[S9]	eval-rscript(bag[$Etype] $Var = {}, $Store)  = assign($Var, *empty*, $Store)
[S10] 	eval-rscript(rel[$Etype1,$Etype2] $Var = {}, $Store)  = assign($Var, *empty*, $Store)

[S11]	eval-rscript($Etype $Var = $Exp, $Store) = assign($Var, evalx($Exp, $Store), $Store)
[S12]	eval-rscript($Etype1 $Var1($Etype2 $Var2) = $Exp, $Store) 
						= assign($Var1, <$Var2, $Exp>, $Store)

[S13]	eval-rscript($Etype1 $Var1($Etype2 $Var2, $Etype3 $Var3) = $Exp, $Store) 
						= assign($Var1, <$Var2, $Var3, $Exp>, $Store)

[S14]	eval-rscript($Etype1 $Var1($Etype2 $Var2, $Etype3 $Var3, $Etype4 $Var4) = $Exp, $Store) 
						= assign($Var1, <$Var2, $Var3, $Var4, $Exp>, $Store)

[S15]	evalp($Exp,$Store) = true
	===================================================
	eval-rscript(assert $String : $Exp, $Store)= increment(NPASSED, $Store)

[S16]	evalp($Exp,$Store) = false, add-elem(FAILED, $String, $Store) = $Store'
	===================================================
	eval-rscript(assert $String : $Exp, $Store)= increment(NFAILED, $Store')

%% --- Solve Sets of equations

[SL1]	eval-rscript(solve {$Var1 = $Exp1}, $Store) = 
	solve1(1000, $Var1, $Exp1, $Store)

[SL1]	eval-rscript(solve($Integer) {$Var1 = $Exp1}, $Store) = 
	solve1($Integer, $Var1, $Exp1, $Store)

[SL2]	eval-rscript(solve {$Var1 = $Exp1, $Var2 = $Exp2}, $Store) = 
	solve2(1000, $Var1, $Exp1, $Var2, $Exp2, $Store)

[SL2]	eval-rscript(solve($Integer){$Var1 = $Exp1, $Var2 = $Exp2}, $Store) = 
	solve2($Integer, $Var1, $Exp1, $Var2, $Exp2, $Store)

[SL1-1]	$Integer > 0 = true,
	$Val1 = value($Var1, $Store),
	$Val1' = evalx($Exp1, $Store),
	$Store' = assign($Var1, $Val1', $Store),
	equal-rval($Val1, $Val1') = false
	===========================================
	solve1($Integer, $Var1, $Exp1, $Store) =
	solve1($Integer - 1, $Var1, $Exp1, $Store')

[default-SL1-2]	
	solve1($Integer, $Var1, $Exp1, $Store) = $Store


[SL2-1]	$Integer > 0 = true,
	$Val1 = value($Var1, $Store),
	$Val1' = evalx($Exp1, $Store),
	$Val2 = value($Var2, $Store),
	$Val2' = evalx($Exp2, $Store),
	$Store' = assign($Var1, $Val1', $Store),
	$Store'' = assign($Var2, $Val2', $Store'),
	equal-rval($Val1, $Val1') & equal-rval($Val2, $Val2') = false
	===========================================
	solve2($Integer, $Var1, $Exp1, $Var2, $Exp2, $Store) =
	solve2($Integer - 1, $Var1, $Exp1, $Var2, $Exp2, $Store'')

[default-SL2-2]	
	solve2($Integer, $Var1, $Exp1, $Var2, $Exp2, $Store) = $Store

%% ---- Eval one REXP

[ER1]	eval-rexp($Exp, $Store) = evalx($Exp, $Store)


%% ---- The main evaluation function evalx

%% ---- Eval true, false, elements and variables

[TR]	evalx(true, $Store) = true

[FL]	evalx(false, $Store) = false

[ELM]	evalx($Elem, $Store) = $Elem

[VAR1]	evalx($Var, $Store)  = value($Var, $Store)

%% ---- Eval tuples

[ET1]	$Elem1 = evale($Exp1, $Store),
	$Elem2 = evale($Exp2, $Store)
	==================================================
	evalx(<$Exp1, $Exp2>, $Store) = <$Elem1, $Elem2>

%% ---- Eval bags

[EB2]   $Elem = evale($Exp, $Store), is-tuple($Elem) = false, 
	{$Elem+} = evalb({$Exp+}, $Store)
	===================================================
	evalx({$Exp, $Exp+}, $Store) = {$Elem, $Elem+}

[EB3]	$Elem = evale($Exp, $Store), is-tuple($Elem) = false
	===================================================
	evalx({$Exp}, $Store) = {$Elem}

%% ---- Eval relations

%%[ER2]	$Elem1 = evale($Exp1, $Store), $Elem2 = evale($Exp2, $Store),
%%	evalr({$Exp+}, $Store) = {$Tuple+}
%%	====================================================
%%	evalx({<$Exp1, $Exp2>, $Exp+}, $Store) = {<$Elem1, $Elem2>, $Tuple+}

[ER2]	$Tuple = evale($Exp, $Store),
	evalr({$Exp+}, $Store) = {$Tuple+}
	====================================================
	evalx({$Exp, $Exp+}, $Store) = {$Tuple, $Tuple+}

%%[ER3]	$Elem1 = evale($Exp1, $Store), $Elem2 = evale($Exp2, $Store)
%%	====================================================
%%	evalx({<$Exp1, $Exp2>}, $Store) = {<$Elem1, $Elem2>}

[ER3]	$Tuple = evale($Exp, $Store)
	====================================================
	evalx({$Exp}, $Store) = {$Tuple}

%% ---- Eval binary infix operators

[AND1]	evalx($Exp1 and $Exp2, $Store) 	= apply(evalx($Exp1, $Store) and evalx($Exp2, $Store))
[AND2]	apply($Bool1 and $Bool2) 	= $Bool1 & $Bool2

[OR1]	evalx($Exp1 or $Exp2, $Store) 	= apply(evalx($Exp1, $Store) or evalx($Exp2, $Store))
[OR2]	apply($Bool1 or $Bool2) 	= $Bool1 | $Bool2	

[IN1]	evalx($Exp1 in $Exp2, $Store) 	= apply(evalx($Exp1, $Store) in evalx($Exp2, $Store))
[IN2]	apply($Elem in *empty*) 	= false
[IN3]	apply($Elem in $Bag) 		= in($Elem, $Bag)
[IN4]	apply(<$Elem1, $Elem2> in *empty*) 
					= false
[IN5]	apply(<$Elem1, $Elem2> in $Rel) = in(<$Elem1, $Elem2>, $Rel)

[NI1]	$Bool = apply(evalx($Exp1, $Store) in evalx($Exp2, $Store))
	===========================================================
	evalx($Exp1 notin $Exp2, $Store)= not($Bool)

[EQ1]	evalx($Exp1 == $Exp2, $Store) 	= apply(evalx($Exp1, $Store) == evalx($Exp2, $Store))

[EQ2]			     apply(*empty* == *empty*)	= true
[EQ3]	$Bag = {}  	===> apply(*empty* == $Bag) 	= true
[EQ4]	$Bag != {} 	===> apply(*empty* == $Bag) 	= false
[EQ5]	$Bag = {}  	===> apply($Bag == *empty*) 	= true
[EQ6]	$Bag != {} 	===> apply($Bag == *empty*) 	= false
[EQ7]	$Rel = {}  	===> apply(*empty* == $Rel) 	= true
[EQ8]	$Rel != {} 	===> apply(*empty* == $Rel) 	= false
[EQ9]	$Rel = {}  	===> apply($Rel == *empty*) 	= true
[EQ10]	$Rel != {} 	===> apply($Rel == *empty*) 	= false
 
[EQ11]			     apply($Elem1 == $Elem2)	= equal-elem($Elem1, $Elem2)

[NE1]	$Bool = apply(evalx($Exp1, $Store) == evalx($Exp2, $Store))
	===========================================================
	evalx($Exp1 != $Exp2, $Store) 	= not($Bool)

[LE1]	evalx($Exp1 <= $Exp2, $Store) 	= apply(evalx($Exp1, $Store) <= evalx($Exp2, $Store))

[LE2]	apply(*empty* <= *empty*)	= true
[LE3]	apply(*empty* <= $Bag2) 	= true
[LE4]	apply($Bag <= *empty*) 		= subset($Bag, EmptyBag)
[LE5]	apply($Bag1 <= $Bag2) 		= subset($Bag1, $Bag2)
[LE6]	apply(*empty* <= $Rel2) 	= true
[LE7]	apply($Rel <= *empty*) 		= subset($Rel, EmptyRel)
[LE8]	apply($Rel1 <= $Rel2) 		= subset($Rel1,  $Rel2)
[LE9]	apply($Integer1 <= $Integer2) 	= $Integer1 <= $Integer2

[LT1]	evalx($Exp1 < $Exp2, $Store) 	= apply(evalx($Exp1, $Store) < evalx($Exp2, $Store))

[LT2]	apply(*empty* < *empty*)	= false
[LT3]	apply(*empty* < $Bag2) 		= strict-subset(EmptyBag, $Bag2)
[LT4]	apply($Bag1 < *empty*) 		= false
[LT5]	apply($Bag1 < $Bag2) 		= strict-subset($Bag1, $Bag2)
[LT6]	apply(*empty* < $Rel2) 		= strict-subset(EmptyRel,  $Rel2)
[LT7]	apply($Rel1 < *empty*) 		= false
[LT8]	apply($Rel1 < $Rel2) 		= strict-subset($Rel1,  $Rel2)
[LT9]	apply($Integer1 < $Integer2) 	= $Integer1 < $Integer2

[GE1]	$Bool = apply(evalx($Exp1, $Store) < evalx($Exp2, $Store))
	==========================================================
	evalx($Exp1 >= $Exp2, $Store) 	= not($Bool)

[GT1]	$Bool = apply(evalx($Exp1, $Store) <= evalx($Exp2, $Store))
	===========================================================
	evalx($Exp1 > $Exp2, $Store) 	= not($Bool)

[UN1]	evalx($Exp1 union $Exp2, $Store)= apply(evalx($Exp1, $Store) union evalx($Exp2, $Store))

[UN2]	apply(*empty* union *empty*)	= *empty*
[UN3]	apply(*empty* union $Bag) 	= $Bag 
[UN4]	apply($Bag union *empty*) 	= $Bag
[UN5]	apply($Bag1 union $Bag2)   	= union($Bag1, $Bag2)

[UN6]	apply($Elem union *empty*)   	= $Elem
[UN7]	apply(*empty* union $Elem)   	= $Elem
[UN8]	apply($Elem1 union $Elem2)     	= union({$Elem1}, {$Elem2})
[UN9]	apply($Elem union $Bag)       	= union({$Elem}, $Bag)
[UN10]	apply($Bag union $Elem)       	= union($Bag, {$Elem})

[UN11]	apply(*empty* union $Rel)   	= $Rel
[UN12]	apply($Rel union *empty*)   	= $Rel
[UN13]	apply($Rel1 union $Rel2)     	= union($Rel1, $Rel2)

[UN14]	apply(*empty* union $Tuple) 	= $Tuple
[UN15]	apply($Tuple union *empty*) 	= $Tuple
[UN16]	apply($Tuple1 union $Tuple2) 	= union({$Tuple1}, {$Tuple2})
[UN17]	apply($Tuple union $Rel)     	= union({$Tuple}, $Rel)
[UN18]	apply($Rel union $Tuple)     	= union($Rel, {$Tuple})

[IN1]	evalx($Exp1 inter $Exp2, $Store)= apply(evalx($Exp1, $Store) inter evalx($Exp2, $Store))

[IN2]	apply(*empty* inter *empty*)	= *empty*
[IN3]	apply(*empty* inter $Bag)	= *empty*
[IN4]	apply($Bag inter *empty*)	= *empty*
[IN5]	apply($Bag1 inter $Bag2) 	= inter($Bag1, $Bag2)

[IN6]	apply($Elem inter *empty*)   	= *empty*
[IN7]	apply(*empty* inter $Elem)   	= *empty*
[IN8]	apply($Elem1 inter $Elem2)     	= inter({$Elem1}, {$Elem2})
[IN9]	apply($Elem inter $Bag)       	= inter({$Elem}, $Bag)
[IN10]	apply($Bag inter $Elem)       	= inter($Bag, {$Elem})

[IN11]	apply(*empty* inter $Rel)   	= *empty*
[IN12]	apply($Rel inter *empty*)   	= *empty*
[IN13]	apply($Rel1 inter $Rel2)     	= inter($Rel1, $Rel2)

[IN14]	apply(*empty* inter $Tuple) 	= *empty*
[IN15]	apply($Tuple inter *empty*) 	= *empty*
[IN16]	apply($Tuple1 inter $Tuple2) 	= inter({$Tuple1}, {$Tuple2})
[IN17]	apply($Tuple inter $Rel)     	= inter({$Tuple}, $Rel)
[IN18]	apply($Rel inter $Tuple)     	= inter($Rel, {$Tuple})

%% -- difference

[DF1]	evalx($Exp1 \ $Exp2, $Store)	= apply(evalx($Exp1, $Store) \ evalx($Exp2, $Store))

[DF2]	apply(*empty* \ *empty*)	= *empty*
[DF3]	apply(*empty* \ $Bag) 		= *empty*
[DF4]	apply($Bag \ *empty*) 		= $Bag
[DF5]	apply($Bag1 \ $Bag2)   		= diff($Bag1, $Bag2)

[DF6]	apply($Elem \ *empty*)   	= $Elem
[DF7]	apply(*empty* \ $Elem)   	= *empty*
[DF8]	apply($Elem1 \ $Elem2)     	= diff({$Elem1}, {$Elem2})
[DF9]	apply($Elem \ $Bag)       	= diff({$Elem}, $Bag)
[DF10]	apply($Bag \ $Elem)       	= diff($Bag, {$Elem})

[DF11]	apply(*empty* \ $Rel)   	= *empty*
[DF12]	apply($Rel \ *empty*)   	= $Rel
[DF13]	apply($Rel1 \ $Rel2)     	= diff($Rel1, $Rel2)

[DF14]	apply(*empty* \ $Tuple) 	= *empty*
[DF15]	apply($Tuple \ *empty*) 	= $Tuple
[DF16]	apply($Tuple1 \ $Tuple2) 	= diff({$Tuple1}, {$Tuple2})
[DF17]	apply($Tuple \ $Rel)     	= diff({$Tuple}, $Rel)
[DF18]	apply($Rel \ $Tuple)     	= diff($Rel, {$Tuple})

%% -- composition

[CM1]	evalx($Exp1 o $Exp2, $Store)	= apply(evalx($Exp1, $Store) o evalx($Exp2, $Store))
[CM2]	apply($Rel1 o $Rel2) 		= compose($Rel1, $Rel2)

[CM2]	apply(*empty* o *empty*)	= *empty*
[CM3]	apply(*empty* o $Bag) 		= *empty*

[CM11]	apply(*empty* o $Rel)   	= *empty*
[CM12]	apply($Rel o *empty*)   	= *empty*
[CM13]	apply($Rel1 o $Rel2)     	= compose($Rel1, $Rel2)

[CM14]	apply(*empty* o $Tuple) 	= *empty*
[CM15]	apply($Tuple o *empty*) 	= *empty*
[CM16]	apply($Tuple1 o $Tuple2) 	= compose({$Tuple1}, {$Tuple2})
[CM17]	apply($Tuple o $Rel)     	= compose({$Tuple}, $Rel)
[CM18]	apply($Rel o $Tuple)     	= compose($Rel, {$Tuple})

%% -- product

[CP1]	evalx($Exp1 x $Exp2, $Store)	= apply(evalx($Exp1, $Store) x evalx($Exp2, $Store))

[CP2]	apply(*empty* x *empty*)	= *empty*
[CP3]	apply(*empty* x $Bag) 		= *empty* 
[CP4]	apply($Bag x *empty*) 		= *empty*
[CP5]	apply($Bag1 x $Bag2)   		= product($Bag1, $Bag2)

[CP6]	apply($Elem x *empty*)   	= *empty*
[CP7]	apply(*empty* x $Elem)   	= *empty*
[CP8]	apply($Elem1 x $Elem2)     	= <$Elem1, $Elem2>
[CP9]	apply($Elem x $Bag)       	= product({$Elem}, $Bag)
[CP10]	apply($Bag x $Elem)       	= product($Bag, {$Elem})

%% -- Image

[IM1]	evalx($Exp1 . $Exp2, $Store)	= apply(evalx($Exp1, $Store) . evalx($Exp2, $Store))

[IM2]	apply(*empty* . *empty*)	= *empty*
[IM3]	apply(*empty* . $Elem)		= *empty*
[IM4]	apply(*empty* . $Bag)		= *empty*
[IM5]	apply($Rel . *empty*)		= *empty*
[IM6]	apply($Rel . $Elem)		= $Rel . $Elem
[IM7]	apply($Rel . $Bag)		= $Rel . $Bag

[IM8]	apply(*empty* . $Rel)		= *empty*
[IM9]	apply($Elem . $Rel)		= $Elem. $Rel
[IM10]	apply($Bag . $Rel)		= $Bag . $Rel

[default-IM11]
	apply($Val1 . $Val2)		= *empty*

%% -- [ ]
[IM1]	evalx($Exp1 [ $Exp2 ], $Store)	= evalx($Exp2 . $Exp1, $Store)

%% --- Eval unary prefix operators

[NOT1]	evalx(not $Exp, $Store) 	= apply(not evalx($Exp, $Store))
[NOT1]	apply(not $Bool)		= not($Bool)

[SZ1]	evalx(# $Exp, $Store) 		= apply(# evalx($Exp, $Store))
[SZ2]	apply(# *empty*)		= 0
[SZ1]	apply(# $Rel) 			= size($Rel)
[SZ2]	apply(# $Bag) 			= size($Bag)

%% --- Eval unary postfix operators

[TC1]	evalx($Exp +, $Store)		= apply(evalx($Exp, $Store) +)

[TC2]	apply(*empty* +)		= *empty*
[TC3]	apply($Rel +)			= trans-closure($Rel)

[RTC1]	evalx($Exp *, $Store)		= apply(evalx($Exp, $Store) *)

[RTC2]	apply(*empty* *)		= *empty*
[TRC3]	apply($Rel *)			= rtrans-closure($Rel)

%% ---- Eval bag and relation formers

%% --- Simplify multiple Froms

[RF1]	{$Exp : $From+}  =  {$From+ :: $Exp}

[RF1]	{$From, $From+ :: $Exp } = {$From :: {$From+ :: $Exp}}

[RF2]	{$Exp : $From+ | $Test} =  {$From+ :: $Test, $Exp}

[RF1]	{$From, $From+ :: $Test, $Exp } = {$From :: {$From+ :: $Test, $Exp}}

%% --- eval-iter: iterate over a bag

[ei-1]	$Bag = evalb($Exp1, $Store)
	=======================================
	evalx({$Var in $Exp1 :: $Exp2}, $Store) = eval-iter($Var, $Bag, *empty*, $Exp2, $Store)

[ei-2]	$Store' = assign($Var, $Elem, $Store),
	$Val' = evalx($Exp, $Store'),
	eval-iter($Var, {$Elem*}, append($Val, $Val'), $Exp, $Store') = $Val''
	======================================================
	eval-iter($Var, {$Elem, $Elem*}, $Val, $Exp, $Store) =  $Val''

[ei-3]	eval-iter($Var, {}, $Val, $Exp2, $Store) = $Val

%% --- eval-citer: iterate over bag, with condition

[eci-1]	$Bag = evalb($Exp1, $Store)
	=======================================
	evalx({$Var in $Exp1 :: $Test, $Exp2}, $Store) = 
	eval-citer($Var, $Bag, *empty*, $Test, $Exp2, $Store)

[eci-2]	$Store' = assign($Var, $Elem, $Store),
	$Bool =  evalp($Test, $Store'),
	$Val' = if $Bool then append($Val, evalx($Exp, $Store')) else $Val fi,
	eval-citer($Var, {$Elem*}, $Val', $Test, $Exp, $Store') = $Val''
	======================================================
	eval-citer($Var, {$Elem, $Elem*}, $Val, $Test, $Exp, $Store) = $Val''

[eci3]	eval-citer($Var, {}, $Val, $Test, $Exp, $Store) = $Val

%% --- eval-iter2: iterate over relation

[ei2-1]	$Rel = evalr($Exp1, $Store)
	=======================================
	evalx({ <$Var1, $Var2> in $Exp1 :: $Exp2 }, $Store) = 
	eval-iter2($Var1, $Var2, $Rel, *empty*, $Exp2, $Store)

[ei2-2]	$Store' = assign($Var1, $Elem1, $Store),
	$Store'' = assign($Var2, $Elem2, $Store'),
	$Val' = evalx($Exp, $Store''),
	eval-iter2($Var1, $Var2, {$Tuple*}, append($Val, $Val'), $Exp, $Store'') = $Val''
	======================================================
	eval-iter2($Var1, $Var2, {<$Elem1, $Elem2>, $Tuple*}, $Val, $Exp, $Store) = $Val''

[ei2-3]	eval-iter2($Var1, $Var2, {}, $Val, $Exp2, $Store) = $Val

%% --- eval-citer2: iterate over relation, with condition

[eci2-1]	
	$Rel = evalr($Exp1, $Store)
	=======================================
	evalx({ <$Var1, $Var2> in $Exp1 :: $Test, $Exp2 }, $Store) = 
	eval-citer2($Var1, $Var2, $Rel, *empty*, $Test, $Exp2, $Store)

[eci2-2]
	$Store' = assign($Var1, $Elem1, $Store),
	$Store'' = assign($Var2, $Elem2, $Store'),
	$Bool = evalp($Test, $Store''),
	$Val' = if $Bool then append($Val, evalx($Exp, $Store'')) else $Val fi, 
	eval-citer2($Var1, $Var2, {$Tuple*}, $Val', $Test, $Exp, $Store'') = $Val''
	======================================================
	eval-citer2($Var1, $Var2, {<$Elem1, $Elem2>, $Tuple*}, $Val, $Test, $Exp, $Store) = $Val''

[eci2-3]
	eval-citer2($Var1, $Var2, {}, $Val, $Test, $Exp, $Store) = $Val

%% ---- Auxiliary functions for bag and relation formers

[AP1]	append(*empty*, *empty*)	= *empty*
[AP2]	append(*empty*, $Elem)		= $Elem
[AP2]	append($Elem, *empty*)		= $Elem

[AP3]	is-tuple($Elem1) = false, is-tuple($Elem2) = false
	==================================================
	append($Elem1, $Elem2)		= union1($Elem1, {$Elem2})

[AP4]	is-tuple($Elem) = false
	=================================================
	append($Elem, {$Elem*})		= union1($Elem, {$Elem*})

[AP5]		is-tuple($Elem) = false
	=================================================
	append({$Elem*}, $Elem)		= union2({$Elem*}, $Elem)

[AP6]	append({$Elem*1}, {$Elem*2}) 	= union({$Elem*1}, {$Elem*2})
[AP7]	append(*empty*, $Tuple) 	= $Tuple

[AP8]	append($Tuple1, $Tuple2)	= union1($Tuple1, {$Tuple2})
[AP9]	append($Tuple, {$Tuple*})	= union1($Tuple, {$Tuple*})
[AP10]	append({$Tuple*}, $Tuple)	= union2({$Tuple*}, $Tuple)
[AP11]	append({$Tuple*1}, {$Tuple*2}) 	= union({$Tuple*1}, {$Tuple*2})
[AP12]	append({$Tuple*}, *empty*)	= {$Tuple*}
[AP12]	append(*empty*, {$Tuple*})	= {$Tuple*}


[it1]   is-tuple(<$Elem1,$Elem2>) = true
[default-it2]
	is-tuple($Val)= false

%% --- Exists in bag

[EXB1]	$Bag = evalb($Exp, $Store)
	=========================================
	evalx(exists($Var in $Exp | $Test), $Store) = existsb($Var, $Bag, $Test, $Store)

[EXB2]	$Store' = assign($Var, $Elem, $Store)
	=========================================
	existsb($Var, {$Elem, $Elem*}, $Test, $Store) =
	if evalp($Test, $Store') then true else existsb($Var, {$Elem*}, $Test, $Store) fi

[EXB3]	existsb($Var, {}, $Test, $Store) = false

%% --- Exists in relation

[EXR1]	$Rel = evalr($Exp, $Store)
	=========================================
	evalx(exists(<$Var1, $Var2> in $Exp | $Test), $Store) = existsr($Var1, $Var2, $Rel, $Test, $Store)

[EXR1]	$Store' = assign($Var1, $Elem1, $Store),
	$Store'' = assign($Var2, $Elem2, $Store')
	=========================================
	existsr($Var1, $Var2, {<$Elem1, $Elem2>, $Tuple*}, $Test, $Store) =
	if evalp($Test, $Store'') then true else existsr($Var1, $Var2, {$Tuple*}, $Test, $Store) fi

[EXR2]	existsr($Var1, $Var2, {}, $Test, $Store) = false

%% --- Forall in bag

[ALB1]	$Bool = evalp(exists($Var in $Exp | not $Test), $Store)
	==========================================
	evalx(forall($Var in $Exp | $Test), $Store) = not($Bool)

%% --- Forall in relation

[ALB1]	$Bool = evalp(exists(<$Var1, $Var2> in $Exp | not $Test), $Store)
	==========================================
	evalx(forall(<$Var1, $Var2> in $Exp | $Test), $Store) = not($Bool)


%% ------------------------------

%% ---- FUN ( EXP ) -> EXP

[default-fun1]
	value($Var1, $Store) = <$Var2, $Exp2>,
	$Val = evalx($Exp1, $Store),
	$Store' = assign($Var2, $Val, $Store)
	====================================================
	evalx($Var1($Exp1), $Store) = evalx($Exp2, $Store')

[Fid]	$Bag  = evalb($Exp, $Store)
	====================================================
	evalx(id($Exp), $Store) = id($Bag)

[Fdom]	$Rel = evalr($Exp, $Store)
	====================================================
	evalx(domain($Exp), $Store) = domain($Rel)

[Fran]	$Rel = evalr($Exp, $Store)
	====================================================
	evalx(range($Exp), $Store) = range($Rel)

[Fcar]	$Rel = evalr($Exp, $Store)
	====================================================
	evalx(carrier($Exp), $Store) = carrier($Rel)
	
[Ftop]	$Rel = evalr($Exp, $Store)
	====================================================
	evalx(top($Exp), $Store) = top($Rel)

[Fbot]	$Rel = evalr($Exp, $Store)
	====================================================
	evalx(bottom($Exp), $Store) = bottom($Rel)

[Finv]	$Rel = evalr($Exp, $Store)
	===================================================
	evalx(inv($Exp), $Store) = inv($Rel)

[Fcmp]	$Rel = evalr($Exp, $Store)
	===================================================
	evalx(compl($Exp), $Store) = compl($Rel)

[Ffst]	evalx($Exp, $Store) = <$Elem1, $Elem2>
	===================================================
	evalx(fst($Exp), $Store) = $Elem1

[Fsec]	evalx($Exp, $Store) = <$Elem1, $Elem2>
	===================================================
	evalx(sec($Exp), $Store) = $Elem2

%% ---- FUN ( EXP , EXP )  -> EXP

[default-fun2]
	value($Var, $Store) = <$Var1, $Var2, $Exp3>,
	$Val1 = evalx($Exp1, $Store),
	$Val2 = evalx($Exp2, $Store),

	$Store' = assign($Var1, $Val1, $Store),
	$Store'' = assign($Var2, $Val2, $Store')
	==================================================
	evalx($Var($Exp1, $Exp2), $Store) = evalx($Exp3, $Store'')

%%      fun domR(REL, $Bag) = { <$x, $y> in REL | $x in $Bag | {<$x, $y}>}

[RdomR]	$Rel = evalr($Exp1, $Store), $Bag = evalb($Exp2, $Store)
	===================================================
	evalx(domR($Exp1, $Exp2), $Store) = domR($Rel, $Bag)

[FranR]	$Rel = evalr($Exp1, $Store), $Bag = evalb($Exp2, $Store)
	===================================================
	evalx(ranR($Exp1, $Exp2), $Store) = ranR($Rel, $Bag)

[FcarR]	$Rel = evalr($Exp1, $Store), $Bag = evalb($Exp2, $Store)
	====================================================
	evalx(carR($Exp1, $Exp2), $Store) = carR($Rel, $Bag)

[FdomX]	$Rel = evalr($Exp1, $Store), $Bag = evalb($Exp2, $Store)
	====================================================
	evalx(domX($Exp1, $Exp2), $Store) = domX($Rel, $Bag)

[FranX]	$Rel = evalr($Exp1, $Store), $Bag = evalb($Exp2, $Store)
	====================================================
	evalx(ranX($Exp1, $Exp2), $Store) = ranX($Rel, $Bag)

[FcarX]	$Rel = evalr($Exp1, $Store), $Bag = evalb($Exp2, $Store)
	====================================================
	evalx(carX($Exp1, $Exp2), $Store) = carX($Rel, $Bag)

%% ---- FUN ( EXP , EXP , EXP)  -> EXP

[default-fun3]
	value($Var, $Store) = <$Var1, $Var2, $Var3, $Exp4>,
	$Val1 = evalx($Exp1, $Store),
	$Val2 = evalx($Exp2, $Store),
	$Val3 = evalx($Exp3, $Store),

	$Store' = assign($Var1, $Val1, $Store),
	$Store'' = assign($Var2, $Val2, $Store'),
	$Store''' = assign($Var3, $Val3, $Store')
	==================================================
	evalx($Var($Exp1, $Exp2, $Exp3), $Store) = evalx($Exp4, $Store''')

[FrX]	$Bag1 = evalb($Exp1, $Store),
	$Bag2 = evalb($Exp2, $Store),
	$Rel  = evalr($Exp3, $Store)
	================================================= 
	evalx(reachX($Exp1, $Exp2, $Exp3), $Store) =
	reachX($Bag1, $Bag2, $Rel)	

[FrR]	$Bag1 = evalb($Exp1, $Store),
        $Bag2 = evalb($Exp2, $Store),
        $Rel  = evalr($Exp3, $Store)
        ================================================= 
        evalx(reachR($Exp1, $Exp2, $Exp3), $Store) =
        reachR($Bag1, $Bag2, $Rel)	





