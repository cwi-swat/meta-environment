dnl $Id$

dnl Process this file with autoconf to produce a configure script.
AC_INIT(configure.in)
AM_INIT_AUTOMAKE(sglr, 3.5)

ACLOCAL="${ACLOCAL} -I ."

dnl Which compiler to use?
AC_REQUIRE([AC_PROG_CC])
AC_PROG_CC

dnl Handle --with-cflags option
AC_ARG_WITH(cflags,
   [  --with-cflags=CFLAGS    standard options of c compiler],
   CFLAGS=${withval},
   if test "a${GCC}" = "ayes"; then
     CFLAGS="-Wall -g -O2"
   fi;
)
AC_SUBST(CFLAGS)

dnl Check for required packages and perform version check when needed.

dnl Require ATerm library
AC_PACKAGE_REQUIRE(aterm, baffle,
   [  --with-aterm=DIR        ATerm-library is in DIR])
AC_SUBST(ATERM)

dnl Require ToolBus library
AC_PACKAGE_REQUIRE(toolbuslib, toolbuslib-config,
   [  --with-toolbuslib=DIR      ToolBus library is in DIR])
AC_SUBST(TOOLBUSLIB)

dnl Optional package ToolBus 
AC_PACKAGE_OPTIONAL(toolbus, tifstoc,
   [  --with-toolbus=DIR      ToolBus is in DIR],
   [  AC_MSG_WARN([ToolBus package not found. It is required when building from CVS.])])
AC_SUBST(TOOLBUS)

dnl Require PT package (for AsFix)
AC_PACKAGE_REQUIRE(pt-support, unparsePT,
   [  --with-pt-support=DIR   ParseTree distribution is in DIR])
AC_SUBST(PT_SUPPORT)

dnl Our own version of libdir whose value is set dynamically
AC_SUBST(INSTLIBDIR)
INSTLIBDIR="$libdir"

dnl Is ranlib needed on this system?
AC_PROG_RANLIB
AC_PATH_PROG(AR,ar,/usr/bin/ar)

dnl What commands are needed to set the variable MAKE in Makefiles?
AC_SET_MAKE

dnl Determine which libraries to use for programs using sockets
NOSOCKLIBS=$LIBS
dnl first, the socket functions
SOCKET_FOUND=no
SOCKLIBS=
AC_CHECK_LIB(sun,socket,[SOCKET_FOUND=yes;SOCKLIBS="-lsun"])
if test ${SOCKET_FOUND} = no; then
  AC_CHECK_LIB(socket, socket,[SOCKET_FOUND=yes;SOCKLIBS="-lsocket"])
  if test ${SOCKET_FOUND} = no; then
    AC_CHECK_LIB(bsd,socket,[SOCKET_FOUND=yes;SOCKLIBS="-lbsd";DEFS="$DEFS -D_BSD=43"])
  fi
fi

dnl then gethostname
GHN_FOUND=no
AC_CHECK_LIB(sun,gethostname,[GHN_FOUND=yes])
if test ${GHN_FOUND} = no; then
  AC_CHECK_LIB(nsl,gethostname,[GHN_FOUND=yes;SOCKLIBS="$SOCKLIBS -lnsl"])
fi

AC_SUBST(SOCKLIBS)

LIBS=$NOSOCKLIBS

AC_CHECK_LIB(dl, dlopen)

dnl Checks for header files.
AC_CHECK_HEADER(getopt.h)

dnl Which files need updating?
AC_OUTPUT(
   Makefile
   doc/Makefile
   dump/Makefile
   libsglr/Makefile
   sglr/Makefile
   test/Makefile
   test/erroneous/Makefile
   test/tables/Makefile
   test/terms/Makefile
)
