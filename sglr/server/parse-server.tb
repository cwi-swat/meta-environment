tool sglr is {
  command = "sglr"
}

process Parser is
  let
    P  : sglr,
    L  : str,
    G  : str,
    S  : str,
    Id : term,
    FN : str,
    T  : term
  in
    execute(sglr, P?) .
    (
      rec-msg(Id?, parse-string(L?, G?, S?)) .
      snd-eval(P, parse-string(L, G, S)) .
      rec-value(P, T?) .
      snd-msg(Id, parse-result(T))
    +
      rec-msg(Id?, parse-string-as-asfix1(L?, G?, S?)) .
      snd-eval(P, parse-string-as-asfix1(L, G, S)) .
      (
        rec-value(P, asfix1-parsetree(T?)) .
        snd-msg(parse-result-asfix1(Id, parse-tree(T)))
      +
        rec-value(P, parse-error(Err?,T?)) .
        snd-msg(parse-result-asfix1(Id, parse-error(Err,T)))
      )
    +
      rec-msg(parse-file(L?, G?, FN?, Id?)) .
      snd-eval(P, parse-file(L, G, FN)) .
      rec-value(P, T?) .
      snd-msg(Id, parse-result(T))
    +
      rec-msg(Id?,open-language(L?, FN?)) .
      snd-eval(P, open-language(L, FN)) .
      (
        rec-value(P, language-opened(L?, FN)) .
        snd-msg(Id?,language-available(L))
      +
        rec-value(P, open-language-failed(L?, FN))
      )
    )
    * rec-disconnect(P)
  endlet


tool parse-client is { }

process FileParse(R : parse-client) is
let
  S    : str,
  TS   : str,
  LN   : str,
  LT   : str,
  L    : str,
  T    : term,
  T1   : term,
  T2   : term,
  Tf   : term,
  I    : int,
  Ch   : int,
  Li   : int,
  Co   : int,
  Char : term
in
  rec-event(R, open-language(LN?,LT?)) .
  snd-msg(R,open-language(LN,LT)) .
  rec-msg(R,language-available(L?)) .
  snd-ack-event(R, open-language(LN,LT)) .
  (
    rec-event(R, parsetext(LN?,TS?,S?)) .
    snd-msg(R, parse-string(LN,TS,S)) .
    (
      rec-msg(R, parse-result(parse-error(T?))) .
      snd-ack-event(R, parsetext(LN, TS, S, parse-error(T)))
    +
      rec-msg(R, parse-result(parsetree(T?,I?))) .
      snd-ack-event(R, parsetext(LN, TS, S, Tf))
    )
  +
    rec-event(R, parsetext-asfix1(LN?,TS?,S?)) .
    snd-msg(R, parse-string-as-asfix1(LN,TS,S)) .
    (
      rec-msg(R, parse-result(parse-error(T?))) .
      snd-ack-event(R, parsetext(LN, TS, S, parse-error(T)))
    +
      rec-msg(R, parse-result(parsetree(T?,I?))) .
      snd-ack-event(R, parsetex-asfix1t(LN, TS, S, Tf))
    )
  ) * rec-disconnect(R)
endlet

process Connect-ParseFile is
let
  R : parse-client,
  P : int
in
  ( rec-connect(R?) .
    create(FileParse(R), P?)
  ) * delta
endlet

toolbus(Parser, AsFix2toAsFix1, Connect-ParseFile)
