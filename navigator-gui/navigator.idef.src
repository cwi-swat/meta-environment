#ifndef __GUI_NAVIGATOR__
#define __GUI_NAVIGATOR__

tool navigator is { }

process Navigator is
let
  T: navigator,
  Graph: graph,
  Menu: term,
  ModuleId: term,
  File: file,
  PopupMenu: list,
  Str : str
in
  rec-connect(T?)
  .
  (
    rec-msg(nav-update-module(ModuleId?, File?))
    . snd-do(T, update-module(ModuleId, File))
  +
    rec-msg(nav-delete-module(ModuleId?))
    . snd-do(T, delete-module(ModuleId))
  +
    rec-msg(nav-select-module(ModuleId?))
    . snd-do(T, select-module(ModuleId))
  +
    rec-event(T, module-selected(ModuleId?))
    . snd-note(nav-module-selected(ModuleId))
    . snd-ack-event(T, module-selected(ModuleId))
  +
    rec-event(T, mouse-event(navigator(ModuleId?),popup))
    . snd-msg(nav-request-popup-event(ModuleId))
    . snd-ack-event(T, mouse-event(navigator(ModuleId),popup))
  +
    rec-msg(nav-show-popup(ModuleId, PopupMenu?))
    . snd-do(T, show-popup(ModuleId, PopupMenu))
  +
    rec-event(T, menu-event(ModuleId?, Menu?))
    . snd-msg(nav-menu-selected(ModuleId, Menu))
    . snd-ack-event(T, menu-event(ModuleId, Menu))
  )
  *
  rec-disconnect(T)
endlet

process NAV-AddListener is
  subscribe(nav-module-selected(<term>))

process NAV-RequestPopup(ModuleId: term?) is
  rec-msg(nav-request-popup-event(ModuleId?))

process NAV-ShowPopup(ModuleId: term, PopupMenu: list) is
  snd-msg(nav-show-popup(ModuleId, PopupMenu))

process NAV-PopupEvent(ModuleId: term?, MenuItem: term?) is
  rec-msg(nav-menu-selected(ModuleId?, MenuItem?))

process StartNavigator is
  snd-msg(load-jar("file://__PKGDATA__/navigator.jar", "__PKGDATA__/gui-util.jar:__PKGDATA__/gui-data.jar:__EXTERNAL_JARS__"))

toolbus(Navigator)

#endif /*__GUI_NAVIGATOR__*/
