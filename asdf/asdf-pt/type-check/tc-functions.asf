equations

[tcm-1] defs() == get-function-defs($AM)
	=====================================================
	typecheck($AM, $TI) = gen-summary(yes)

[tcm-1] <sigs($AFS1*), $STS, $DOS1, $DCS> := $TI,	
	defs($FD+) := get-function-defs($AM),
	sigs($AFS2*) := remove-dobble-sigs(sigs($AFS1*), $STS),
	$ENV1 := set-signatures(type-info-to-env($TI), sigs($AFS2*)),
	$LDF1 := get-dataop-sigs($ENV1),
	$LDF2 := initial-dataop-sigs,
	$DOS2 := concat($LDF1, $LDF2),
        $ENV2 := set-dataop-sigs($ENV1, $DOS2),
	$ANS := typecheck-definitions(defs($FD+), $ENV2),
	$SUM := gen-summary($ANS)
	=====================================================
	typecheck($AM, $TI) = $SUM

[tcd-1] $ANS1 := typecheck-definitions(defs($FD), $ENV),
	$ANS2 := typecheck-definitions(defs($FD+), $ENV)
        ====================================================
        typecheck-definitions(defs($FD, $FD+), $ENV) = $ANS1 $ANS2

[tcd-3] 	

      $ET $LT $CO = $AC := $FD,
      sigs($AFS1*, $AFS1, $AFS2*) := get-signatures($ENV),
      $LT == get-fun-name($AFS1),
      $ANS1 := check-typing($FD, $AFS1, $ENV),
      $ANS2 := check-terminates($FD, $AFS1, $ENV)
      =========================================================================
      typecheck-definitions(defs($FD), $ENV) = $ANS1 $ANS2

[check-typing-1]

      $TY1 := get-using($AFS),
      $TY2 := get-giving($AFS),
      $TY3 := get-raising($AFS),
      AR[nor=$TY4, exc=$TY5, $END] := typecheck-action($AC, $TY1, $ENV),
      yes == subtype-of($TY4, $TY2, get-subtypes($ENV)), 
      yes == subtype-of($TY5, $TY3, get-subtypes($ENV)),
      $ANS := check-infallible($ET $LT $CO = $AC, $AFS, $END)
      =========================================================================
      check-typing($ET $LT $CO = $AC, $AFS, $ENV) = yes $ANS

[check-typing-2] 

      $TY := get-using($AFS),
      $ER := typecheck-action($AC, $TY, $ENV)
      ========================================================================
      check-typing($ET $LT $CO = $AC, $AFS, $ENV) = $ER

[default-check-typing] 

  check-typing($FD, $AFS, $ENV) = 
	 error("Type error", 
	     [localized("Error in function definitions", get-location($FD))])

[tca-function] 

      sigs($AFS1*, $AFS1, $AFS2*) := get-signatures($ENV),
      $LT == get-fun-name($AFS1),
      $TY2 := get-using($AFS1),
      $TY3 := get-giving($AFS1),
      $TY4 := get-raising($AFS1),
      $END := failing($AFS1),
      $TY5 := lub($TY1, $TY2, get-subtypes($ENV))
      ============================================================
      typecheck-action($LT $CO, $TY1, $ENV) = AR[nor=$TY3, exc=$TY4, $END]

[default-function] typecheck-action($LT $CO, $TY1, $ENV) = 
		error("Type error", 
          [localized("Unable to find semantic function with right type", 
			get-location($LT $CO))])

[feedback-0] yes $ANS = $ANS

[feedback-0] $ANS yes = $ANS

[feedback-1] 

  summary("","",[$ER1, $ER1*, info("Type check OK", [localized("", file($SC))]), $ER2*]) =
	      summary("","",[$ER1, $ER1*, $ER2*])

[feedback-2] 

  summary("","",[$ER1*, info("Type check OK", [localized("", file($SC))]), $ER2, $ER2*]) =
	      summary("","",[$ER1*, $ER2, $ER2*])

 
[summary-1] $ER := $AN,
	    summary("", "",[$ER*]) := gen-summary($ANS)
	    ===========================================
	    gen-summary($AN $ANS) = summary("", "", [$ER, $ER*])

[default-summary-1] $ER := $AN
	    ===========================================
	    gen-summary($AN) = summary("", "", [$ER])

[default-summary-3] 

            gen-summary(yes) = summary("","",[info("Type check OK", [localized("", file(""))])])
