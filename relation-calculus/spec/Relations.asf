equations

%% ---- Reduce tuples of one element to that element

[st]	<$Elem> = $Elem

%% ---- is-rel

%%[ir1]	is-rel(bag($C, $Bag1, <$Elem1, $Elem2>, $Bag2)) = true

[ir1]	is-rel({<$Elem1, $Elem2>, $Elem*}) = true

[default-ir]
	is-rel($Bag) = false

%% ---- is-tuple

[it1]	is-tuple(<$Elem+>) = true

[default-it]
	is-tuple($Elem) = false

%% ----	compose

[comp-0] compose($Bag1, $Bag2) = compose1(mk-iterator($Bag1), $Bag2)

[comp1-1]
	is-empty-iter($BagIter) == false,
	<$Elem, $Elem'> := head($BagIter)
	=======================================================
	compose1($BagIter, $Bag) = union(product($Elem, rimage($Bag, $Elem')), compose1(tail($BagIter), $Bag))

[com1p-2]   
	is-empty-iter($BagIter) == true
	=============================================
	compose1($BagIter, $Bag) = empty-bag

%% ----	product

[prode-0]
	product($Elem, $Bag) = product1($Elem, mk-iterator($Bag))

[prode1-1]   
	is-empty-iter($BagIter) == false,
	$Elem' := head($BagIter)
	=============================================
	product1($Elem, $BagIter) = union1(<$Elem, $Elem'>, product1($Elem, tail($BagIter)))

[prode1-2]
	is-empty-iter($BagIter) == true
	=============================================
	product1($Elem, $BagIter) = empty-bag

[prodb-0]
	product($Bag1, $Bag2) = product1(mk-iterator($Bag1), $Bag2)

[prodb1-1] 
	is-empty-iter($BagIter) == false,
	$Elem := head($BagIter)
	===============================================
	product1($BagIter, $Bag) = union(product1($Elem, $Bag), product1(tail($BagIter), $Bag))

[prodb1-2] 
	is-empty-iter($BagIter) == true
	================================================
	product1($BagIter, $Bag) = empty-bag

[iprod] $Bag1 x $Bag2 = product($Bag1, $Bag2)

%% ----	domain

[dom-0]	 domain($Bag) = domain1(mk-iterator($Bag))

[dom1-1] is-empty-iter($BagIter) == false,
	 <$Elem,$Elem+> := head($BagIter)
	 =================================================
	 domain1($BagIter) = insert($Elem, domain1(tail($BagIter)))

[dom1-2] is-empty-iter($BagIter) == true
	 =================================================
	 domain1($BagIter) = empty-bag

%% ---- range

[ran-0]	 range($Bag) = range1(mk-iterator($Bag))

[ran1-1]  is-empty-iter($BagIter) == false, 
	 <$Elem,$Elem+> := head($BagIter)
	 =================================================
	 range1($BagIter) = insert(<$Elem+>, range1(tail($BagIter)))

[ran1-2]  is-empty-iter($BagIter) == true
	 =================================================
	 range1($BagIter) = empty-bag

%% ----	 carrier

[car-0]	 carrier($Bag) = carrier1(mk-iterator($Bag))

[car1-1] is-empty-iter($BagIter) == false,
	 <$Elem, $Elem+> := head($BagIter)
	 ==============================================
	 carrier1($BagIter) = insert($Elem, insert(<$Elem+>, carrier1(tail($BagIter))))

[car1-2]	is-empty-iter($BagIter) == true
	================================================
	carrier1($BagIter) = empty-bag

%% ----	domainR

[domR-0] domainR($Bag1, $Bag2) = domainR1(mk-iterator($Bag1), $Bag2)

[domR1-1] is-empty-iter($BagIter) == false,
	 <$Elem, $Elem+> := head($BagIter),
	 in($Elem, $Bag) == false
	 ===================================================
	 domainR1($BagIter, $Bag) = domainR1(tail($BagIter), $Bag)

[domR1-2] is-empty-iter($BagIter) == false,
	 <$Elem, $Elem+> := head($BagIter),
	 in($Elem, $Bag) == true
	 ===================================================
	 domainR1($BagIter, $Bag) = union1(<$Elem, $Elem+>, domainR1(tail($BagIter),
	 $Bag))

[domR1-3] is-empty-iter($BagIter) == true
	 ===================================================
	 domainR1($BagIter, $Bag) = empty-bag

%% ----	 domainX

[domX-0] domainX($Bag1, $Bag2) = domainX1(mk-iterator($Bag1), $Bag2)

[domX1-1] is-empty-iter($BagIter) == false,
	 <$Elem, $Elem+> := head($BagIter),
	 in($Elem, $Bag) == true
	 ===================================================
	 domainX1($BagIter, $Bag) = domainX1(tail($BagIter), $Bag)

[domX1-2] is-empty-iter($BagIter) == false,
	 <$Elem, $Elem+> := head($BagIter),
	 in($Elem, $Bag) == false
	 ===================================================
	 domainX1($BagIter, $Bag) = union1(<$Elem, $Elem+>, domainX1(tail($BagIter), $Bag))

[domX1-3] is-empty-iter($BagIter) == true
	 ===================================================
	 domainX1($BagIter, $Bag) = empty-bag
	
%% ----	rangeR

[ranR-0] rangeR($Bag1, $Bag2) = rangeR1(mk-iterator($Bag1), $Bag2)

[ranR1-1] is-empty-iter($BagIter) == false,
	 <$Elem, $Elem+> := head($BagIter),
	 in(<$Elem+>, $Bag) == false
	 ===================================================
	 rangeR1($BagIter, $Bag) = rangeR1(tail($BagIter), $Bag)

[ranR1-2] is-empty-iter($BagIter) == false,
	 <$Elem, $Elem+> := head($BagIter),
	 in(<$Elem+>, $Bag) == true
	 ===================================================
	 rangeR1($BagIter, $Bag) = union1(<$Elem, $Elem+>, rangeR1(tail($BagIter), $Bag))

[ranR1-3] is-empty-iter($BagIter) == true
	 ===================================================
	 rangeR1($BagIter, $Bag) = empty-bag

%% ----	 rangeX

[ranX-0] rangeX($Bag1, $Bag2) = rangeX1(mk-iterator($Bag1), $Bag2)

[ranX1-1] is-empty-iter($BagIter) == false,
	 <$Elem, $Elem+> := head($BagIter),
	 in(<$Elem+>, $Bag) == true
	 ===================================================
	 rangeX1($BagIter, $Bag) = rangeX1(tail($BagIter), $Bag)

[ranX1-2] is-empty-iter($BagIter) == false,
	 <$Elem, $Elem+> := head($BagIter),
	 in(<$Elem+>, $Bag) == false
	 ===================================================
	 rangeX1($BagIter, $Bag) = union1(<$Elem, $Elem+>, rangeX1(tail($BagIter), $Bag))

[ranX1-3] is-empty-iter($BagIter) == true
	 ===================================================
	 rangeX1($BagIter, $Bag) = empty-bag

%% ----	 carrierR

[carR-0] carrierR($Bag1, $Bag2) = carrierR1(mk-iterator($Bag1), $Bag2)

[carR1-1] is-empty-iter($BagIter) == false,
	 <$Elem, $Elem+> := head($BagIter),
	 in($Elem, $Bag) & in(<$Elem+>, $Bag) == false
	 ===================================================
	 carrierR1($BagIter, $Bag) = carrierR1(tail($BagIter), $Bag)

[carR1-2] is-empty-iter($BagIter) == false,
	 <$Elem, $Elem+> := head($BagIter),
	 in($Elem, $Bag) & in(<$Elem+>, $Bag) == true	 
	 ===================================================
	 carrierR1($BagIter, $Bag) = union1(<$Elem, $Elem+>, carrierR1(tail($BagIter), $Bag))

[carR1-3] is-empty-iter($BagIter) == true
	 ===================================================
	 carrierR1($BagIter, $Bag) = empty-bag

%% ----	 carrierX

[carX-0] carrierX($Bag1, $Bag2) = carrierX1(mk-iterator($Bag1), $Bag2)

[carX1-1] is-empty-iter($BagIter) == false,
	 <$Elem, $Elem+> := head($BagIter),
	 in($Elem, $Bag) | in(<$Elem+>, $Bag) == true
	 ===================================================
	 carrierX1($BagIter, $Bag) = carrierX1(tail($BagIter), $Bag)

[carX1-2] is-empty-iter($BagIter) == false,
	 <$Elem, $Elem+> := head($BagIter),
	 in($Elem, $Bag) | in(<$Elem+>, $Bag) == false	 
	 ===================================================
	 carrierX1($BagIter, $Bag) = union1(<$Elem, $Elem+>, carrierX1(tail($BagIter), $Bag))

[carX1-3] is-empty-iter($BagIter) == true
	 ===================================================
	 carrierX1($BagIter, $Bag) = empty-bag

%% ---- bottom

[top-1]  top($Rel) = diff(unique(domain($Rel)), range($Rel))

%% ----	 bottom

[bot-1]  bottom($Rel) = diff(unique(range($Rel)), domain($Rel))

%% ----	 rimage

[rim-0]	 rimage($Bag, $Elem) = rimage1(mk-iterator($Bag), $Elem)

[rim1-1]	 is-empty-iter($BagIter) == false,
	 <$Elem1, $Elem+> := head($BagIter),
	 $Elem == $Elem1						%% sb elem-equal
	 ==================================================
	 rimage1($BagIter, $Elem) = union1(<$Elem+>, rimage1(tail($BagIter), $Elem))

[rim1-2]	 is-empty-iter($BagIter) == false,
	 <$Elem1, $Elem+> := head($BagIter),
	 $Elem != $Elem1					%% sb not elem-equal
	 ==================================================
	 rimage1($BagIter, $Elem) = rimage1(tail($BagIter), $Elem)

[rim1-2]	 is-empty-iter($BagIter) == true
	 ==================================================
	 rimage1($BagIter, $Elem) = empty-bag

[brim-0] rimage($Bag1, $Bag2) = rimage1($Bag1, mk-iterator($Bag2))

[brim1-1] is-empty-iter($BagIter) == false,
	 $Elem := head($BagIter)
	 ===================================================
	 rimage1($Bag, $BagIter) = union(rimage($Bag, $Elem), rimage1($Bag, tail($BagIter)))

[brim1-2] is-empty-iter($BagIter) == true
	 ====================================================
	 rimage1($Bag, $BagIter) = empty-bag

%% ----	 limage

[lim-0]	 limage($Bag, $Elem) = limage1(mk-iterator($Bag), $Elem)

[lim1-1]	 is-empty-iter($BagIter) == false,
	 <$Elem1, $Elem1'> := head($BagIter),
	 $Elem == $Elem1'					%% sb elem-equal
	 ==================================================
	 limage1($BagIter, $Elem) = union1($Elem1, limage1(tail($BagIter), $Elem))

[lim1-2]	 is-empty-iter($BagIter) == false,
	 <$Elem1, $Elem1'> := head($BagIter),
	 $Elem != $Elem1'					%% sb not elem-equal
	 ==================================================
	 limage1($BagIter, $Elem) = limage1(tail($BagIter), $Elem)

[lim1-2]	 is-empty-iter($BagIter) == true
	 ==================================================
	 limage1($BagIter, $Elem) = empty-bag

[blim-0] limage($Bag1, $Bag2) = limage1($Bag1, mk-iterator($Bag2))

[blim1-1] is-empty-iter($BagIter) == false,
	 $Elem := head($BagIter)
	 ===================================================
	 limage1($Bag, $BagIter) = union(limage($Bag, $Elem), limage1($Bag, tail($BagIter)))

[blim1-2] is-empty-iter($BagIter) == true
	 ====================================================
	 limage1($Bag, $BagIter) = empty-bag

%% ----	 inv

[inv-0]	 inv($Bag) = inv1(mk-iterator($Bag))

[inv1-1]	 is-empty-iter($BagIter) == false,
	 <$Elem, $Elem+> := head($BagIter)
	 ====================================================
	 inv1($BagIter) = union1(<$Elem+, $Elem>, inv1(tail($BagIter)))

[inv1-2]	 is-empty-iter($BagIter) == true
	 ===================================================
	 inv1($BagIter) = empty-bag

%% ----	 compl

[cmp-1]  $Bag := carrier($Rel)
         =====================================
         compl($Rel) = diff(product($Bag, $Bag), $Rel)

%% ---- id

[id-0]	 id($Bag) = id1(mk-iterator($Bag))

[id1-1]	 is-empty-iter($BagIter) == false,
	 $Elem := head($BagIter)
	 ==================================================
	 id1($BagIter) = union1(<$Elem, $Elem>, id1(tail($BagIter)))

[id1-2]	 is-empty-iter($BagIter) == true
	 ==================================================
	 id1($BagIter) = empty-bag

%% ---- closure

%%[cl-1] equal($Rel1, $Rel2) = false
%%       =========================================
%%       closure($Rel, $Rel1, $Rel2) = closure($Rel, $Rel2, union($Rel2, compose($Rel2, $Rel)))

%%[default-cl]
%%	closure($Rel, $Rel1, $Rel2) = $Rel1

[cl-1]	$Rel1 != $Rel2,
	$Rel2' := closure1(mk-iterator($Rel2), $Rel, $Rel2) 
	==============================================	
	closure($Rel, $Rel1, $Rel2) = closure($Rel, $Rel2, $Rel2')

[default-cl]
	closure($Rel, $Rel1, $Rel2) = $Rel2

[cl1-1]	is-empty-iter($BagIter) == false,
	<$Elem1, $Elem1'> := head($BagIter),
	$Rel2' := closure-comp(mk-iterator($Rel1), $Elem1, $Elem1', $Rel2)
	==============================================================
	closure1($BagIter, $Rel1, $Rel2) = closure1(tail($BagIter), $Rel1, $Rel2')

[cl1-2]	is-empty-iter($BagIter) == true
	==============================================================
	closure1($BagIter, $Rel1, $Rel2) = $Rel2

[cc-1]	is-empty-iter($BagIter) == false,
	<$Elem2, $Elem2'> := head($BagIter),
	$Elem1' == $Elem2
	=======================================================
	closure-comp($BagIter, $Elem1, $Elem1', $Bag) = 
	closure-comp(tail($BagIter), $Elem1, $Elem1', union1(<$Elem1, $Elem2'>, $Bag))

[cc-2]	is-empty-iter($BagIter) == false,
	<$Elem2, $Elem2'> := head($BagIter),
	$Elem1' != $Elem2					%% sb not elem-equal
	=======================================================
	closure-comp($BagIter, $Elem1, $Elem1', $Bag) = 
	closure-comp(tail($BagIter), $Elem1, $Elem1', $Bag)

[cc-3] 	is-empty-iter($BagIter) == true
	=============================================
	closure-comp($BagIter, $Elem1, $Elem1', $Bag) = $Bag

%% ---- trans-closure

[tcl-1]  trans-closure($Rel) = closure($Rel, empty-bag, $Rel)

%% ---- rtrans-closure

[rtcl-1] rtrans-closure($Rel) = union(trans-closure($Rel), id(carrier($Rel)))

%% ---- reachX

[rX-3]	$Rel' := carrierX($Rel, $Bag2),
	$Rel'':= domainR($Rel', $Bag1)
	=========================================
	reachX($Bag1, $Bag2, $Rel) = closure($Rel', empty-bag, $Rel'')

%% ---- reachR

[rX-3]	$Rel' := carrierR($Rel, $Bag2),
	$Rel'':= domainR($Rel', $Bag1)
	=========================================
	reachR($Bag1, $Bag2, $Rel) = closure($Rel', empty-bag, $Rel'')

