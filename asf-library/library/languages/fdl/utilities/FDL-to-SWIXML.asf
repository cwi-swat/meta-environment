equations

[feature-diagram-to-swixml-top]
        featurediagram-to-swixml($featurediagram) =
                <?xml version="1.0" encoding="UTF-8"?>
		features-to-swixml($featurediagram, <panel></panel>)
                
equations

[features-to-swixml-trv-top]
        $attvalue := featurename-to-attvalue($featurename),
        $element' := <panel><label text=$attvalue/></panel>
        ===
        features-to-swixml($featurename : $featureexpression, $stag $content* $etag) =
		$stag
		$content*
		featureexpression-to-widgets($featureexpression, $element')
		$etag

equations

[featureexpression-to-widgets-1]
	$element := features-to-radiobuttons($featureexpression+, <buttongroup></buttongroup>)
	===
        	featureexpression-to-widgets(one-of($featureexpression+), $stag $content* $etag) =
		$stag $content* $element $etag

[featureexpression-to-widgets-2]
	$element' := features-to-checkboxes($featureexpression+, $element)
	===
        	featureexpression-to-widgets(more-of($featureexpression+), $element) =
		$element'

[featureexpression-to-widgets-3]
	$attvalue := atomicfeature-to-attvalue($atomicfeature)
	===
	feature-to-radiobutton($atomicfeature, $stag $content* $etag) =
		$stag 
		$content*	
		<radiobutton name=$attvalue label=$attvalue/>
		$etag 

equations

[features-to-radiobuttons-single]
	features-to-radiobuttons($featureexpression, $element) =
		feature-to-radiobutton($featureexpression, $element)

[features-to-radiobuttons-multi]
	$element' := features-to-radiobuttons($featureexpression, $element),
	$element'' := features-to-radiobuttons($featureexpression+, $element')
	===
	features-to-radiobuttons($featureexpression, $featureexpression+, $element) =
		$element''



equations

[features-to-checkboxes-single]
	features-to-checkboxes($featureexpression, $element) =
		feature-to-checkbox($featureexpression, $element)

[features-to-checkboxes-multi]
	$element' := features-to-checkboxes($featureexpression, $element),
	$element'' := features-to-checkboxes($featureexpression+, $element')
	===
	features-to-checkboxes($featureexpression, $featureexpression+, $element) =
		$element''



equations

[atomicfeature-to-attvalue-single]
	atomicfeature-to-attvalue(atomicfeature($atomic-head)) = attvalue(\" av-chard($atomic-head) \")

[atomicfeature-to-attvalue-multi]
	attvalue(\" av-chard($av-chard) $rest \") := atomicfeature-to-attvalue(atomicfeature(x $tail-char*))
	=== 
	atomicfeature-to-attvalue(atomicfeature($atomic-head $tail-char $tail-char*)) =  
		attvalue(\" av-chard($atomic-head) av-chard($tail-char) $rest \")

tests

[single]	atomicfeature-to-attvalue(a) == "a"

[multi]	atomicfeature-to-attvalue(abc) == "abc"

[many]	atomicfeature-to-attvalue(aBCdE) == "aBCdE"

equations

[featurename-to-attvalue-single]
	featurename-to-attvalue(featurename($featurename-head)) = attvalue(\" av-chard($featurename-head) \")

[featurename-to-attvalue-multi]
	attvalue(\" av-chard($av-chard) $rest \") := featurename-to-attvalue(featurename(X $tail-char*))
	=== 
	featurename-to-attvalue(featurename($featurename-head $tail-char $tail-char*)) =  
		attvalue(\" av-chard($featurename-head) av-chard($tail-char) $rest \")

tests

[single]	featurename-to-attvalue(A) == "A"

[multi]	featurename-to-attvalue(Abc) == "Abc"

[many]	featurename-to-attvalue(AbcDe) == "AbcDe"
