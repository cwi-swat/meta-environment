equations

%% \noindent
%% Merging and ordering of grammars.

  [r1] restrictions restr*1  <> restrictions restr*2 = <restrictions restr*1 restr*2, (/)>
  [r2] restrictions restr* <> syntax p* = <syntax p*, restrictions restr*>

  [r3] restr*1 'a1 _A 'a2 -/- las1 restr*2 'b1 _A 'b2 -/- las2 restr*3 =
     restr*1 _A -/- las1 | las2 'a1 'a2 -/- las1 restr*2 'b1 'b2 -/- las2 restr*3

  [r4] restr*1 -/- las restr*2 = restr*1 restr*2

%% Converting binary union to lists of lookaheads.

  [r5]    'a -/- cc            = 'a -/- [[ cc ]]
  [r6]    'a -/- cc . las      = 'a -/- [[ cc . las]]

  [las-1'1]    cc . cc'             = cc . [[cc']]

  [las-1'2]    cc . cc'. las             = cc . [[cc' . las]]

  [r7]    cc       | las       = [[cc]] | las
  [r8]    las      | cc        = las | [[cc]]
  [r9]    cc . las | las'      = [[cc . las]] | las'
  [r10]    las      | cc . las' = las | [[cc . las']]

  [r11]    [[la*1]] | [[la*2]] = [[la*1, la*2]]

%% Normalizing lookaheads

  [r12]    [] . las   = []
  [r13]    cc . cc'   = cc when cc' == ~[]
  [r14]    cc . [[ ]] = cc

  [r15]    [[la*1, [], la*2]] = [[la*1, la*2]]

  [r16]    [[cc2 . las, la*1, cc1, la*2]] =
        [[cc1, cc2 . las, la*1, la*2]]

  [r17]    [[la*1, cc1, la*2, cc2, la*3]] =
        [[la*1, cc1 \/ cc2, la*2, la*3]]

  [r18]    cc3 := cc1 /\ cc2, cc3 != []
        ==================================
        [[cc1, la*2, cc2 . las, la*3]] =
        [[cc1, la*2, cc2 / cc3 . las, la*3]]

  [r19]    cc3 := cc1 /\ cc2, cc3 != []
        =======================================================================
        [[la*1, cc1 . las1, la*2, cc2 . las2, la*3]] =
        [[la*1, cc1 / cc3 . las1, cc3 . (las1 | las2), la*2, cc2 / cc3 . las2, la*3]]
