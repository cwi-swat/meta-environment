equations

[ff-top]
  find-process-facts(TscriptDefs toolbus(ProcessInvocations)) = 
    find-process-facts(TscriptDefs, {})

[ff-basic]
  get-process-name(ProcessSignature) = ProcessName,
  get-formal-parameters(ProcessSignature) = FormalParams,
  get-local-variables(ProcessBody) = VarDecls,
  find-facts-in-process(ProcessBody, 
                        make-process-facts(process-declaration ProcessName,{},{},{},{}),
                        FormalParams,VarDecls) = ProcessFacts
  ===>
  find-process-facts(process ProcessSignature is ProcessBody, ProcessFactsSet) =
    ProcessFactsSet + { ProcessFacts }

[ff-create-basic]
  find-facts-in-process(create(ProcessName, GenVar), CollectedFacts,
                        FormalParams, VarDecls) =
    add-process-creation(CollectedFacts, process-creation ProcessName)

[ff-create-param]
  find-facts-in-process(create(ProcessName(Terms), GenVar), CollectedFacts,
                        FormalParams, VarDecls) =
    add-process-creation(CollectedFacts, process-creation ProcessName)

[ff-invocation-basic]
  find-facts-in-process(ProcessName, CollectedFacts,
                        FormalParams, VarDecls) =
    add-process-invocation(CollectedFacts, process-invocation ProcessName)

[ff-invocation-param]
  find-facts-in-process(ProcessName(Terms), CollectedFacts,
                        FormalParams, VarDecls) =
  add-process-invocation(CollectedFacts, process-invocation ProcessName)

[ff-send-complex]
  make-signature(snd-msg(TermPlus), FormalParams, VarDecls) = SendFact
  ===>
  find-facts-in-process( snd-msg(TermPlus), CollectedFacts,
                        FormalParams,VarDecls) = 
  add-send-message(CollectedFacts, SendFact)

[ff-recv-complex]
  make-signature(rec-msg(TermPlus), FormalParams, VarDecls) = RecFact
  ===>
  find-facts-in-process( rec-msg(TermPlus), CollectedFacts,
                        FormalParams,VarDecls) =     
     add-receive-message(CollectedFacts, RecFact)

[fpf-make-signature]
  make-signature-term(TermPlus1, FormalParams, VarDecls) = TermPlus2
  ===>
  make-signature(snd-msg(TermPlus1), FormalParams, VarDecls) = send-atom TermPlus2

[fpf-make-signature]
  make-signature-term(TermPlus1, FormalParams, VarDecls) = TermPlus2
  ===>
  make-signature(rec-msg(TermPlus1), FormalParams, VarDecls) = recv-atom TermPlus2

[fpf-make-signature-term]
  make-signature-term(Var, FormalParams1, Var : Term, FormalParams2, VarDecls) = Term

[fpf-make-signature-term]
  make-signature-term(Var?, FormalParams1, Var : Term?, FormalParams2, VarDecls) = Term

[fpf-make-signature-term]
  make-signature-term(Var, FormalParams, VarDecls1, Var : Term, VarDecls2) = Term

[fpf-make-signature-term]
  make-signature-term(Var?, FormalParams, VarDecls1, Var : Term, VarDecls2) = Term