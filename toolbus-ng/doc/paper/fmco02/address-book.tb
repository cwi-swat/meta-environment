process CREATE(AB : address-book) is
let AID : int in
  rec-msg(create-address) .
  snd-eval(AB, create-entry) .
  rec-value(AB, new-entry(AID?)) .
  snd-msg(address-created(AID))
endlet

process DELETE(AB : address-book) is
let
  AID : int
in
  rec-msg(delete-address(AID?)) .
  snd-do(AB, delete-entry(AID)) .
  snd-msg(address-deleted(AID))
endlet

process SEARCH(AB : address-book) is
let
  Aid : int,
  Name : str,
  Address : str
in
  ( rec-msg(find-by-address(Address?)) .
    snd-eval(AB, find-by-address(Address))
  +
    rec-msg(find-by-name(Name?)) .
    snd-eval(AB, find-by-name(Name))
  ) .
  ( rec-value(AB, found(Aid?)) .
    snd-msg(found(Aid))
  +
    rec-value(AB, not-found) .
    snd-msg(not-found)
  )
endlet

process UPDATE(AB : address-book) is
let
  AID : int,
  Name : str,
  Address : str
in
  rec-msg(update-entry(AID?)) .
  ( rec-msg(set-name(Name?)) .
    snd-do(AB, set-name(AID, Name))
  + rec-msg(set-address(Address?)) .
    snd-do(AB, set-address(AID, Address))
  ) *
  rec-msg(update-entry-done)
endlet

process ADDRESSBOOK is
let ABTool : address-book
in
  execute(address-book, ABTool?) .
  (
    CREATE(ABTool)
  + DELETE(ABTool)
  + SEARCH(ABTool)
  + UPDATE(ABTool)
  ) * delta
endlet

tool address-book is { command = "java-adapter -class TBAddressBookService" }

process UI is
let
  UITool : ui,
  AID : int,
  Name : str
in
  rec-connect(UITool?) .
  (
    rec-event(UITool, create-address) .
    snd-msg(create-address) .
    rec-msg(address-created) .
    snd-ack-event(UITool, create-address)
  +
    rec-event(UITool, update-name(AID?, Name?)) .
    snd-msg(update-address(AID)) .
    snd-msg(set-name(Name)) .
    snd-msg(update-entry-done(AID)) .
    snd-ack-event(UITool, new-name(AID, Name))
  )
  * rec-disconnect(UITool)
endlet

tool ui is { }

toolbus(ADDRESSBOOK)
