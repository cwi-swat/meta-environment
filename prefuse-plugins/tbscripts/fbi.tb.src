#ifndef _FBI_
#define _FBI_

tool factbrowser is { }

/**
 * @desc FactBrowser Connection Process
 *
 */

process StartFBI is
  snd-msg(load-jar("file://__PKGDATA__/vis-base.jar", "__PKGDATA__/relationstores.jar:__PKGDATA__/log4j-1.2.14.jar:__PKGDATA__/commons-logging-1.1.jar:__PKGDATA__/gui-util.jar:__PKGDATA__/gui-data.jar:__EXTERNAL_JARS__"))

process FBI is
let 
  Tid: factbrowser,
  FactType: term,
  PluginId: int,
  PluginName: str,
  RstorePath: str,
  RstoreName: str,
  RstoreId: int,
  RstoreFacts: list,
  FactId: int,
  FileName: str
in
    rec-connect(Tid?)
    . subscribe(rc-rstore-unloaded(<int>))
    .
    (
      rec-msg(fb-visualization-plugin-available(FactType?, PluginId?, PluginName?))
       . snd-do(Tid, fb-add-visualization-plugin(FactType, PluginId, PluginName))
    +
      rec-event(Tid, fb-load-rstore)
      . snd-ack-event(Tid, fb-load-rstore)
      . snd-msg(ask-for-file("Select the RStore", [], ".rstore"))
      . 
      (   
         rec-msg(ask-for-file-result(FileName?))
         . snd-msg(fb-load-rstore(FileName))
       + 
         rec-msg(ask-for-file-cancel)
      )	
    +
      rec-event(Tid, fb-type-selected(FactType?))
      . snd-ack-event(Tid, fb-type-selected(FactType))
      . snd-msg(fb-type-selected(FactType))
    +
      rec-msg(fb-show-rstore-facts(RstoreName?, RstoreId?, RstoreFacts?))
      . snd-do(Tid, fb-show-rstore-facts(RstoreName, RstoreId, RstoreFacts))
    +
      rec-event(Tid, fb-visualization-selected(RstoreId?, FactId?, PluginId?))
      . snd-ack-event(Tid, fb-visualization-selected(RstoreId, FactId, PluginId))
      . snd-msg(fb-visualization-selected(RstoreId, FactId, PluginId))
    +
      rec-event(Tid, fb-unload-rstore(RstoreId?))
      . snd-ack-event(Tid, fb-unload-rstore(RstoreId))
      . snd-msg(fb-unload-rstore(RstoreId))
    +
      rec-note(rc-rstore-unloaded(RstoreId?))
      . snd-do(Tid, fb-rstore-unloaded(RstoreId))
   )
   * delta
endlet


toolbus(FBI)
toolbus(StartFBI)

#endif /*_FBI_*/
