/*

    ToolBus -- The ToolBus Application Architecture
    Copyright (C) 1998-2000  Stichting Mathematisch Centrum, Amsterdam, 
                             The  Netherlands.

    This program is free software; you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation; either version 2 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program; if not, write to the Free Software
    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307 USA

*/


process PLAYER(Bid : board, Player : str) is
  let Pid : player, Row : int, Col : int, Msg : str, Player1 : str
  in
     execute(player(Player), Pid?) .
     snd-do(Pid, mk-board(Player, 10)) .
     subscribe(error(<str>)) . subscribe(wins(<str>)) . subscribe(draw) .
     subscribe(occupy(<str>,<int>,<int>)) .
     ( rec-event(Pid, push(Row?, Col?)) .
         snd-eval(Bid, push(Player, Row, Col)) .
         ( rec-value(Bid, ok) . snd-note(occupy(Player, Row, Col)) .
		snd-do(Pid,occupy(Player, Row, Col))
         + rec-value(Bid, error(Msg?)) . snd-do(Pid, error(Msg))
	 ) . snd-ack-event(Pid, push(Row, Col))
     + rec-note(error(Msg?)) . snd-do(Pid, error(Msg))
     + rec-note(occupy(Player1?, Row?, Col?)) .
	 snd-do(Pid,occupy(Player1, Row, Col))
     ) *
     ( rec-value(Bid, wins(Player1?)) . snd-note(wins(Player1)) .
	snd-ack-event(Pid, push(Row, Col))
     + rec-value(Bid, draw) . snd-note(draw) . snd-ack-event(Pid, push(Row, Col))
     + rec-note(draw) . snd-do(Pid, draw)
     + rec-note(wins(Player1?)) . snd-do(Pid, wins(Player1))
     )
  endlet

tool player(A:str) is {command = "wish-adapter -script ui-game.tcl -script-args -name A"}


process GAME is
  let Bid : board, A : int, B : int
  in
    execute(board, Bid?) .
    create(PLAYER(Bid, "playerA"), A?) .
    create(PLAYER(Bid, "playerB"), B?)
  endlet

tool board is  {command = "board -size 10"}

toolbus(GAME)
