dnl $Id$

dnl Process this file with autoconf to produce a configure script.
AC_INIT
AC_CONFIG_SRCDIR([configure.in])
GLT_INIT_PACKAGE
AC_PREREQ(2.59)

AC_PROG_CC

dnl {{{  Set CFLAGS

dnl Handle --with-cflags option
AC_ARG_WITH(cflags,
   [  --with-cflags=CFLAGS    standard options of c compiler],
   CFLAGS=${withval},
   if test "a${GCC}" = "ayes"; then
     CFLAGS="-Wall -g -O2"
   fi;
)
AC_SUBST(CFLAGS)

dnl }}}

dnl {{{  ATerm

dnl Require ATerm library
AC_PACKAGE_REQUIRE(aterm, baffle,
   [  --with-aterm=DIR        ATerm-library is in DIR])
AC_SUBST(ATERM)


dnl }}}
dnl {{{  toolbus library
AC_PACKAGE_REQUIRE(toolbuslib, toolbuslib-config,
   [  --with-toolbuslib=DIR   ToolBus library is in DIR])
AC_SUBST(TOOLBUSLIB)
dnl }}}
dnl {{{  toolbus Java library
AC_PACKAGE_REQUIRE(toolbus-java-adapter, toolbus-java-adapter-config,
   [  --with-toolbus-java-adapter=DIR   ToolBus Java library is in DIR])
AC_SUBST(TOOLBUS_JAVA_ADAPTER)
dnl }}}
dnl {{{  error-support

dnl Require error-support package
AC_PACKAGE_REQUIRE(error-support, error-support-config,
   [  --with-error-support=DIR   error-support is in DIR])
AC_SUBST(ERROR_SUPPORT)


dnl }}}
dnl {{{  pt-support

dnl Require pt-support package
AC_PACKAGE_REQUIRE(pt-support, unparsePT,
   [  --with-pt-support=DIR   pt-support is in DIR])
AC_SUBST(PT_SUPPORT)


dnl }}}
dnl {{{  graph-support

dnl Require graph-support package
AC_PACKAGE_REQUIRE(graph-support, graph-support-config,
   [  --with-graph-support=DIR   graph-support is in DIR])
AC_SUBST(GRAPH_SUPPORT)


dnl }}}
dnl {{{  Require socket/gethostname

NOSOCKLIBS=$LIBS
dnl {{{  check for socket in -lsun and -lsocket
SOCKET_FOUND=no
SOCKLIBS=
AC_CHECK_LIB(sun,socket,[SOCKET_FOUND=yes;SOCKLIBS="-lsun"])
if test ${SOCKET_FOUND} = no; then
  AC_CHECK_LIB(socket, socket,[SOCKET_FOUND=yes;SOCKLIBS="-lsocket"])
fi

dnl }}}
dnl {{{  check for gethostname in -lsun and -lsocket

GHN_FOUND=no
AC_CHECK_LIB(sun,gethostname,[GHN_FOUND=yes])
if test ${GHN_FOUND} = no; then
  AC_CHECK_LIB(nsl,gethostname,[GHN_FOUND=yes;SOCKLIBS="$SOCKLIBS -lnsl"])
fi

dnl }}}
AC_SUBST(SOCKLIBS)
LIBS=$NOSOCKLIBS

dnl }}}

AC_PATH_PROG(JAVA, java)
if test "x${JAVA}" = "x"; then
  AC_MSG_ERROR("java not found")
fi

dnl This variable holds all external jarfiles
EXTERNAL_JARS=${JAVA_ATERM}:\`${TOOLBUS_JAVA_ADAPTER}/bin/toolbus-java-adapter-config\`:\`${GRAPH_SUPPORT}/bin/graph-support-config\`
AC_SUBST(EXTERNAL_JARS)

dnl Our own version of libdir whose value is set dynamically
AC_SUBST(INSTLIBDIR)
INSTLIBDIR="$libdir"

dnl Is ranlib needed on this system?
AC_PROG_RANLIB
AC_PATH_PROG(AR,ar,/usr/bin/ar)

dnl What commands are needed to set the variable MAKE in Makefiles?
AC_PROG_MAKE_SET([])

dnl Which files need updating?
AC_CONFIG_FILES([Makefile
  lib/Makefile
  utils/Makefile
  utils/sdf-modules/Makefile
  utils/sdf-modules/src/Makefile
  utils/sdf-renaming/Makefile
  utils/sdf-renaming/src/Makefile
  utils/import-graph/Makefile
  utils/import-graph/sdf/Makefile
  test/Makefile
])
AC_OUTPUT
