/* $Id$ */

tool editor-plugin is { }

process EditorPlugin is
let
  T : editor-plugin,
  ActionList : list,
  Area : term,
  Column: int,
  EditorId : term,
  Editor : str,
  Filename : str,
  Line: int,
  Offset : int,
  MenuEvent : term,
  Message : str,
  Modified : term,
  Pid: int,
  Categories: term,
  Slices: term
in
  StartEditorPanel()
  . rec-connect(T?)
  .
  (
    rec-msg(te-edit-text(EditorId?, Filename?))
    . snd-do(T, edit-file(EditorId, Filename))
  +
    rec-msg(te-set-readonly(EditorId?))
    . snd-do(T, set-readonly(EditorId))
  +
    rec-msg(te-add-actions(EditorId?, ActionList?))
    . snd-do(T, add-actions(EditorId, ActionList))
  +
    rec-msg(te-write-contents(EditorId?))
    . snd-do(T, write-contents(EditorId))
  +
    rec-msg(te-reread-contents(EditorId?))
    . snd-do(T, reread-contents(EditorId))
  +
    rec-msg(te-is-modified(EditorId?))
    . snd-do(T, is-modified(EditorId))
  +
    rec-msg(te-set-focus(EditorId?, Area?))
    . snd-do(T, set-focus(EditorId, Area))
  +
    rec-msg(te-set-cursor-at-offset(EditorId?, Offset?))
    . snd-do(T, set-cursor-at-offset(EditorId, Offset))
  +
    rec-msg(te-clear-focus(EditorId))
    . snd-do(T, clear-focus(EditorId))
  +
    rec-msg(te-register-text-categories(EditorId, Categories?)) 
    . snd-do(T, register-text-categories(EditorId, Categories))
  +
    rec-msg(te-highlight-slices(EditorId, Slices?)) 
    . snd-do(T, highlight-slices(EditorId, Slices))
  +
    rec-msg(te-display-message(EditorId?, Message?))
    . snd-do(T, display-message(EditorId, Message))
  +
    rec-msg(te-editor-to-front(EditorId?))
    . snd-do(T, editor-to-front(EditorId))
  +
    rec-msg(te-kill-text-editor(EditorId?)) 
    . snd-do(T, kill-editor(EditorId))
  +
    rec-event(T, is-modified(EditorId?, Modified?))
    . snd-msg(te-is-modified(EditorId, Modified))
    . snd-ack-event(T, is-modified(EditorId, Modified))
  +
    rec-event(T, contents-written(EditorId?))
    . snd-msg(te-contents-written(EditorId))
    . snd-ack-event(T, contents-written(EditorId))
  +
    rec-event(T, contents-changed(EditorId?))
    . create(ContentsChanged(EditorId), Pid?)
    . snd-ack-event(T, contents-changed(EditorId))
  +
    rec-event(T, mouse-event(EditorId?, Line?, Column?))
    . create(MouseEventXY(EditorId, Line, Column), Pid?)
    . snd-ack-event(T, mouse-event(EditorId, Line, Column))
  +
    rec-event(T, mouse-event(EditorId?, Offset?))
    . create(MouseEventOffset(EditorId, Offset), Pid?)
    . snd-ack-event(T, mouse-event(EditorId, Offset))
  +
    rec-event(T, menu-event(EditorId?, MenuEvent?))
    . create(MenuEvent(EditorId, MenuEvent), Pid?)
    . snd-ack-event(T, menu-event(EditorId, MenuEvent))
  +
    rec-event(T, editor-disconnected(EditorId?))
    . snd-msg(te-text-editor-disconnected(EditorId))
    . snd-ack-event(T, editor-disconnected(EditorId))
  )
  * delta
endlet

process ContentsChanged(EditorId: term) is
  snd-msg(te-contents-changed(EditorId))

process MouseEventXY(EditorId: term, Line: int, Column: int) is
  snd-msg(te-mouse-click-at-line-column(EditorId, Line, Column))
  
process MouseEventOffset(EditorId: term, Offset: int) is 
  snd-msg(te-mouse-click-at-offset(EditorId, Offset))

process MenuEvent(EditorId: term, MenuEvent: term) is    
  snd-msg(te-menu-event(EditorId, MenuEvent))

process StartEditorPanel is
  snd-msg(load-jar("file://__PKGDATA__/__JARFILE__", "__EXTERNAL_JARS__"))
toolbus(EditorPlugin)
