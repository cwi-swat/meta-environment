#ifndef __ASF_LISTENERS__
#define __ASF_LISTENERS__

#include <asf-namespace.h>
#include <asf-utils.tb>


process ASFModuleOpener is
let
  Event: attribute-changed-event,
  ModuleId: term,
  NewPath: term,
  Idle: term
in
  Idle := no
  . MM-Subscribe-Attribute-Changed(<term>,SDF_NAMESPACE,"status",<term>,complete) 
  .
  (
    rec-note(mm-attribute-changed(Event?))
    . ACE-GetModuleId(Event, ModuleId?)
    . OpenASFModule(ModuleId)
  )
  *
  delta
endlet

toolbus(ASFModuleOpener)

process ASFModuleChecker is
let
  Event: attribute-changed-event,
  ModuleId: term,
  NewPath: term,
  Tree: term
in
  MM-Subscribe-Attribute-Changed(<term>,ASF_NAMESPACE,"status",available,parsed)  
  .
  (
    rec-note(mm-attribute-changed(Event?))
    . ACE-GetModuleId(Event, ModuleId?)
    . CheckASFModule(ModuleId)
  )
  *
  delta
endlet

toolbus(ASFModuleChecker)

process ASFModuleShapeChanger is
let
  Event: attribute-changed-event,
  ModuleId: term,
  NewPath: term,
  Tree: term
in
  MM-Subscribe-Attribute-Changed(<term>,ASF_NAMESPACE,"status",<term>,available)  
  .
  (
    rec-note(mm-attribute-changed(Event?))
    . ACE-GetModuleId(Event, ModuleId?)
    . MM-SetModuleGraphAttribute(ModuleId, "shape", shape(ellipse))
  )
  *
  delta
endlet

toolbus(ASFModuleShapeChanger)

process ASFModuleParser is
let
  Event: attribute-changed-event,
  ModuleId: term,
  Tree: term
in
  MM-Subscribe-Attribute-Changed(<term>,ASF_NAMESPACE,"status",<term>,available)
  .
  (
    rec-note(mm-attribute-changed(Event?))
    . ACE-GetModuleId(Event, ModuleId?)
    . GetASFParseTree(ModuleId, Tree?)
    . 
    if not-equal(Tree, UNDEFINED) then
       MM-SetAttribute(ModuleId, ASF_NAMESPACE, "status", parsed)
    else
       MM-SetAttribute(ModuleId, ASF_NAMESPACE, "status", error)
    fi
    . Tree := UNDEFINED
  )
  *
  delta
endlet

/* TODO: add syntax checker */

toolbus(ASFModuleParser)

process ASFStatusToProgress is
let
  Event: attribute-changed-event,
  ModuleId: term,
  Name: str,
  Value: term
in
  MM-Subscribe-Attribute-Changed(<term>,ASF_NAMESPACE,"status",<term>,<term>)
  .
  (
    rec-note(mm-attribute-changed(Event?))
    . ACE-GetModuleId(Event, ModuleId?)
    . MM-GetAttribute(ModuleId, ASF_NAMESPACE, "name", Name?)
    . ACE-GetNewValue(Event, Value?)
    . snd-msg(pro-set-status(Name, ASF_NAMESPACE, Value))
  ) * delta 
endlet

toolbus(ASFStatusToProgress)
#endif /* __ASF_LISTENERS__ */
