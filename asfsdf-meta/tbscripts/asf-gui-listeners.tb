#ifndef __ASF_GUI_LISTENERS__
#define __ASF_GUI_LISTENERS__

#include <undefined.h>
#include <sdf-module-utils.tb>

process LanguageFeatureMenuListener(Feature : str, Action : str) is
let
  Event: attribute-changed-event,
  ModuleId: term,
  Path: term,
  Menu: term,
  Modulename: str,
  FeatureModulename: str,
  Languagename: str,
  MenuName: str,
  OldValue: term,
  Pid: int
in
  MM-Subscribe-Attribute-Changed(<term>,SDF_NAMESPACE,"status",identified,<term>) 
  .
  (
    rec-note(mm-attribute-changed(Event?))
    . ACE-GetModuleId(Event, ModuleId?)
    . MM-GetAttribute(ModuleId, SDF_NAMESPACE, "name", Modulename?)
    .
    if not-equal(Modulename, UNDEFINED) then
      ComputeFeatureModulename(Modulename, Feature, FeatureModulename?)
      .
      if equal(Modulename, FeatureModulename) then
         ModulenameBase(Modulename, MenuName?)
         . ComputeCanonicalLanguagename(Modulename, Languagename?)
	 . Menu := quote(action([description(term-editor(Languagename),menu([label(MenuName),label(Feature)]))],Action))
	 . snd-msg(cm-add-system-property(Menu))
         . create(LanguageFeatureRemoveListener(ModuleId, Menu), Pid?)
      else
        tau
      fi	 
    else
      tau
    fi
  )
  *
  delta
endlet

toolbus(LanguageFeatureMenuListener("format", "PrettyPrintAction"))
toolbus(LanguageFeatureMenuListener("run", "RunAction"))
toolbus(LanguageFeatureMenuListener("debug", "DebugAction"))
toolbus(LanguageFeatureMenuListener("check", "CheckAction"))
toolbus(LanguageFeatureMenuListener("extract", "DebugAction"))

process LanguageFeatureRemoveListener(ModuleId: term, Property: term) is
  subscribe(mm-module-deleted(<term>))
  .
  (
    rec-note(mm-module-deleted(ModuleId))
    . snd-msg(cm-remove-system-property(Property))
  )
  *
  delta

#endif
