#ifndef __ASF_EDITOR_UTILS__
#define __ASF_EDITOR_UTILS__
#include <editing.tb>
#include <asf-namespace.h>

process EditEquations(ModuleId: term) is
let
  AlreadyExists: bool,
  Available: bool,
  EditorType: term,
  HasAttribute: bool,
  Path: str,
  Editable: bool,
  Sid: term,
  Tree: term
in
  EditorType := equations-editor
  . GetModulePath(ModuleId, ASF_NAMESPACE, Path?)
  . Edit(Path, Sid?)
  . EditText(Sid, EditorType, "", AlreadyExists?)
  . snd-msg(equations-editor-started(Path, Sid))
  .
  if equal(AlreadyExists, false) then
    MM-HasAttribute(ModuleId, ASF_NAMESPACE, "editable", HasAttribute?)
    .
    if equal(HasAttribute, true) then
      MM-GetAttribute(ModuleId, ASF_NAMESPACE, "editable", Editable?)
    else
      Editable := true
    fi
    . snd-msg(te-set-editable(Sid, Editable)) 
    . snd-msg(em-bind-session(Sid, ModuleId))
    . rec-msg(em-session-bound(Sid, ModuleId))
    . GetASFParseTree(ModuleId, Tree?)
    .
    if not-equal(Tree, UNDEFINED) then
      EditStructure(Sid, Tree)
    else
      tau
    fi
    . MM-Subscribe-Attribute-Changed(ModuleId,ASF_NAMESPACE,"status",available,parsed)
    .
    (
      MenuSelected(Sid, EditorType)
    +
      MouseClicked(Sid)
    +
      rec-msg(te-contents-changed(Sid))
      . MM-SetAttribute(ModuleId, ASF_NAMESPACE, "status", edited)
    +
      rec-msg(te-contents-saved(Sid))
      . MM-SetAttribute(ModuleId, ASF_NAMESPACE, "status", available)
    +
      UpdateEquationsStructure(Sid, EditorType)
    )
    *
    EditorDisconnected(Sid)
    . DeleteSession(Sid)
  else
    tau
  fi
endlet

process UpdateEquationsStructure(Sid: term, EditorType: term) is
let
  Event: attribute-changed-event,
  ModuleId: term,
  Slices: term,
  Tree: term
in
  rec-note(mm-attribute-changed(Event?))
  . GetModuleId(Sid, ModuleId?)
  . GetASFParseTree(ModuleId, Tree?) 
  . RegisterTree(Sid, Tree)
endlet

#endif /* __ASF_EDITOR_UTILS__ */
