module Pico-controlflow
imports pico/syntax/Pico;

subtype CP EXP;         %% A Code Point, union of two types
subtype CP STATEMENT;

subtype CFSEGMENT tuple(set[CP] entry, 
                        rel[CP,CP] graph, 
                        set[CP] exit);

fun CFSEGMENT cflow({STATEMENT ";"}* Stats){ 
    switch Stats {
      case <STATEMENT Stat> ; <{STATEMENT ";"}* Stats2>: { 
           <set[CP] En1, rel[CP,CP] R1, set[CP] Ex1> = cflow(Stat);
           <set[CP] En2, rel[CP,CP] R2, set[CP] Ex2> = 
                                                     cflow(Stats2);
           return <En1, R1 + R2 + (Ex1 x En2), Ex2>
      }

      case [| |]: return <{}, {}, {}>
    }
}; 

fun CFSEGMENT cflow(STATEMENT Stat){
    switch Stat {
      case [| while <EXP Exp> do <{STATEMENT ";"}* Stats> od |] : {
           <set[CP] En,  rel[CP,CP] R,  set[CP] Ex> = cflow(Stats);
           return <{Exp}, ({Exp} x En) + R + (Ex x {Exp}),{Exp}>;
      }
                
      case [| if <EXP Exp> then <{STATEMENT ";"}* Stats1> 
                   else <{STATEMENT ";"}* Stats2> fi |]: {
           <set[CP] En1, rel[CP,CP] R1, set[CP] Ex1> = 
                                                      cflow(Stats1);
           <set[CP] En2, rel[CP,CP] R2, set[CP] Ex2> = 
                                                      cflow(Stats2);
           return < {Exp}, 
                    ({Exp} x En1) + ({Exp} x En2) + R1 + R2,
                    Ex1 + Ex2 
                  >;
      }
         
      case [| <STATEMENT Stat> |]: return <{Stat}, {}, {Stat}>
    };
}
