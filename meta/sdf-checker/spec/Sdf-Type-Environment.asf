equations

  [cimt-1'1] create-info-msg-tuple() = [{},{}]

  [cme-1'1] create-module-entry($ModuleId,{ModuleInfoEntry*}) =
              {[$ModuleId, true, [symbols(), productions()]] 
               ModuleInfoEntry*}

  [gmi-1'1] get-module-info([ModuleInfoTable, Messages]) = ModuleInfoTable

  [gm-1'1] get-messages([ModuleInfoTable, Messages]) = Messages

  [pmi-1'1] put-module-info(ModuleInfoTable1,
                            [ModuleInfoTable2, Messages]) = 
              [ModuleInfoTable1, Messages]

  [pm-1'1] put-messages(Messages1, [ModuleInfoTable, Messages2]) = 
             [ModuleInfoTable, Messages1]

  [mic-1'1] module-is-checked($ModuleId,
                              {ModuleInfoEntry*1
                               [$ModuleId, Bool, ModuleInfo]
                               ModuleInfoEntry*2}) = Bool

  [default-mic-1'2] module-is-checked($ModuleId,
                                      {ModuleInfoEntry*}) = false

  [psi-1'1] put-symbol-info($ModuleId, 
                            SymbolsSet1,
                            {ModuleInfoEntry*1 
                             [$ModuleId, Bool, 
                              [SymbolsSet2, ProductionsSet]]
                             ModuleInfoEntry*2}) =
              {ModuleInfoEntry*1
               [$ModuleId, true, [SymbolsSet1, ProductionsSet]] 
               ModuleInfoEntry*2}

  [default-psi-1'1] put-symbol-info($ModuleId, 
                                    SymbolsSet,
                                    {ModuleInfoEntry*}) =
                      {[$ModuleId, true, [SymbolsSet, productions()]] 
                        ModuleInfoEntry*}

  [gds-1'1] get-declared-symbols($ModuleId,
                                 {ModuleInfoEntry*1
                                  [$ModuleId, Bool, 
                                   [SymbolsSet, ProductionsSet]]
                                  ModuleInfoEntry*2}) = SymbolsSet

  [gds-1'1] get-declared-symbols(imports(), ModuleInfoTable) = symbols()

  [gds-1'2] get-module-id($ModuleName) = $ModuleId,
            get-declared-symbols($ModuleId, ModuleInfoTable) = 
               symbols(Symbol*1),
            get-declared-symbols(imports(Import*), ModuleInfoTable) =
               symbols(Symbol*2)
            =========================================================
            get-declared-symbols(imports($ModuleName Import*), 
                                 ModuleInfoTable) = symbols(Symbol*1 Symbol*2)

  [ppi-1'1] put-production-info($ModuleId, 
                                ProductionsSet1,
                                {ModuleInfoEntry*1 
                                 [$ModuleId, Bool, 
                                  [SymbolsSet, ProductionsSet2]]
                                 ModuleInfoEntry*2}) =
              {ModuleInfoEntry*1
               [$ModuleId, true, [SymbolsSet, ProductionsSet1]] 
               ModuleInfoEntry*2}

  [default-ppi-1'1] put-production-info($ModuleId, 
                                        ProductionsSet,
                                        {ModuleInfoEntry*}) =
                      {[$ModuleId, true, [symbols(), ProductionsSet]] 
                        ModuleInfoEntry*}

  [gdp-1'1] get-defined-productions($ModuleId,
                                    {ModuleInfoEntry*1
                                     [$ModuleId, Bool, 
                                      [SymbolsSet, ProductionsSet]]
                                     ModuleInfoEntry*2}) = ProductionsSet

  [gdp-1'1] get-defined-productions(imports(), ModuleInfoTable) = productions()

  [gdp-1'2] get-module-id($ModuleName) = $ModuleId,
            get-defined-productions($ModuleId, ModuleInfoTable) = 
               productions(Production*1),
            get-defined-productions(imports(Import*), ModuleInfoTable) =
               productions(Production*2)
            ============================================================
            get-defined-productions(imports($ModuleName Import*), 
                                    ModuleInfoTable) = 
              productions(Production*1 Production*2)