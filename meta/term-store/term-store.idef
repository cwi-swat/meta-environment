
tool term-store is { command = "term-store" }

process TermStore is
let
 TS : term-store,
 TableName : str,
 Key : term,
 Value : list,
 ContainsKey : bool,
 Keys : list,
 Values : list
in
  execute(term-store, TS?).
  (
    rec-msg(ts-add-table(TableName?)).
    snd-do(TS,add-table(TableName))
  +
    rec-msg(ts-remove-table(TableName?)).
    snd-do(TS,remove-table(TableName))
  +
    rec-msg(ts-clear-table(TableName?)).
    snd-do(TS,clear-table(TableName))
  +
    rec-msg(ts-put-value(TableName?,Key?,Value?)).
    snd-do(TS,put-value(TableName,Key,Value))
  +
    rec-msg(ts-get-value(TableName?,Key?)).
    snd-eval(TS,get-value(TableName,Key)) .
    (
      rec-value(TS,result(Value?)).
      snd-msg(ts-value(Value))
    +
      rec-value(TS,no-result) .
      snd-msg(ts-no-value)
    )
  +
    rec-msg(ts-remove-value(TableName?,Key?)).
    snd-do(TS,remove-value(TableName,Key))
  +
    rec-msg(ts-contains-key(TableName?,Key?)).
    snd-eval(TS,contains-key(TableName,Key)).
    (
      rec-value(TS,result(yes)).
      snd-msg(ts-contains-key)
    +
      rec-value(TS,result(no)).
      snd-msg(ts-not-contains-key)
    )
  +
    rec-msg(ts-get-all-keys(TableName?)).
    snd-eval(TS,get-all-keys(TableName)).
    rec-value(TS,result(Keys?)).
    snd-msg(ts-all-keys(Keys))
  +
    rec-msg(ts-get-all-values(TableName?)) .
    snd-eval(TS, get-all-values(TableName)) .
    rec-value(TS,result(Values?)) .
    snd-msg(ts-all-values(Values))
  +
    rec-msg(ts-remove-value-from-all-tables(Key?)).
    snd-do(TS,remove-value-from-all-tables(Key))
  )
  * delta
endlet
