
tool term-store is { command = "term-store" }

process TermStore is
let
 TS : term-store,
 TableName : str,
 Key : term,
 Value : list,
 ContainsKey : bool,
 Keys : list,
 Values : list
in
  execute(term-store, TS?).
  (
    rec-msg(ts-add-table(TableName?)).
    snd-do(TS,add-table(TableName))
  +
    rec-msg(ts-remove-table(TableName?)).
    snd-do(TS,remove-table(TableName))
  +
    rec-msg(ts-clear-table(TableName?)).
    snd-do(TS,clear-table(TableName))
  +
    rec-msg(ts-put-value(TableName?,Key?,Value?)).
    snd-do(TS,put-value(TableName,Key,Value))
  +
    rec-msg(ts-get-value(TableName?,Key?)).
    snd-eval(TS,get-value(TableName,Key)) .
    rec-value(TS,result(Value?)).
    snd-msg(ts-get-value(TableName,Key,Value))
  +
    rec-msg(ts-remove-value(TableName?,Key?)).
    snd-do(TS,remove-value(TableName,Key))
  +
    rec-msg(ts-contains-key(TableName?,Key?)).
    snd-eval(TS,contains-key(TableName,Key)).
    rec-value(TS,result(ContainsKey?)).
    snd-msg(ts-contains-key(TableName,Key,ContainsKey))
  +
    rec-msg(ts-get-all-keys(TableName?)).
    snd-eval(TS,get-all-keys(TableName)).
    rec-value(TS,result(Keys?)).
    snd-msg(ts-all-keys(TableName,Keys))
  +
    rec-msg(ts-remove-value-from-all-tables(Key?)).
    snd-do(TS,remove-value-from-all-tables(Key))
  )
  * delta
endlet
