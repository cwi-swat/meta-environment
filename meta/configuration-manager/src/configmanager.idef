/*
    $Id$
*/        

#ifndef CONFIG_FILE
#define CONFIG_FILE "meta.conf"
#endif
#ifndef BUTTON_FILE
#define BUTTON_FILE "meta.buttons"
#endif
#ifndef ASF_LIBRARY_PATH
#define ASF_LIBRARY_PATH "."
#endif

process IfEqual(ButtonAction : term,
                Stack : list,
		ButtonActions : list,
                RStack : list?,
		RButtonActions : list?) is
let
  Tree1       : term,
  Tree2       : term,
  ThenActions : list,
  ElseActions : list,
  IStack      : list
in
  Tree1 := first(Stack) .
  IStack := next(Stack) .
  Tree2 := first(IStack) .
  IStack := next(IStack) .
  snd-msg(equal-trees(Tree1, Tree2)) .
  (
    rec-msg(true) .
    ThenActions := first(args(ButtonAction)) .
    RButtonActions := join(ThenActions, ButtonActions)
  +
    rec-msg(false) .
    ElseActions := first(next(args(ButtonAction))) .
    RButtonActions := join(ElseActions, ButtonActions)
  ) .
  RStack := IStack
endlet

process ProcessButtons(CMNG : configmanager) is
let
  Term          : term
in
  snd-msg(get-button-file(BUTTON_FILE)) .
  (
    rec-msg(button-file(BUTTON_FILE, Term?)) .
    snd-eval(CMNG, process-button-file(BUTTON_FILE, Term))
  +
    rec-msg(no-button-file(BUTTON_FILE)) .
    snd-eval(CMNG, process-button-file(BUTTON_FILE, "buttons([])"))
  ) .
  rec-value(CMNG, buttons-processed) .
  snd-note(search-paths-processed)
endlet
  
process ConfigManager is
let
  CMNG          : configmanager,
  Text          : str,
  Fid           : str,
  Paths         : list,
  ButtonName    : str,
  ButtonNames   : list,
  Stack         : list,
  ButtonActions : list,
  ButtonAction  : term,
  ModuleName    : str,
  FunName       : str,
  EditorType    : str,
  Tree          : term,
  EditorId      : term,
  Term          : term
in
  execute(configmanager, CMNG?) .
  snd-msg(get-config-file(CONFIG_FILE)) .
  (
    rec-msg(config-file(CONFIG_FILE, Text?)) .
    snd-eval(CMNG, process-config-file(CONFIG_FILE, Text))
  +
    rec-msg(no-config-file(CONFIG_FILE)) .
    snd-eval(CMNG, process-config-file(CONFIG_FILE, "."))
  ) .
  rec-value(CMNG, search-paths(Paths?)) .
  Paths := join(Paths, ASF_LIBRARY_PATH) .
  snd-msg(process-search-paths(CONFIG_FILE, Paths)) .
  rec-msg(search-paths-processed(CONFIG_FILE)) .
  ProcessButtons(CMNG) .
  snd-note(search-paths-processed) .
  (
    rec-msg(refresh-buttons) .
    ProcessButtons(CMNG) .
    snd-msg(buttons-refreshed)
  +
    rec-msg(get-auxilary-buttons(EditorType?, ModuleName?)) .
    snd-eval(CMNG, get-button-names(EditorType, ModuleName)) .
    rec-value(CMNG, button-names(ButtonNames?)) .
    snd-msg(button-list(ModuleName, ButtonNames))
  +
    rec-msg(extra-button-action(ButtonName?, ModuleName?, EditorId?)) .
    snd-eval(CMNG, get-button-actions(ButtonName, ModuleName)) .
    rec-value(CMNG, button-actions(ButtonActions?)) .
    Stack := [] .
    (
      if not-equal(ButtonActions, []) then
        ButtonAction := first(ButtonActions) .
        ButtonActions := next(ButtonActions) .
	FunName := fun(ButtonAction) .
	if equal(FunName, "if-equal") then
	  IfEqual(ButtonAction, Stack, ButtonActions, Stack?, ButtonActions?)
	else
          snd-msg(ButtonAction, Stack, EditorId) .
          (
            rec-msg(stack(Stack?))
          +
            rec-msg(error-stack) .
            ButtonActions := []
          )
	fi
      fi
    )*
    if equal(ButtonActions, []) then
      tau
    fi
  ) * delta
endlet

tool configmanager is { command = "configmanager" }
