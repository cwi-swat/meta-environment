dnl
dnl    Meta-Environment - An environment for language prototyping.
dnl    Copyright (C) 2000  Stichting Mathematisch Centrum, Amsterdam, The Netherlands. 
dnl
dnl    This program is free software; you can redistribute it and/or modify
dnl    it under the terms of the GNU General Public License as published by
dnl    the Free Software Foundation; either version 2 of the License, or
dnl    (at your option) any later version.
dnl
dnl    This program is distributed in the hope that it will be useful,
dnl    but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl    GNU General Public License for more details.
dnl
dnl    You should have received a copy of the GNU General Public License
dnl    along with this program; if not, write to the Free Software
dnl    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307 USA
dnl
dnl $Id$

dnl Process this file with autoconf to produce a configure script.
AC_INIT(configure.in)
AM_INIT_AUTOMAKE(meta, 0.1.3)

dnl Temporary COPY of ./config/program-check.m4
AC_DEFUN(AC_PACKAGE_REQUIRE,
[AC_PACKAGE_REQUIRE1([$1],[$2],[$3],
    dnl Program found; evaluate <actions_of_found>
    [translit($1,`a-z',`A-Z')=`dirname \`dirname $translit($1,`a-z',`A-Z')\``])
])

AC_DEFUN(AC_PROGRAM_REQUIRE,
   [AC_PACKAGE_REQUIRE1([$1],[$2],[$3],)])

AC_DEFUN(AC_PACKAGE_REQUIRE1,
[
   dnl Add configuration switch
   AC_ARG_WITH($1, [$3],
      dnl switch was specified
      translit($1,`a-z',`A-Z')=${withval},
      dnl If switch not specified, try to find the program automatically
      [
         AC_PATH_PROGS(translit($1,`a-z',`A-Z'),$2,
         [
            dnl Not found; abort configuration
            AC_ERROR(Required \"$1\" program not found.)
         ])
         dnl Program found; evaluate <actions_of_found>
         $4
      ])
])

dnl Check for required packages and perform version check when needed.

dnl Check for wish program
AC_PROGRAM_REQUIRE(wish, 
                   wish8.1 wish8.0 wish,
                   [  --with-wish=DIR         wish program is in DIR],)
dnl META_WISH_VERSION_CHECK($WISH)
AC_SUBST(WISH)

dnl Check for graphviz package
AC_PACKAGE_REQUIRE(graphviz, dot,
                   [  --with-graphviz=DIR     graphviz package is in DIR])
dnl META_GRAPHVIZ_VERSION_CHECK($GRAPHVIZ)
dnl META_TCLDOT_CHECK($GRAPHVIZ)
AC_SUBST(GRAPHVIZ)

dnl Try to find xemacs
AC_PACKAGE_REQUIRE(xemacs, xemacs,
   [  --with-xemacs=DIR       Xemacs is in DIR])
AC_SUBST(XEMACS)

dnl Require toolbus package
AC_PACKAGE_REQUIRE(toolbus, toolbus,
   [  --with-toolbus=DIR      ToolBus distribution is in DIR])
AC_SUBST(TOOLBUS)

dnl Require ATerm library
AC_PACKAGE_REQUIRE(aterm, tifstoc,
   [  --with-aterm=DIR        ATerm-library is in DIR])
AC_SUBST(ATERM)

dnl Require AsFix library
AC_PACKAGE_REQUIRE(asfix, asource,
   [  --with-asfix=DIR        AsFix-library is in DIR])
AC_SUBST(ASFIX)

dnl Require sdf package
AC_PACKAGE_REQUIRE(sdf, sglr,
   [  --with-sdf=DIR         sdf is in DIR])
AC_SUBST(SDF)

dnl Require evaluator package
AC_PACKAGE_REQUIRE(evaluator, evaluator, 
   [  --with-evaluator=DIR    evaluator is in DIR])
AC_SUBST(EVALUATOR)

dnl Require asf package
AC_PACKAGE_REQUIRE(asf, addeqssyntax,
   [  --with-asf=DIR          asf is in DIR])
AC_SUBST(ASF)

ASC_SUPPORT='${prefix}'
dnl Handle --with-asc-support=... argument to configure
AC_ARG_WITH(asc-support,
   [  --with-asc-support=DIR  asc-support is in DIR],
   ASC_SUPPORT=${withval},)

AC_SUBST(ASC_SUPPORT)

dnl Our own version of libdir iwhose value is set dynamically
AC_SUBST(INSTLIBDIR)
INSTLIBDIR="$libdir"

dnl Is ranlib needed on this system?
AC_PROG_RANLIB
AC_PATH_PROG(AR,ar,/usr/bin/ar)

AC_REQUIRE([AC_PROG_CC])

dnl Which compiler to use?
AC_PROG_CC

dnl If anyone knows why this is used...
AC_AIX

dnl Where to locate a bourne-compatible shell?

dnl Where is perl5?
AC_PATH_PROGS(PERL,perl5 perl, /usr/bin/perl)

dnl What commands are needed to set the variable MAKE in Makefiles?
AC_SET_MAKE



dnl Handle --with-tide=... argument to configure
AC_ARG_WITH(tide,
   [  --with-tide             TIDE is in DIR],
   TIDE=${withval}
   TIDEDEF=-DUSE_TIDE
   TIDEINC=-I${TIDE}
   USE_TIDE=1,
   USE_TIDE=0
)
AC_SUBST(TIDE)
AC_SUBST(TIDEDEF)
AC_SUBST(TIDEINC)
AC_SUBST(USE_TIDE)


dnl Handle --with-debug-c-code to activate c debug info 
dnl and shut off optimizations
CFLAGS="-Wall -O4 -DNDEBUG"
AC_ARG_WITH(debug-c-code,
   [  --with-debug-c-code     C flags set to -Wall -g],
   CFLAGS="-Wall -g -O2"
)
AC_SUBST(CFLAGS)

dnl Determine which libraries to use for programs using sockets
NOSOCKLIBS=$LIBS
dnl first, the socket functions
SOCKET_FOUND=no
SOCKLIBS=
AC_CHECK_LIB(sun,socket,[SOCKET_FOUND=yes;SOCKLIBS="-lsun"])
if test ${SOCKET_FOUND} = no; then
  AC_CHECK_LIB(socket, socket,[SOCKET_FOUND=yes;SOCKLIBS="-lsocket"])
  if test ${SOCKET_FOUND} = no; then
    AC_CHECK_LIB(bsd,socket,[SOCKET_FOUND=yes;SOCKLIBS="-lbsd";DEFS="$DEFS -D_BSD=43"])
  fi
fi

dnl then gethostname
GHN_FOUND=no
AC_CHECK_LIB(sun,gethostname,[GHN_FOUND=yes])
if test ${GHN_FOUND} = no; then
  AC_CHECK_LIB(nsl,gethostname,[GHN_FOUND=yes;SOCKLIBS="$SOCKLIBS -lnsl"])
fi

AC_SUBST(SOCKLIBS)

LIBS=$NOSOCKLIBS

AC_CHECK_LIB(dl, dlopen)

dnl Checks for header files.
AC_CHECK_HEADER(getopt.h)

dnl Which files need updating?
AC_OUTPUT(
   Makefile
   config/Makefile
   graph-browser/Makefile
   graph-browser/graph-browser.tcl
   in-output/Makefile
   in-output/test/Makefile
   module-db/Makefile
   module-db/test/Makefile
   editors/Makefile
   editors/manager/Makefile
   editors/manager/src/Makefile
   editors/text/Makefile
   editors/structure/Makefile
   editors/structure/src/Makefile
   tbscripts/Makefile
   tbscripts/test/Makefile
   utils/Makefile
   utils/genmakefile
   demo/Makefile
   demo/pico/Makefile
   demo/user-manual-examples/Makefile
   doc/Makefile
   doc/html/Makefile
   configuration-manager/Makefile
   configuration-manager/src/Makefile
, chmod +x utils/genmakefile
)
