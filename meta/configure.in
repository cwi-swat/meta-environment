dnl $Id$

dnl Process this file with autoconf to produce a configure script.
AC_INIT

dnl Product name and version number
PRODUCT=new-meta
VERSION=0.02

AC_SUBST(PRODUCT)
AC_SUBST(VERSION)

dnl Default location of this component
AC_PREFIX_DEFAULT(`echo $PWD`/..)

dnl Default locations of other components
EPIC='$(prefix)'
GEL='$(prefix)'
TOOLBUS='$(prefix)'
ATERM='$(prefix)'

dnl Installation scripts 
INSTALL='$(top_srcdir)/install-sh'
INSTALL_PROG='$(top_srcdir)/install-prog-sh'
INSTALL_DATA='$(top_srcdir)/install-data-sh'
UNINSTALL='$(top_srcdir)/uninstall-sh'
AC_SUBST(INSTALL)
AC_SUBST(INSTALL_PROG)
AC_SUBST(INSTALL_DATA)
AC_SUBST(UNINSTALL)

dnl Is ranlib needed on this system?
AC_PROG_RANLIB
AR=ar
AC_SUBST(AR)

dnl Which compiler to use?
AC_PROG_CC
AC_AIX

dnl Where to locate the bourne shell?
SHELL=/bin/sh
AC_SUBST(SHELL)

dnl Check for noweb components.
dnl Use 'true' if 'noweb', 'noweave', or 'notangle' do not exist.
AC_CHECK_PROG(NOWEB,noweb,noweb,true)
AC_CHECK_PROG(NOWEAVE,noweave,noweave,)
AC_CHECK_PROG(NOTANGLE,notangle,notangle,)
AC_SUBST(NOWEB)
AC_SUBST(NOWEAVE)
AC_SUBST(NOTANGLE)

dnl What commands are needed to set the variable MAKE in Makefiles?
AC_SET_MAKE

dnl Which CFLAGS to use?
if test ${CC} = gcc ; then
	CFLAGS="-g -pedantic -Wall"
fi
AC_SUBST(CFLAGS)
AC_SUBST(DEFS)
AC_SUBST(INCLUDES)

dnl Handle --with-gel=... argument to configure
GELLIBS="-L$(GEL)/gel -lgel"
AC_ARG_WITH(gel,
[  --with-gel    	use GeL for interfacing with ASF+SDF system],
if test ${withval} = "yes"
then
   GEL="$(top_srcdir)/../gel"
else
   if test ${withval} = "no"
   then
     GEL="."
   else
     GEL=${withval};
   fi
fi,
)
AC_SUBST(GEL)
AC_SUBST(GELLIBS)

dnl Handle --with-epic=... argument to configure
AC_ARG_WITH(epic,
[  --with-epic		which epic distribution to use],
EPIC=${withval},)
AC_SUBST(EPIC)

dnl Handle --with-aterm=... argument to configure
AC_ARG_WITH(aterm,
[  --with-aterm		which aterm-library to use],
ATERM=${withval},)
AC_SUBST(ATERM)

dnl Handle --with-toolbus=... argument to configure
AC_ARG_WITH(toolbus,
[  --with-toolbus	which ToolBus distribution to use],
TOOLBUS=${withval},)
AC_SUBST(TOOLBUS)

dnl Handle --with-tcldot=... argument to configure
AC_ARG_WITH(tcldot,
[  --with-tcldot 	location of tcldot library to use],
TCLDOT=${withval},
AC_MSG_ERROR(\nPlease specify location of tcldot using --with-tcldot=/path/to/tcldot.so\nIf you are at CWI/UvA a good guess would be ~kuipers/lib/Tcldot)
)
AC_SUBST(TCLDOT)

dnl Of course, the latest version of wish is not always called "wish". :-(
dnl If we can't find something called wish8.0, we'll assume that "wish"
dnl is at version 8 or higher (as it is in a _normal_ setup)
AC_CHECK_PROG(WISH, wish8.0, wish8.0, wish)
AC_SUBST(WISH)


dnl Determine which libraries to use for programs using sockets
NOSOCKLIBS=$LIBS
dnl first, the socket functions
SOCKET_FOUND=no
SOCKLIBS=
AC_CHECK_LIB(sun,socket,[SOCKET_FOUND=yes;SOCKLIBS="-lsun"])
if test ${SOCKET_FOUND} = no; then
  AC_CHECK_LIB(socket, socket,[SOCKET_FOUND=yes;SOCKLIBS="-lsocket"])
  if test ${SOCKET_FOUND} = no; then
    AC_CHECK_LIB(bsd,socket,[SOCKET_FOUND=yes;SOCKLIBS="-lbsd";DEFS="$DEFS -D_BSD=43"])
  fi
fi

dnl then gethostname
GHN_FOUND=no
AC_CHECK_LIB(sun,gethostname,[GHN_FOUND=yes])
if test ${GHN_FOUND} = no; then
  AC_CHECK_LIB(nsl,gethostname,[GHN_FOUND=yes;SOCKLIBS="$SOCKLIBS -lnsl"])
fi

LIBS=$NOSOCKLIBS
AC_CHECK_LIB(dl, dlopen)

AC_SUBST(SOCKLIBS)
 
dnl Which files need updating?
AC_OUTPUT(Makefile meta install-prog-sh install-data-sh \
	Makefile.skel \
	evaluator/Makefile \
	toolbus/Makefile \
	import-db/Makefile \
	tree-db/Makefile \
	in-output/Makefile \
	graph-browser/Makefile graph-browser/graph-browser \
	editor/Makefile editor/editor \
	compiler/Makefile \
	compiler/genmakefile.sh \
	compiler/c-source/Makefile \
	compiler/support/Makefile \
	stred/nl/cwi/gipe/stred/Makefile \
	asource/Makefile \
	a2toa1/Makefile \
	sglr/src/Makefile \
, chmod +x meta install-prog-sh install-data-sh; \
  chmod +x graph-browser/graph-browser editor/editor \
)
