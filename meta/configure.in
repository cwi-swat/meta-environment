dnl $Id$

dnl Process this file with autoconf to produce a configure script.
AC_INIT

dnl Product name and version number
PRODUCT=new-meta
VERSION=0.02

AC_SUBST(PRODUCT)
AC_SUBST(VERSION)

dnl Default location of this component
AC_PREFIX_DEFAULT(`echo $PWD`/..)

dnl Default locations of other components
TOOLBUS='\$(prefix)'
ATERM='\$(prefix)'
NEWATERM='\$(prefix)'

dnl Installation scripts
INSTALL='$(top_srcdir)/install-sh'
INSTALL_PROG='$(top_srcdir)/install-prog-sh'
INSTALL_DATA='$(top_srcdir)/install-data-sh'
UNINSTALL='$(top_srcdir)/uninstall-sh'
AC_SUBST(INSTALL)
AC_SUBST(INSTALL_PROG)
AC_SUBST(INSTALL_DATA)
AC_SUBST(UNINSTALL)

dnl Is ranlib needed on this system?
AC_PROG_RANLIB
AC_PATH_PROG(AR,ar,/usr/bin/ar)

dnl Which compiler to use?
AC_PROG_CC
AC_AIX

dnl Where to locate a bourne-compatible shell?
SHELL=""
AC_PATH_PROG(SHELL,sh,/bin/sh)

dnl where is perl5?
AC_PATH_PROGS(PERL,perl5 perl, /usr/bin/perl)

dnl Check for noweb components.
dnl Use 'true' if 'noweb', 'noweave', or 'notangle' do not exist.
AC_CHECK_PROG(NOWEB,noweb,noweb,true)
AC_CHECK_PROG(NOWEAVE,noweave,noweave,)
AC_CHECK_PROG(NOTANGLE,notangle,notangle,)

dnl What commands are needed to set the variable MAKE in Makefiles?
AC_SET_MAKE

dnl Which CFLAGS to use?
if test ${CC} = gcc ; then
	CFLAGS="-g -Wall -O4"
fi
AC_SUBST(CFLAGS)
AC_SUBST(DEFS)
AC_SUBST(INCLUDES)

dnl Handle --with-aterm=... argument to configure
AC_ARG_WITH(aterm,
[  --with-aterm		which aterm-library to use],
ATERM=${withval},)
AC_SUBST(ATERM)

dnl Handle --with-new-aterm=... argument to configure
AC_ARG_WITH(new-aterm,
[  --with-new-aterm		which new-aterm-library to use],
NEWATERM=${withval},)
AC_SUBST(NEWATERM)

dnl Handle --with-toolbus=... argument to configure
AC_ARG_WITH(toolbus,
[  --with-toolbus	which ToolBus distribution to use],
TOOLBUS=${withval},)
AC_SUBST(TOOLBUS)

dnl Handle --with-tcldot=... argument to configure
AC_ARG_WITH(tcldot,
[  --with-tcldot 	location of tcldot library to use],
TCLDOT=${withval},
AC_MSG_ERROR(\nPlease specify location of tcldot using --with-tcldot=/path/to/tcldot.so\nIf you are at CWI/UvA a good guess would be ~gipe/petr/lib/Tcldot)
)
AC_SUBST(TCLDOT)

dnl Of course, the latest version of wish is not always called "wish". :-(
dnl If we can't find something called wish8.0, we'll assume that "wish"
dnl is at version 8 or higher (as it is in a _normal_ setup)
AC_PATH_PROGS(WISH, wish8.1 wish8.0 wish, /usr/bin/wish)

dnl Determine which libraries to use for programs using sockets
NOSOCKLIBS=$LIBS
dnl first, the socket functions
SOCKET_FOUND=no
SOCKLIBS=
AC_CHECK_LIB(sun,socket,[SOCKET_FOUND=yes;SOCKLIBS="-lsun"])
if test ${SOCKET_FOUND} = no; then
  AC_CHECK_LIB(socket, socket,[SOCKET_FOUND=yes;SOCKLIBS="-lsocket"])
  if test ${SOCKET_FOUND} = no; then
    AC_CHECK_LIB(bsd,socket,[SOCKET_FOUND=yes;SOCKLIBS="-lbsd";DEFS="$DEFS -D_BSD=43"])
  fi
fi

dnl then gethostname
GHN_FOUND=no
AC_CHECK_LIB(sun,gethostname,[GHN_FOUND=yes])
if test ${GHN_FOUND} = no; then
  AC_CHECK_LIB(nsl,gethostname,[GHN_FOUND=yes;SOCKLIBS="$SOCKLIBS -lnsl"])
fi

LIBS=$NOSOCKLIBS
AC_CHECK_LIB(dl, dlopen)

dnl Checks for header files.
AC_CHECK_HEADER(getopt.h)

AC_SUBST(SOCKLIBS)

dnl Which files need updating?
AC_OUTPUT(
	Makefile Makefile.skel install-prog-sh install-data-sh meta \
	asource/Makefile \
	compiler/Makefile compiler/c-source/Makefile \
	compiler/support/Makefile compiler/support-muasf/Makefile \
	editor/Makefile editor/editor \
	evaluator/Makefile \
	graph-browser/Makefile graph-browser/graph-browser \
	in-output/Makefile \
	module-db/Makefile module-db/genmakefile.sh \
	pgen/c-source/Makefile pgen/c-files/Makefile \
	pgen/addeqs/c-source/Makefile \
	sglr/a2toa1/Makefile \
	sglr/client/Makefile \
	sglr/dump/Makefile \
	sglr/parser/Makefile \
	sglr/sdf1tosdf2/Makefile \
	sglr/sdf1tosdf2/sdf1tosdf2 \
	sglr/tree2dot/Makefile \
	stred/nl/cwi/gipe/stred/Makefile \
	toolbus/Makefile toolbus/edit-module.tb \
	toolbus/open-modules.tb toolbus/revert-module.tb \
	utils/Makefile utils/gen-table utils/idef2tif \
	utils/parse-sdf2 utils/ps-dot utils/sdf2table \
  , chmod +x meta install-prog-sh install-data-sh; \
    chmod +x graph-browser/graph-browser editor/editor; \
    chmod +x utils/gen-table utils/idef2tif \
             utils/parse-sdf2 utils/ps-dot utils/sdf2table 
)
