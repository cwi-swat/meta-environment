dnl
dnl    Meta-Environment - An environment for language prototyping.
dnl    Copyright (C) 2000  Stichting Mathematisch Centrum, Amsterdam, The Netherlands. 
dnl
dnl    This program is free software; you can redistribute it and/or modify
dnl    it under the terms of the GNU General Public License as published by
dnl    the Free Software Foundation; either version 2 of the License, or
dnl    (at your option) any later version.
dnl
dnl    This program is distributed in the hope that it will be useful,
dnl    but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl    GNU General Public License for more details.
dnl
dnl    You should have received a copy of the GNU General Public License
dnl    along with this program; if not, write to the Free Software
dnl    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307 USA
dnl
dnl $Id$

dnl Process this file with autoconf to produce a configure script.
AC_INIT(configure.in)
AM_INIT_AUTOMAKE(meta, 0.0.13)


dnl Default locations of other components
dnl Default locations of used components
AC_PATH_PROG(TOOLBUS, toolbus, no)
if test "${TOOLBUS}" = "no"; then
	TOOLBUS='${prefix}'
else
	TOOLBUS=`dirname \`dirname ${TOOLBUS}\``
fi
AC_PATH_PROG(ATERM, tifstoc, no)
if test "${ATERM}" = "no"; then
	ATERM='${prefix}'
else
	ATERM=`dirname \`dirname ${ATERM}\``
fi

dnl Our own version of libdir iwhose value is set dynamically
AC_SUBST(INSTLIBDIR)
INSTLIBDIR="$libdir"

dnl Is ranlib needed on this system?
AC_PROG_RANLIB
AC_PATH_PROG(AR,ar,/usr/bin/ar)

AC_REQUIRE([AC_PROG_CC])

dnl Which compiler to use?
AC_PROG_CC

dnl If anyone knows why this is used...
AC_AIX

dnl Where to locate a bourne-compatible shell?

dnl Where is perl5?
AC_PATH_PROGS(PERL,perl5 perl, /usr/bin/perl)

dnl What commands are needed to set the variable MAKE in Makefiles?
AC_SET_MAKE

dnl Handle --with-aterm=... argument to configure
AC_ARG_WITH(aterm,
[  --with-aterm=DIR        ATerm-library is in DIR],
ATERM=${withval},)
AC_SUBST(ATERM)
AC_ARG_WITH(aterm-bin,
[  --with-aterm-bin=DIR    ATerm executables are in DIR],
ATERMBIN=${withval},ATERMBIN=${ATERM}/bin)
AC_SUBST(ATERMBIN)
AC_ARG_WITH(aterm-lib,
[  --with-aterm-lib=DIR    ATerm libraries are in DIR],
ATERMLIB=${withval},ATERMLIB=${ATERM}/lib)
AC_SUBST(ATERMLIB)
AC_ARG_WITH(aterm-inc,
[  --with-aterm-inc=DIR    ATerm header files are in DIR],
ATERMINC=${withval},ATERMINC=${ATERM}/include)
AC_SUBST(ATERMINC)

dnl Handle --with-toolbus[-...]=... arguments to configure
AC_ARG_WITH(toolbus,
[  --with-toolbus=DIR      ToolBus distribution is in DIR],
TOOLBUS=${withval},)
AC_SUBST(TOOLBUS)
AC_ARG_WITH(toolbus-bin,
[  --with-toolbus-bin=DIR  ToolBus executables are in DIR],
TOOLBUSBIN=${withval},TOOLBUSBIN=${TOOLBUS}/bin)
AC_SUBST(TOOLBUSBIN)
AC_ARG_WITH(toolbus-lib,
[  --with-toolbus-lib=DIR  ToolBus libraries are in DIR],
TOOLBUSLIB=${withval},TOOLBUSLIB=${TOOLBUS}/lib)
AC_SUBST(TOOLBUSLIB)
AC_ARG_WITH(toolbus-inc,
[  --with-toolbus-inc=DIR  ToolBus header files are in DIR],
TOOLBUSINC=${withval},TOOLBUSINC=${TOOLBUS}/include)
AC_SUBST(TOOLBUSINC)

dnl Handle --with-tide=... argument to configure
AC_ARG_WITH(tide,
[  --with-tide		TIDE is in DIR],
TIDE=${withval}
TIDEDEF=-DUSE_TIDE
USE_TIDE=1,
USE_TIDE=0
)
AC_SUBST(TIDE)
AC_SUBST(TIDEDEF)
AC_SUBST(USE_TIDE)

dnl Handle --with-graphviz=... argument to configure
AC_ARG_WITH(graphviz,
   [  --with-graphviz=DIR     graphviz package is in DIR],
   GRAPHVIZ=${withval})
AC_SUBST(GRAPHVIZ)

dnl Of course, the latest version of wish is not always called "wish". :-(
dnl If we can't find something called wish8.1 or wish8.0, we'll assume
dnl that "wish" is at version 8 or higher (as it is in a _normal_ setup)
AC_PATH_PROGS(WISH,wish8.1 wish8.0 wish, /usr/bin/wish)

dnl Determine which libraries to use for programs using sockets
NOSOCKLIBS=$LIBS
dnl first, the socket functions
SOCKET_FOUND=no
SOCKLIBS=
AC_CHECK_LIB(sun,socket,[SOCKET_FOUND=yes;SOCKLIBS="-lsun"])
if test ${SOCKET_FOUND} = no; then
  AC_CHECK_LIB(socket, socket,[SOCKET_FOUND=yes;SOCKLIBS="-lsocket"])
  if test ${SOCKET_FOUND} = no; then
    AC_CHECK_LIB(bsd,socket,[SOCKET_FOUND=yes;SOCKLIBS="-lbsd";DEFS="$DEFS -D_BSD=43"])
  fi
fi

dnl then gethostname
GHN_FOUND=no
AC_CHECK_LIB(sun,gethostname,[GHN_FOUND=yes])
if test ${GHN_FOUND} = no; then
  AC_CHECK_LIB(nsl,gethostname,[GHN_FOUND=yes;SOCKLIBS="$SOCKLIBS -lnsl"])
fi

LIBS=$NOSOCKLIBS
AC_CHECK_LIB(dl, dlopen)

dnl Checks for header files.
AC_CHECK_HEADER(getopt.h)

AC_SUBST(SOCKLIBS)

ASFIX='${top_srcdir}/asfix'
ASFIXBIN='$(ASFIX)'
ASFIXINC='$(ASFIX)/libasfix'
ASFIXLIB='$(ASFIX)/libasfix'

AC_SUBST(ASFIX)
AC_SUBST(ASFIXBIN)
AC_SUBST(ASFIXINC)
AC_SUBST(ASFIXLIB)

ASC='${top_srcdir}/asc-support'
ASCBIN='$(ASC)'
ASCINC='$(ASC)'
ASCLIB='$(ASC)'

AC_SUBST(ASC)
AC_SUBST(ASCBIN)
AC_SUBST(ASCINC)
AC_SUBST(ASCLIB)

dnl Which files need updating?
AC_OUTPUT(
Makefile
compiler/Makefile compiler/c-source/Makefile
asfix2c-compiler/Makefile
editor/Makefile
evaluator/Makefile evaluator/src/Makefile evaluator/test/Makefile 
evaluator/test/terms/Makefile
graph-browser/Makefile graph-browser/graph-browser.tcl
in-output/Makefile in-output/test/Makefile
module-db/Makefile module-db/test/Makefile
stred/Makefile stred/nl/Makefile stred/nl/cwi/Makefile
stred/nl/cwi/gipe/Makefile stred/nl/cwi/gipe/stred/Makefile
stred/nl/cwi/gipe/stred/test/Makefile
stred/src/Makefile stred/src/test/Makefile 
toolbus/Makefile toolbus/test/Makefile
utils/Makefile 
utils/genmakefile
grammars/Makefile
grammars/aterm/Makefile
grammars/asfix/Makefile
grammars/sdf2/Makefile
demo/Makefile
demo/pico/Makefile
demo/user-manual-examples/Makefile
doc/Makefile
doc/html/Makefile
, chmod +x utils/genmakefile
)


dnl Handle subpackages...
AC_CONFIG_PKG(asfix)
AC_CONFIG_PKG(asc-support, --with-asfix-inc='\${top_builddir}/../asfix/libasfix'
                           --with-asfix-lib='\${top_builddir}/../asfix/libasfix')
AC_CONFIG_PKG(sglr,        --with-asfix-inc='\${top_builddir}/../asfix/libasfix'
                           --with-asfix-lib='\${top_builddir}/../asfix/libasfix')

AC_CONFIG_PKG(pgen,        --with-asfix-inc='\${top_builddir}/../asfix/libasfix'
                           --with-asfix-lib='\${top_builddir}/../asfix/libasfix'
                           --with-asc-inc='\${top_builddir}/../asc-support'
                           --with-asc-lib='\${top_builddir}/../asc-support'
                           --with-sglr='\${top_builddir}/../sglr/sglr')

