#ifndef _GUI_DIALOG
#define _GUI_DIALOG

tool dialog is { }

process Dialog is
let
  T: dialog,
  Arguments: list,
  DialogTitle: str,
  ErrorMessage: str,
  Path: str,
  Question: str,
  Extension: str
in
  rec-connect(T?)
  .
  (
    rec-msg(ask-for-file(DialogTitle?, Path?, Extension?))
    . ShowFileDialog(T, DialogTitle, Path, Extension)
  +
    rec-msg(ask-question(Question?))
    . ShowQuestionDialog(T, Question)
  +
    rec-msg(show-error-dialog(ErrorMessage?))
    . snd-do(T, show-error-dialog(ErrorMessage)) 
  +
    rec-msg(show-error-dialog-with-arguments(ErrorMessage?, Arguments?))
    . snd-do(T, show-error-dialog-with-arguments(ErrorMessage, Arguments))
  +
    rec-msg(show-progress-list(DialogTitle?))
    . ShowProgressList(T, DialogTitle)
  )
  *
  rec-disconnect(T)
endlet

process ShowFileDialog(T: dialog, DialogTitle: str, Path: str, Extension: str) is
let
  Filename: str
in
  snd-eval(T, show-file-dialog(DialogTitle, Path, Extension))
  .
  (
    rec-value(T, file-dialog-approve(Filename?))
    . snd-msg(ask-for-file-result(Filename))
  +
    rec-value(T, file-dialog-cancel)
    . snd-msg(ask-for-file-cancel)
  )
endlet

process ShowQuestionDialog(T: dialog, Question: str) is
let
  Answer: term
in
  snd-eval(T, show-question-dialog(Question))
  . rec-value(T, answer(Answer?))
  . snd-msg(ask-question-result(Answer))
endlet

process ShowProgressList(T: dialog, DialogTitle: str) is
let
  Arguments: list,
  Message: str
in
  snd-do(T, show-progress-list(DialogTitle))
  .
  (
    rec-msg(show-progress-message(Message?))
    . snd-do(T, show-progress-message(Message))
  +
    rec-msg(show-progress-message-with-arguments(Message?, Arguments?))
    . snd-do(T, show-progress-message-with-arguments(Message, Arguments))
  )
  *
  rec-msg(close-progress-list)
  . snd-do(T, close-progress-list)
endlet

process ShowProgressDialog(Title: str) is 
  snd-msg(show-progress-list(Title))

process ShowProgressMessage(Message: str) is
  snd-msg(show-progress-message(Message))

process ShowProgressMessageWithArguments(Message: str, Arguments: list) is
  snd-msg(show-progress-message-with-arguments(Message, Arguments))

process CloseProgressDialog is
  snd-msg(close-progress-list)

process ShowErrorDialog(Message: str) is
  snd-msg(show-error-dialog(Message))

process ShowErrorDialogWithArguments(Message: str, Arguments: list) is
  snd-msg(show-error-dialog-with-arguments(Message, Arguments))

toolbus(Dialog)

#endif
