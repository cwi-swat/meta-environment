#ifndef _GUI_GRAPH_PAINTER
#define _GUI_GRAPH_PAINTER

tool graph-painter is { }

process GraphPainter is
let
  T: graph-painter,
  Graph: term,
  GraphId: str,
  Menu: term,
  PopupMenu: list,
  NodeId: str
in
  rec-connect(T?)
  .
  (
    rec-msg(gp-display-graph(GraphId?, Graph?))
    . snd-do(T, display-graph(GraphId, Graph))
  +
    rec-msg(gp-render-graph(GraphId?, Graph?))
    . snd-eval(T, size-graph(GraphId, Graph))
    . rec-value(T, sized-graph(Graph?))
    . snd-msg(gp-rendered-graph(GraphId, Graph))
  +
    rec-msg(gp-select-node(GraphId?, NodeId?))
    . snd-do(T, select-node(GraphId, NodeId))
  +
    rec-event(T, node-selected(GraphId?, NodeId?))
    . snd-note(gp-node-selected(GraphId, NodeId))
    . snd-ack-event(T, node-selected(GraphId, NodeId))
  +
    rec-event(T, request-popup-event(GraphId?, NodeId?))
    . snd-msg(gp-request-popup-event(GraphId, NodeId))
    . snd-ack-event(T, request-popup-event(GraphId, NodeId))
  +
    rec-msg(gp-show-popup(GraphId?, NodeId?, PopupMenu?))
    . snd-do(T, show-popup(GraphId, NodeId, PopupMenu))
  +
    rec-event(T, popup-menu-event(GraphId?, NodeId?, Menu?))
    . snd-msg(gp-menu-selected(GraphId, NodeId, Menu))
    . snd-ack-event(T, popup-menu-event(GraphId, NodeId, Menu))
  )
  *
  rec-disconnect(T)
endlet

process GP-AddListener is
  subscribe(gp-node-selected(<str>,<str>))



process GP-RequestPopup(GraphId: str?, NodeId: str?) is
  rec-msg(gp-request-popup-event(GraphId?, NodeId?))

process GP-ShowPopup(GraphId: str, NodeId: str, PopupMenu: list) is
  snd-msg(gp-show-popup(GraphId, NodeId, PopupMenu))

process GP-PopupEvent(GraphId: str?, NodeId: str?, MenuItem: term?) is
  rec-msg(gp-menu-selected(GraphId?, NodeId?, MenuItem?))

toolbus(GraphPainter)

#endif
