#ifndef _GUI_GRAPH_PAINTER
#define _GUI_GRAPH_PAINTER

tool graph-painter is { }

process GraphPainter is
let
  T: graph-painter,
  Graph: term,
  GraphId: str,
  Menu: str,
  PopupMenu: list,
  NodeId: str
in
  rec-connect(T?)
  .
  (
    rec-msg(gp-display-graph(GraphId?, Graph?))
    . snd-do(T, display-graph(GraphId, Graph))
  +
    rec-msg(gp-render-graph(GraphId?, Graph?))
    . snd-eval(T, size-graph(GraphId, Graph))
    . rec-value(T, sized-graph(Graph?))
    . snd-msg(gp-rendered-graph(GraphId, Graph))
  +
    rec-msg(gp-select-node(GraphId?, NodeId?))
    . snd-do(T, select-node(GraphId, NodeId))
  +
    rec-event(T, node-selected(GraphId?, NodeId?))
    . snd-note(gp-node-selected(GraphId, NodeId))
    . snd-ack-event(T, node-selected(GraphId, NodeId))
  +
    rec-event(T, request-popup-event(NodeId?))
    . snd-note(gp-request-popup-event(NodeId))
    . snd-ack-event(T, request-popup-event(NodeId))
  +
    rec-msg(gp-show-popup(NodeId?, PopupMenu?))
    . snd-do(T, show-popup(NodeId, PopupMenu))
  +
    rec-event(T, popup-menu-event(Menu?, NodeId?))
    . snd-msg(nav-menu-selected(Menu, NodeId))
    . snd-ack-event(T, popup-menu-event(Menu, NodeId))
  )
  *
  rec-disconnect(T)
endlet

process GP-AddListener is
  subscribe(gp-request-popup-event(<str>))

process GP-PopupEvent(NodeId: str?) is
  rec-note(gp-request-popup-event(NodeId?))

process GP-ShowPopup(NodeId: str, PopupMenu: list) is
  snd-msg(gp-show-popup(NodeId, PopupMenu))

toolbus(GraphPainter)

#endif
