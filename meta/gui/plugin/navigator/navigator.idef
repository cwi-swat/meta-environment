#ifndef _GUI_NAVIGATOR_
#define _GUI_NAVIGATOR_

tool navigator is { }

process Navigator is
let
  Menu: term,
  Graph: term,
  ModuleId: term,
  ImportGraph : term,
  PopupMenu: list,
  Str : str,
  T: navigator
in
  rec-connect(T?)
  .
    (
      rec-msg(nav-set-modules(ImportGraph?))
      . snd-do(T, set-modules(ImportGraph))
    +
      rec-msg(nav-select-module(ModuleId?))
      . snd-do(T, select-module(ModuleId))
    +
      rec-event(T, module-selected(ModuleId?))
      . snd-note(nav-module-selected(ModuleId))
      . snd-ack-event(T, module-selected(ModuleId))
    +
      rec-event(T, request-popup-event(ModuleId?))
      . snd-msg(nav-request-popup-event(ModuleId))
      . snd-ack-event(T, request-popup-event(ModuleId))
    +
      rec-msg(nav-show-popup(ModuleId, PopupMenu?))
      . snd-do(T, show-popup(ModuleId, PopupMenu))
    +
      rec-event(T, popup-menu-event(ModuleId?, Menu?))
      . snd-msg(nav-menu-selected(ModuleId, Menu))
      . snd-ack-event(T, popup-menu-event(ModuleId, Menu))
    )
    *
      rec-disconnect(T)
endlet

process NAV-AddListener is
  subscribe(nav-module-selected(<str>))

process NAV-RequestPopup(ModuleId: term?) is
  rec-msg(nav-request-popup-event(ModuleId?))

process NAV-ShowPopup(ModuleId: term, PopupMenu: list) is
  snd-msg(nav-show-popup(ModuleId, PopupMenu))

process NAV-PopupEvent(ModuleId: term?, MenuItem: term?) is
  rec-msg(nav-menu-selected(ModuleId?, MenuItem?))

toolbus(Navigator)

#endif /*_GUI_NAVIGATOR_*/
