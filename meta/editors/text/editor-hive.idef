/* $Id$ */

tool editor-hive is {
  command = "editor-hive"
}

process EditorHive is
let
  ActionList : list,
  EditorId : term,
  Editor : str,
  FileName : str,
  Focus : term,
  FocusText : str,
  Hive : editor-hive,
  Location : term,
  Offset : int,
  MenuEvent : term,
  Message : str
in
#ifdef ECLIPSE
  rec-connect(Hive?)
#else
  execute(editor-hive, Hive?)
#endif
  .
  (
    rec-msg(te-edit-text(EditorId?, Editor?, FileName?, ActionList?))
    . snd-do(Hive, edit-file(EditorId, Editor, FileName))
    . snd-do(Hive, set-actions(EditorId, ActionList))
  +
    rec-msg(te-write-contents(EditorId?))
    . snd-do(Hive, write-contents(EditorId))
  +
    rec-msg(te-reread-contents(EditorId?))
    . snd-do(Hive, reread-contents(EditorId))
  +
    rec-msg(te-set-focus(EditorId?, Focus?))
    . snd-do(Hive, set-focus(EditorId, Focus))
  +
    rec-msg(te-get-focus-text(EditorId?, Focus?))
    /* TODO: separate writing and retrieval of contents */
    . snd-do(Hive, write-contents(EditorId))
    . snd-do(Hive, get-contents(EditorId, Focus))
  + 
    rec-msg(te-clear-focus(EditorId))
    . snd-do(Hive, clear-focus(EditorId))
  +
    rec-msg(te-display-message(EditorId?, Message?))
    . snd-do(Hive, display-message(EditorId, Message))
  +
    rec-msg(te-set-cursor-at-offset(EditorId?, Offset?))
    . snd-do(Hive, set-cursor-at-offset(EditorId, Offset))
  +
    rec-msg(te-set-focus-at-location(EditorId?, Location?))
    . snd-do(Hive, set-focus-at-location(EditorId, Location))
  +
    rec-msg(te-editor-to-front(EditorId?))
    . snd-do(Hive, editor-to-front(EditorId))
  +
    rec-msg(te-kill-text-editor(EditorId?))
    . snd-do(Hive, kill-editor(EditorId))
  +
    rec-event(Hive, contents-changed(EditorId?))
    . snd-ack-event(Hive, contents-changed(EditorId))
    . snd-msg(te-contents-changed(EditorId))
  +
    rec-event(Hive, mouse-event(EditorId?, Offset?))
    . snd-ack-event(Hive, mouse-event(EditorId, Offset))
    . snd-msg(te-mouse-click-at-offset(EditorId, Offset))
  +
    rec-event(Hive, menu-event(EditorId?, MenuEvent?))
    . snd-ack-event(Hive, menu-event(EditorId, MenuEvent))
    . snd-msg(te-menu-event(EditorId, MenuEvent))
  +
    rec-event(Hive, contents(EditorId, FocusText?))
    . snd-msg(te-focus-text(EditorId, FocusText))
    . snd-ack-event(Hive, contents(EditorId, FocusText))
  +
    rec-event(Hive, editor-disconnected(EditorId?))
    . snd-ack-event(Hive, editor-disconnected(EditorId))
    . snd-msg(te-text-editor-disconnected(EditorId))
  )
  * delta
endlet

toolbus(EditorHive)
