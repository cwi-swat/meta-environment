/* $Id$ */

process Swing-Editor is
let
  ActionList : list,
  Area : term,
  Contents : str,
  EditorId : term,
  Editor : swing-editor,
  Filename : str,
  Focus : term,
  Offset : int,
  MenuEvent : term,
  Message : str,
  Modified : term,
  SortName: str,
  Categories: term,
  Slices: term
in
  execute(swing-editor, Editor?)
  .
  (
    rec-msg(te-edit-text(Filename?))
    . snd-do(Editor, edit-file(Filename))
  +
    rec-msg(te-add-actions(ActionList?))
    . snd-do(Editor, add-actions(ActionList))
  +
    rec-msg(te-write-contents)
    . snd-do(Editor, write-contents)
  +
    rec-msg(te-reread-contents)
    . snd-do(Editor, reread-contents)
  +
    rec-msg(te-is-modified)
    . snd-do(Editor, is-modified)
  +
    rec-msg(te-set-focus(Area?))
    . snd-do(Editor, set-focus(Area))
  +
    rec-msg(te-set-cursor-at-offset(Offset?))
    . snd-do(Editor, set-cursor-at-offset(Offset))
  +
    rec-msg(te-clear-focus)
    . snd-do(Editor, clear-focus)
  +
    rec-msg(te-register-text-categories(Categories?))
    . snd-do(Editor, register-text-categories(Categories))
  +
    rec-msg(te-highlight-slices(Slices?))
    . snd-do(Editor, highlight-slices(Slices))
  +
    rec-msg(te-display-message(Message?))
    . snd-do(Editor, display-message(Message))
  +
    rec-msg(te-editor-to-front)
    . snd-do(Editor, editor-to-front)
  +
    rec-msg(te-kill-text-editor) 
    . snd-do(Editor, kill-editor)
  +
    rec-event(Editor, is-modified(Modified?))
    . snd-ack-event(Editor, is-modified(Modified))
    . snd-msg(te-is-modified(Modified))
  +
    rec-event(Editor, contents-changed)
    . snd-ack-event(Editor, contents-changed)
    . snd-msg(te-contents-changed)
  +
    rec-event(Editor, mouse-event(Offset?))
    . snd-ack-event(Editor, mouse-event(Offset))
    . snd-msg(te-mouse-click-at-offset(Offset))
  +
    rec-event(Editor, menu-event(MenuEvent?))
    . snd-ack-event(Editor, menu-event(MenuEvent))
    . snd-msg(te-menu-event(MenuEvent))
  +
    rec-event(Editor, contents(Contents?))
    . snd-msg(te-contents(Contents))
    . snd-ack-event(Editor, contents(Contents))
  +
    rec-event(Editor, editor-disconnected)
    . snd-ack-event(Editor, editor-disconnected)
    . snd-msg(te-text-editor-disconnected)
  )
  * delta
endlet

tool swing-editor is {
  command="java-adapter -class Main"
}

toolbus(Swing-Editor)
