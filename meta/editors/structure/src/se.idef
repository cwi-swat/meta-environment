/*

   Meta-Environment - An environment for language prototyping.
   Copyright (C) 2000  Stichting Mathematisch Centrum, Amsterdam, 
                       The Netherlands. 

   This program is free software; you can redistribute it and/or modify
   it under the terms of the GNU General Public License as published by
   the Free Software Foundation; either version 2 of the License, or
   (at your option) any later version.

   This program is distributed in the hope that it will be useful,
   but WITHOUT ANY WARRANTY; without even the implied warranty of
   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
   GNU General Public License for more details.

   You should have received a copy of the GNU General Public License
   along with this program; if not, write to the Free Software
   Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307 USA

   $Id$
*/

tool se is {
  command = "se"
}

process SE is
let
  EditorId     : term,
  Focus        : term,
  Error        : list,
  Focuses      : list,
  FocusList    : list,
  StartSymbols : list,
  Location     : int,
  SE           : se,
  Text         : str,
  Move         : term,
  Number       : int,
  Nonterminal  : str,
  Tree         : term,
  ModifiedStatus : term
in 
  execute(se, SE?) .
  (
    rec-msg(initialize-structure-editor(EditorId?, Text?)) .
    snd-eval(SE, new-editor-given-text(EditorId, Text)) .
    rec-value(SE, Focus?) .       
    snd-msg(initial-focus(Focus))
  +
    rec-msg(initialize-structure-editor-with-tree(EditorId?, Tree?)) .
    snd-eval(SE, new-editor-given-tree(EditorId, Tree)) .
    rec-value(SE, Focus?) .       
    snd-msg(initial-focus(Focus))
  +
    rec-msg(insert-chars(EditorId?, Location?, Text?)) .
    snd-eval(SE, insert-chars(EditorId, Location, Text)) .
    rec-value(SE, Focus?) .       
    snd-msg(ack-insert-chars(EditorId)).
    snd-msg(set-focus(EditorId, Focus))
  +
    rec-msg(delete-chars(EditorId?, Location?, Number?)).
    snd-eval(SE, delete-chars(EditorId, Location, Number)) .
    rec-value(SE, Focus?) .
    snd-msg(ack-delete-chars(EditorId)).
    snd-msg(set-focus(EditorId, Focus))
  +
    rec-msg(replace-focus(EditorId?, Focus?, Tree?)) .
    snd-do(SE, replace-focus(EditorId, Focus, Tree))
  +
    rec-msg(set-focus(EditorId?, Focuses?)) .
    snd-do(SE, set-multiple-focus(EditorId, Focuses))
  +
    rec-msg(move-focus(EditorId?, Move?)) .
    snd-eval(SE, move-focus(EditorId, Move)) .
    rec-value(SE, Focus?) .
    snd-msg(set-focus(EditorId, Focus))
  +
    rec-msg(invalidate-tree(EditorId?)) .
    snd-eval(SE, invalidate-tree(EditorId)) .
    ( rec-value(SE, focus(Focus?)) .
      snd-msg(set-focus(EditorId, Focus))
      +
      rec-value(SE, no-such-editor(editorId))
    )
  +
    rec-msg(set-current-location(EditorId?, Location?)) .
    snd-eval(SE, set-focus(EditorId, Location)) .
    rec-value(SE, Focus?) .  
    snd-msg(set-focus(EditorId, Focus)) 
  +
    rec-msg(get-tree(EditorId?)) .
    snd-eval(SE, get-parse-tree(EditorId)) .
    rec-value(SE, Tree?) .
    snd-msg(tree(Tree))
  +
    rec-msg(register-startsymbols(EditorId?, StartSymbols?)) .
    snd-do(SE, register-symbols(EditorId, StartSymbols)) .
    snd-msg(startsymbols-registered)
  + 
    rec-msg(get-focus-sort(Nonterminal?, Focus?)) .
    snd-eval(SE, get-focus-sort(Nonterminal, Focus)) .
    rec-value(SE, Nonterminal?) .
    snd-msg(focus-sort(Nonterminal))
  +
    rec-msg(get-dirty-focuses(EditorId?)) .
    snd-eval(SE, get-dirty-focuses(EditorId)) .
    (
      rec-value(SE, foci(FocusList?))
      +
      rec-value(SE, no-such-editor(EditorId)) .
      FocusList := []
    ) .
    snd-msg(dirty-focuses(FocusList))
  +
    rec-msg(get-error-position(Focus?, Error?)) .
    snd-eval(SE, calc-error-location(Focus, Error)) .
    rec-value(SE, Location?) .
    snd-msg(error-position(Location))
  +
    rec-msg(delete-structure-editor(EditorId?)).
    snd-do(SE, remove-tree(EditorId))
  +
    rec-msg(get-modified-status(EditorId?)).
    snd-eval(SE, get-modification-status(EditorId)).
    rec-value(SE, modification-status(ModifiedStatus?)).
    snd-msg(modified-status(EditorId, ModifiedStatus))
  ) *
  delta
endlet
