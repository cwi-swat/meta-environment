/* $Id$ */

tool structure-editor is {
  command = "se"
}

/* Because EditorIds can dissappear suddenly, the structure editor
 * should always be robust against non-existing EditorIds. If possible
 * a default answer should be returned, otherwise an error message to
 * ensure that normal operation can continue even if an editor is missing.
 */

process StructureEditor is
let
  EditorId : term,
  Focus : term,
  Error : term,
  Focuses : list,
  FocusList : list,
  Location : int,
  SE : structure-editor,
  Text : str,
  Move : term,
  Nonterminal : str,
  Tree : term,
  Modified : term,
  Msg : term,
  PosInfo : term
in 
  execute(structure-editor, SE?) .
  (
    rec-msg(se-set-text(EditorId?, Text?)) .
    snd-do(SE, new-editor-given-text(EditorId, Text)) 
  +
    rec-msg(se-set-tree(EditorId?, Tree?)) .
    snd-do(SE, new-editor-given-tree(EditorId, Tree)) 
  +
    rec-msg(se-replace-focus(EditorId?, Focus?, Tree?)) .
    snd-do(SE, replace-focus(EditorId, Focus, Tree))
  +
    rec-msg(se-move-focus-to-root(EditorId?)) .
    snd-eval(SE, move-focus-to-root(EditorId)) .
    rec-value(SE, focus(Focus?)) .
    snd-msg(se-focus-moved(EditorId, Focus))
  +
    rec-msg(se-move-focus(EditorId?, Move?)) .
    snd-eval(SE, move-focus(EditorId, Move)) .
    rec-value(SE, focus(Focus?)) .
    /* TODO: remove reference to Text Editor internals */
    snd-msg(te-set-focus(EditorId, Focus))
  +
    rec-msg(se-invalidate-tree(EditorId?)) .
    snd-eval(SE, invalidate-tree(EditorId)) .
    rec-value(SE, focus(Focus?)) .
    snd-msg(se-invalidation-done(EditorId, Focus))
  +
    rec-msg(se-set-location(EditorId?, Location?)) .
    snd-eval(SE, set-focus(EditorId, Location)) .
    rec-value(SE, focus(Focus?)) .  
    snd-msg(se-focus-at-location(EditorId, Focus)) 
  +
    rec-msg(se-get-tree(EditorId?)) .
    snd-eval(SE, get-parse-tree(EditorId)) .
    (
      rec-value(SE, parse-tree(Tree?)) .
      snd-msg(se-tree(Tree))
    +
      rec-value(SE, no-such-editor) .
      snd-msg(se-no-tree)
    )
  +
    rec-msg(se-get-focussed-tree(EditorId?)) .
    snd-eval(SE, get-focussed-tree(EditorId)) .
    ( 
      rec-value(SE, parse-tree(Tree?)) .
      snd-msg(se-tree(Tree))
    +
      rec-value(SE, no-parse-tree) .
      snd-msg(se-no-tree)
    )
  +
    rec-msg(se-replace-focussed-tree(EditorId?, Tree?)) .
    snd-eval(SE, replace-focussed-tree(EditorId, Tree)) .
    rec-value(SE, focus(Focus?)) .
    snd-msg(se-focus(Focus))
  +
    rec-msg(se-check-tree-sort(Nonterminal?, Tree?)) .
    snd-eval(SE, check-tree-sort(Nonterminal, Tree)) .
    (
      rec-value(SE, sort-ok) .
      snd-msg(se-sort-ok)
    +
      rec-value(SE, wrong-sort(Nonterminal?)) .
      snd-msg(se-wrong-sort(Nonterminal))
    )
#if 0 /* TODO: remove if indeed unused */
  +
    rec-msg(get-focus-sort(EditorId?)) .
    snd-eval(SE, get-focus-sort(EditorId)) .
    (
      rec-value(SE, focus-sort(Nonterminal?)) .
      snd-msg(focus-sort(Nonterminal))
    +
      rec-value(SE, no-focus) .
      snd-msg(no-focus)
    )
#endif
  +
    rec-msg(se-get-focus-location(EditorId?)) .
    snd-eval(SE, get-focus-location(EditorId)) .
    rec-value(SE, focus-location(PosInfo?)) .
    snd-msg(se-focus-location(EditorId, PosInfo))
  +
    rec-msg(se-get-dirty-focuses(EditorId?)) .
    snd-eval(SE, get-dirty-focuses(EditorId)) .
    (
      rec-value(SE, foci(FocusList?))
      +
      rec-value(SE, no-such-editor) .
      FocusList := []
    ) .
    snd-msg(se-dirty-focuses(FocusList))
  +
    rec-msg(se-delete-structure-editor(EditorId?)).
    snd-do(SE, remove-tree(EditorId)) 
  ) *
  delta
endlet

toolbus(StructureEditor)
