/* $Id$ */

tool structure-editor is {
  command = "structure-editor"
}

process StructureEditor is
let
  SE: structure-editor,
  Column: int,
  Direction: term,
  EditorId: term,
  Focus: term,
  Line: int,
  Offset: int,
  ParseTree: term,
  Sort: str,
  Tree: term,
  Slices : term
in
  execute(structure-editor, SE?)
  .
  (
    rec-msg(se-create(EditorId?, ParseTree?))
    . snd-do(SE, create-editor(EditorId, ParseTree))
  +
    rec-msg(se-update(EditorId?, ParseTree?))
    . snd-do(SE, update-editor(EditorId, ParseTree))
  +
    rec-msg(se-delete(EditorId?))
    . snd-do(SE, delete-editor(EditorId))
  +
    rec-msg(se-get-parse-tree(EditorId?))
    . snd-eval(SE, get-parse-tree(EditorId))
    . rec-value(SE, parse-tree(ParseTree?))
    . snd-msg(se-parse-tree(EditorId, ParseTree))
  +
    rec-msg(se-get-tree-slices(EditorId?))
    . snd-eval(SE, get-tree-slices(EditorId)) 
    . 
    (
      rec-value(SE, tree-slices(Slices?)) 
      . snd-msg(se-tree-slices(EditorId, Slices))
    +
      rec-value(SE, no-tree-slices) 
      . snd-msg(se-no-tree-slices(EditorId))
    )
  +
    rec-msg(se-set-cursor-at-offset(EditorId?, Offset?))
    . snd-do(SE, set-cursor-at-offset(EditorId, Offset))
  +
    rec-msg(se-set-cursor-at-line-column(EditorId?, Line?, Column?))
    . snd-do(SE, set-cursor-at-line-column(EditorId, Line, Column))
  +
    rec-msg(se-get-cursor(EditorId?))
    . snd-eval(SE, get-cursor(EditorId))
    .
    (
      rec-value(SE, cursor(Tree?))
      . snd-msg(se-cursor(EditorId, Tree))
    +
      rec-value(SE, no-cursor)
      . snd-msg(se-no-cursor(EditorId))
    )
  +
    rec-msg(se-get-focus-at-cursor(EditorId?))
    . snd-eval(SE, get-focus-at-cursor(EditorId))
    . rec-value(SE, focus(Focus?))
    . snd-msg(se-focus-at-cursor(EditorId, Focus))
  +
    rec-msg(se-get-sort-at-cursor(EditorId?))
    . snd-eval(SE, get-sort-at-cursor(EditorId))
    .
    (
      rec-value(SE, sort(Sort?))
      . snd-msg(se-sort-at-cursor(EditorId, Sort))
    +
      rec-value(SE, no-cursor)
      . snd-msg(se-no-cursor(EditorId))
    )
  +
    rec-msg(se-move-cursor(EditorId?, Direction?))
    . snd-do(SE, move-cursor(EditorId, Direction))
  )
  * delta
endlet

toolbus(StructureEditor)
