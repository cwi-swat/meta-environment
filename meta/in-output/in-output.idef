process Io is
let
  IO   : in-output,
  Id   : term,
  Asfix: term,
  Name : str,
  Dir  : str,
  Path : str,
  T    : term,
  Text : str,
  TS   : int
in
  execute(in-output,IO?).
  (
    rec-msg(Id?,locate-parse-table-file(id(Name?))).
    snd-eval(IO,locate-parse-table-file(Name)).
    (
      rec-value(IO,located-parse-table-file(Name?,Path?,timestamp(TS?))).
      snd-msg(Id,located-parse-table-file(id(Name),Path,timestamp(TS)))
      +
      rec-value(IO,unavailable-parse-table(Name?)).
      snd-msg(Id,error-locating(Name))
    )
  +
    rec-msg(Id?,read-parse-table(id(Name?))).
    snd-eval(IO,read-parse-table(Name)).
    (
      rec-value(IO,table-on-disk(Text?,timestamp(TS?))).
      snd-msg(Id,table-on-disk(Text,timestamp(TS)))
    +
      rec-value(IO,error-opening(Name?)).
      snd-msg(Id,no-table-on-disk)
    )
  +
    rec-msg(Id?,open-trm-file(id(Name?))).
    snd-eval(IO,open-trm-file(Name)).
    ( rec-value(IO,opened-file(raw,Name,Text?,Path?,timestamp(TS?))).
      snd-msg(Id,opened-file(raw,id(Name),Text,Path,timestamp(TS)))
      +
      rec-value(IO,error-opening(Name?)).
      snd-msg(Id, error-opening(Name))
    )
  +
    rec-msg(Id?,open-file(id(Name?))).
    snd-eval(IO, open-old-asfix-file(Name)).
    (
      rec-value(IO,opened-file(asfix,Name?,T?,Path?,timestamp(TS?))).
      snd-msg(Id,opened-file(asfix,id(Name),T,Path,timestamp(TS)))
    +
      rec-value(IO,error-opening(Name?)).
      snd-eval(IO,open-sdf2-file(Name)).
      (
        rec-value(IO,opened-file(raw,Name?,Text?,Path?,timestamp(TS?))).
        snd-msg(Id,opened-file(raw,id(Name),Text,Path,timestamp(TS)))
        + 
        rec-value(IO,opened-file(baf,Name?,T?,Path?,timestamp(TS?))).
        snd-msg(Id,opened-file(baf,id(Name),T,Path,timestamp(TS)))
        +
        rec-value(IO,error-opening(Name?)).
        snd-msg(Id, error-opening(Name))
      )
    )
  +
    rec-msg(Id?,open-eqs-asfix-file(id(Name?))).
    snd-eval(IO,open-eqs-asfix-file(Name)).
    (
      rec-value(IO,opened-file(baf,Name?,T?,Path?,timestamp(TS?))).
      snd-msg(Id,opened-file(baf,id(Name),T,Path,timestamp(TS)))
      +
      rec-value(IO,error-opening(Name?)).
      snd-msg(Id, error-opening(Name))
    )
  +
    rec-msg(Id?,open-eqs-text-file(id(Name?))).
    snd-eval(IO,open-eqs-text-file(Name)).
    (
      rec-value(IO,opened-file(raw,Name?,Text?,Path?,timestamp(TS?))).
      snd-msg(Id,opened-file(raw,id(Name),Text,Path,timestamp(TS))) 
      +
      rec-value(IO,error-opening(Name?)).
      snd-msg(Id, error-opening(Name))
    )
  +
    rec-msg(save-sdf2-asfix(id(Name?),Path?,Asfix?)).
    snd-eval(IO,save-sdf2-asfix(Name,Path,Asfix)).
    rec-value(IO,save-done(Name?)).
    snd-msg(save-done)
  +
    rec-msg(save-eqs-asfix(id(Name?),Path?,Asfix?)).
    snd-eval(IO,save-eqs-asfix(Name,Path,Asfix)).
    rec-value(IO,save-done(Name?)).
    snd-msg(save-done)
  )*
  delta
endlet


tool in-output is { command = "in-output" }

