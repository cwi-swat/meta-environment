/*
 * $Id$
 */

process Io is
let
  IO        : in-output,
  Asfix     : term,
  Name      : str,
  Dir       : str,
  Path      : str,
  T         : term,
  Text      : str,
  EqsTree   : term,
  EqsText   : str,
  FileName  : str,
  Contents  : term,
  TimeStamp : term
in
  execute(in-output,IO?).
  (
    rec-msg(get-table-timestamp(Name?)).
    snd-eval(IO,get-timestamp(Name,".tbl")).
    rec-value(IO, TimeStamp?) .
    snd-msg(table-timestamp(TimeStamp))
    +
    rec-msg(read-parse-table(Name?)).
    snd-eval(IO,read-parse-table(Name)).
    (
      rec-value(IO,table-on-disk(Text?,TimeStamp)).
      snd-msg(table-on-disk(Text,TimeStamp))
    +
      rec-value(IO,error-opening(Name?)).
      snd-msg(no-table-on-disk)
    )
  +
    rec-msg(open-trm-file(Name?)).
    snd-eval(IO,open-trm-file(Name)).
    ( rec-value(IO,opened-file(Name,Contents?,Path?,TimeStamp?)).
      snd-msg(opened-file(Name,Contents,Path,TimeStamp))
      +
      rec-value(IO,error-opening(Name?)).
      snd-msg(error-opening(Name))
    )
  +
    %% Check if a sdf2 module exists.
    rec-msg(exists-sdf2-module(Name?)) .
    snd-eval(IO, exists-sdf2-module(Name)) .
    (
      rec-value(IO, exists) .
      snd-msg(sdf2-module-exists(Name))
    +
      rec-value(IO, not-exists) .
      snd-msg(sdf2-module-not-exists(Name))
    )
  +
    %% Create a (new) empty sdf2 module
    rec-msg(create-empty-sdf2-module(Name?)) .
    snd-eval(IO, create-empty-sdf2-module(Name)) .
    (
      rec-value(IO, creation-succeeded) .
      snd-msg(module-created(Name))
    +
      rec-value(IO, creation-failed(Text?)) .
      snd-msg(module-creation-failed(Name, Text))
    )
  +
    %% Check if an equations section exists.
    rec-msg(exists-eqs-section(Name?)) .
    snd-eval(IO, exists-eqs-section(Name)) .
    (
      rec-value(IO, exists) .
      snd-msg(eqs-section-exists(Name))
    +
      rec-value(IO, not-exists) .
      snd-msg(eqs-section-not-exists(Name))
    )
  +
    %% Create a (new) equations section for a given module
    rec-msg(create-empty-equations(Name?)) .
    snd-eval(IO, create-empty-eqs-section(Name)) .
    (
      rec-value(IO, creation-succeeded(FileName?)) .
      snd-msg(empty-equations-created(FileName))
    +
      rec-value(IO, creation-failed(Text?)) .
      snd-msg(empty-equations-not-created(Name, Text))
    )
  +
    %% Name represents the module-name (without extension)
    rec-msg(open-file(Name?)).
    snd-eval(IO, open-old-asfix-file(Name)).
    (
      rec-value(IO,opened-file(asfix,Name?,T?,Path?,TimeStamp?)).
      snd-msg(opened-file(asfix,Name,T,Path,TimeStamp))
    +
      rec-value(IO,error-opening(Name?)).
      snd-eval(IO,open-asdf2-file(Name, sdf2)).
      (
        rec-value(IO,opened-file(Name?,Contents?,Path?,TimeStamp?)).
        snd-msg(opened-file(Name,Contents,Path,TimeStamp))
        + 
        rec-value(IO,opened-file(Name?,Contents?,Path?,TimeStamp?)).
        snd-msg(opened-file(Name,Contents,Path,TimeStamp))
        +
        rec-value(IO,error-opening(Name?)).
        snd-msg(error-opening(Name))
      )
    )
  +
    rec-msg(open-eqs-file(Name?)).
    snd-eval(IO,open-asdf2-file(Name, eqs)).
    (
      rec-value(IO, opened-file(Name, Contents?, Path?, TimeStamp?)) .
      snd-msg(opened-file(Name, Contents, Path, TimeStamp)) 
    +
      rec-value(IO, error-opening(Name)).
      snd-msg(error-opening(Name))
    )
  +
    rec-msg(open-eqs-text-file(Name?)).
    snd-eval(IO,open-eqs-text-file(Name)).
    (
      rec-value(IO,opened-file(Name,text(EqsText?),Path?,TimeStamp?)).
      snd-msg(opened-file(Name,EqsText,Path,TimeStamp)) 
      +
      rec-value(IO,error-opening(Name)).
      snd-msg(error-opening(Name))
    )
  +
    rec-msg(save-sdf2-asfix(Name?,Path?,Asfix?)).
    snd-eval(IO,save-sdf2-asfix(Name,Path,Asfix)).
    rec-value(IO,save-done(Name?)).
    snd-msg(save-done)
  +
    rec-msg(save-eqs-asfix(Name?,Path?,Asfix?)).
    snd-eval(IO,save-eqs-asfix(Name,Path,Asfix)).
    rec-value(IO,save-done(Name?)).
    snd-msg(save-done)
  )*
  delta
endlet

tool in-output is { command = "in-output" }
