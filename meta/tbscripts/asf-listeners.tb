#ifndef __ASF_LISTENERS__
#define __ASF_LISTENERS__

#include <asf-namespace.h>
#include <asf-utils.tb>

process ASFModuleOpener is
let
  Event: attribute-changed-event,
  ModuleId: term,
  NewPath: term
in
  MM-Subscribe-Attribute-Changed(<term>,SDF_NAMESPACE,"status",<term>,opened)  .
  (
    rec-note(mm-attribute-changed(Event?))
    . ACE-GetModuleId(Event, ModuleId?)
    . OpenASFModule(ModuleId)
  )
  *
  delta
endlet

toolbus(ASFModuleOpener)

process ASFModuleChecker is
let
  Event: attribute-changed-event,
  ModuleId: term,
  NewPath: term,
  Tree: term
in
  MM-Subscribe-Attribute-Changed(<term>,ASF_NAMESPACE,"status",available,parsed)  
  .
  (
    rec-note(mm-attribute-changed(Event?))
    . ACE-GetModuleId(Event, ModuleId?)
    . CheckASFModule(ModuleId)
  )
  *
  delta
endlet

toolbus(ASFModuleChecker)

process ASFModuleParser is
let
  Event: attribute-changed-event,
  ModuleId: term,
  Tree: term
in
  MM-Subscribe-Attribute-Changed(<term>,ASF_NAMESPACE,"status",<term>,available)
  .
  (
    rec-note(mm-attribute-changed(Event?))
    . printf("ASF module available for parsing\n")
    . ACE-GetModuleId(Event, ModuleId?)
    . GetASFParseTree(ModuleId, Tree?)
    . Tree := help-garbage-collector
  )
  *
  delta
endlet

/* TODO: add syntax checker */

toolbus(ASFModuleParser)

#endif /* __ASF_LISTENERS__ */
