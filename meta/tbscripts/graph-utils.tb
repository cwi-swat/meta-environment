#ifndef __GRAPH_UTILS__
#define __GRAPH_UTILS__

#define GRAPH_NAMESPACE "graph"

#include <idle-state.tb>
#include <layout-graph.idef>
#include <graph-converter.idef>
#include <graph-painter.idef>

process GraphCleanup(GraphType: str, GraphId: term, Sid: term) is
  GP-AddCloseListener
  . rec-note(gp-panel-closed(GraphType, GraphId)) 
  . snd-msg(em-delete-session(Sid)) 

process RenderGraph(GraphType: str, GraphId: term, Shared: bool, 
                   Closable: bool, Graph: term?) is
let
  Sid: term,
  PanelId: term,
  Id: int
in
  AddJob("Rendering")
  .
  if equal(Shared, false) then
    snd-msg(em-create-session)
    . rec-msg(em-session(Sid?))
    . PanelId := Sid
    . create(GraphCleanup(GraphType, GraphId, Sid), Id?)
  else
    PanelId := GraphId
  fi
  . snd-msg(gp-create-panel(GraphType, PanelId, Shared, Closable))
  . rec-msg(gp-panel-created(GraphType, PanelId))
  . snd-msg(gp-render-graph(GraphType, PanelId, Graph))
  . rec-msg(gp-rendered-graph(GraphType, PanelId, Graph?))
  . snd-msg(gl-layout-graph(Graph))
  . rec-msg(gl-graph(Graph?))
  . snd-msg(gp-display-graph(GraphType, PanelId, Graph))
  . JobDone("Rendering")
endlet

process ShowTree(Tree: term, ShowLeafs: term, 
                 SharingOn: term) is
let
  Graph: term
in
  snd-msg(gc-tree-to-graph(Tree, ShowLeafs, SharingOn))
  . rec-msg(gc-graph(Graph?))
  . RenderGraph("parsetree","0",false,true,Graph?)
endlet


#endif /*__GRAPH_UTILS__*/
