/*
  $Id$
 */
process Save-modules is
let
  Id           : term,
  Syntax       : term,
  Eqs          : term,
  Snapshot     : term,
  Module       : term,
  Path         : str,
  Modules      : list,
  Table        : term,
  Text         : str,
  ErrorMessage : str,
  FileName     : str,
  Extension    : str
in
  Id := process-id. 
  (
    rec-msg(save-sdf-module(Module?)).
    snd-note(ui-status(statf(Id, "Saving %s",[Module]))) .
    snd-msg(get-path-from-db(Module)).
    (
      rec-msg(path(Path?)).
      snd-msg(file-extension-hook(sdf)) .
      rec-msg(file-extension-hook-result(Extension?)) .
      snd-msg(io-get-filename(Path, Module, Extension)) .
      rec-msg(io-filename(FileName?)) .
      snd-msg(get-sdf2-asfix(Module)).
      (
        rec-msg(syntax(Syntax?)) .
        snd-msg(unparse(Syntax)) .
        rec-msg(unparsed-text(Text?)) .
        snd-msg(io-write-text-file(FileName, [Text])) .
        (
          rec-msg(io-file-written)
        +
          rec-msg(io-file-not-written(ErrorMessage?)) .
	  snd-note(ui-status(errorf("Saving failed: %s", [ErrorMessage])))
        )
      )
    +
      rec-msg(no-path)
    ) .
    snd-note(ui-status(endstat(Id))) .
    snd-msg(saved-sdf-module(Module))
  +
    rec-msg(save-asfsdf-module(Module?)).
    snd-note(ui-status(statf(Id, "Saving %s",[Module]))) .
    snd-msg(get-path-from-db(Module)).
    (
      rec-msg(path(Path?)).
      snd-msg(file-extension-hook(sdf)) .
      rec-msg(file-extension-hook-result(Extension?)) .
      snd-msg(io-get-filename(Path, Module, Extension)) .
      rec-msg(io-filename(FileName?)) .
      snd-msg(get-sdf2-asfix(Module)).
      (
        rec-msg(syntax(Syntax?)) .
        snd-msg(unparse(Syntax)) .
        rec-msg(unparsed-text(Text?)) .
        snd-msg(io-write-text-file(FileName, [Text])) .
        (
          rec-msg(io-file-written) 
        +
          rec-msg(io-file-not-written(ErrorMessage?)) .
	  snd-note(ui-status(errorf("Saving failed: %s", [ErrorMessage])))
        )
      ) .
      snd-msg(get-eqs-text(Module)).
      (
        rec-msg(eqs-text(Module, Text?)) .
        snd-msg(file-extension-hook(asf)) .
        rec-msg(file-extension-hook-result(Extension?)) .
        snd-msg(io-get-filename(Path, Module, Extension)) .
        rec-msg(io-filename(FileName?)) .
        snd-msg(io-write-text-file(FileName, [Text])) .
        (
          rec-msg(io-file-written) 
        +
          rec-msg(io-file-not-written(ErrorMessage?)) .
          snd-note(ui-status(errorf("Saving failed: %s", [ErrorMessage])))
        )
      +
        rec-msg(no-eqs-text(Module))
      )
    +
      rec-msg(no-path)
    ) .
    snd-note(ui-status(endstat(Id))) .
    snd-msg(saved-asfsdf-module(Module))
  +
    rec-msg(save-all) .
    snd-msg(ts-get-snapshot) .
    rec-msg(ts-snapshot(Snapshot?)) .
    snd-msg(io-write-packed-term-file("Meta.snapshot", Snapshot)) .
    (
      rec-msg(io-file-written) 
    +
      rec-msg(io-file-not-written(ErrorMessage?)) .
      snd-note(ui-status(errorf("Saving failed: %s", [ErrorMessage])))
    ) .
    snd-msg(saved)
  )*
  delta
endlet
