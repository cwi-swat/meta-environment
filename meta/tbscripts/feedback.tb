/* $Id$ */

process GoToEditorLocation(EditorId : term, Location : term) is
let
  Offset : int
in
  snd-msg(get-location-offset(Location)) .
  (
    rec-msg(location-offset(Offset?)) .
    snd-msg(te-set-cursor-at-offset(EditorId, Offset)) .
    snd-msg(te-set-focus-at-location(EditorId, Location)) 
  +
    rec-msg(no-location-offset)
  )
endlet

process ShowLocation(Location : term) is
let
  EditorId : term,
  ModuleName : str,
  FileName : str
in
  snd-msg(get-location-filename(Location)) .
  (
    rec-msg(location-filename(FileName?)) .
    snd-msg(edit-general(FileName)) .
    rec-msg(editor-opened(ModuleName?, EditorId?)) .
    GoToEditorLocation(EditorId, Location)
  +
    rec-msg(no-location-filename)
  )
endlet

process ShowFeedbackLocation(Feedback : term) is
let
  Location : term
in
  snd-msg(get-feedback-first-location(Feedback)) .
  rec-msg(feedback-first-location(Location?)) .
  ShowLocation(Location)
endlet
