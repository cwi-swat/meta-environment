/* this file deals with errorapi terms, they are apified, but of course
 * not in ToolBus... where should this decomposition be done??? 
 *
 * Some changes in the editor interface might make it possible to get
 * rid of most of this file
 *
 * Known problems: the errorapi does not know the language of the file
 * that the error occurred in. That means that an editor is opened over
 * the "unknown language" with obvious disadvantages
 */

process ShowFeedbackLocation(Feedback : term) is
let
  Producer : str,
  Description : str,
  Subjects : list,
  FirstSubject : term
in
  Producer := first(args(Feedback)) .
  Description := first(next(args(Feedback))) .
  Subjects := first(next(next(args(Feedback)))) .

  FirstSubject := first(Subjects) .
  ShowSubjectLocation(FirstSubject)
endlet

process ShowSubjectLocation(Subject : term) is
let
  Description : str,
  Location : term,
  Filename : str
in
  Description := first(args(Subject)) .
  Location := first(next(args(Subject))) .
  if not(equal(Location,no-location)) then
    Filename := first(args(Location)) .
    ShowLocation(Filename, Location)
  else
     snd-note(ui-status(messagef("Subject %s has no source location", [Description])))
  fi
endlet

process ShowLocation(Filename : str, Location : term) is
let
  EditorId : term,
  ModuleName : str
in
  snd-msg(edit-general(Filename)) .
  rec-msg(editor-opened(ModuleName?, EditorId?)) .
  snd-msg(set-cursor-at-error-location(EditorId, Location)) .
  snd-msg(set-focus-at-error-location(EditorId, Location)) 
endlet  
