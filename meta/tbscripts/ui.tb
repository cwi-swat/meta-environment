/* $Id$
 */

tool user-interface is {
  command = "graph-browser"
}

process Ui is
let
SD    : int,
Om    : int,
EM    : int,
ET    : int,
UI    : user-interface,
Id    : term,
Name  : term,
Str   : term,
Str1  : term,
Graph : term,
Mod   : term,
Pairs : list,
ModName : str,
T       : str,
FT      : str,
Path    : str
in
  Id := process-id.
  subscribe(import-relations(<term>)).
  execute(user-interface,UI?).
  create(Status-display(UI),SD?).
  ( 
    /* Add a new module by a user via the user-interface */
    ( rec-event(UI, open-module(Str?)).
      snd-msg(open-modules([Str])).
      ( rec-msg(ui,open-module(Str1?)).
        snd-do(UI,open-module(Str1))
      + rec-msg(ui,imports(Name?,Str1?)).
        snd-do(UI,imports(Name,Str1))
      + rec-note(import-relations(irels(Pairs?))).
        snd-do(UI,add-imports(Pairs))
      + rec-msg(ui,cannot-open-module(ModName?)).
	snd-note(ui-status(errorf("cannot open module \"%s\"",[ModName])))
      ) *
      rec-msg(ui,finished-opening-modules).
      snd-do(UI,finished-opening-modules(Str)).
      snd-ack-event(UI, open-module(Str))
    ) +
    ( rec-event(UI,delete-module(Str?)).
      snd-msg(close-module(Str)).
      rec-msg(closed-module(Str)).
      snd-do(UI,delete-module(Str)) .
      snd-ack-event(UI, delete-module(Str))
    ) +
    ( rec-event(UI,save-module(Str?)).
      snd-msg(save-module(Str)).
      rec-msg(saved-module(Str)).
      snd-ack-event(UI,save-module(Str))
    ) +
    ( rec-event(UI,edit-module(Str?)).
      create(Edit-module(Str),EM?).
      snd-ack-event(UI,edit-module(Str))
    ) +
    ( rec-event(UI,revert-module(id(ModName?))).            /* Not yet implemented */ 
      snd-note(ui-status(errorf("TB: not implemented: revert-module(%s)",[ModName]))).
      snd-ack-event(UI,revert-module(id(ModName)))
    ) +
    ( rec-event(UI,get-module-info(Str?)).          /* Not yet implemented */ 
      snd-do(UI,module-info(Str,[["sorry","not implemented"],
          ["example",""],["dir","/home/leon/asf+sdf/spec/"],["size","10kb"]])).
      snd-ack-event(UI,get-module-info(Str))
    ) +
    ( rec-event(UI,save-all).                 
      snd-msg(save-all).
      rec-msg(saved).
      snd-ack-event(UI,save-all)
    ) +
    ( rec-event(UI,revert-all).                     /* Not yet implemented */
      snd-note(ui-status(error("TB: not implemented: revert-all"))).
      snd-ack-event(UI,revert-all)
    ) +
    ( rec-event(UI,clear-all).
      snd-msg(clear-all).
      rec-msg(cleared-all).
      snd-ack-event(UI,clear-all)
   ) +
   ( rec-event(UI,compile-all).
     snd-note(ui-status(stat(Id,"Compiling ..."))).
     snd-msg(com,compile-all).
     rec-msg(com,done).
     snd-note(ui-status(endstat(Id))). 
     snd-ack-event(UI,compile-all)
   ) +
   ( rec-event(UI,compile-module(Str?)).
     snd-note(ui-status(stat(Id,"Compiling ..."))).
     snd-msg(compile-module(Str)).
     rec-msg(compile-done).
     snd-note(ui-status(endstat(Id))).
     snd-ack-event(UI,compile-module(Str))
   ) +
   ( rec-event(UI,parse-equations(Str?)).
     snd-msg(process-eqs(Str)).
     rec-msg(eqs-processed(Str1?)).
     snd-ack-event(UI,parse-equations(Str))
   ) +
   ( rec-event(UI,edit-term(Mod?,Str?)).
     create(Edit-term(Mod,Str),ET?).
     snd-ack-event(UI,edit-term(Mod,Str))
   ) 
  ) *
  rec-event(UI,button(quit)).
  shutdown("MSM exiting")
endlet
