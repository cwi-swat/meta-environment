/*
  $Id$
 */

/** Name -- The name of the module over which we edit as a term: id(RealName)
    Text -- The string we are editing
    Filename -- The filename where the string lives
    Module -- Same as Name above, but as a string (without id(..))
              These obviously need to be integrated
*/
process Editor(Name: term, Text: str, Filename: str, Module: str) is
let
  TE  : text-edit,
  Tid : int,
  L   : str,
  Fid : str,
  Id  : int
in
/*
   Changed concept. No longer one editor executable per window. This was
   never a good idea.  Now, for each window an Editor process is created.
   The editor binary is executed from the Edit-master process, which knows
   about its tool id.
*/
  Id := process-id.
  snd-note(ui-status(stat(Id,"Starting text editor"))).
  snd-msg(get-editor-tool-id).
  rec-msg(editor-tool-id(TE?)).
  snd-eval(TE,tb-init(Filename)).
  rec-value(TE,file-id(Fid?)).
  snd-do(TE,tb-add-menu-item("Meta-Environment", "Parse")).
  snd-do(TE,tb-add-menu-item("Meta-Environment", "Reduce")).
/*  snd-do(TE,tb-add-menu-item("Meta-Environment", "Show Tree")). */
  snd-do(TE,tb-add-menu-item("Move", "Left")).
  snd-do(TE,tb-add-menu-item("Move", "Right")).
  snd-do(TE,tb-add-menu-item("Move", "Up")).
  snd-do(TE,tb-add-menu-item("Move", "Down")).
  snd-note(ui-status(endstat(Id))).
  create(Structure-editor(TE, Fid, Text, Module), Tid?).
  create(Focus(TE, Fid), Tid?). (
    rec-note(language-available(L?)).
    snd-do(TE,registerlang(L))
  ) *
  rec-note(disconnected(TE))
endlet
