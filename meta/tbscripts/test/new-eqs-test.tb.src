#include <in-output.idef>
#include <module-db.idef>
#include <file-creator.tb>

process Test is
let
  SdfTree : term,
  Path    : str,
  Time    : term,
  ErrMsg: str
in
  snd-msg(process-search-paths("meta.conf", ["__SRCDIR__","__BUILDDIR__"])) .
  rec-msg(search-paths-processed("meta.conf")) .
  snd-msg(open-file("Empty")) .
  (
    rec-msg(opened-file("Empty", tree(SdfTree?), Path?, Time?))
  +
    shutdown("new-eqs-test FAILED! (timeout in read)\n") delay(sec(5)) 
  ) .

  %% Update the module-db.
  snd-msg(add-sdf2-module-to-db("Empty", Path, SdfTree, Time, "unchanged")) .
  (
    rec-msg(need-modules([]))
  +
    shutdown("new-eqs-test FAILED! (timeout in update-db)\n") delay(sec(5))
  ) .

  snd-msg(create-new-equations("Empty")) .
  (
    rec-msg(new-equations-created("Empty", Path?))
  +
    rec-msg(new-equations-not-created(ErrMsg?)) .
    printf("new-eqs-test FAILED! (%s)\n", ErrMsg)
  +
    shutdown("new-eqs-test FAILED! (timeout)\n") delay(sec(5))
  ) .
  shutdown("") delay(sec(2))
endlet

toolbus(Test, Io, ModuleDB, File-creator)
