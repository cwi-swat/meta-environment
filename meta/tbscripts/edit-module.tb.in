/*
  $Id$
 */
process Edit-module(Module: str) is
let
  Id     : term,
  T      : term,
  Tid    : int,
  Fn     : str,
  Mname  : str,
  TE     : text-edit,
  Path   : str,
  Syntax : term,
  Time   : term,
  Src    : str
in
  Id := process-id.
  snd-note(ui-status(stat(Id,"Opening module editor..."))). 
  snd-msg(get-sdf2-path-from-db(Module)).
  rec-msg(path(Path?)).
  snd-msg(get-sdf2-asfix(Module)).
  (
    rec-msg(syntax(Syntax?)).
    snd-msg(pp(Syntax)).
    rec-msg(source(Src?))
  +
    rec-msg(syntax-unchanged(Syntax?)).
    snd-msg(pp(Syntax)).
    rec-msg(source(Src?))
  +
    rec-msg(unavailable).
    snd-msg(Id, open-file(Module)).
    (
      rec-msg(Id, opened-file(raw,Mname?,Src?,Path?,Time?))
    +
      rec-msg(Id, error-opening(Path?)).
      Src := ""
    )
  ).
  create(Editor("Sdf2",Src,Path,"Sdf2","sdf"), Tid?). 
  snd-msg(open-language("Sdf2", 
                        "@datadir@/Sdf2.baf")).
  snd-note(ui-status(endstat(Id)))
endlet

process Edit-eqs-module(Module: str) is
let
  Id     : term,
  T      : term,
  Tid    : int,
  Fn     : str,
  TE     : text-edit,
  Path   : str,
  Eqs    : term,
  Time   : term,
  Mname  : str,
  Src    : str
in
  Id := process-id.
  snd-note(ui-status(stat(Id,"Opening module editor..."))).
  snd-msg(set-parsetable(Module)).
  rec-msg(parsetable-set(Module)).
  snd-msg(get-eqs-path-from-db(Module)).
  rec-msg(path(Path?)).
  snd-msg(get-eqs-asfix(Module)).
  (
    rec-msg(eqs(Eqs?)).
    snd-msg(pp(Eqs)).
    rec-msg(source(Src?))
  +
    rec-msg(eqs-unchanged(Eqs?)).
    snd-msg(pp(Eqs)).
    rec-msg(source(Src?))
  +
    rec-msg(no-eqs).
    Src := ""
  +
    rec-msg(unavailable).
    snd-msg(Id, open-eqs-text-file(Module)).
    (
      rec-msg(Id, opened-file(raw,Mname?,Src?,Path?,Time?))
    +
      rec-msg(Id, error-opening(Path?)).
      Src := ""
    )
  ).
  create(Editor(Module,Src,Path,Module,"eqs"), Tid?). 
  snd-note(ui-status(endstat(Id)))
endlet
