process PrintModule is
let
  Id: term,
  SdfText: str,
  AsfText: str,
  AsfTree: term,
  ModuleId: term,
  Modulename: str,
  Path: str,
  Filename: str,
  ErrMsg: str
in
  Id := process-id
  .
  (
    rec-msg(print-module(ModuleId?, Filename?))
    . GetModulePath(ModuleId, SDF_NAMESPACE, Path?)
    . snd-msg(ts-get-str-value("sdf-text", ModuleId))
    .
    (
      rec-msg(ts-value("sdf-text", ModuleId, SdfText?))
      . snd-msg(ts-get-term-value("asf-tree", ModuleId))
      .
      (
        rec-msg(ts-value("asf-tree", ModuleId, AsfTree?))
        . snd-msg(unparse(AsfTree))
        . rec-msg(unparsed-text(AsfText?))
        . snd-msg(io-write-text-list(Filename, [SdfText, "\n", AsfText]))
      +
        rec-msg(ts-no-value("asf-tree", ModuleId))
        . snd-msg(io-write-text-list(Filename, [SdfText]))
      )
      .
      (
        rec-msg(io-file-written)
      +
        rec-msg(io-file-not-written(ErrMsg?))
        . snd-note(ui-status(errorf("Printing failed: %s",
                                    [ErrMsg])))
      )
    +
      rec-msg(ts-no-value("sdf-text", ModuleId))
      . snd-note(ui-status(errorf("Printing failed")))
    )
    . snd-msg(module-printed(ModuleId))
  )
  *
  delta
endlet
