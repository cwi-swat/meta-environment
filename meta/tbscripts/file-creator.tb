/*
 * $Id$
 */

process File-creator is
let
  FileName   : str,
  ModuleName : str,
  RealModule : str,
  ErrMsg     : str,
  Path       : str,
  Text       : str
in
  (
    %% Handle request to create a new module.
    rec-msg(create-new-module(Path?, ModuleName?)) .
    snd-msg(is-module-in-db(ModuleName)) .
    (
      rec-msg(exists-in-module-db(ModuleName)) .
      snd-note(ui-status(errorf("Module %s already exists", 
                                [ModuleName])))
    +
      rec-msg(not-exists-in-module-db(ModuleName)) .
      snd-msg(exists-file(Path, ".sdf")) .
      (
        rec-msg(file-exists) .
        snd-note(ui-status(
	errorf("Module %s already exists on disk", [ModuleName])))
      +
        rec-msg(file-not-exists) .
        snd-msg(write-text-file(Path, ".sdf", 
	                        ["module ", ModuleName])) .
        (
          rec-msg(file-written) .
	  snd-msg(open-initial-module(Path, ModuleName))
        +
          rec-msg(file-not-written(ErrMsg?)) .
	  snd-note(ui-status(errorf("%s", [ErrMsg])))
        )
      )
    )
  +
    %% Handle request to create a new rules section.
    rec-msg(create-new-equations(ModuleName?)) .
    snd-msg(is-module-in-db(ModuleName)) .
    (
      rec-msg(not-exists-in-module-db(ModuleName)) 
    +
      rec-msg(exists-in-module-db(ModuleName)) .
      snd-msg(get-path-from-db(ModuleName)) .
      rec-msg(path(Path?)) .
      snd-msg(exists-file(Path, ".asf")) .
      (
        rec-msg(file-exists) .
	snd-msg(open-file(Path, ".asf")) .
	rec-msg(opened-file(text(Text?))) .
	snd-msg(add-eqs-to-db(ModuleName, text(Text))) .
	rec-msg(eqs-added) .
	snd-msg(new-equations-created)
      +
        rec-msg(file-not-exists) .
	snd-msg(write-text-file(Path, ".asf", [])) .
        (
          rec-msg(file-written) .
	  snd-msg(open-file(Path, ".asf")) .
	  rec-msg(opened-file(text(Text?))) .
          snd-msg(add-eqs-to-db(ModuleName, text(Text))) .
          rec-msg(eqs-added) .
          snd-msg(new-equations-created)
        +
          rec-msg(file-not-written(ErrMsg?)) . 
	  snd-note(ui-status(errorf("%s", ErrMsg))) .
          snd-msg(new-equations-not-created)
        )
      )
    )
  ) * delta
endlet
