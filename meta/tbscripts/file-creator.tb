/*
 * $Id$
 */

process File-creator is
let
  FileName   : str,
  ModuleName : str,
  RealModule : str,
  ErrMsg     : str,
  Path       : str,
  Text       : str
in
  (
    %% Handle request to create a new module.
    rec-msg(create-new-module(Path?, ModuleName?)) .
    snd-msg(is-module-in-db(ModuleName)) .
    (
      rec-msg(exists-in-module-db(ModuleName)) .
      snd-note(ui-status(errorf("Module %s already exists", 
                                [ModuleName])))
    +
      rec-msg(not-exists-in-module-db(ModuleName)) .
      snd-msg(io-get-filename(Path, ModuleName, ".sdf")) .
      rec-msg(io-filename(FileName?)) .
      snd-msg(io-exists-file(FileName)) .
      (
        rec-msg(io-file-exists) .
        snd-note(ui-status(
	errorf("Module %s already exists on disk", [ModuleName])))
      +
        rec-msg(io-file-not-exists) .
        snd-msg(io-write-text-file(FileName, ["module ", ModuleName, "\n"])) .
        (
          rec-msg(io-file-written) .
	  snd-msg(open-initial-module(Path, ModuleName, ".sdf"))
        +
          rec-msg(io-file-not-written(ErrMsg?)) .
	  snd-note(ui-status(errorf("%s: %s", [FileName, ErrMsg])))
        )
      )
    )
  +
    %% Handle request to create a new rules section.
    rec-msg(create-new-equations(ModuleName?)) .
    snd-msg(is-module-in-db(ModuleName)) .
    (
      rec-msg(not-exists-in-module-db(ModuleName)) 
    +
      rec-msg(exists-in-module-db(ModuleName)) .
      snd-msg(get-path-from-db(ModuleName)) .
      rec-msg(path(Path?)) .
      snd-msg(io-get-filename(Path, ModuleName, ".sdf")) .
      rec-msg(io-filename(FileName?)) .
      snd-msg(io-exists-file(FileName)) .
      (
        rec-msg(io-file-exists) .
	snd-msg(io-read-file(FileName)) .
	rec-msg(io-file-contents(text(Text?))) .
	snd-msg(add-eqs-to-db(ModuleName, text(Text))) .
	rec-msg(eqs-added) .
	snd-msg(new-equations-created)
      +
        rec-msg(io-file-not-exists) .
	snd-msg(io-write-text-file(FileName, [])) .
        (
          rec-msg(io-file-written) .
	  snd-msg(io-read-file(FileName)) .
	  (
	    rec-msg(io-file-contents(Text?)) .
            snd-msg(add-eqs-to-db(ModuleName, text(Text))) .
            rec-msg(eqs-added) .
            snd-msg(new-equations-created)
          +
	    rec-msg(io-error-reading(ErrMsg?)) .
	    snd-note(ui-status(errorf("%s: %s", [FileName,ErrMsg]))) .
            snd-msg(new-equations-not-created)
          )
        +
          rec-msg(io-file-not-written(ErrMsg?)) . 
	  snd-note(ui-status(errorf("%s: %s", [FileName,ErrMsg]))) .
          snd-msg(new-equations-not-created)
        )
      )
    )
  ) * delta
endlet
