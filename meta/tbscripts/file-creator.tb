/*
 * $Id$
 */

process File-creator is
let
  FileName   : str,
  ModuleName : str,
  ErrMsg     : str,
  Path       : str
in
  (
    %% Handle request to create a new module.
    rec-msg(create-new-module(Path?, ModuleName?)) .
    snd-msg(is-module-in-db(ModuleName)) .
    (
      rec-msg(exists-in-module-db(ModuleName)) .
      snd-msg(module-not-created("Module already in database"))
    +
      rec-msg(not-exists-in-module-db(ModuleName)) .
      snd-msg(exists-sdf-module(Path)) .
      (
        rec-msg(sdf-module-exists(Path)) .
        snd-msg(module-not-created("Module already exists in text or baf"))
      +
        rec-msg(sdf-module-not-exists(Path)) .
        snd-msg(create-empty-sdf-module(Path, ModuleName)) .
        (
          rec-msg(module-created(ModuleName)) .
      	  snd-msg(module-created)
        +
          rec-msg(module-creation-failed(ModuleName, ErrMsg?)) .
          snd-msg(module-not-created(ErrMsg))
        )
      )
    )
  +
    %% Handle request to create a new equations section.
    rec-msg(create-new-equations(ModuleName?)) .
    snd-msg(is-module-in-db(ModuleName)) .
    (
      rec-msg(not-exists-in-module-db(ModuleName)) .
      snd-msg(equations-not-created("Module not in database"))
    +
      rec-msg(exists-in-module-db(ModuleName)) .
	  snd-msg(get-eqs-path-from-db(ModuleName)) .
	  rec-msg(path(FileName?)) .
      snd-msg(exists-asf-section(ModuleName, FileName)) .
      (
        rec-msg(asf-section-exists(ModuleName)) .
        snd-msg(equations-not-created("Equations section already exists."))
      +
        rec-msg(asf-section-not-exists(ModuleName)) .
        snd-msg(create-empty-equations(ModuleName, FileName)) .
        (
          rec-msg(empty-equations-created(FileName)) .
          snd-msg(add-empty-eqs-to-db(ModuleName, FileName)) .
          rec-msg(empty-eqs-added) .
          snd-msg(new-equations-created(ModuleName, FileName))
        +
          rec-msg(empty-equations-not-created(ModuleName, ErrMsg?)) .
          snd-msg(new-equations-not-created(ErrMsg))
        )
      )
    )
  ) * delta
endlet
