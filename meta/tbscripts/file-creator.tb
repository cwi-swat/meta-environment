/*

    Meta-Environment - An environment for language prototyping.
    Copyright (C) 2000  Stichting Mathematisch Centrum, Amsterdam, The Netherlands. 

    This program is free software; you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation; either version 2 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program; if not, write to the Free Software
    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307 USA

*/
/*
 * $Id$
 */

process File-creator is
let
  FileName : str,
  ModuleName: str,
  ErrMsg: str
in
  (
    %% Handle request to create a new module.
    rec-msg(create-new-module(ModuleName?)) .
    snd-msg(is-module-in-db(ModuleName)) .
    (
      rec-msg(exists-in-module-db(ModuleName)) .
      snd-msg(module-not-created("Module already in database"))
    +
      rec-msg(not-exists-in-module-db(ModuleName)) .
      snd-msg(exists-sdf2-module(ModuleName)) .
      (
        rec-msg(sdf2-module-exists(ModuleName)) .
        snd-msg(module-not-created("Module already exists in text or baf"))
      +
        rec-msg(sdf2-module-not-exists(ModuleName)) .
        snd-msg(create-empty-sdf2-module(ModuleName)) .
        (
	  rec-msg(module-created(ModuleName)) .
      	  snd-msg(module-created)
        +
          rec-msg(module-creation-failed(ModuleName, ErrMsg?)) .
	  snd-msg(module-not-created(ErrMsg))
        )
      )
    )
  +
    %% Handle request to create a new equations section.
    rec-msg(create-new-equations(ModuleName?)) .
    snd-msg(is-module-in-db(ModuleName)) .
    (
      rec-msg(not-exists-in-module-db(ModuleName)) .
      snd-msg(equations-not-created("Module not in database"))
    +
      rec-msg(exists-in-module-db(ModuleName)) .
      snd-msg(exists-eqs-section(ModuleName)) .
      (
        rec-msg(eqs-section-exists(ModuleName)) .
	snd-msg(equations-not-created("Equations section already exists."))
      +
        rec-msg(eqs-section-not-exists(ModuleName)) .
	snd-msg(create-empty-equations(ModuleName)) .
	(
	  rec-msg(empty-equations-created(FileName?)) .
          snd-msg(add-empty-eqs-to-db(ModuleName, FileName)) .
          rec-msg(empty-eqs-added) .
	  snd-msg(new-equations-created(ModuleName, FileName))
	+
	  rec-msg(empty-equations-not-created(ModuleName, ErrMsg?)) .
	  snd-msg(new-equations-not-created(ErrMsg))
	)
      )
    )
  ) * delta
endlet
