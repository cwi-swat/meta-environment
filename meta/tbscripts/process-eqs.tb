/*
  $Id$
 */
process ProcessEqs is
let
  Id      : term,
  Mod     : term,
  Module  : term,
  Modules : list,
  Mname   : str,
  Table   : str,
  Path    : str,
  FT      : str,
  I       : int,
  T1      : term,
  T2      : term,
  Ch      : int,
  Li      : int,
  Co      : int,
  Char    : term,
  Time1   : term,
  Time2   : term,
  ErrList : list,
  Cycle   : list
in
  Id := process-id.
  ( rec-msg(process-eqs(Mod?)).
    snd-msg(update-eqs-for-modules(Mod)).
    rec-msg(eqs-for-modules(Modules?)).
    ( if not-equal(Modules, []) then
       Module  := first(Modules).
       Modules := next(Modules).
       snd-msg(Id,open-eqs-text-file(Module)).
       ( rec-msg(Id, opened-file(raw,id(Mname?),FT?,Path?,Time1?)).
         snd-msg(get-table-from-db(Module)).
         ( rec-msg(table(Table?)).
           snd-note(ui-status(endstat(Id)))
         +
           rec-msg(no-table).
           snd-note(ui-status(endstat(Id))).
           snd-msg(generate-parsetable(id(Mname))).
           rec-msg(parsetable(Table?)).
           snd-msg(Id,locate-parse-table-file(id(Mname))).
           rec-msg(Id,located-parse-table-file(id(Mname),Table?,Time2?)).
           snd-msg(add-table-to-db(id(Mname),Table,Time2)).
           rec-msg(done)
         ).
         snd-note(ui-status(stat(Id,"Parsing Equations ..."))).
         snd-msg(reopen-language(Mname,Table)).
         snd-msg(Id,parse-string-as-asfix1(Mname,"",FT)).
         ( rec-msg(parse-result-asfix1(Id?,parse-tree(T1?))).
           snd-note(ui-status(endstat(Id))). 
           snd-msg(add-eqs-to-db(id(Mname),Path,T1,Time1,"changed")).
           rec-msg(done) 
         + 
           rec-msg(parse-result-asfix1(Id?,parse-error(ErrList?,
                                                       ambiguities(I?)))).
           printf("Parse of %s returned %d ambiguities",Mname,I). 
           snd-msg(add-eqs-to-db(id(Mname),Path,error,Time1,"unchanged")).
           rec-msg(done)  
         + 
           rec-msg(parse-result-asfix1(Id?,parse-error(ErrList?,
                                                       cycle(Cycle?)))).
           printf("Parse returned a cycle\n").
           printf("Cycle is: %t\n",Cycle).
           rec-msg(done)  
         +
           rec-msg(parse-result-asfix1(Id?,parse-error(ErrList?,eof))). 
           printf("EOF unexpected while parsing %s\n",Mname).
           snd-msg(add-eqs-to-db(id(Mname),Path,error,Time1,"unchanged")).
           rec-msg(done) 
         +
           rec-msg(parse-result-asfix1(Id?,
                                       parse-error([character(Ch?),
                                                    line(Li?),
                                                    col(Co?),
                                                    char(Char?)],plain))).
           printf("Character %d unexpected in line %d, column %d\n",Ch,Li,Co).
           snd-msg(add-eqs-to-db(id(Mname),Path,error,Time1,"unchanged")).
           rec-msg(done) 
         ).
         snd-note(ui-status(endstat(Id)))
       +
       rec-msg(Id, error-opening(Mod?)).
       snd-msg(add-eqs-to-db(Module,"",no-equations,0,"unchanged")).
       rec-msg(done)
      )
      fi
    )*
    if equal(Modules,[]) then 
      snd-msg(eqs-processed(Mod))
    fi 
  )* delta
endlet
