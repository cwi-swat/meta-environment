/*

    Meta-Environment - An environment for language prototyping.
    Copyright (C) 2000  Stichting Mathematisch Centrum, Amsterdam, 
                        The Netherlands. 

    This program is free software; you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation; either version 2 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program; if not, write to the Free Software
    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307 USA

*/
/*
  $Id$
 */
process ProcessEqs is
let
  Id      : term,
  ModName : str,
  Module  : term,
  Modules : list,
  Table   : str,
  Path    : str,
  FT      : str,
  I       : int,
  T1      : term,
  T2      : term,
  Ch      : int,
  Li      : int,
  Co      : int,
  Char    : term,
  Time1   : term,
  Time2   : term,
  ErrList : list,
  Ambs    : list,
  Cycle   : list,
  EqsText : str,
  EqsTree : term
in
  Id := process-id.
  ( rec-msg(process-eqs(ModName?)).
    snd-msg(eqs-available-for-modules(ModName)).
    rec-msg(eqs-for-modules(Modules?)).
    ( if not-equal(Modules, []) then
       Module  := first(Modules).
       Modules := next(Modules).
       /* Get the text representation of the unparsed equations section. 
        */
       snd-msg(get-eqs-text(Module)).
       ( rec-msg(eqs-text(Module?, EqsText?)).
         snd-msg(set-parsetable(eqs(Module))).
         (
	   rec-msg(parsetable-set(eqs(Module))).
           snd-note(ui-status(stat(Id,"Parsing Equations ..."))).
           snd-msg(Id,parse-string-as-asfix1(eqs(Module), "Equations", EqsText)).
           ( rec-msg(parse-result-asfix1(Id?, parse-tree(EqsTree?))).
             snd-note(ui-status(endstat(Id))). 
             snd-msg(update-eqs-tree-in-db(Module, tree(EqsTree))) .
             rec-msg(eqs-updated)
           + 
             rec-msg(parse-result-asfix1(Id?,parse-error(ErrList?,
                                                         ambiguities(I?, Ambs?)))).
             snd-note(ui-status(errorf("Parse of %s returned %d ambiguities",
                                       [Module,I])))
           + 
             rec-msg(parse-result-asfix1(Id?,parse-error(ErrList?,
                                                         cycle(Cycle?)))).
             snd-note(ui-status(errorf("Parse returned a cycle: %t\n",
                                       [Cycle])))
           +
             rec-msg(parse-result-asfix1(Id?,parse-error(ErrList?,eof))). 
             snd-note(ui-status(errorf("EOF unexpected while parsing %s\n",
                                       [Module])))
           +
             rec-msg(parse-result-asfix1(Id?,
                                         parse-error([character(Ch?),
                                                      line(Li?),
                                                      col(Co?),
                                                      char(Char?)],plain))).
             snd-note(ui-status(errorf(
                      "Character %d unexpected in %s, line %d, column %d\n",
                      [Ch, Module, Li, Co])))
           ).
           snd-note(ui-status(endstat(Id)))
         +
	   rec-msg(parsetable-not-set(Module)).
	   snd-note(ui-status(errorf("Unable to open parse table for \"%s\"",
				     [Module])))
         )
      +
        rec-msg(no-eqs-text(Module))
      )
      fi
    )*
    if equal(Modules,[]) then 
      snd-msg(eqs-processed(ModName))
    fi 
  )* delta
endlet
