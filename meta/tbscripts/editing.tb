/* $Id$ */

/*{{{  process FindModuleFilename(Modulename: str, Type: term, Filename: str?) */

process FindModuleFilename(Modulename: str, Type: term, Filename: str?) is
let
  Extension: str,
  Path: str
in
  snd-msg(get-path-from-db(Modulename))
  . rec-msg(path(Path?))
  . snd-msg(file-extension-hook(Type))
  . rec-msg(file-extension-hook-result(Extension?))
  . snd-msg(io-get-filename(Path, Modulename, Extension))
  . rec-msg(io-filename(Filename?))
endlet

/*}}}  */

/*{{{  process GetFilename(Sid: term, Filename: str?) */ 
process GetFilename(Sid: term, Filename: str?) is
  snd-msg(em-get-filename(Sid))
  . rec-msg(em-filename(Sid, Filename?))

/*}}}  */
/*{{{  process GetModulename(Sid: term, Modulename: str?) */

process GetModulename(Sid: term, Modulename: str?) is
  snd-msg(em-get-modulename(Sid))
  . rec-msg(em-modulename(Sid, Modulename?))

/*}}}  */
/*{{{ process GetEditorType(Filename: str, EditorType: term?) */

process GetEditorType(Path: str, EditorType: term?) is
let
  Extension: str
in
  snd-msg(io-get-path-extension(Path)) .
  rec-msg(io-extension(Path, Extension?)) .
  if equal(Extension, ".sdf") then
    EditorType := syntax-editor
  else
    if equal(Extension, ".asf") then
      EditorType := equations-editor
    else
      EditorType := term-editor
    fi
  fi
endlet

/*}}}  */

/*{{{  process Edit(Filename: str, Modulename: str, Sid: term?) */

process Edit(Filename: str, Modulename: str, Sid: term?) is
  snd-msg(em-create-session(Filename, Modulename))
  . rec-msg(em-session(Filename, Modulename, Sid?))

/*}}}  */

/*{{{  process IsStructureEditorRegistered(Sid: term Registered: bool?) */

process IsStructureEditorRegistered(Sid: term, Registered: bool?) is
  snd-msg(em-is-editor-registered(Sid, structure))
  .
  (
    rec-msg(em-editor-registered(Sid, structure))
    . Registered := true
  +
    rec-msg(em-editor-not-registered(Sid, structure))
    . Registered := false
  )

/*}}}  */
/*{{{  process IsTextEditorRegistered(Sid: term Registered: bool?) */

process IsTextEditorRegistered(Sid: term, Registered: bool?) is
  snd-msg(em-is-editor-registered(Sid, text))
  .
  (
    rec-msg(em-editor-registered(Sid, text))
    . Registered := true
  +
    rec-msg(em-editor-not-registered(Sid, text))
    . Registered := false
  )

/*}}}  */

/*{{{  process EditText(Sid: term, Type: term) */

process EditText(Sid: term, Type: term) is
let
  Actions: list,
  Filename: str,
  Modulename: str,
  Registered: bool
in
  IsTextEditorRegistered(Sid, Registered?)
  .
  if equal(Registered, true)
  then
    snd-msg(te-editor-to-front(Sid))
  else
    snd-msg(em-register-editor(Sid, text))
    . GetModulename(Sid, Modulename?)
    . snd-msg(get-auxiliary-buttons(Type, Modulename))
    . rec-msg(button-list(Modulename, Actions?))
    . GetFilename(Sid, Filename?)
    . snd-msg(te-edit-text(Sid, EDITOR, Filename, Actions))
    /*. snd-msg(te-set-actions(Sid, Actions))*/
  fi
endlet

/*}}}  */
/*{{{  process EditStructure(Sid: term) */

process EditStructure(Sid: term, Tree: term) is
let
  Registered: bool
in
  IsStructureEditorRegistered(Sid, Registered?)
  .
  if equal(Registered, true)
  then
    snd-msg(se-delete(Sid))
  else
    snd-msg(em-register-editor(Sid, structure))
  fi
  . snd-msg(se-create(Sid, Tree))
endlet

/*}}}  */

/*{{{ process SynchronizeFocus(Sid: term) */

process SynchronizeFocus(Sid: term) is
let
  Focus: term,
  Sortname: str
in
  snd-msg(se-get-sort-at-cursor(Sid))
  . rec-msg(se-sort-at-cursor(Sid, Sortname?))
  . snd-msg(te-display-message(Sid, Sortname))
  . snd-msg(se-get-focus-at-cursor(Sid))
  . rec-msg(se-focus-at-cursor(Sid, Focus?))
  . snd-msg(te-set-focus(Sid, Focus))
endlet

/*}}}  */

/*{{{  process MenuSelected(Sid: term, EditorType: term) */

process MenuSelected(Sid: term, EditorType: term) is
let
  Actions: list,
  Args: list,
  Event: term,
  Modulename: str
in
  rec-msg(te-menu-event(Sid, Event?))
  . snd-msg(em-request-transaction(Sid))
  .
  (
    rec-msg(em-no-transaction(Sid))
  +
    rec-msg(em-transaction-started(Sid))
    . Args := []
    . GetModulename(Sid, Modulename?)
    . snd-msg(get-actions(Event, EditorType, Modulename))
    . rec-msg(actions(Event, EditorType, Modulename, Actions?))
    . snd-msg(perform-actions(Sid, Actions))
    . rec-msg(actions-performed(Sid, Actions))
    . snd-msg(em-end-transaction(Sid))
  )
endlet

/*}}}  */
/*{{{  process MouseClicked(Sid: term) */

process MouseClicked(Sid: term) is
let
  Offset: int,
  Registered: bool,
  Tid: term
in
  rec-msg(te-mouse-click-at-offset(Sid, Offset?))
  . snd-msg(em-request-transaction(Sid))
  .
  (
    rec-msg(em-no-transaction(Sid))
  +
    rec-msg(em-transaction-started(Sid))
    . IsStructureEditorRegistered(Sid, Registered?)
    .
    if equal(Registered, true)
    then
      snd-msg(se-set-cursor-at-offset(Sid, Offset))
      . SynchronizeFocus(Sid)
    else
      tau
    fi
    . snd-msg(em-end-transaction(Sid))
  )
endlet

/*}}}  */
/*{{{  process TextChanged(Sid: term) */

process TextChanged(Sid: term) is
let
  Registered: bool
in
  rec-msg(te-contents-changed(Sid))
  . snd-msg(em-request-transaction(Sid))
  .
  (
    rec-msg(em-no-transaction(Sid))
  +
    rec-msg(em-transaction-started(Sid))
    . IsStructureEditorRegistered(Sid, Registered?)
    .
    if equal(Registered, true)
    then
      DeleteStructureEditor(Sid)
      . snd-msg(te-clear-focus(Sid))
      . snd-msg(te-display-message(Sid,
	"Editor contents have changed, focus is unavailable until next parse."))
    else
      tau
    fi
    . snd-msg(em-end-transaction(Sid))
  )
endlet

/*}}}  */

/*{{{  process EditorDisconnected(Sid: term) */

process EditorDisconnected(Sid: term) is
  (
    rec-msg(te-text-editor-disconnected(Sid))
  +
    rec-msg(kill-editor(Sid))
  )

/*}}}  */

/*{{{  process DeleteTextEditor(Sid: term) */

process DeleteTextEditor(Sid: term) is
  snd-msg(te-kill-text-editor(Sid))
  . snd-msg(em-unregister-editor(Sid, text))

/*}}}  */
/*{{{  process DeleteStructureEditor(Sid: term) */

process DeleteStructureEditor(Sid: term) is
  snd-msg(se-delete(Sid))
  . snd-msg(em-unregister-editor(Sid, structure))

/*}}}  */
/*{{{  process DeleteSession(Sid: term) */

process DeleteSession(Sid: term) is
let
  Registered: bool
in
  IsTextEditorRegistered(Sid, Registered?)
  .
  if equal(Registered, true)
  then
    DeleteTextEditor(Sid)
  else
    tau
  fi

  . IsStructureEditorRegistered(Sid, Registered?)
  .
  if equal(Registered, true)
  then
    DeleteStructureEditor(Sid)
  else
    tau
  fi

  . snd-msg(em-delete-session(Sid))
endlet

/*}}}  */
/*{{{  process DeleteSessions(Modulename: str) */

process DeleteSessions(Modulename: str) is
let
  Sid: term,
  Sids: list
in
  snd-msg(em-get-sessions-by-modulename(Modulename))
  . rec-msg(em-sessions(Modulename, Sids?))
  . printf("session ids: %t\n", Sids)
  .
  (
    if not-equal(Sids, []) then
      Sid := first(Sids)
      . DeleteSession(Sid)
      . Sids := next(Sids)
    fi
  ) *
  if equal(Sids, []) then
    tau
  fi
endlet

/*}}}  */

/*{{{  process GetPendingChanges(Sid: term, HasChanges: bool?) */

process GetPendingChanges(Sid: term, HasChanges: bool?) is
let
  HasText: bool,
  HasStructure: bool
in
  IsTextEditorRegistered(Sid, HasText?)
  . IsStructureEditorRegistered(Sid, HasStructure?)
  . HasChanges := and(HasText, not(HasStructure))
endlet

/*}}}  */
/*{{{  process CommitPendingChanges(Modulename: str, HasChanges: bool) */

process CommitPendingChanges(Modulename: str, HasChanges: bool) is
  if equal(HasChanges, true) then
    snd-msg(revert-module(Modulename))
    . rec-msg(module-reverted(Modulename))
  else tau fi

/*}}}  */

/*{{{  process GetSyntaxTree(Modulename: str, Tree: term?, Available: bool?) */

process GetSyntaxTree(Modulename: str, Tree: term?, Available: bool?) is
  snd-msg(get-syntax-tree(Modulename))
  .
  (
    rec-msg(syntax(Tree?))
    . Available := true
  +
    rec-msg(unavailable)
    . Available := false
  )

/*}}}  */
/*{{{  process EditSyntax(Modulename: str) */

process EditSyntax(Modulename: str) is
let
  Available: bool,
  EditorType: term,
  Filename: str,
  HasChanges: bool,
  Sid: term,
  Tree: term
in
  EditorType := syntax-editor
  . FindModuleFilename(Modulename, sdf, Filename?)
  . Edit(Filename, Modulename, Sid?)
  . EditText(Sid, EditorType)
  . snd-msg(syntax-editor-started(Modulename, Sid))
  . GetSyntaxTree(Modulename, Tree?, Available?)
  .
  if equal(Available, true)
  then
    EditStructure(Sid, Tree)
  else
    tau
  fi
  .
  (
    MenuSelected(Sid, EditorType)
  +
    MouseClicked(Sid)
  +
    TextChanged(Sid)
  )
  *
  EditorDisconnected(Sid)
  . GetPendingChanges(Sid, HasChanges?)
  . DeleteSession(Sid)
  . CommitPendingChanges(Modulename, HasChanges)
endlet

/*}}}  */

/*{{{  process GetEquationsTree(Modulename: str, Tree: term?, Available: bool?) */

process GetEquationsTree(Modulename: str, Tree: term?, Available: bool?) is
  snd-msg(get-equations-for-module(Modulename))
  .
  (
    rec-msg(equations(Tree?))
    . Available := true
  +
    rec-msg(no-equations)
    . Available := false
  )

/*}}}  */
/*{{{ process EditEquations(Modulename: str) */

process EditEquations(Modulename: str) is
let
  Available: bool,
  EditorType: term,
  Filename: str,
  Sid: term,
  Tree: term
in
  EditorType := equations-editor
  . FindModuleFilename(Modulename, asf, Filename?)
  . Edit(Filename, Modulename, Sid?)
  . EditText(Sid, EditorType)
  . snd-msg(equations-editor-started(Modulename, Sid))
  . GetEquationsTree(Modulename, Tree?, Available?)
  .
  if equal(Available, true)
  then
    EditStructure(Sid, Tree)
  else
    tau
  fi
  .
  (
    MenuSelected(Sid, EditorType)
  +
    MouseClicked(Sid)
  +
    TextChanged(Sid)
  )
  *
  EditorDisconnected(Sid)
  . DeleteSession(Sid)
endlet

/*}}}  */

/*{{{  process GetTermTree(Filename: str, Tree: term?, Available: bool?) */

process GetTermTree(Filename: str, Tree: term?, Available: bool?) is
  snd-msg(get-term-tree(Filename))
  .
  (
    rec-msg(term-tree(Filename, Tree?))
    . Available := true
  +
    rec-msg(no-term-tree(Filename))
    . Available := false
  )

/*}}}  */
/*{{{  process CheckSanity(Filename: str, RequestedModulename: str, Valid: bool?) */

process CheckSanity(Filename: str, RequestedModulename: str, Valid: bool?) is
let
  CurrentModulename: str,
  Sid: term
in
  snd-msg(em-get-session-by-filename(Filename))
  .
  (
    rec-msg(em-session(Filename, Sid?))
    . snd-msg(em-get-modulename(Sid))
    . rec-msg(em-modulename(Sid, CurrentModulename?))
    . Valid := equal(CurrentModulename, RequestedModulename)
  +
    rec-msg(em-no-such-session(Filename))
    . Valid := true
  )
endlet

/*}}}  */
/*{{{ process EditTerm(Filename: str, Modulename: str) */

process EditTerm(Filename: str, Modulename: str) is
let
  Available: bool,
  EditorType: term,
  Sid: term,
  Tree: term,
  Valid: bool
in
  EditorType := term-editor
  . CheckSanity(Filename, Modulename, Valid?)
  .
  if equal(Valid, true)
  then
    Edit(Filename, Modulename, Sid?)
    . EditText(Sid, EditorType)
    . snd-msg(term-editor-started(Filename, Modulename, Sid))
    . GetTermTree(Filename, Tree?, Available?)
    .
    if equal(Available, true)
    then
      EditStructure(Sid, Tree)
    else
      tau
    fi
    .
    (
      MenuSelected(Sid, EditorType)
    +
      MouseClicked(Sid)
    +
      TextChanged(Sid)
    )
    *
    EditorDisconnected(Sid)
    . DeleteSession(Sid)
  else
    Error("Term is already being edited over a different module.", [])
  fi
endlet

/*}}}  */

/*{{{  process GetModuleForPath(Path: str, Modulename: str?) */

process GetModuleForPath(Path: str, Modulename: str?) is
let
  Directory: str,
  Filename: str
in
  snd-msg(io-get-path-directory(Path))
  . rec-msg(io-directory(Path, Directory?))
  . snd-msg(io-get-path-filename(Path))
  . rec-msg(io-filename(Path, Filename?))
  . MDB-GetModulename(Directory, Filename, Modulename?)
endlet

/*}}}  */
/*{{{  process GetModuleByExtension(Path: str, Modulename: str?) */

process GetModuleByExtension(Path: str, Modulename: str?, Available: bool?) is
let
  Extension: str
in
  snd-msg(io-get-path-extension(Path))
  . rec-msg(io-extension(Path, Extension?))
  . snd-msg(get-extension-modulename(Extension))
  .
  (
    rec-msg(extension-modulename(Extension, Modulename?))
    . Available := true
  +
    rec-msg(no-extension-modulename(Extension))
    . Available := false
  )
endlet

/*}}}  */

/*{{{ process StartEditor(Path: str, EditorType: term) */

process StartEditor(Path: str, EditorType: term) is
let
  Available: bool,
  Modulename: str,
  Pid: int,
  Sid: term
in
  if equal(EditorType, term-editor)
  then
    GetModuleByExtension(Path, Modulename?, Available?)
    .
    if equal(Available, true)
    then
      create(EditTerm(Path, Modulename), Pid?)
      . rec-msg(term-editor-started(Path, Modulename, Sid?))
    else
      snd-note(ui-status(errorf("Unable to determine modulename for: %s",
      [Path])))
    fi
  else
    GetModuleForPath(Path, Modulename?)
    .
    if equal(EditorType, syntax-editor)
    then
      create(EditSyntax(Modulename), Pid?)
      . rec-msg(syntax-editor-started(Modulename, Sid?))
    else
      create(EditEquations(Modulename), Pid?)
      . rec-msg(equations-editor-started(Modulename, Sid?))
    fi
  fi
endlet

/*}}}  */
