/*
  $Id$
 */

process WriteEquations(Path : str, Extension : str, Equations : term) is
let
  Message : str
in
  snd-msg(write-term-file(Path, Extension, Equations)) .
  (
    rec-msg(file-written)
  +
    rec-msg(file-not-written(Message?))
  )
endlet

process DumpEquations is
let
  ModuleName   : str,
  Equations    : term,
  Path         : str
in
  (
    rec-msg(dump-equations(ModuleName?)) .
    snd-msg(get-path-from-db(ModuleName)) .
    rec-msg(path(Path?)) .
    snd-msg(get-all-equations(ModuleName)) .
    (
      rec-msg(all-equations(Equations?)) .
      WriteEquations(Path, ".eqs", Equations)
      +
      rec-msg(equations-incomplete) .
      snd-note(ui-status(errorf("Specification incomplete for %s", [ModuleName])))
    ) .
    snd-msg(equations-dumped)
  ) * delta

endlet

