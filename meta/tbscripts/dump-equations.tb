/*
  $Id$
 */

process DumpEquations is
let
  Id         : term,
  ModuleName : str,
  FileName   : str,
  Equations  : term,
  Text       : str
in
  Id := process-id.

  (
    (
      rec-msg(dump-equations(ModuleName?)) .
      FileName := ""
    +
      rec-msg(dump-equations(ModuleName?, FileName?))
    ) .
    snd-note(ui-status(statf(Id, "Dumping specification for %s",[ModuleName]))).
    snd-msg(process-eqs(ModuleName)).
    rec-msg(eqs-processed(ModuleName)).
    snd-msg(get-all-equations(ModuleName)).
    (
      rec-msg(all-equations(Equations?)).
      snd-msg(create-equations-dump-file(ModuleName, FileName, Equations)).
      (
        rec-msg(create-equations-dump-file-done(ModuleName))
        +
        rec-msg(create-equations-dump-file-failed(ModuleName, Text?)).
        snd-note(ui-status(errorf("Dumping of specification for %s failed: %s", 
                 ModuleName, Text)))
      )
      +
      rec-msg(equations-incomplete) .
      snd-note(ui-status(errorf("Specification incomplete for %s", [ModuleName])))
    ) .
    snd-msg(equations-dumped) .
    snd-note(ui-status(endstat(Id)))

  ) * delta

endlet

