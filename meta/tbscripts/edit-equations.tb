/*
   $Id$
*/
process EditEquations(EditorId: term, ModuleName: str) is
let
  Path           : str,
  Focus          : term,
  Tree           : term,
  ErrMsg         : str,
  ErrorFoci      : term,
  Text           : str,
  DirtyFoci      : list,
  ModifiedStatus : term,
  Killed         : term,
  Msgs           : list
in
  Text := "" .
  subscribe(syntax-changed(ModuleName)) . 
  snd-msg(get-eqs-tree(ModuleName)) .
  (
    rec-msg(tree(Tree?)) .
    snd-msg(initialize-structure-editor-with-tree(EditorId, Tree))
  +
    rec-msg(unavailable) .
    snd-msg(get-eqs-text(ModuleName)) .
    (
      rec-msg(eqs-text(ModuleName, Text?))
    +
      %% If there are no equations in the module-db, assume we
      %% want to create a new equations section.
      rec-msg(no-eqs-text(ModuleName)) .
      snd-msg(create-new-equations(ModuleName)) .
      (
        rec-msg(new-equations-created(ModuleName, Path?))
      +
        rec-msg(new-equations-not-created(ErrMsg?)) .
        snd-note(ui-status(errorf(
          "Could not create empty equations: %s", [ErrMsg])))
      )
    ) .
    snd-msg(initialize-structure-editor(EditorId, Text))
  ) .
  rec-msg(initial-focus(Focus?)) .
  snd-msg(initialization-done(EditorId)) .
  Killed := false .
  (
    (
      (
	rec-note(syntax-changed(ModuleName)) .
	snd-msg(invalidate-tree(EditorId))
      +
	rec-msg(parse-buffer(EditorId)) .
	snd-msg(invalidate-tree(EditorId)).
	snd-msg(parse-focusses(EditorId, eqs(ModuleName))) .
	(
	  rec-msg(parse-ok(eqs(ModuleName))) .
	  snd-msg(get-tree(EditorId)) .
          rec-msg(tree(Tree?)) .
	  snd-msg(postprocess-semantics-hook(Tree)).
	  rec-msg(postprocess-semantics-hook-result(Tree?)).
          snd-msg(update-eqs-tree-in-db(ModuleName, tree(Tree))) .
          rec-msg(eqs-updated) .
          snd-msg(clear-focus(EditorId))
	+
	  rec-msg(parse-failed(eqs(ModuleName), ErrorFoci?))
	)
      )
    ) *
    if equal(Killed, true) then
      tau
    fi
  )
  ||
  rec-msg(kill-editor(EditorId)).
  Killed := true .
  snd-msg(get-modified-status(EditorId)) .
  rec-msg(modified-status(EditorId, ModifiedStatus?)) .
  snd-msg(kill-editor-done(EditorId)) .
  if equal(ModifiedStatus, modified) then
    snd-msg(revert-module([ModuleName])).
    rec-msg(module-reverted([ModuleName])).
    snd-note(ui-status(errorf("Module %s is reread from disk",[ModuleName])))
  else
    tau
  fi
endlet
