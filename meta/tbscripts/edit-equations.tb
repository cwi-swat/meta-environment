process Edit-equations(Module: str) is
let
  Id          : term,
  T           : term,
  Tid         : int,
  Fn          : str,
  TE          : text-edit,
  Path        : str,
  Eqs         : term,
  Time        : term,
  ErrMsg      : str,
  EqsText     : str,
  StartEditor : term
in
  EqsText := "" .
  StartEditor := true .
  Id := process-id .
  snd-note(ui-status(stat(Id,"Opening equations editor..."))) .
  snd-msg(get-eqs-path-from-db(Module)) .
  rec-msg(path(Path?)) .
  snd-msg(get-eqs-tree(Module)) .
  (
    rec-msg(tree(Eqs?)) .
    snd-msg(unparse(Eqs)) .
    rec-msg(unparsed-text(EqsText?))
  +
    %% If there are no equations in the module-db, assume we
    %% want to create a new equations section.
    rec-msg(unavailable) .
    snd-msg(get-eqs-text(Module)) .
    (
      rec-msg(eqs-text(Module, EqsText?))
    +
      rec-msg(no-eqs-text(Module)) .
      snd-msg(create-new-equations(Module)) .
      (
        rec-msg(new-equations-created(Module, Path?))
      +
        rec-msg(new-equations-not-created(ErrMsg?)) .
	StartEditor := false .
	snd-note(ui-status(errorf("Could not create empty equations: %s",
				  [ErrMsg])))
      )
    )
  ).
  if equal(StartEditor, true) then
    create(Editor(Module, EqsText, Path, Module, "eqs"), Tid?)
  fi .
  snd-note(ui-status(endstat(Id)))
endlet
