#ifndef __GUI_UTILS__
#define __GUI_UTILS__

#include <gui-actions.tb>
#include <gui.idef>
#include <dialog.idef>
#include <graph-painter.idef>
#include <progress.idef>
#include <moduledetails.idef>
#include <error-viewer.idef>

process AddJob(Message: str) is
  snd-msg(gui-add-job(Message))

process JobDone(Message: str) is
  snd-msg(gui-job-done(Message))

process Error(Message: str, SubjectStrings: list) is
let
  Error : term,
  SubjectString : str,
  Subject : term,
  Subjects: list,
  Runner : list,
  Summary : term
in
  Runner := SubjectStrings
  . Subjects := []
  . 
  if not-equal(Runner, []) then
    SubjectString := first(Runner)
    . Runner := next(Runner)
    . snd-msg(es-make-subject(SubjectString))
    . rec-msg(es-subject(Subject?))
    . Subjects := join(Subject, Subjects)
  fi
  *
  if equal(Runner, []) then
    snd-msg(es-make-error(Message, Subjects))
    . rec-msg(es-error(Error?))
    . snd-msg(es-make-summary("alert", "alert", [Error]))
    . rec-msg(es-summary(Summary?))
    . snd-msg(ui-remove-feedback-summary("Alerts", "alert", "alert"))
    . snd-msg(ui-show-feedback-summary("Alerts", Summary))
  fi  
endlet

process RemoveSummary(Producer: str, SummaryId: str) is
  snd-msg(ui-remove-feedback-summary("Issues", Producer, SummaryId))
  
process DisplaySummary(Summary: term) is
  snd-msg(ui-show-feedback-summary("Issues", Summary))

process HandleEvent(Type: term, Event: term) is
let
  Actions: list,
  Action: str
in
  snd-msg(cm-get-action(Type, Event))
  . rec-msg(cm-action(Action?))
  .
  (
    printf("Warning: process not found %s\n", Action)
  +>
    Action()
  )
endlet

process LocationSelected(Location: term) is
let
  Action: str
in
  snd-msg(cm-get-action(feedback-list, click))
  . rec-msg(cm-action(Action?))
  . 
  (
    printf("Warning: process not found %s\n", Action)
  +>
    Action(Location)
  )
endlet

process MessageSelected(Modulename: str, Tree: term) is
let
  Action: str
in
  snd-msg(get-action(click, message-list, Modulename))
  . rec-msg(action(click, message-list, Modulename, Action?))
  . 
  (
    printf("Warning: process not found %s\n", Action)
  +>
    Action(Tree, Modulename)
  )
endlet

process NodeSelected(GraphId: str, Node: term) is
let
  Action: str,
  Location: term
in
  snd-msg(gc-get-node-origin(Node))
  .
  (
    rec-msg(gc-origin(Location?))
    . snd-msg(cm-get-action(tree-panel, click))
    . rec-msg(cm-action(Action?))
    .
    (
      printf("Warning: process not found %s\n", Action)
    +>
      Action("Tree Panel selection", Location)
    )
  +
    rec-msg(gc-no-origin)
  )
endlet

process UserInterfaceHandler is
let
  Error: term,
  Event: term,
  GraphId: str,
  Modulename: str,
  Node: term,
  Pid: int,
  Tree: term,
  Type: term
in
  (
    rec-msg(ui-button-selected(Type?, Event?))
    . create(HandleEvent(Type, Event), Pid?)
  +
    rec-msg(ui-location-selected(Error?))
    . create(LocationSelected(Error), Pid?)
  +
    rec-msg(ui-node-selected(GraphId?, Node?))
    . create(NodeSelected(GraphId, Node), Pid?)
  +
    rec-msg(ui-message-selected(Modulename?, Tree?))
    . create(MessageSelected(Modulename, Tree), Pid?)
  +
    rec-msg(gui-window-closing)
    . ExitAction
  )
  *
  delta
endlet

toolbus(UserInterfaceHandler)

process StatusDisplay is
let
  Lst: list,
  Str: str,
  Summary: term
in
  subscribe(ui-status(<term>))
  . subscribe(show-feedback-summary(<term>))
  .
  (
    rec-note(ui-status(error(Str?)))
    . printf("Error: %s", Str)
  +
    rec-note(ui-status(errorf(Str?, Lst?)))
    . printf("Error: %s [%t]\n", Str, Lst)
  +
    rec-note(ui-status(warning(Str?)))
    . printf("Warning: %s", Str)
  +
    rec-note(ui-status(warningf(Str?, Lst?)))
    . printf("Warning: %s [%t]\n", Str, Lst)
  +
    rec-note(ui-status(message(Str?)))
    . printf("Message: %s", Str)
  +
    rec-note(ui-status(messagef(Str?, Lst?)))
    . printf("Message: %s [%t]\n", Str, Lst)
  +
    rec-note(show-feedback-summary(Summary?))
    . snd-msg(display-feedback(Summary))
  )
  *
  delta
endlet

#endif /* __GUI_UTILS__ */
