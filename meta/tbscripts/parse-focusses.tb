/*
  $Id$
*/       

process ParseFocusses is
let
  Id           : term,
  Path         : str,
  Focus        : term,
  SyntaxTree   : term,
  DirtyFoci    : list,
  ErrorFoci    : list,
  Text         : str,
  Type         : str,
  ModuleName   : str,
  EditorId     : term,
  Pos          : int,
  Nonterminal  : str,
  Tree         : term,
  ErrList      : list,
  Ambs         : list,
  Cycle        : term,
  Amount       : int,
  ModuleId     : term
in                     
  Id := process-id .     
  (
    rec-msg(parse-focusses(EditorId?, ModuleId?)) .
    Type := fun(ModuleId) .
    ModuleName := first(args(ModuleId)).
    if equal(Type, "eqs") then
      snd-msg(get-rules-top-sort) .
      rec-msg(set-rules-top-sort(sort(Nonterminal?)))
    else
      Nonterminal := ""
    fi . 
    ErrorFoci := [] .
    snd-msg(get-dirty-focuses(EditorId)) .
    rec-msg(dirty-focuses(DirtyFoci?)) . 
    (
      if not-equal(DirtyFoci, []) then
        Focus := first(DirtyFoci).
        DirtyFoci := next(DirtyFoci).
        snd-msg(get-focus-text(EditorId, Focus)).
        rec-msg(focus-text(EditorId, Text?)).
        snd-msg(get-focus-sort(Nonterminal, Focus)) .
        rec-msg(focus-sort(Nonterminal?)) .
        UpdateTextInModuleDb(Type, ModuleName, Text).
        snd-msg(Id, parse-string-as-asfix2me(ModuleId, Nonterminal, Text)).
        snd-note(ui-status(statf(Id,"Parsing %s",[ModuleId]))).
        (
          rec-msg(parse-result-asfix2me(Id, parse-tree(Tree?))) .
          snd-msg(replace-focus(EditorId, Focus, Tree))
        +
          rec-msg(parse-result-asfix2me(Id, 
            ambiguous-tree(Tree?, ErrList?, ambiguities(Amount?, Ambs?)))) .
          snd-msg(get-error-position(Focus, ErrList)).
          rec-msg(error-position(Pos?)).
          snd-msg(display-error(EditorId, Pos)).
          snd-note(ui-status(errorf("Ambiguities detected (%d)",[Amount]))).
          printf("Ambiguities(%d):\n%t\n",Amount, Ambs).
          snd-msg(replace-focus(EditorId, Focus, Tree))
        +
          (
            rec-msg(parse-result-asfix2me(Id, parse-error(ErrList?, plain)))
          +
            rec-msg(parse-result-asfix2me(Id, parse-error(ErrList?, eof)))
          ) .
          /* This should be improved. */ 
          snd-msg(get-error-position(Focus, ErrList)) .
          rec-msg(error-position(Pos?)) .
          snd-msg(display-error(EditorId, Pos)) .
          ErrorFoci := join(ErrorFoci, Focus)
        +
          rec-msg(parse-result-asfix2me(Id,
            parse-error(ErrList?, ambiguities(Amount?, Ambs?)))) .
          snd-msg(get-error-position(Focus, ErrList)).
          rec-msg(error-position(Pos?)).
          snd-msg(display-error(EditorId, Pos)).
          snd-note(ui-status(errorf("Ambiguities detected (%d)",[Amount]))).
          printf("Ambiguities(%d):\n%t\n",Amount, Ambs).
          ErrorFoci := join(ErrorFoci, Focus)
        +
          rec-msg(parse-result-asfix2me(Id, 
                  parse-error(ErrList?, cycle(Cycle?)))) .
          snd-msg(get-error-position(Focus, ErrList)).
          rec-msg(error-position(Pos?)).
          snd-msg(display-error(EditorId, Pos)).
          snd-note(ui-status(error("Cycle detected\n"))). 
          ErrorFoci := join(ErrorFoci, Focus)
        ).
        snd-note(ui-status(endstat(Id)))
      else
        tau
      fi
    ) *
    if equal(DirtyFoci, []) then
      if equal(ErrorFoci, []) then
        snd-msg(parse-ok(ModuleId))
      else
        snd-msg(parse-failed(ModuleId, ErrorFoci))
      fi
    fi 
  )
  *
  delta
endlet

process UpdateTextInModuleDb(Type : str, ModuleName : str, Text : str) is
  if equal(Type, "eqs") then
    snd-msg(update-eqs-text-in-db(ModuleName, text(Text))) .
    rec-msg(eqs-text-updated(ModuleName))
  else
    tau
  fi
