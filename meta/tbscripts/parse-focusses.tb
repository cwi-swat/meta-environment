/*
  $Id$
*/       

process GetText (EditorId : term, Text : str?, Focus : term?, Status : term?) is
let
  DirtyFoci    : list
in
  snd-msg(get-dirty-focuses(EditorId)) .
  rec-msg(dirty-focuses(DirtyFoci?)) . 
  if not-equal(DirtyFoci, []) then
    Focus := first(DirtyFoci).
    DirtyFoci := next(DirtyFoci).
    snd-note(get-focus-text(EditorId, Focus)).
    (
      rec-msg(focus-text(EditorId, Text?)) .
      Status := parse-needed 
    +
      /* If the editor suddenly dissappears were done */
      rec-note(delete-editor(EditorId)) .
      Text := "" .
      Status := no-parse-needed
    )
  else
    Text := "" .
    Status := no-parse-needed
  fi
endlet

process ParseFocusses is
let
  Id           : term,
  Focus        : term,
  Text         : str,
  FunType      : str,
  Type         : term,
  ModuleName   : str,
  EditorId     : term,
  Nonterminal  : str,
  Tree         : term,
  Errors       : term,
  Status       : term
in                     
  Id := process-id .     
  (
    rec-msg(parse-focusses(EditorId?, ModuleName?, Type?)) .
    subscribe(delete-editor(EditorId)) .
    if equal(Type, eqs) then
      snd-msg(get-rules-top-sort) .
      rec-msg(set-rules-top-sort(sort(Nonterminal?)))
    else
      Nonterminal := ""
    fi . 
    GetText(EditorId, Text?, Focus?, Status?) .
    if equal(Status, parse-needed) then
      UpdateTextInModuleDb(Type, ModuleName, Text).
      ParseString(ModuleName, Type, Nonterminal, Text, Tree?, Errors?) .
      PrepareParseErrorForEditor(ModuleName, EditorId, Focus, Errors) .
      ProcessTree(ModuleName, EditorId, Focus, Tree) .
      Tree := no-tree
    else
      snd-msg(parse-ok(ModuleName))
    fi .
    unsubscribe(delete-editor(EditorId))
  )
  *
  delta
endlet

process UpdateTextInModuleDb(Type : term, ModuleName : str, Text : str) is
  if equal(Type, eqs) then
    snd-msg(update-eqs-text-in-db(ModuleName, text(Text))) .
    (
      rec-msg(eqs-text-updated(ModuleName)) 
    +
      rec-msg(no-updating-needed)
    )
  else
    tau
  fi
