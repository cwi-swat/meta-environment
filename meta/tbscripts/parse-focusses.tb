/* $Id$ */       

/*{{{  process GetText(EditorId: term, Text: str?) */

process GetText(EditorId: term, Text: str?) is
  snd-msg(te-get-contents(EditorId))
  . rec-msg(te-contents(EditorId, Text?))

/*}}}  */
/*{{{  process AnnotateTree(Tree: term, Filename: str, AnnotateTree: term?) */

process AnnotateTree(Tree: term, Filename: str, AnnotatedTree: term?) is
  snd-msg(add-posinfo-packed(Filename, Tree))
  . rec-msg(tree-with-pos-info(AnnotatedTree?))

/*}}}  */

process ParseEditorCreator is
let
  Nonterminal: term,
  Pid: int,
  Sid: term,
  Table: term
in
  (
    rec-msg(parse-editor(Sid?, Table?, Nonterminal?))
    . create(ParseEditor(Sid, Table, Nonterminal), Pid?)
  )
  * delta
endlet

process ParseErrorToSummary(Error: term, Filename: str, Summary: term?) is
let
  Result: term
in
  snd-msg(es-add-filename-in-error(Filename, Error))
  . rec-msg(es-filename-in-error(Filename, Result?))
  . snd-msg(es-make-summary("sglr", Filename, [Result]))
  . rec-msg(es-summary(Summary?))
endlet

process ParseEditor(Sid: term, Table: term, Nonterminal: term) is
let
  AnnotatedTree: term,
  Error: term,
  Filename: str,
  ParseResult: term,
  Registered: bool,
  Text: str,
  Summary: term
in 
  IsStructureEditorRegistered(Sid, Registered?)
  .
  if equal(Registered, true)
  then
    snd-msg(parse-ok(Sid))
  else
    GetFilename(Sid, Filename?)
    . GetText(Sid, Text?)
    . ClearSummaryInGUI("sglr", Filename)
    . snd-msg(sglr-parse(Text, Table, Nonterminal))
    .
    (
      rec-msg(sglr-parse-tree(ParseResult?))
      . AnnotateTree(ParseResult, Filename, AnnotatedTree?)
      . EditStructure(Sid, AnnotatedTree)
      . snd-msg(parse-ok(Sid))
    +
      rec-msg(sglr-parse-forest(ParseResult?, Error?))
      . AnnotateTree(ParseResult, Filename, AnnotatedTree?)
      . EditStructure(Sid, AnnotatedTree)
      . ParseErrorToSummary(Error, Filename, Summary?)
      . ShowSummary(Sid, Summary)
      . ShowSummaryInGUI(Summary)
      . snd-msg(parse-failed(Sid))
    +
      rec-msg(sglr-parse-error(Error?))
      . ParseErrorToSummary(Error, Filename, Summary?)
      . ShowSummary(Sid, Summary)
      . ShowSummaryInGUI(Summary)
      . snd-msg(parse-failed(Sid))
    )
  fi
endlet
