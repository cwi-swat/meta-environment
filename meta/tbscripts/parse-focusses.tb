/* $Id$ */       

/*{{{  process GetText(EditorId: term, Text: str?) */

process GetText(EditorId: term, Text: str?) is
  snd-msg(te-get-contents(EditorId))
  . rec-msg(te-contents(EditorId, Text?))

/*}}}  */

process ParseFocusses is
  let
    Text: str,
    Type: term,
    ModuleName: str,
    EditorId: term,
    Nonterminal: str,
    Tree: term,
    Errors: term,
    Status: term,
    Filename: str,
    Checking : term
  in 
    (
      /* parse contents of <EditorId> over language <ModuleName> */
      rec-msg(parse-focusses(EditorId?, ModuleName?, Type?)) .
      subscribe(delete-editor(EditorId)) .
      if equal(Type, eqs) then
        Checking := true .
        snd-msg(get-rules-top-sort) .
        rec-msg(set-rules-top-sort(sort(Nonterminal?)))
      else
        if equal(Type, sdf) then
          Checking := true .
          snd-msg(syntax-top-sort-hook(sdf)) .
          rec-msg(syntax-top-sort-hook-result(sort(Nonterminal?)))
        else
          Checking := false .
          snd-msg(other-top-sort-hook(Type)) .
          rec-msg(other-top-sort-hook-result(sort(Nonterminal?)))
        fi
      fi .
      snd-msg(em-is-editor-registered(EditorId, structure))
      .
      (
        rec-msg(em-editor-registered(EditorId, structure))
        . snd-msg(parse-ok(ModuleName))
      +
        rec-msg(em-editor-not-registered(EditorId, structure))
	. GetText(EditorId, Text?)
        . UpdateTextInModuleDb(Type, ModuleName, Text)
        . snd-msg(em-get-filename(EditorId))
        . rec-msg(em-filename(EditorId, Filename?))
        . ParseString(ModuleName, Filename, Type, Checking, Nonterminal, Text, 
                    Tree?, Errors?)
        . HandleParseErrors(ModuleName, EditorId, Errors)
        . ProcessTree(ModuleName, EditorId, Tree, Errors)
        . Tree := no-tree
      )
      . unsubscribe(delete-editor(EditorId))
    ) *
    delta
  endlet

process UpdateTextInModuleDb(Type: term, ModuleName: str, Text: str) is
  if equal(Type, eqs) then
    snd-msg(update-eqs-text-in-db(ModuleName, text(Text))) .
    (
      rec-msg(eqs-text-updated(ModuleName))
      +
      rec-msg(no-updating-needed)
    )
  else
    tau
  fi
