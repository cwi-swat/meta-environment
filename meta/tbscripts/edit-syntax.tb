/*
  $Id$
*/

process EditSyntax(EditorId: term, ModuleName: str) is
let
  Path      : str,
  Focus     : term,
  Tree      : term, 
  RTree          : term,
  STree          : term,
  Time      : term,
  Modules   : term,
  ErrorFoci : term,
  Text      : str,
  DirtyFoci : list,
  ModifiedStatus : term,
  Killed    : term,
  ButtonName     : str
in
  Text := "" .
  snd-msg(get-sdf2-asfix(ModuleName)).
  (
    rec-msg(syntax(Tree?)) .
    snd-msg(initialize-structure-editor-with-tree(EditorId, Tree)) .
    rec-msg(initial-focus(Focus?))
  +
    rec-msg(unavailable).
    snd-msg(open-syntax-file(ModuleName)) .
    (
      rec-msg(opened-file(ModuleName, text(Text?), Path?, Time?))
    +
      rec-msg(error-opening(ModuleName))
    ) .
    snd-msg(initialize-structure-editor(EditorId, Text)) .
    rec-msg(initial-focus(Focus?))
  ) .
  snd-msg(initialization-done(EditorId)) .
  Killed := false .
  (
    (   
      rec-msg(extra-button-action(EditorId, ButtonName?)) .
      snd-msg(extra-button-action(ButtonName, "Syntax", ModuleName, EditorId))
    ) *
    if equal(Killed, true) then
      tau
    fi
  ) 
  ||
  rec-msg(kill-editor(EditorId)).
  Killed := true .
  snd-msg(get-modified-status(EditorId)) .
  rec-msg(modified-status(EditorId, ModifiedStatus?)) .
  snd-msg(kill-editor-done(EditorId)).
  if equal(ModifiedStatus, modified) then
    snd-msg(revert-module(ModuleName)).
    rec-msg(module-reverted(ModuleName)).
    snd-note(ui-status(errorf("Dirty module %s is reread from disk",
                       [ModuleName])))
  else
    tau
  fi
  
endlet
