process DumpParseTable(ModuleName : str, FileName : str, Type : term) is
let
  Id: term,
  Table: term,
  Path: str,
  Error: term,
  Extension: str,
  NewFileName: str
in
  Id := process-id
  .  snd-note(ui-status(statf(Id, "Dumping parse table for %s", [ModuleName])))
  . snd-msg(get-parsetable(ModuleName, Type, true))
  .
  (
    rec-msg(parse-table(Table?))
    . snd-msg(get-path-from-db(ModuleName))
    . rec-msg(path(Path?))
    .
    if equal(FileName, "") then
      snd-msg(parsetable-extension-hook(Type))
      . rec-msg(parsetable-extension-hook-result(Extension?))
      . snd-msg(io-get-filename(Path, ModuleName, Extension))
      . rec-msg(io-filename(NewFileName?))
    else
      NewFileName := FileName
    fi
    . snd-msg(io-unpack-and-write-term-in-baf(NewFileName, Table))
    .
    (
      rec-msg(io-file-written)
    +
      rec-msg(io-file-not-written(Error?))
    )
  +
    rec-msg(no-parse-table(ModuleName))
  )
  . snd-note(ui-status(endstat(Id)))
endlet
