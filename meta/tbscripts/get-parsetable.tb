process GetParseTable(ModuleId: term, Type: term, Table: term?) is
  if equal(Type, sdf) then
    snd-msg(get-parse-table(sdf))
    .
    (
      rec-msg(parse-table(sdf, Table?))
    +
      rec-msg(no-parse-table)
      . Table := UNDEFINED
    )
  else
    snd-msg(obtain-parse-table-hook(Type, ModuleId))
    . rec-msg(obtain-parse-table-hook-result(Table?))
  fi

process BuildParseTable is
let
  ModuleId: term,
  Modulename: str,
  Type: term,
  Table: term,
  Syntax: term,
  ErrorMsg: str,
  Id: int,
  Error : term
in
  Id := process-id
  .
  (
    rec-msg(get-parsetable(ModuleId?, Type?))
    . snd-msg(get-table-from-db(ModuleId, Type))
    .
    (
      rec-msg(table(Table?))
      . snd-msg(parse-table(Table))
    +
      rec-msg(no-table)
      . snd-msg(get-all-syntax-definitions(ModuleId, Type))
      .
      (
        rec-msg(syntax(Syntax?)) 
	. MM-GetAttribute(ModuleId, SDF_NAMESPACE, "name", Modulename?)
        . snd-msg(preprocess-syntax-hook(ModuleId, Type, Syntax))
        . rec-msg(preprocess-syntax-hook-result(ModuleId, Syntax?))
        . snd-msg(generate-parsetable(Syntax, Modulename))
        . Syntax := UNDEFINED
        .
        (
          rec-msg(parsetable(Table?))
          . snd-msg(add-parse-table-to-db(ModuleId, Type, Table))
          . rec-msg(parse-table-added)
          . snd-note(ui-status(endstat(Id)))
          . snd-msg(parse-table(Table))
          . Table := no-table
        +
          rec-msg(no-parsetable)
          . snd-note(ui-status(endstat(Id)))
          . snd-note(ui-status(error("Unexpected error in definition")))
          . snd-msg(no-parse-table(ModuleId))
        )
      +
        rec-msg(no-syntax(ErrorMsg?))
        . snd-note(ui-status(endstat(Id)))
        . snd-note(ui-status(errorf("%s", [ErrorMsg])))
        . snd-msg(no-parse-table(ModuleId))
      )
    )
  )
  *
  delta
endlet
