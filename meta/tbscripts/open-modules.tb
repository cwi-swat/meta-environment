/* $Id$
 */

process Open-modules(Eval: evaluator, Tdb: module-db) is
let
  Id   : term,
  Name : term,
  Str  : str,
  Fn   : str,
  T    : term,
  M1s  : list,
  M2s  : list,
  Ms   : list,
  Pairs: list
in
  Id := process-id. (
    rec-msg(open-modules(Ms?)).
    snd-note(ui-status(stat(Id,"Opening Modules"))). (
      if not-equal(Ms, []) then
        Name := first(Ms).
        Ms   := next(Ms).
       /* First we should check here whether this module has already been 
        * opened...
        */
        snd-eval(Tdb,exists(Name)). ( 
          rec-value(Tdb,result(exists(Name?)))
          +
          /* If it is not open, open it. */
          rec-value(Tdb,result(notexists(Name?))). ( 
            snd-msg(Id, open-file(Name)). ( 
              rec-msg(Id, opened-file(asfix,Name,T?,Fn?)).
             /* Tell UI we added module Name */
              snd-msg(ui,add-module(Name)).
             /* Tell mdb we add module Name containing term T */
              snd-eval(Tdb,add-module(T)).
             /* mdb returns the imports section of Name as well as the
              * list of modules which have to be still opened M1s */
              rec-value(Tdb,imports(Name,need-modules(M1s?))).
             /* Add those modules to the list */
              Ms := join(Ms,M1s).
             /* This is UI stuff and should move somewhere else 
              * Tdb should trigger an event for the UI process to catch
              */
              snd-eval(Tdb,calc-import-rels(Name)).
              rec-value(Tdb,irels(Pairs?)).
              snd-msg(ui,irels(Pairs)) 
              +
             /* File would not open (unreadable, not exist, wrong path...) */
              rec-msg(Id, error-opening(Str?)).
              printf("the string is %s\n",Str).
              snd-note(ui-status(stat(Id,"Cannot open module"))).
              snd-msg(ui,cannot-open-module(Str)).
              snd-note(ui-status(endstat(Id)))
            )
          )
        )
      fi
    ) *
    if equal(Ms,[]) then
     /* We are finished. Inform UI */
      snd-msg(ui, finished-opening-modules)
    fi.
    snd-note(ui-status(endstat(Id)))
  ) *
  delta
endlet
