#ifndef __PARSE_FOCUSES__
#define __PARSE_FOCUSES__

process ReadFile(Path: str, Contents: str?) is
let
  Error: term
in
  snd-msg(io-read-file(Path))
  .
  (
    rec-msg(io-file-contents(Contents?))
  +
    rec-msg(io-error-reading(Error?))
    . Contents := ""
  )
endlet

process AnnotateTree(Tree: term, File: str, AnnotatedTree: term?) is
  snd-msg(add-posinfo-packed(File, Tree))
  . rec-msg(tree-with-pos-info(AnnotatedTree?))

process HasTextChanged(Sid: term, TextChanged: bool?) is
  snd-msg(te-is-modified(Sid))
  . rec-msg(te-is-modified(Sid, TextChanged?))

process AddFilenameToParseError(Error: term, Filename: str, Result: term?) is
  snd-msg(es-add-filename-in-error(Filename, Error))
  . rec-msg(es-filename-in-error(Filename, Result?))

process ParseEditor(Sid: term, Table: term, Nonterminal: term) is
let
  Path: str,
  Pid: int,
  StructureEditor: bool,
  Text: str,
  TextChanged: bool
in
  HasTextChanged(Sid, TextChanged?)
  .
  if equal(TextChanged, quote(true)) then
    snd-msg(te-write-contents(Sid))
    . rec-msg(te-contents-written(Sid))
    . StructureEditor := false
  else
    IsStructureEditorRegistered(Sid, StructureEditor?)
  fi
  .
  if equal(StructureEditor, quote(true)) then
    snd-msg(parse-editor-ok(Sid))
  else
    GetPath(Sid, Path?)
    . ReadFile(Path, Text?)
    . create(EditorParseHandler(Sid), Pid?)
    . ParseText(Pid, Text, Table, Nonterminal)
  fi
endlet

process EditorParseHandler(Sid: term) is
let
  AnnotatedTree: term,
  Error: term,
  Filename: str,
  Pid: int,
  ParseError: term,
  ParseResult: term
in
  Pid := process-id
  . GetPath(Sid, Filename?)
  .
  (
    rec-msg(parse-tree(Pid, ParseResult?))
    . AnnotateTree(ParseResult, Filename, AnnotatedTree?)
    . EditStructure(Sid, AnnotatedTree)
    . snd-msg(parse-editor-ok(Sid))
  +
    rec-msg(parse-forest(Pid, ParseResult?, Error?))
    . AnnotateTree(ParseResult, Filename, AnnotatedTree?)
    . EditStructure(Sid, AnnotatedTree)
    . AddFilenameToParseError(Error, Filename, ParseError?)
    . snd-msg(parse-editor-failed(Sid, ParseError))
  +
    rec-msg(parse-error(Pid, Error?))
    . AddFilenameToParseError(Error, Filename, ParseError?)
    . ShowError(Sid, ParseError)
    . snd-msg(parse-editor-failed(Sid, ParseError))
  )
endlet

process ParseText(Pid: int, Text: str, Table: term, Nonterminal: term) is
let
  Error: term,
  ParseResult: term
in
  snd-msg(sglr-parse(Text, Table, Nonterminal))
  .
  (
    rec-msg(sglr-parse-tree(ParseResult?))
    . snd-msg(parse-tree(Pid, ParseResult))
  +
    rec-msg(sglr-parse-forest(ParseResult?, Error?))
    . snd-msg(parse-forest(Pid, ParseResult, Error))
  +
    rec-msg(sglr-parse-error(Error?))
    . snd-msg(parse-error(Pid, Error))
  )
endlet

process SdfParseText(Pid: int, Text: str, Table: term, Nonterminal: term) is
let
  Error: term,
  ParseResult: term
in
  snd-msg(sdf-parse(Text, Table, Nonterminal))
  .
  (
    rec-msg(sglr-parse-tree(ParseResult?))
    . snd-msg(parse-tree(Pid, ParseResult))
  +
    rec-msg(sglr-parse-forest(ParseResult?, Error?))
    . snd-msg(parse-forest(Pid, ParseResult, Error))
  +
    rec-msg(sglr-parse-error(Error?))
    . snd-msg(parse-error(Pid, Error))
  )
endlet

#endif /*__PARSE_FOCUSES__*/
