/* $Id$ */

process EditorCreator is
let
  EditorId   : term,
  ModuleName : str,
  FileName   : str,
  Tree       : term
in
  (
    rec-msg(edit-general(FileName?)) .
    GeneralEditorCreator(FileName)
  +
    rec-msg(edit-syntax(ModuleName?)) .
    SyntaxEditorCreator(ModuleName)
  +
    rec-msg(edit-equations(ModuleName?)) .
    EquationsEditorCreator(ModuleName)
  +
    rec-msg(edit-term-file(ModuleName?, FileName?)).
    TermEditorCreator(ModuleName, FileName, none)
  +
    rec-msg(edit-term-file-with-tree(ModuleName?, FileName?, Tree?)) .
    TermEditorCreator(ModuleName, FileName, Tree)
  +
    rec-msg(delete-editors-for-module(ModuleName?)) .
    KillEditorsForModule(ModuleName)
  )
  * delta
endlet

process GeneralEditorCreator (FullFileName : str) is
let
  Path : str,
  FileName : str,
  Extension : str,
  AskedExtension : str,
  ModuleName : str
in
  snd-msg(file-extension-hook(sdf)) .
  rec-msg(file-extension-hook-result(AskedExtension?)) .
  snd-msg(io-get-extension(FullFileName)) .
  rec-msg(io-extension(FullFileName, Extension?)) .
  if equal(Extension, AskedExtension) then
    MDB-GetModuleName(Path, FileName, ModuleName?) .
    SyntaxEditorCreator(ModuleName)
  else
    snd-msg(file-extension-hook(asf)) .
    rec-msg(file-extension-hook-result(AskedExtension?)) .
    snd-msg(io-decons-file-name(FullFileName, AskedExtension)) .
    rec-msg(io-decons-file-name-result(Path?, FileName?, Extension?)) .
    if equal(Extension, AskedExtension) then
      MDB-GetModuleName(Path, FileName, ModuleName?) .
      EquationsEditorCreator(ModuleName)
    else
      ExtensionBasedTermEditorCreator(FullFileName)
    fi
  fi
endlet

process SyntaxEditorCreator (ModuleName : str) is
let
  Tree : term,
  FileName : str,
  Path : str,
  Extension : str,
  EditorId: term
in
  Tree := none .
  EditorId := no-editor .
  snd-msg(get-path-from-db(ModuleName)) .
  (
    rec-msg(path(Path?)) .
    snd-msg(get-syntax-tree(ModuleName)).
    (
      rec-msg(syntax(Tree?))
    +
      rec-msg(unavailable)
    ) .
    snd-msg(file-extension-hook(sdf)) .
    rec-msg(file-extension-hook-result(Extension?)) .
    snd-msg(io-get-filename(Path, ModuleName, Extension)) .
    rec-msg(io-filename(FileName?)) .
    CreateEditor(syntax-editor, ModuleName, FileName, Tree, EditorId?)
  +
    rec-msg(no-path) .
    snd-note(ui-status(errorf("Internal error: could not find path to %s",
                       [ModuleName])))
  ) .
  snd-msg(editor-opened(ModuleName, EditorId))
endlet

process EquationsEditorCreator (ModuleName : str) is
let
  Tree : term,
  FileName : str,
  Path : str,
  Time : int,
  Extension : str,
  EditorId: term
in
  Tree := none .
  EditorId := no-editor .
  snd-msg(get-path-from-db(ModuleName)) .
  (
    rec-msg(path(Path?)) .
    snd-msg(get-eqs-tree(ModuleName)) .
    (
      rec-msg(tree(Tree?)) 
    +
      rec-msg(unavailable)
    ) .
    snd-msg(file-extension-hook(asf)) .
    rec-msg(file-extension-hook-result(Extension?)) .
    snd-msg(io-get-filename(Path, ModuleName, Extension)) .
    rec-msg(io-filename(FileName?)) .
    CreateEditor(equations-editor, ModuleName, FileName, Tree, EditorId?)
  +
    rec-msg(no-path) .
    snd-note(ui-status(errorf("Internal error: could not find path to %s",
                       [ModuleName])))
  ) .
  snd-msg(editor-opened(ModuleName, EditorId))
endlet

process ExtensionBasedTermEditorCreator (FileName : str) is
let
  Extension : str,
  ModuleName : str,
  Tree: term,
  EditorId: term
in
  Tree := none .
  ModuleName := "" .
  EditorId := no-editor .
  snd-msg(io-get-extension(FileName)) .
  rec-msg(io-extension(FileName, Extension?)) .
  snd-msg(get-extension-modulename(Extension)) .
  (
    rec-msg(extension-modulename(Extension, ModuleName?)) .
    CreateEditor(term-editor, ModuleName, FileName, Tree, EditorId?)
  +
    rec-msg(no-extension-modulename(Extension)) .
    snd-note(ui-status(errorf("Extension %s is not registered. Please edit your meta.buttons. For example: configuration([extension(\"MyModuleName\",\"%s\")])",
                       [Extension, Extension])))
  ) .
  snd-msg(editor-opened(ModuleName, EditorId))
endlet

process TermEditorCreator(ModuleName: str, FileName: str, Tree: term) is
let
  EditorId: term
in
  CreateEditor(term-editor, ModuleName, FileName, Tree, EditorId?) .
  snd-msg(editor-opened(ModuleName, EditorId))
endlet

process CreateEditor(Type : term,
                     ModuleName : str, 
                     FileName : str, 
		     Tree : term,
		     EditorId: term?) is
let
  ButtonList : list,
  Pid : int,
  CurrentModuleName : str
in
  snd-msg(get-editor-id(FileName, ModuleName)) .
  (
    rec-msg(new-editor(EditorId?)) .
    snd-msg(se-create(EditorId, Tree)) .
    create(EditorActionsLoop(EditorId, Type, ModuleName, Tree), Pid?) .
    snd-msg(get-auxiliary-buttons(Type, ModuleName)) .
    rec-msg(button-list(ModuleName, ButtonList?)).
    snd-msg(te-edit-text(EditorId, EDITOR, FileName, ButtonList))
  +
    rec-msg(existing-editor(EditorId?)) .
    snd-msg(get-editor-module-name(EditorId)) .
    rec-msg(editor-module-name(EditorId, CurrentModuleName?)) .
    if equal(CurrentModuleName, ModuleName) then
      snd-msg(te-editor-to-front(EditorId))
    else
      Error("Term %s is already being edited for a different module",
	    [FileName])
    fi
  )
endlet

process CloseEditor(ModuleName : str, FileName : str) is
let
  EditorId : term
in
  snd-msg(check-editor-id(FileName, ModuleName)) .
  (
    rec-msg(existing-editor(EditorId?)) .
    snd-msg(kill-editor(EditorId))
  +
    rec-msg(non-existing-editor(FileName))
  )
endlet

process EditorActionsLoop(EditorId: term,
                          Type: term,
			  ModuleName: str,
			  Tree: term) is
let
  ActionEvent : term,
  Pid : int,
  Command : term,
  EditorsSynchronized : bool,
  Offset : int,
  Focus : term,
  NewTree : term,
  SortName: str
in
  subscribe(delete-editor(<term>)) .
  subscribe(syntax-changed(ModuleName)) .
  EditorsSynchronized := true .
  (
    rec-msg(te-menu-event(EditorId, ActionEvent?)) .
    snd-msg(extra-button-action(ActionEvent, [], Type, ModuleName, EditorId)) 
  +
    rec-msg(te-text-editor-disconnected(EditorId)) .
    create(KillEditor(EditorId, Type, ModuleName, EditorsSynchronized), Pid?)
  +
    rec-msg(kill-editor(EditorId)) .
    snd-msg(te-kill-text-editor(EditorId)) .
    create(KillEditor(EditorId, Type, ModuleName, EditorsSynchronized), Pid?)
  +
    rec-msg(te-contents-changed(EditorId)) .
    if equal(EditorsSynchronized, true)
    then
      snd-msg(te-clear-focus(EditorId)) .
      snd-msg(te-display-message(EditorId,
      "Editor contents have changed, focus is unavailable until next parse.")) .
      EditorsSynchronized := false
    else
      tau
    fi
  +
    rec-msg(replace-focus(EditorId, Focus?, NewTree?)) .
    snd-msg(se-replace-focus(EditorId, Focus, NewTree)) .
    EditorsSynchronized := true
  +
    rec-msg(te-mouse-click-at-offset(EditorId, Offset?)) .
    if EditorsSynchronized then
      snd-msg(se-set-cursor-at-offset(EditorId, Offset)) .
      snd-msg(se-get-focus-at-cursor(EditorId)) .
      rec-msg(se-focus-at-cursor(EditorId, Focus?)) .
      snd-msg(se-get-sort-at-cursor(EditorId)) .
      rec-msg(se-sort-at-cursor(EditorId, SortName?)) .
      snd-msg(te-set-focus(EditorId, Focus, SortName))
    else
      tau
    fi
  +
    rec-note(syntax-changed(ModuleName)) .
    EditorsSynchronized := false
  ) *
  rec-note(delete-editor(EditorId)) 
endlet

process KillEditor(EditorId: term,
                   Type: term,
		   ModuleName: str,
		   EditorsSynchronized: bool) is
  snd-note(delete-editor(EditorId)) .
  if equal(Type, syntax-editor) then
    KillModuleEditor(EditorId, ModuleName, EditorsSynchronized) 
  else	
    if not(or(equal(Type, term-editor), equal(Type, equations-editor))) then
      snd-msg(kill-editor-unknown-type-hook(EditorId, Type, ModuleName)) .
      rec-msg(kill-editor-unknown-type-hook-result)
    else
      tau
    fi
  fi.
  snd-msg(se-delete-structure-editor(EditorId)) 

process KillModuleEditor(EditorId : term, Modulename : str, EditorsSynchronized : bool) is
  if equal(EditorsSynchronized, false)
  then
    snd-msg(revert-module(Modulename))
    . rec-msg(module-reverted(Modulename))
  else
    tau
  fi

process KillEditorsForModule(ModuleName: str) is
let
  EditorId: term,
  EditorList: list
in
  snd-msg(get-editors-by-module(ModuleName)) .
  rec-msg(editors-by-module(EditorList?)) .
  (
    if not-equal(EditorList, []) then
      EditorId := first(EditorList) .
      snd-msg(kill-editor(EditorId)) .
      EditorList := next(EditorList)
    fi
  ) *
  if equal(EditorList, []) then
    tau
  fi
endlet
