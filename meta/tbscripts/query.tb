process Query is
let
  EditorId : term,
  ModuleName : str,
  ModuleNames : list(<str>),
  Focus : term,
  Module : term,
  Term : term,
  Errors : term
in
  (
    rec-msg(find-focus-source(EditorId?)) .
    snd-msg(get-focussed-tree(EditorId)) .
    (
      rec-msg(tree(Focus?)) .
      snd-msg(get-all-modules) .
      rec-msg(all-modules(ModuleNames?)) .
      (
        if not-equal(ModuleNames, []) then
        ModuleName := first(ModuleNames) .
	snd-msg(get-sdf2-asfix(ModuleName)) .
	(
	  rec-msg(syntax(Module?)) .
	  snd-msg(query("FindSortDefinition", Module, Focus)) .
	  rec-msg(result("FindSortDefinition", Term?)) .
	  printf("term = %t\n", Term) .
	  if equal(Term, found) then
	    printf("Spawning editor for module %s\n", ModuleName) .
	    snd-msg(edit-syntax(ModuleName)) .
	    ModuleNames := [] /* stop searching after 1st hit, for now */
	  else
	    tau
	  fi
	+
	  rec-msg(unavailable) .
	  printf("Unable to get parse tree for focus!\n")
	) .
        ModuleNames := next(ModuleNames)
        fi
      ) *
      if equal(ModuleNames, []) then
        tau
      fi
    +
      rec-msg(no-tree) .
      printf("No tree!\n")
    )
  ) * delta
endlet
