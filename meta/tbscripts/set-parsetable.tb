/* $Id$
   Get a parsetable for language X
*/

process Set-parsetable is
let
  Module: str, Table: str, Time: term, Id: term
in
Id := process-id.
( rec-msg(set-parsetable(Module?)) .
  snd-msg(get-table-from-db(Module)) .
  (
    rec-msg(table(Table?))
  +
    rec-msg(no-table) .
    snd-note(ui-status(stat(Id,"Generating parse table"))) .
    snd-msg(generate-parsetable(Module)) .
    rec-msg(parsetable(Table?)) .
    snd-msg(get-table-timestamp(Module)) .
    rec-msg(table-timestamp(Time?)) .
    snd-msg(add-table-to-db(Module,Table,Time)) .
    rec-msg(done) .
    snd-note(ui-status(endstat(Id)))
  ).
  snd-msg(reopen-language(Module,Table)) .
  (
    rec-msg(language-opened(Module)) .
    snd-msg(parsetable-set(Module))
  +
    rec-msg(language-not-opened(Module)) .
    snd-msg(parsetable-not-set(Module))
  )
) *
delta
endlet
