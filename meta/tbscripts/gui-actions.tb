#ifndef __GUI_ACTIONS__
#define __GUI_ACTIONS__

#include <graph-utils.tb>
#include <module-manager.tb>
#include <dialog.idef>

process AskQuestion(Question : str, Answer : term?) is
  snd-msg(ask-question(Question))
  . rec-msg(ask-question-result(Answer?))


process PromptForFile(Title : str, 
                      Paths : list, 
		      Modulename : str, 
		      Cancel: bool?,
		      Filename : str?) is
let
  Extension : str
in
  snd-msg(get-modulename-extension(Modulename))
  .
  (
    rec-msg(modulename-extension(Modulename, Extension?))
  +
    rec-msg(no-modulename-extension(Modulename))
    . Extension := ".trm"
  )
  . snd-msg(ask-for-file(Title, Paths, Extension))
  .
  (
    rec-msg(ask-for-file-result(Filename?))
    . Cancel := false
  +
    rec-msg(ask-for-file-cancel)
    . Cancel := true
  )
endlet

process PromptForFileWithExtension(String : str, 
                                   Paths : list, 
				   Extension : str, 
				   Cancel: bool?,
				   Filename : str?) is
  snd-msg(ask-for-file(String, Paths, Extension))
  .
  (
    rec-msg(ask-for-file-result(Filename?))
    . Cancel := false
  +
    rec-msg(ask-for-file-cancel)
    . Cancel := true
  )


process ExitAction is
  shutdown("")

process ShowTreeAction(EditorId : term) is
let
  Tree : term
in
  GetFocus(EditorId, Tree?)
  .
  if not-equal(Tree, UNDEFINED) then
    ShowTree(Tree, false, false)
  else
    tau
  fi
endlet

process ShowFullTreeAction(EditorId : term) is
let
  Tree : term
in
  GetFocus(EditorId, Tree?)
  .
  if not-equal(Tree, UNDEFINED) then
    ShowTree(Tree, true, false)
  else
    tau
  fi
endlet

process ShowSharedTreeAction(EditorId : term) is
let
  Tree : term
in
  GetFocus(EditorId, Tree?)
  .
  if not-equal(Tree, UNDEFINED) then
    ShowTree(Tree, true, true)
  else
    tau
  fi
endlet

process SetStatus(Message : str) is
  snd-msg(gui-set-status(Message))

#endif /*__GUI_ACTIONS__*/
