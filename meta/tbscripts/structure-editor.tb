/*
  $Id$
 */

tool se is {
  command = "java-adapter -TB_HOST localhost -class nl.cwi.gipe.stred.SeTool"
}

process Structure-editor(TE: text-edit, Fid: str, Text: str, Language: str) is
let
  Id: term, SE: se, Fl: list, Foc: term, Loc: int, Str: str, FF: list,
  I: int, T: term, T1: term, T2: term, Ch: int, Li: int, Co: int,
  Char: term, G: str, ErrList: list, Cycle: list, Src:str,
  Fun: str, Args: list
in
  Id := process-id.
  subscribe(disconnected(<term>)).
/* We're not selective about start symbols yet */
  G := "" .
  snd-note(ui-status(stat(Id,"Starting structure editor..."))).
  execute(se, SE?).
/*printf("newEditor is being started\n").*/
  snd-eval(SE, newEditor(Fid, Text)).
/*printf("newEditor waiting\n").*/
  rec-value(SE,Fl?).
/*printf("newEditor is started\n").*/
  Foc := first(Fl).
  snd-msg(TE,set-focus(Fid,Foc)).
  snd-note(ui-status(endstat(Id))). ( ( 
      rec-event(TE,insert(Fid, Loc?, Str?)).
      snd-eval(SE,insertChars(Fid, Loc, Str)).
      snd-ack-event(TE,insert(Fid, Loc, Str))
      +
      rec-event(TE,delete(Fid,Loc?)).
      snd-eval(SE,deleteChars(Fid, Loc)).
      snd-ack-event(TE,delete(Fid,Loc))
    ).
    rec-value(SE,Fl?).
/*    printf("Fl: %t\n", Fl).*/
    Foc := first(Fl).
    snd-msg(TE,set-focus(Fid, Foc))
    +
    rec-event(TE,menu-event("Meta-Environment","Parse",Fid)).
    snd-note(ui-status(stat(Id,"Parsing..."))).
    FF := []. (
      if not-equal(Fl,[]) then
        Foc:= first(Fl).
        snd-msg(TE,get-focus-text(Foc)).
        rec-msg(TE,focus-text(Str?)).
        snd-msg(Id,parse-string-as-asfix1(Language, G, Str)).(
          rec-msg(parse-result-asfix1(Id,parse-tree(T?))).
/* The parser should return two different top-level function symbols for
   succesfull parses, and for parse errors. Until that happens, this is 
   a workaround
*/
          Fun := fun(T).
          if equal(Fun,"parse-error") then
            Args := args(T).
            ErrList := index(Args,1).
            snd-msg(set-error-focus(Fid,Foc,ErrList)). 
            FF := join(FF,Foc)
          else
            snd-eval(SE,replaceFocus(Fid, T)).
            rec-value(SE,Fl?)
          fi
        + rec-msg(parse-result-asfix1(Id,parse-error(ErrList?,ambiguities(I?)))).
          printf("Parse of %s returned %d ambiguities",Str,I).
          printf("Term is: %t\n",T).
          FF :=join(FF,Foc)
        + rec-msg(parse-result-asfix1(Id,parse-error(ErrList?,cycle(Cycle?)))).
          printf("Parse of %s returned a cycle\n",Str).
          printf("Cycle is: %t\n",Cycle).
          FF :=join(FF,Foc)
        + rec-msg(parse-result-asfix1(Id,parse-error(ErrList?,plain))).
          printf("Error parsing %s\n",Str).
          printf("Error: %l\n", ErrList).
          FF := join(FF,Foc)
        )
        .
        snd-eval(SE,nextFocus(Fid)).
        rec-value(SE,Fl?)
      fi
    ) *
    if equal(Fl,[]) then
      if equal(FF,[]) then
        /* All parses succeeded */
        snd-msg(TE,clear-focus)
      else
        /* Some parses were ambiguous */
        snd-eval(SE,setFocus(Fid, FF)).
        rec-value(SE,Fl?)
      fi
    fi.
    /* Finished parsing operations */
    snd-note(ui-status(endstat(Id))).
    snd-ack-event(TE,menu-event("Meta-Environment","Parse",Fid))
    +
    rec-event(TE,menu-event("Meta-Environment","Reduce",Fid)).
    if equal(FF,[]) then
      snd-note(ui-status(stat(Id,"Rewriting..."))).
      snd-eval(SE,getTree(Fid)).
      rec-value(SE,T1?).
      snd-msg(rewrite(id(Language),T1)).
      rec-msg(result(T2?)).
      snd-msg(pp(T2)).
      rec-msg(source(Src?)).
      printf("\nNormal form is:\n%s\n", Src).
      snd-note(ui-status(endstat(Id)))
    fi.
    snd-ack-event(TE,menu-event("Meta-Environment","Reduce",Fid))
    +
    rec-event(TE,get-focus(Fid,I?)).
    snd-eval(SE,getFocus(Fid, I)).
    rec-value(SE,Foc?).
    snd-msg(TE,set-focus(Fid,Foc)).
    snd-eval(SE,setFocus(Fid,Foc)).
    rec-value(SE,Fl?).
printf("New focus list: %t\n", Fl).
    snd-ack-event(TE,get-focus(Fid,I))
    +
    rec-event(TE,menu-event("Meta-Environment","Show Tree", Fid)).
    printf("Tree display not yet implemented\n").
    snd-ack-event(TE,menu-event("Meta-Environment","Show Tree",Fid))
    +
    rec-event(TE,menu-event("Move",Str?,Fid)).
    snd-eval(SE,moveFocus(Fid,Str)).
    rec-value(SE,Fl?).
    Foc := first(Fl).
    snd-msg(TE,set-focus(Fid,Foc)).
    snd-ack-event(TE,menu-event("Move",Str,Fid))
  ) *
  rec-note(disconnected(TE)).
  snd-terminate(SE,"Editor terminated")
endlet
