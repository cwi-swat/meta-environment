#ifndef __SDF_PROGRESS_LISTENER__
#define __SDF_PROGRESS_LISTENER__

process SDFStatusToProgress is
let
  ModuleId: term,
  Name: str,
  OldValue: term,
  NewValue: term
in
  MM-Subscribe-Attribute-Changed(<term>,SDF_NAMESPACE,"status",<term>,<term>)
  . MM-Subscribe-Attribute-Changed(<term>,SDF_NAMESPACE,"name",<term>,<term>)
  . subscribe(mm-module-deleted(<term>))
  .
  (
    rec-note(mm-attribute-changed(attribute-changed-event(ModuleId?, SDF_NAMESPACE, "status", OldValue?, NewValue?)))
    . snd-msg(pro-set-status(ModuleId, SDF_NAMESPACE, NewValue))
  +
    rec-note(mm-attribute-changed(attribute-changed-event(ModuleId?, SDF_NAMESPACE, "name", OldValue?, NewValue?)))
    . snd-msg(pro-set-message(ModuleId, NewValue))
  +
    rec-note(mm-module-deleted(ModuleId?)) 
    . snd-msg(pro-remove-status(ModuleId))
  ) * delta 
endlet

toolbus(SDFStatusToProgress)

#endif /* __SDF_PROGRESS_LISTENER__ */
