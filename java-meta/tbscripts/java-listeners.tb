#ifndef __JAVA_LISTENERS__
#define __JAVA_LISTENERS__

#include <module-manager.tb>
#include <java-namespace.h>
#include <progress.idef>

#define BG_ERROR_COLOR rgb(224,64,60)
#define BG_CHILD_ERROR_COLOR rgb(255,128,114)
#define BG_CORRECT_COLOR rgb(255,255,221)
#define BG_GRAYED_OUT rgb (211,211,211)

process InitializeInheritedStatus is
  /* the first one must not have any <term> holes because of a ToolBus bug */
  
  /* Once we are opened and our kids are opened, then we become complete */
  MM-RegisterInheritedAttribute(JAVA_NAMESPACE, "status", is-set, opened, opened, complete, all)

  /* When we are not in error and one of our kids has an error, we become child-error */
  . MM-RegisterInheritedAttribute(JAVA_NAMESPACE, "status", is-not-set, error, error, child-error, one)

toolbus(InitializeInheritedStatus)


process JAVAModuleLabelSetter is
let
  Event: attribute-changed-event,
  Extension: str,
  File: file,
  ModuleId: term,
  Modulename: str,
  Name: str,
  Prefix: str
in
  MM-Subscribe-Attribute-Changed(<term>,JAVA_NAMESPACE,"name",<term>,<term>)
  .
  (
    rec-note(mm-attribute-changed(Event?))
    . ACE-GetModuleId(Event, ModuleId?)
    . ACE-GetNewValue(Event, Modulename?)
    . SplitFilename(Modulename, Name?, Prefix?, Extension?)
    . MM-SetModuleGraphAttribute(ModuleId, "label", label(Name))
    . snd-msg(io-make-file(Modulename))
    . rec-msg(io-file(Modulename, File?))
    . MM-SetModuleGraphAttribute(ModuleId, "file", file(File))
  )
  *
  delta
endlet

toolbus(JAVAModuleLabelSetter)

process JAVAModuleColorChanger is
let
  Event: attribute-changed-event,
  ModuleId: term,
  NewPath: term,
  Tree: term,
  Value: term
in
  MM-Subscribe-Attribute-Changed(<term>,JAVA_NAMESPACE,"status",<term>,error)  
  . MM-Subscribe-Attribute-Changed(<term>,JAVA_NAMESPACE,"status",<term>,child-error)  
  . MM-Subscribe-Attribute-Changed(<term>,JAVA_NAMESPACE,"status",error,<term>)  
  . MM-Subscribe-Attribute-Changed(<term>,JAVA_NAMESPACE,"status",child-error,<term>)  
  .
  (
    rec-note(mm-attribute-changed(Event?))
    . ACE-GetModuleId(Event, ModuleId?)
    . ACE-GetNewValue(Event, Value?)
    .
    if equal(Value, error) then
      MM-SetModuleGraphAttribute(ModuleId, "bgcolor", fill-color(BG_ERROR_COLOR))
    else
      if equal(Value,child-error) then
        MM-SetModuleGraphAttribute(ModuleId, "bgcolor", fill-color(BG_CHILD_ERROR_COLOR))

      else
        MM-SetModuleGraphAttribute(ModuleId, "bgcolor", fill-color(BG_CORRECT_COLOR))
      fi
    fi
  )
  *
  delta
endlet

toolbus(JAVAModuleColorChanger)

process JAVAModuleVisualizeEditable is
let
  Event: attribute-changed-event,
  ModuleId: term,
  NewPath: term,
  Tree: term,
  Value: term
in
  MM-Subscribe-Attribute-Changed(<term>,JAVA_NAMESPACE,"editable",<term>,true)  
  . MM-Subscribe-Attribute-Changed(<term>,JAVA_NAMESPACE,"editable",<term>,false)  
  .
  (
    rec-note(mm-attribute-changed(Event?))
    . ACE-GetModuleId(Event, ModuleId?)
    . ACE-GetNewValue(Event, Value?)
    .
    if equal(Value, quote(false)) then
      MM-SetModuleGraphAttribute(ModuleId, "bgcolor", fill-color(BG_GRAYED_OUT))
    else
      MM-SetModuleGraphAttribute(ModuleId, "bgcolor", fill-color(BG_CORRECT_COLOR))
    fi
  )
  *
  delta
endlet


toolbus(JAVAModuleVisualizeEditable)

process JAVAModuleParser is
let
  Event: attribute-changed-event,
  ModuleId: term,
  Tree: term
in
  MM-Subscribe-Attribute-Changed(<term>,JAVA_NAMESPACE,"status",<term>,available)
  .
  (
    rec-note(mm-attribute-changed(Event?))
    . ACE-GetModuleId(Event, ModuleId?)
    . GetJAVAParseTree(ModuleId, Tree?)
    .
    if equal(Tree, UNDEFINED) then
       MM-SetAttribute(ModuleId, JAVA_NAMESPACE, "status", error)
    else
       MM-SetAttribute(ModuleId, JAVA_NAMESPACE, "status", parsed)
    fi
    . Tree := help-garbage-collector
  )
  *
  delta
endlet

toolbus(JAVAModuleParser)

process JAVAModuleIdentifier is
let
  Event: attribute-changed-event,
  ModuleId: term,
  Tree: term,
  Result: term
in
  MM-Subscribe-Attribute-Changed(<term>,JAVA_NAMESPACE,"status",<term>,parsed)
  .
  (
    rec-note(mm-attribute-changed(Event?))
    . ACE-GetModuleId(Event, ModuleId?)
    . JAVAIdentifyModule(ModuleId, Result?)
    . MM-SetAttribute(ModuleId, JAVA_NAMESPACE, "status", Result)
  )
  *
  delta
endlet

toolbus(JAVAModuleIdentifier)



process JAVASetModuleImports is
let
  Event: attribute-changed-event,
  Import: str,
  ImportId: term,
  Imports: list,
  ModuleId: term,
  Tree: term
in
  MM-Subscribe-Attribute-Changed(<term>,JAVA_NAMESPACE,"status",<term>,identified)
  .
  (
    rec-note(mm-attribute-changed(Event?))
    . ACE-GetModuleId(Event, ModuleId?)
    . MM-SetAttribute(ModuleId, JAVA_NAMESPACE, "status", opening)
    . GetJAVAParseTree(ModuleId, Tree?)
    . MM-DeleteModuleDependencies(ModuleId)
    . GetImportedModulenames(Tree, Imports?)
    .
    if not-equal(Imports, []) then 
      if not-equal(Imports, []) then 
        Import := first(Imports)
        . Imports := next(Imports)
        . OpenModule(Import, ImportId?)
        . MM-AddModuleDependency(ModuleId, ImportId)
      fi
      *
      if equal(Imports, []) then
        MM-SetAttribute(ModuleId, JAVA_NAMESPACE, "status", opened)
      fi
    else
      MM-SetAttribute(ModuleId, JAVA_NAMESPACE, "status", opened) 
    fi
  )
  *
  delta
endlet

toolbus(JAVASetModuleImports)

process JAVARemoveDependencies is
let
  Event: attribute-changed-event,
  ModuleId: term
in
  MM-Subscribe-Attribute-Changed(<term>,JAVA_NAMESPACE,"status",<term>,unknown)
  .
  (
    rec-note(mm-attribute-changed(Event?))
    . ACE-GetModuleId(Event, ModuleId?)
    . MM-DeleteModuleDependencies(ModuleId)
  )
  *
  delta
endlet

toolbus(JAVARemoveDependencies)

#endif /* __JAVA_LISTENERS__ */
