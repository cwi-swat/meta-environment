#ifndef __JAVA_EDITOR_UTILS__
#define __JAVA_EDITOR_UTILS__

#include <editing.tb>
#include <config-utils.tb>

process EditSyntax(ModuleId: term) is
let
  AlreadyExists: bool,
  Available: bool,
  EditorType: term,
  HasAttribute: bool,
  HasText: bool,
  HasStructure: bool,
  Modulename: str,
  Path: str,
  Editable: bool,
  Sid: term,
  Event: attribute-changed-event,
  Tree: term
in
  EditorType := syntax-editor
  . GetModulePath(ModuleId, JAVA_NAMESPACE, Path?)
  . 
  if equal(Path, EMPTY_PATH) then
    TODO("dit is ook raar\n")
    . MM-GetAttribute(ModuleId, JAVA_NAMESPACE, "name", Modulename?) 
    . BuildPath(".", Modulename, JAVA_EXTENSION, Path?)
    . printf("EditSyntax: Use CreateNewModule instead???\n")
  else
    tau
  fi
  . Edit(Path, Sid?)
  . EditText(Sid, EditorType, "", AlreadyExists?)
  . snd-msg(syntax-editor-started(ModuleId, Sid))
  .
  if equal(AlreadyExists, false) then
    MM-HasAttribute(ModuleId, JAVA_NAMESPACE, "editable", HasAttribute?)
    .
    if equal(HasAttribute, true) then
      MM-GetAttribute(ModuleId, JAVA_NAMESPACE, "editable", Editable?)
    else
      Editable := true
    fi
    . snd-msg(te-set-editable(Sid, Editable))
    . snd-msg(em-bind-session(Sid, ModuleId))
    . rec-msg(em-session-bound(Sid, ModuleId))
    . GetJavaParseTree(ModuleId, Tree?)
    . SE-UpdateTree(Sid, Tree)
    . MM-Subscribe-Attribute-Changed(ModuleId,JAVA_NAMESPACE,"status",<term>,parsed)
    .
    (
      MenuSelected(Sid, EditorType)
    +
      MouseClicked(Sid)
    +
      rec-msg(te-contents-changed(Sid))
      . MM-SetAttribute(ModuleId, JAVA_NAMESPACE, "status", edited)
    +
      rec-msg(te-contents-saved(Sid))
      . MM-SetAttribute(ModuleId, JAVA_NAMESPACE, "status", available)
      . UpdateSyntaxStructure(Sid)
    +
      rec-note(mm-attribute-changed(Event?))
      . UpdateSyntaxStructure(Sid)
    )
    *
    EditorDisconnected(Sid)
    . DeleteSession(Sid)
  else
    tau
  fi
endlet

process UpdateSyntaxStructure(Sid: term) is
let
  ModuleId: term,
  Slices: term,
  Tree: term
in
  GetModuleId(Sid, ModuleId?)
  . GetJavaParseTree(ModuleId, Tree?)
  . SE-UpdateTree(Sid, Tree) 
endlet

#endif /* __JAVA_EDITOR_UTILS__ */
