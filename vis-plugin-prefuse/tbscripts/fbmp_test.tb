


#define TRY(a) if not-equal(true, Error) then \
                 ( a ) \
                 + \
                 Error := true delay(5000) \
               else \
                 tau \
               fi \


/**
 * Part of scenario's 1 and 6 where and RStore is loaded into the Rstore
 * Container and shown in the FactBrowser.
 *
 * out msg:rc-load-rstore(filename)
 * in msg:rc-rstore-loaded(filename, rstoreId)
 * out msg:rc-get-rstore-facts(rstoreId)
 * in msg:rc-get-rstore-facts(rstoreId, facts[] <factId, factName, rtype>)
 * out msg:fb-show-rstore-facts(filename, rstoreId, facts[])
 *
 * @author Bas Basten & Anton Lycklama a Nijeholt
 * @date 16-03-2007
 */ 
process SCENARIO-LOAD-RSTORE(RStoreFile : str, Error : bool?) is
let
  RStoreId : int,
  Facts : term
in
  TRY(rec-msg(rc-load-rstore(RStoreFile))) .

  RStoreId := 123 .    
  TRY(snd-msg(rc-rstore-loaded(RStoreFile, RStoreId))) .
  TRY(rec-msg(rc-get-rstore-facts(RStoreId))) .

  Facts := [[1, "fact1", "rtype1" ], [2, "fact2", "rtype2"]] .        
  TRY(snd-msg(rc-get-rstore-facts(RStoreId, Facts))) .
  TRY(rec-msg(fb-show-rstore-facts(RStoreFile, RStoreId, Facts)))
endlet


/**
 * Scenario 1: The user asks to load an RStore
 * in msg:fb-load-rstore(filename)
 * followed by SCENARIO-LOAD-RSTORE
 * 
 * @author Bas Basten & Anton Lycklama a Nijeholt
 * @date 07-03-2007
 */ 
process SCENARIO-1 is
let
  RStoreFile : str,
  Error : bool
in
  (
    rec-msg(start-scenario(1)) .

    Error := false .
    
    RStoreFile := "something.rstore" .    
    TRY(snd-msg(fb-load-rstore(RStoreFile))) .        
	
	SCENARIO-LOAD-RSTORE(RStoreFile, Error?) .

    if not-equal(true, Error) then
      snd-msg(scenario-successful(1))
    else
      snd-msg(scenario-failed(1))
    fi
  )
  *
  delta
endlet


/**
 * Scenario 2: The user selects a fact in the FactBrowser
 * in msg:fb-type-selected(rtype)
 * out note:vp-get-available-visualization-plugins(rtype)
 * in note:vp-get-available-visualization-plugins(rtype, pluginId, pluginName)
 * out msg:fb-visualization-plugin-available(rtype, pluginId, pluginName)
 *
 * @author Bas Basten & Anton Lycklama a Nijeholt
 * @date 07-03-2007 
 */
process SCENARIO-2 is
let
  RType : term,
  PluginId1 : int,
  PluginName1 : str,
  PluginId2 : int,
  PluginName2 : str,
  Error : bool
in
  subscribe(vp-get-available-visualization-plugins(<term>)) .
  (
    rec-msg(start-scenario(2)) .
  
    Error := false .
    
    RType := "rtype1" .        
    TRY(snd-msg(fb-type-selected(RType))) .
    TRY(rec-note(vp-get-available-visualization-plugins(RType))) .

    %% Simulate Plugin 1
    PluginId1 := 456 .
    PluginName1 := "SomeCoolMetaStudioVisualizationPlugin" .    
    TRY(snd-note(vp-get-available-visualization-plugins(RType, PluginId1, PluginName1))) .    
    TRY(rec-msg(fb-visualization-plugin-available(RType, PluginId1, PluginName1))) .        
    
    %% Simulate Plugin 2
    PluginId2 := 133 .
    PluginName2 := "MetaStudioVisualizationPlugin" .
    TRY(snd-note(vp-get-available-visualization-plugins(RType, PluginId2, PluginName2))) .                
    TRY(rec-msg(fb-visualization-plugin-available(RType, PluginId2, PluginName2))) .

    if not-equal(true, Error) then
      snd-msg(scenario-successful(2))
    else
      snd-msg(scenario-failed(2))
    fi
  )
  *
  delta
endlet


/**
 * Scenario 3: The user starts a visualization
 * in msg:fb-visualization-selected(rstoreId, factId, pluginId)
 * out msg:rc-get-fact-data(rstoreId, factId)
 * in msg:rc-get-fact-data(rstoreId, factId, rtuple)
 * out msg: vp-visualize-fact(pluginId, rstoreId, factId, rtuple)
 *
 * @author Bas Basten & Anton Lycklama a Nijeholt
 * @date 07-03-2007
 */
process SCENARIO-3 is
let
  RStoreId : int,
  FactId : int,
  PluginId : int,
  RTuple : term,
  Error : bool
in
  (
    rec-msg(start-scenario(3)) .

    Error := false .

    RStoreId := 123 .
    FactId := 456 .
    PluginId := 789 .
    TRY(snd-msg(fb-visualization-selected(RStoreId, FactId, PluginId))) .
    TRY(rec-msg(rc-get-fact-data(RStoreId, FactId))) .

    RTuple := ["SomeTuple", "rtype1", [["a", 1], ["b", 2], ["c", 3]]] .
    TRY(snd-msg(rc-get-fact-data(RStoreId, FactId, RTuple))) .
    TRY(rec-msg(vp-visualize-fact(PluginId, RStoreId, FactId, RTuple))) .

    if not-equal(true, Error) then
      snd-msg(scenario-successful(3))
    else
      snd-msg(scenario-failed(3))
    fi
  )
  *
  delta
endlet


/**
 * Scenario 4: The user clicks a link in a visualization plugin
 * in msg: vp-link-clicked(Location)
 * out msg: fbmp-open-location(Location)
 *
 * @author Bas Basten & Anton Lycklama a Nijeholt
 * @date 16-03-2007
 */
process SCENARIO-4 is
let
  Location : term,
  Error : bool
in
  (
    rec-msg(start-scenario(4)) .

    Error := false .

    Location := "area-in-file(\"somefile.java\", area(1,2,3,4,5,6))" .
    TRY(snd-msg(vp-link-clicked(Location))) .
    TRY(rec-msg(fbmp-open-location(Location))) .

    if not-equal(true, Error) then
      snd-msg(scenario-successful(4))
    else
      snd-msg(scenario-failed(4))
    fi
  )
  *
  delta
endlet 

/**
 * Scenario 5: The user unloads an RStore
 * in msg: fb-unload-rstore(rstoreId)
 * out msg: rc-unload-rstore(rstoreId)
 *
 * @author Bas Basten & Anton Lycklama a Nijeholt
 * @date 16-03-2007
 */
process SCENARIO-5 is
let
  RStoreId : int,
  Error : bool
in
  (
    rec-msg(start-scenario(5)) .

    Error := false .

    RStoreId := 951 .
    TRY(snd-msg(fb-unload-rstore(RStoreId))) .
    TRY(rec-msg(rc-unload-rstore(RStoreId))) .

    if not-equal(true, Error) then
      snd-msg(scenario-successful(5))
    else
      snd-msg(scenario-failed(5))
    fi
  )
  *
  delta
endlet


/**
 * Scenario 6: An RStore is exported and loaded into the Factbrowser
 * in msg: vp-rstore-exported(filename)
 * followed by SCENARIO-LOAD-RSTORE
 *
 * @author Bas Basten & Anton Lycklama a Nijeholt
 * @date 16-03-2007
 */
process SCENARIO-6 is
let
  RStoreFile : str,
  Error : bool
in
  (
    rec-msg(start-scenario(6)) .

    Error := false .

    RStoreFile := "something.rstore" .    
    TRY(snd-msg(vp-rstore-exported(RStoreFile))) .        
	
	SCENARIO-LOAD-RSTORE(RStoreFile, Error?) .

    if not-equal(true, Error) then
      snd-msg(scenario-successful(6))
    else
      snd-msg(scenario-failed(6))
    fi
  )
  *
  delta
endlet


tool fbmp-test-tool is {}

/**
 * FactBrowser Management Process Test Stub.
 * This test process creates 3 scenario processes.
 * each scenario can be remotely started from the
 * Java ToolStub. If the scenario succeeds the tool
 * will receive a snd-do(scenario-successful<int>)
 * with the corresponding started scenario number.
 *
 * @author Bas Basten & Anton Lycklama a Nijeholt
 * @date 07-03-2007
 */
process FBMP-TEST is
let
  Tid : fbmp-test-tool,
  ScenarioId : int,
  Pid : int
in
  create(SCENARIO-1, Pid?) .
  create(SCENARIO-2, Pid?) .
  create(SCENARIO-3, Pid?) .
  create(SCENARIO-4, Pid?) .
  create(SCENARIO-5, Pid?) .
  create(SCENARIO-6, Pid?) .
  
  rec-connect(Tid?) .
  (
    rec-event(Tid, start-scenario(ScenarioId?)) .
    snd-ack-event(Tid, start-scenario(ScenarioId)) .
    snd-msg(start-scenario(ScenarioId)) .
    (
      rec-msg(scenario-successful(ScenarioId)) .
      snd-do(Tid, scenario-successful(ScenarioId))
    )
    +
    (
      rec-msg(scenario-failed(ScenarioId)) .
      snd-do(Tid, scenario-failed(ScenarioId))
    )
  ) * delta
endlet
