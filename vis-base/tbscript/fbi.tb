#ifndef _FBI_
#define _FBI_

tool factbrowser is { }

/**
 * @desc FactBrowser Connection Process
 *
 * @author Renze de Vries & Arjen van Schie & Michel Rijnders
 * @date 16-07-2007
 *
 * Reviewed & Comments written by Raymond Bergen
 * Date: 07-03-2007
 */

process StartFBI is
  snd-msg(load-jar("file:///home/jurgenv/glt/svn-installed/vis-base/share/vis-base.jar", "/home/jurgenv/glt/svn-installed/vis-base/share/relationstores.jar:/home/jurgenv/glt/svn-installed/vis-base/share/log4j-1.2.14.jar:/home/jurgenv/glt/svn-installed/vis-base/share/commons-logging-1.1.jar:/home/jurgenv/glt/svn-installed/vis-base/share/gui-util.jar:/home/jurgenv/glt/svn-installed/vis-base/share/gui-data.jar::/home/jurgenv/glt/svn-installed/aterm-java/share/aterm-java.jar:/home/jurgenv/glt/svn-installed/shared-objects/share/shared-objects.jar:/home/jurgenv/glt/svn-installed/JJTraveler/share/jjtraveler.jar:/home/jurgenv/glt/svn-installed/config-support/share/configapi.jar:/home/jurgenv/glt/svn-installed/toolbus-java-adapter/share/toolbus-java-adapter.jar:/home/jurgenv/glt/svn-installed/meta-studio/share/meta-studio.jar:/home/jurgenv/glt/svn-installed/meta-studio/share/idw-gpl-1.5.0.jar:/home/jurgenv/glt/svn-installed/svg-support/share/svg-support/batik.jar:/home/jurgenv/glt/svn-installed/rstore-container/share/rstore-container.jar:/home/jurgenv/glt/svn-installed/rstore-container/share/log4j-1.2.14.jar:/home/jurgenv/glt/svn-installed/rstore-container/share/junit.jar:/home/jurgenv/glt/svn-installed/rstore-container/share/commons-logging-1.1.jar:/home/jurgenv/glt/svn-installed/rstore-container/share/relationstores.jar:/home/jurgenv/glt/svn-installed/tunit/share/tunit.jar:/home/jurgenv/glt/svn-installed/tunit/share/tunit/junit.jar:/home/jurgenv/glt/svn-installed/error-support/share/error-support/errorapi.jar:/home/jurgenv/glt/svn-installed/error-support/share/error-support/locationapi.jar:/home/jurgenv/glt/svn-installed/console-gui/share/console-gui.jar:/home/jurgenv/glt/svn-installed/dialog-gui/share/dialog-gui.jar:/home/jurgenv/glt/svn-installed/editor-plugin/share/editor-plugin.jar:/home/jurgenv/glt/svn-installed/error-gui/share/error-gui.jar:/home/jurgenv/glt/svn-installed/error-gui/share/jlfgr-1_0.jar:/home/jurgenv/glt/svn-installed/graph-gui/share/graph-painter.jar:/home/jurgenv/glt/svn-installed/graph-gui/share/prefuse.jar:/home/jurgenv/glt/svn-installed/graph-support/share/graph.jar:/home/jurgenv/glt/svn-installed/module-support/share/moduleapi.jar:/home/jurgenv/glt/svn-installed/io-support/share/ioapi.jar:/home/jurgenv/glt/svn-installed/module-details-gui/share/moduledetails.jar:/home/jurgenv/glt/svn-installed/module-manager/share/module-manager.jar:/home/jurgenv/glt/svn-installed/navigator-gui/share/navigator.jar:/home/jurgenv/glt/svn-installed/progress-gui/share/progress.jar:/home/jurgenv/glt/svn-installed/console-grabber/share/console-grabber.jar"))

process FBI is
let 
  Tid: factbrowser,
  FactType: term,
  PluginId: int,
  PluginName: str,
  RstorePath: str,
  RstoreName: str,
  RstoreId: int,
  RstoreFacts: list,
  FactId: int,
  FileName: str
in
  printf("[FBI] FactBrowser Interface creation\n")

    . rec-connect(Tid?)
    . printf("[FBI] FactBrowser connected\n")
    %% Added by: Bas Basten & Anton Lycklama a Nijeholt
    . subscribe(rc-rstore-unloaded(<int>))
    .
    if true then
    (
      /* Recieve the available visualisation plugins and their supported fact types */
      rec-msg(fb-visualization-plugin-available(FactType?, PluginId?, PluginName?))
      . printf("[FBI] VisualizationPlugin registration message received\n")
       . snd-do(Tid, fb-add-visualization-plugin(FactType, PluginId, PluginName))
    )
    +
    (
      /* Recieve a load rstore request and initialize the open file dialog */
      rec-event(Tid, fb-load-rstore)
      . snd-ack-event(Tid, fb-load-rstore)
      . snd-msg(ask-for-file("Select the RStore", [], ".rstore"))
      . 
      (   
         (
            /* Recieve the path of the selected file and initialize the loading of the rstore */
            rec-msg(ask-for-file-result(FileName?))
            . snd-msg(fb-load-rstore(FileName))
         )
         + 
         (
            rec-msg(ask-for-file-cancel)
         )
      )	
    )
    +
    (
      /* Recieve the selected facttype and initialize the gathering of the available visiualizations */
      rec-event(Tid, fb-type-selected(FactType?))
     
       %%Added By: Raymond Bergen & Srinivasan Tharmarajah
      . snd-ack-event(Tid, fb-type-selected(FactType))
     
      . printf("[FBI] A FactType event was received\n")
      . snd-msg(fb-type-selected(FactType))
    )
    +
    (
      /* Recieve the rstore facts and initialize the displaying of the facts in the factbrowser */
      rec-msg(fb-show-rstore-facts(RstoreName?, RstoreId?, RstoreFacts?))
      . printf("[FBI] Receiving Rstore with Facts\n")
      . snd-do(Tid, fb-show-rstore-facts(RstoreName, RstoreId, RstoreFacts))
    )
    +
    (
      /* Recieve the selected visualization type and initialize the display of the visualization */
      rec-event(Tid, fb-visualization-selected(RstoreId?, FactId?, PluginId?))
    
      %%Added By: Raymond Bergen & Srinivasan Tharmarajah
      . snd-ack-event(Tid, fb-visualization-selected(RstoreId, FactId, PluginId))
      . printf("[FBI] fb-visualization-selected request received\n")
      . snd-msg(fb-visualization-selected(RstoreId, FactId, PluginId))
    )    
    +
    (
      %% Added by: Bas Basten & Anton Lycklama a Nijeholt
      rec-event(Tid, fb-unload-rstore(RstoreId?))
      . printf("[FBI] fb-unload-rstore event recieved\n")
      . snd-ack-event(Tid, fb-unload-rstore(RstoreId))
      . snd-msg(fb-unload-rstore(RstoreId))
    )
    +
    (
      %% Added by: Bas Basten & Anton Lycklama a Nijeholt    
      rec-note(rc-rstore-unloaded(RstoreId?))
      . printf("[FBI] rc-rstore-unloaded note recieved\n")
      . snd-do(Tid, fb-rstore-unloaded(RstoreId))
    )
    .
    printf("[FBI] FactBrowser process closed\n")
  fi *
    delta
endlet


toolbus(FBI)
toolbus(StartFBI)

#endif /*_FBI_*/
