process UI is
  let Tid : ui
  in
     execute(ui, Tid?) .
     ( CALC-BUTTON(Tid) 
       + 
       QUIT-BUTTON(Tid) 
     ) * delta
  endlet

tool ui is {command = "wish-adapter -script ui-calc.tcl"}


process CALC-BUTTON(Tid : ui) is
  let  E : str, V : int
  in
     rec-event(Tid, button(calc)) .
     snd-eval(Tid, get-expr-dialog).
     ( rec-value(Tid, cancel)
       + 
       rec-value(Tid, expr(E?)) .
       snd-msg(compute, E) . 
       rec-msg(compute, E, V?) .
       snd-do(Tid, display-value(V))
     ) . snd-ack-event(Tid, button(calc))
  endlet

process QUIT-BUTTON(Tid : ui) is
  rec-event(Tid, button(quit)) .
  snd-ack-event(Tid, button(quit)) .
  shutdown("Bye!")

process CALC is
  let Tid : calc, E : str, V : int
  in
     execute(calc, Tid?) .
     ( rec-msg(compute, E?) .
       snd-eval(Tid, expr(E)) .
       rec-value(Tid, V?) .
       snd-msg(compute, E, V)
     ) * delta
  endlet

tool calc is {command = "./calc"}


toolbus(UI, CALC)
