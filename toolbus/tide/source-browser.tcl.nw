
%{{{ source-browser.tcl

This tcl script creates the master control window, and
handles user interaction for it.

<<source-browser.tcl>>=
<<create-viewer>>
<<message>>
<<message-dialog>>
<<highlight>>
<<remove-highlight>>
<<load-module>>
<<load-file>>

<<do-location-selected>>
<<do-toggle-simple-break>>

<<tide-dap-connected-sb>>
<<tide-dap-disconnected-sb>>
<<tide-observation-point-set-sb>>
<<tide-observation-point-cleared-sb>>

<<tide-exec-state-sb>>
<<tide-cpe-sb>>
<<tide-current-port-sb>>

<<track-source>>
<<exception>>

set Viewers {}

set Style(breakpoint)	"-background orange"
set Style(watchpoint)	"-background green"
set Style(cpe-before)	"-borderwidth 2 -relief groove -foreground blue"
set Style(cpe-after)	"-borderwidth 2 -relief ridge -foreground blue"
set Style(selected)	"-borderwidth 2 -relief sunken"
set Style(exception)	"-borderwidth 2 -relief raised -background yellow"

wm withdraw .
source "tide-modules.tcl"
tide-init "Source browser" { 
  daps 
  processes 
  observation-points 
  cpe 
  exec-state 
  exec-control 
  current-port
  ui-process-list
}
@

%}}}

%{{{ proc create-viewer { dap }

Create a new viewer window.

<<create-viewer>>=
proc create-viewer { dap } {
  global Viewers Viewer Dap Proc ES Style
 
  set w .$dap

  set Dap($dap,viewer) 1
  set Viewer($dap,win) $w
  set Viewer($dap,wbreaks) ""
  set Viewer($dap,module) "<none>"

  toplevel $w
  wm positionfrom $w program
  wm sizefrom $w program
  wm title $w "Source Viewer: $dap"
  wm maxsize $w 1152 900

  frame $w.menu
  frame $w.toolbar -borderwidth 2 -relief groove
  frame $w.middle
  frame $w.buttons

  frame $w.middle.left

  label $w.middle.left.label -text "Source:"
  frame $w.middle.left.f

  text $w.middle.left.f.text -relief sunken -borderwidth 1 \
	-xscrollcommand "$w.middle.left.x set" \
	-yscrollcommand "$w.middle.left.f.y set" \
        -width 2 -height 2 \
	-wrap none
  set Viewer($dap,wtext) $w.middle.left.f.text
  bind $Viewer($dap,wtext) <Key> ""
  bind $Viewer($dap,wtext) <ButtonPress-1> "do-location-selected $dap %x %y"
  bind $Viewer($dap,wtext) <ButtonPress-2> "do-toggle-simple-break $dap %x %y"

  scrollbar $w.middle.left.f.y -orient vert -command "$Viewer($dap,wtext) yview"
  scrollbar $w.middle.left.x -orient hor -command "$Viewer($dap,wtext) xview"
  label $w.middle.left.msg -textvariable Viewer($dap,msg) -borderwidth 2 -relief groove

  tide-ui-process-list-create $dap $w.middle.procs
  if { [lsearch $Dap($dap,info) parallelism] == -1 } {
    pack $w.middle.procs -side top -fill x
  } else {
    pack $w.middle.procs -side left -fill y
  }

  pack $w.middle.left.f.text -side left -expand 1 -fill both
  pack $w.middle.left.f.y -side left -fill y
  pack $w.middle.left.f -side top -expand 1 -fill both
  pack $w.middle.left.x -side top -fill x
  pack $w.middle.left.msg -side top -fill x

  pack $w.middle.left -side left -expand 1 -fill both

  pack $w.menu -side top -fill x
  pack $w.toolbar -side top -fill x
  pack $w.middle -side top -expand 1 -fill both
  pack $w.buttons -side bottom -fill y
}
@

%{{{ exec-control buttons

When the debugging adapter supports execution control primitives,
we need to add a button for each of them.

<<exec-control buttons>>=
  if { [lsearch $Dap($dap,info) exec-control] != -1 } {
    foreach elem $Dap($dap,info,exec-control) {
      set file "@bitmaps/${elem}.xbm"
      button $w.toolbar.button$elem \
		-bitmap $file \
		-command "do-exec-control-button $dap exec-control $elem" \
		-state normal
      pack $w.toolbar.button$elem -side left
      set Viewer($dap,button,$elem) $w.toolbar.button$elem
    }
    frame $w.toolbar.f1 -width 8
    pack $w.toolbar.f1
  }
@

%}}}
%{{{ create process list

When the debugging adapter can support more than one process
(indicated by the parallelism event class), we need to add
a process list window.

<<create process list>>=
  if { [lsearch $Dap($dap,info) parallelism] != -1 } {
    frame $w.middle.right
    label $w.middle.right.label -text "Processes:"
    tixScrolledHList $w.middle.right.list -width 120
    checkbutton $w.middle.right.selection \
	-text "Select all" \
	-onvalue all \
	-offvalue partial \
	-variable Viewer($dap,pid-selection) \
	-command "do-select-all $dap"

    set Viewer($dap,wprocs) $w.middle.right.list
    $Viewer($dap,wprocs) subwidget hlist configure -selectmode extended
    $Viewer($dap,wprocs) subwidget hlist configure \
		-browsecmd "do-process-selected $dap"

    pack $w.middle.right.label -side top -fill x
    pack $w.middle.right.list -side top -expand 1 -fill both
    pack $w.middle.right.selection -side top -fill x
    pack $w.middle.right -fill both
 
    foreach pid $Dap($dap,pids) {
      $Viewer($dap,wprocs) subwidget hlist add $pid \
		-itemtype imagetext \
		-bitmap $ES($Proc($dap,$pid,es)) \
		-text $Proc($dap,$pid,name)
    }
    pack $w.middle.right -side left -fill y
  }
@

%}}}

%}}}
%{{{ proc message { dapargs }

Print a message in the message window.

<<message>>=
proc print-message { dap args } {
  global Dap Viewer

  if { $Dap($dap,viewer) } {
    set Viewer($dap,msg) [eval format $args]
  }
}
@

%}}}
%{{{ proc message-dialog { title msg }

Popup a message dialog window.

<<message-dialog>>=
set dnr 0
proc message-dialog { title msg } {
  global dnr

  incr dnr
  set w .dialog$dnr
  toplevel $w
  wm positionfrom $w program
  wm sizefrom $w program
  wm title $w $title
  wm maxsize $w 1152 900

  message $w.msg -text $msg -borderwidth 2 -relief groove
  button $w.ok -text Ok -command "destroy $w"

  pack $w.msg -side top -expand 1 -fill both
  pack $w.ok -side top -fill x 
}
@

%}}}
%{{{ proc highlight { dap hl port style }

Add a highlight on a portion of a module.
Whenever this module is shown, the highlight is turned on.
If the module is currently shown, the highlight is turned on also.

<<highlight>>=
proc highlight { dap hl port style } {
  global Dap Viewer Style

  # analyze pos
  set type [lindex $port 0]
  set when [lindex $port 1]
  set mod  [lindex $port 2]
  
  if { $type != "loc" } {
    return
  }
  set fmt [lindex $port 3]

  # tag becomes something like 'cpe-after', or 'break1-before'
  set tag  ${hl}-$when

  switch $fmt {
	lc   { # In the line-column case we highlight the complete line!
	       set start [lindex $port 4].0
	       set end   "[lindex $port 4].0 lineend" }
	lcp  { # In the line-column-pair case, we highlight the designated area
	       set start [lindex $port 4].[mkcol [lindex $port 5]]
	       set end   [lindex $port 6].[mkcol [lindex $port 7] 1] }
	lp   { # In the line-pair case, we highlight the designated area
	       set start [lindex $port 4].0
	       set end   "[lindex $port 5].0 lineend" }
	line { # In the line case, we highlight the designated line
	       set start [lindex $port 4].0 
               set end "[lindex $port 4].0 lineend" }
  }

  # Now we can remove any previous highlight of the same type
  if { [lsearch $Dap($dap,hls) $hl] == -1 } {
    lappend Dap($dap,hls) $hl
  } else {
    set oldpos $Dap($dap,hl,$hl,pos)
    set oldtag $Dap($dap,hl,$hl,tag)
    if { $Dap($dap,viewer) && $Dap($dap,hl,$hl,mod) == $Viewer($dap,module) } {
      $Viewer($dap,wtext) tag remove $oldtag [lindex $oldpos 0] [lindex $oldpos 1]
    }
  }

  # Add the new highlight
  set Dap($dap,hl,$hl,pos) [list $start $end]
  set Dap($dap,hl,$hl,mod) $mod
  set Dap($dap,hl,$hl,tag) $tag
  if { $Dap($dap,viewer) && $Viewer($dap,module) == $mod } {
    eval $Viewer($dap,wtext) tag configure $tag $Style($style)
    $Viewer($dap,wtext) tag add $tag $start $end
    $Viewer($dap,wtext) see $start
    # $Viewer($dap,wtext) see $end
  }
}

proc mkcol { col {offset 0} } {
  if { $col == -1 } {
    return end
  }
  return [expr $col+$offset]
}
@

%}}}
%{{{ proc remove-highlight { dap hl }

Remove a highlight from a module.

<<remove-highlight>>=
proc remove-highlight { dap hl } {
  global Dap Viewer

  set idx [lsearch $Dap($dap,hls) $hl]

  if { $idx != -1 } {
    set tag $Dap($dap,hl,$hl,tag)
    set pos $Dap($dap,hl,$hl,pos)
    # When the highlighted module is currently viewed,
    # we have to remove the physical evidence of the highlight.
    if { $Dap($dap,viewer) && $Dap($dap,hl,$hl,mod) == $Viewer($dap,module) } {
      $Viewer($dap,wtext) tag remove $tag [lindex $pos 0] [lindex $pos 1]
    }
    set Dap($dap,hls) [lreplace $Dap($dap,hls) $idx $idx]
    unset Dap($dap,hl,$hl,pos)
    unset Dap($dap,hl,$hl,mod)
    unset Dap($dap,hl,$hl,tag)
  }
}
@

%}}}
%{{{ proc load-module { dap module }

<<load-module>>=
proc load-module { dap module } {
  global Dap Viewer

  set paths [tide-get-search-paths $dap source]
  puts stderr "load-module: $module in $paths"
  foreach path $paths {
    set file $path/$module
    puts stderr "trying $file"
    if {[load-file $dap $file]} {
      set Viewer($dap,module) $module
      set Viewer($dap,file) $file
      return 1
    }
  }
  return 0
}
@

%}}}
%{{{ proc load-file { dap file }

Load a file in the editor.

<<load-file>>=
proc load-file { dap file } {
  global Viewer

  $Viewer($dap,wtext) configure -state normal
  $Viewer($dap,wtext) delete 1.0 end

  if { [catch {set f [open $file]}] } {
    return 0
  }
  puts stderr "load-file: $file"

  while {![eof $f]} {
    set data [read $f 1000]
    $Viewer($dap,wtext) insert end $data
  }
  close $f
  $Viewer($dap,wtext) configure -state disabled
  set Viewer($dap,file) $file
  return 1
}
@

%}}}

%{{{ proc do-location-selected { dap x y }

The user clicked on a source line. Make it the current location.

<<do-location-selected>>=
proc do-location-selected { dap x y } {
  global Viewer Dap

  set mod $Viewer($dap,module)
  if { $mod != "" && $mod != "<none>" } {
    set w $Viewer($dap,wtext)
    scan [$w index @$x,$y] "%d.%d" line col
    set procs $Viewer($dap,pids)
    set port [list loc at $mod lc $line $col]
    tide-change-current-port $dap $procs $port
    highlight $dap selected [list loc at $mod lc $line $col] selected
 }
}
@
%}}}
%{{{ proc do-toggle-simple-break { dap x y }

Toggle a observation-point.

<<do-toggle-simple-break>>=
proc do-toggle-simple-break { dap x y } {
  global Viewer Dap Break OPoint

  set mod $Viewer($dap,module)
  if { $mod != "" } {
    # First, we are going to try to delete a simple observation-point at this line.
    set tags [$Viewer($dap,wtext) tag names @$x,$y]
    set opoints {}
    foreach tag $tags {
      if { [scan $tag "opoint%d" oid] } {
        lappend opoints $oid
      }
    }
    if { $opoints != {} } {
      foreach oid $opoints {
        if { $OPoint($dap,$oid,cond) == "true" && \
	     $OPoint($dap,$oid,acts) == "break" && \
	     [lindex $OPoint($dap,$oid,port) 0] == "loc" } {
	  tide-clear-observation-point $dap $oid
	  print-print-message $dap "Removing observation-point $oid"
	  return
        }
      }
      print-message $dap "Cannot remove complex observation point(s): $opoints"
    } else {
      set w $Viewer($dap,wtext)
      scan [$w index @$x,$y] "%d.%d" line col
      set procs $Viewer($dap,pids)

      # <PO>: after should actually depend on info!
      set loc [list loc after $mod lc $line $col]
      tide-set-observation-point $dap $procs $loc true break
      print-message $dap "Setting observation-point at line $line"
    }
  }
}
@

%}}}

%{{{ proc tide-dap-connected-sb { dap info }

A new debugging-adapter created a connection with the master ToolBus.

<<tide-dap-connected-sb>>=
proc tide-dap-connected-sb { dap info } {
  global Dap

  set Dap($dap,viewer) 0
  set Dap($dap,hls) {}
  set Dap($dap,file) ""
}
@

%}}}
%{{{ proc tide-dap-disconnected-sb { dap }

A new debugging-adapter was disconnected from the debugging ToolBus.

<<tide-dap-disconnected-sb>>=
proc tide-dap-disconnected-sb { dap } {
  global Daps Dap Viewers Viewer

  set index [lsearch $Viewers $dap]
  if { $index != -1 } {
    set Viewers [lreplace $Viewers $index $index]
    destroy $Viewer($dap,win)
    if { $Viewer($dap,wbreaks) != "" } {
      destroy $Viewer($dap,wbreaks)
    }
    unset Viewer($dap,win)
    unset Dap($dap,file)
    unset Dap($dap,hls)
    unset Dap($dap,viewer)
  }
}
@

%}}}
%{{{ proc tide-observation-point-set-sb { dap oid pids port cond }

A new observation-point has been set.

<<tide-observation-point-set-sb>>=
proc tide-observation-point-set-sb { dap oid pids port cond act } {
  global Dap Viewer OPoint
  global PAT_PROCS PAT_ALL

  if { [lsearch $act break] != -1 } {
    set style breakpoint
  } else {
    set style watchpoint
  }
  highlight $dap opoint$oid $port $style
}
@

%}}}
%{{{ proc tide-observation-point-cleared-sb { dap oid }

A observation-point was cleared.

<<tide-observation-point-cleared-sb>>=
proc tide-observation-point-cleared-sb { dap oid } {
  global Dap Viewer Proc OPoint

  remove-highlight $dap opoint$oid
}
@

%}}}

%{{{ proc tide-exec-state-sb { dap pids state }

<<tide-exec-state-sb>>=
proc tide-exec-state-sb { dap pids state } {
  if { $state != "stop" } {
    foreach pid $pids {
      remove-highlight $dap cpe$pid
    }
  }
}
@

%}}}
%{{{ proc tide-cpe-sb { pid dap port }

The current point of execution of a process has changed.

<<tide-cpe-sb>>=
proc tide-cpe-sb { dap pid port } {
  global Dap Viewer


  set mod [lindex $port 2]
  set when [lindex $port 1]

  if { $mod != "" } {
    if { $Dap($dap,viewer) } {
      if { $Viewer($dap,module) != $mod } {
        load-module $dap $mod
      }
    }
    highlight $dap cpe$pid $port cpe-$when
  }
}
@

%}}}
%{{{ proc tide-current-port-sb { dap pids port }

The system has a new current port. Highlight it.

<<tide-current-port-sb>>=
proc tide-current-port-sb { dap pids port } {
  highlight $dap selected $port selected
}
@

%}}}

%{{{ proc track-source { dap }

Track the source of a process.

<<track-source>>=
proc track-source { dap } {
  global Daps Dap Viewers Viewer

  if { [lsearch $Viewers $dap] == -1 } {
    # Create a new viewer
    set Viewer($dap,pids) "all"
    set Viewer($dap,pid-selection) "all"
    lappend Viewers $dap
    create-viewer $dap
    if { $Dap($dap,current-port) != "" } {
      tide-current-port-sb $dap $Dap($dap,current-pids) $Dap($dap,current-port)
    }
  } else {
    raise $Viewer($dap,win)
  }
}
@

%}}}
%{{{ proc exception { dap Port Msg }

<<exception>>=
proc exception { P Port Exception Msg Details } {
  global Dap Viewer PAT_PROC PAT_PROCS PAT_ALL

  set port [TCLport $Port]

  if { [regexp $PAT_ALL $P P dap] } {
    set pids all
  } elseif { [regexp $PAT_PROCS $P P dap Pids] } {
      set pids $Pids
  } else {
    puts stderr "*** exception: wrongly formatted process spec $P"
    return
  }

  set msg [TCLstring $Msg]
  set exception [TCLstring $Exception]

  if { [string first "\n" $msg] == -1 && [string length $msg] < 40} {
    print-message $dap "$exception: $msg"
  } else {
    message-dialog "Exception" "$exception: $msg"
  }
  set mod [lindex $port 2]
  set when [lindex $port 1]
  
  if { $mod != "" } {
    if { $Dap($dap,viewer) } {
      if { $Viewer($dap,module) != $mod } {
        load-module $dap $mod
      }
    }
    highlight $dap exception $port exception
  }
}
@

%}}}

