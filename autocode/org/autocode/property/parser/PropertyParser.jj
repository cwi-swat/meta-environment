PARSER_BEGIN(PropertyParser)

package org.autocode.property.parser;

import org.autocode.property.*;
import java.util.*;

public class PropertyParser
{
  //{{{ public static void main(String args[])

  public static void main(String args[]) throws ParseException
  {
    PropertyParser parser = new PropertyParser(System.in);
    PropertyForest forest = parser.PropertyFile();
    //System.out.println("forest: " + forest);
    System.out.println("Type.name.color: " + forest.getValue("Type.name.color"));
    System.out.println("Type.name.type: " + forest.getValue("Type.name.type"));
    System.out.println("type: " + forest.getValue("type"));
  }

  //}}}
  //{{{ private static String unquote(String string)

  private static String unquote(String string)
  {
    return string.substring(1, string.length()-1);
  }

  //}}}

  //{{{ private static PropertyTree buildType(String type, List params)

  private static PropertyTree buildType(String type, List params)
  {
    PropertyTree tree;

    if (type.equals("string") || type.equals("integer") || type.equals("real")
	|| type.equals("Date")) {
      if (params.size() != 0) {
	throw new IllegalArgumentException("builtin type " + type
					   + " cannot have params");
      }
      tree = new PropertyTree("type", type, new PropertyForest());
      tree.setType(PropertyTree.SET);
    } else if (type.equals("list") || type.equals("set")) {
      if (params.size() != 1) {
	throw new IllegalArgumentException("type " + type
					   + " requires exactly one paramter");
      }
      PropertyForest children = new PropertyForest();
      PropertyTree elementType =
	new PropertyTree("element-type", (String)params.get(0), new PropertyForest());
      elementType.setType(PropertyTree.SET);
      children.addTree(elementType);
      tree = new PropertyTree("type", type, children);
    } else if (type.equals("map")) {
      if (params.size() != 1) {
	throw new IllegalArgumentException("type " + type
					   + " requires exactly two paramters");
      }
      PropertyForest children = new PropertyForest();
      PropertyTree keyType =
	new PropertyTree("key-type", (String)params.get(0), new PropertyForest());
      keyType.setType(PropertyTree.SET);
      children.addTree(keyType);
      PropertyTree valueType =
	new PropertyTree("value-type", (String)params.get(1), new PropertyForest());
      valueType.setType(PropertyTree.SET);
      children.addTree(valueType);
      tree = new PropertyTree("type", type, children);
    } else {
      if (params.size() != 0) {
	throw new IllegalArgumentException("user-defined type " + type
					   + " cannot have params");
      }
      tree = new PropertyTree("type", type, new PropertyForest());
      tree.setType(PropertyTree.SET);
    }

    return tree;
  }

  //}}}
}

PARSER_END(PropertyParser)

//{{{ Whitespace

SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
}


//}}}
//{{{ Comments

SPECIAL_TOKEN : /* COMMENTS */
{
  <HASH_LINE_COMMENT: "#" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
| <SLASH_LINE_COMMENT: "//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
| <MULTI_LINE_COMMENT: "/*" (~["*"])* "*" ("*" | (~["*","/"] (~["*"])* "*"))* "/">
}


//}}}
//{{{ Punctuation

TOKEN :
{
  <LBRACE: "{">
| <RBRACE: "}">
| <COMMA: ",">
| <PERIOD: ".">
| <ADD: "+=">
| <SET: ":=">
}   

//}}}
//{{{ Identifiers

TOKEN :
{
  < IDENTIFIER: <WILDCARD> | ( <LETTER> (<LETTER>|<DIGIT>|<DASH>)* )>
|
  < #LETTER:
      [
       "\u0024",
       "\u002d",
       "\u0041"-"\u005a",
       "\u005f",
       "\u0061"-"\u007a",
       "\u00c0"-"\u00d6",
       "\u00d8"-"\u00f6",
       "\u00f8"-"\u00ff",
       "\u0100"-"\u1fff",
       "\u3040"-"\u318f",
       "\u3300"-"\u337f",
       "\u3400"-"\u3d2d",
       "\u4e00"-"\u9fff",
       "\uf900"-"\ufaff"
      ]
  >
|
  < #DIGIT:
      [
       "\u0030"-"\u0039",
       "\u0660"-"\u0669",
       "\u06f0"-"\u06f9",
       "\u0966"-"\u096f",
       "\u09e6"-"\u09ef",
       "\u0a66"-"\u0a6f",
       "\u0ae6"-"\u0aef",
       "\u0b66"-"\u0b6f",
       "\u0be7"-"\u0bef",
       "\u0c66"-"\u0c6f",
       "\u0ce6"-"\u0cef",
       "\u0d66"-"\u0d6f",
       "\u0e50"-"\u0e59",
       "\u0ed0"-"\u0ed9",
       "\u1040"-"\u1049"
      ]
  >
| < #DASH: "-" >
| < #WILDCARD: "*" >
}


//}}}
//{{{ Literals

TOKEN :
{
  < STRING_LITERAL:
      "\""
      (   (~["\"","\\","\n","\r"])
        | ("\\"
            ( ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
          )
      )*
      "\""
  >
   |  < INTEGER_LITERAL:
        <DECIMAL_LITERAL> (["l","L"])?
      | <HEX_LITERAL> (["l","L"])?
      | <OCTAL_LITERAL> (["l","L"])?
      >
   |
      < #DECIMAL_LITERAL: ["1"-"9"] (["0"-"9"])* >
   |
      < #HEX_LITERAL: "0" ["x","X"] (["0"-"9","a"-"f","A"-"F"])+ >
   |
  < #OCTAL_LITERAL: "0" (["0"-"7"])* >
   |
  < FLOATING_POINT_LITERAL:
        (["0"-"9"])+ "." (["0"-"9"])* (<EXPONENT>)? (["f","F","d","D"])?
      | "." (["0"-"9"])+ (<EXPONENT>)? (["f","F","d","D"])?
      | (["0"-"9"])+ <EXPONENT> (["f","F","d","D"])?
      | (["0"-"9"])+ (<EXPONENT>)? ["f","F","d","D"]
  >
   |
  < #EXPONENT: ["e","E"] (["+","-"])? (["0"-"9"])+ >

}

//}}}

//{{{ Name

String Name() :
{
  StringBuffer nameBuf;
  Token first, next;
}
{
  first=<IDENTIFIER>
    { nameBuf = new StringBuffer(first.image); }
  ( "." next=<IDENTIFIER>
    { nameBuf.append("."); nameBuf.append(next.image); }
  )*
  { return nameBuf.toString(); }
}


//}}}

//{{{ PropertyFile

PropertyForest PropertyFile() :
{
  PropertyForest forest;
}
{
  forest=PropertyForest() <EOF>
  { return forest; }
}

//}}}

//{{{ PropertyForest

PropertyForest PropertyForest() :
{
  PropertyForest trees, forest = new PropertyForest();
  PropertyTree tree;
  String key;
  int type;
  List list;
  String value;
}
{
  (
    trees=PropertyTrees() {
      Iterator iter = trees.fetchTreeIterator();
      while (iter.hasNext()) {
	forest.addTree((PropertyTree)iter.next());
      }
    }
  ) *
  { return forest; }
}

//}}}
//{{{ PropertyTree

PropertyForest PropertyTrees() :
{
  String key;
  Token field;
  PropertyTree fieldType;
  int treeType;
  List list;
  PropertyForest forest = new PropertyForest();
  PropertyForest children;
  String value;
}
{
  LOOKAHEAD(2)
  field=<IDENTIFIER> ":" fieldType=FieldType()
  {
   children = new PropertyForest();
   children.addTree(fieldType);
   forest.addTree(new PropertyTree("field", field.image, children));
   return forest;
  }
|
  key=Name()
  (
    LOOKAHEAD(2)
    value=PropertyValue() "{" children=PropertyForest() "}"
    {
      forest.addTree(new PropertyTree(key, value, children));
      return forest;
    }
  |
    ( "+=" {treeType=PropertyTree.ADD;}
    | ":=" {treeType=PropertyTree.SET;}
    )
    (
      "{" list=PropertyValueList() "}"
      {
	Iterator iter = list.iterator();
	while (iter.hasNext()) {
	  PropertyTree tree =
	    new PropertyTree(key, (String)iter.next(), new PropertyForest());
	  tree.setType(treeType);
	  forest.addTree(tree);
	}
	return forest;
      }
    |
      value=PropertyValue()
      {
	PropertyTree tree = new PropertyTree(key, value, new PropertyForest());
	tree.setType(treeType);
	forest.addTree(tree);
	return forest;
      }
    )
  )
}

//}}}

//{{{ FieldType

PropertyTree FieldType() :
{
  Token type;
  List params = new LinkedList();
}
{
  type=<IDENTIFIER> ( "<" params=FieldParams() ">" )?
  { return buildType(type.image, params); }
}

//}}}

//{{{ FieldParams

List FieldParams() :
{
  List params = new LinkedList();
  Token param;
}
{
  param=<IDENTIFIER> { params.add(param.image); }
  ( "," param=<IDENTIFIER> { params.add(param.image); }
  )*
  { return params; }
}

//}}}

//{{{ PropertyValue

String PropertyValue() :
{
  Token string;
  String result;
}
{
   ( string=<IDENTIFIER> { result = string.image; }
   | string=<STRING_LITERAL> { result = unquote(string.image); }
   | string=<INTEGER_LITERAL> { result = string.image; }
   | string=<FLOATING_POINT_LITERAL> { result = string.image; }
   )
   { return result; }
}

//}}}
//{{{ PropertyValueList

List PropertyValueList() :
{
  List result = new LinkedList();
  String propValue;
}
{
  (
  propValue=PropertyValue() { result.add(propValue); }
  ( "," propValue=PropertyValue() { result.add(propValue); }
  ) *
  |
  { /* empty list allowed as well */ }
  )

  { return result; }
}

//}}}
